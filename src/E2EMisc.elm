module E2EMisc exposing
    ( adminConnectionsShowWhatIsViewedTest
    , banMemberTest
    , channelSearchTest
    , channelSuggestionTest
    , codeBlockCopyButtonTest
    , codeBlockInputTest
    , colorPickerTest
    , deleteAccountTest
    , dmThreadsTest
    , emojiSuggestionTest
    , exportChannelTest
    , exportDmChannelTest
    , friendsSearchTest
    , hourlyOrphanedFilesTest
    , importChannelTest
    , inactiveDmThreadsAreHiddenTest
    , inactiveThreadsAreHiddenTest
    , inviteUserAndDmChat
    , largePasteBecomesAttachment
    , leaveGuildTest
    , longMentionTest
    , markMessageAsUnreadTest
    , mentionSuggestionTest
    , noTimestampSuggestionTest
    , openLastViewedGuildOnStartupTest
    , orphanedFilesTest
    , profileImageOpensDm
    , reactionPopupNamesEmojiTest
    , reloadingAConversationLeavesItUnreadTest
    , richTextMessage
    , startingACallOrGameStaysReadTest
    , staysReadWhileViewingTest
    , swipedAwayConversationStopsBeingViewedTest
    , timeOfDaySuggestionTest
    , timeOffsetSuggestionTest
    , touchingTextInputDoesntStartDragTest
    )

import Audio
import Broadcast
import ChannelExport
import Color
import DiscordUserData
import DmChannel
import DmChannelId
import Drawing
import Duration
import E2EHelper
import E2EVoiceChat
import Effect.Browser.Dom as Dom
import Effect.Test as T
import Effect.Time as Time
import EmailAddress exposing (EmailAddress)
import Emoji
import Env
import Expect
import FileStatus
import FrontendExtra
import Html.Attributes
import Id
import IdArray
import Json.Decode
import Json.Encode
import List.Nonempty
import Local
import LocalState exposing (LocalState)
import MembersAndOwner
import Message
import MessageDropdown
import MyUi
import NonemptyDict
import Pages.Admin
import Pages.Guild
import PersonName
import Quantity
import Range exposing (Range)
import RichText
import Route exposing (ChannelsVisibleOnMobile(..))
import SeqDict
import SeqSet
import String.Nonempty
import Test.Html.Query
import Test.Html.Selector
import TimeInMinutes
import Touch
import Types exposing (BackendMsg, FrontendModel, FrontendMsg, ImportChannelError(..), ToBackend, ToFrontend)
import User
import UserColor
import UserOptions
import UserSession


{-| Pasting a large chunk of text that would push the message over the max message length converts the pasted text into a text file attachment instead of inserting it into the text input.
-}
largePasteBecomesAttachment :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
largePasteBecomesAttachment config =
    E2EHelper.startTest
        "Pasted text too long to fit in a message is attached as a text file instead"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                let
                    pastedText : String
                    pastedText =
                        String.repeat 250 "0123456789"
                in
                [ E2EHelper.focusEvent admin 1000 (Just (Dom.id "channel_textinput")) (Just { start = 0, end = 0 })
                , admin.click 100 (Dom.id "channel_textinput")
                , admin.input 100 (Dom.id "channel_textinput") "Check this out! "
                , admin.input 100 (Dom.id "channel_textinput") ("Check this out! " ++ pastedText)

                -- The pasted text is removed from the draft and replaced with an attached file placeholder
                , T.checkState
                    100
                    (\data ->
                        case SeqDict.get admin.clientId data.frontends |> Maybe.map Audio.userModel of
                            Just (Types.Loaded loaded) ->
                                case loaded.loginStatus of
                                    Types.LoggedIn loggedIn ->
                                        if
                                            List.map
                                                String.Nonempty.toString
                                                (SeqDict.values loggedIn.drafts)
                                                == [ "Check this out! [!1]" ]
                                        then
                                            Ok ()

                                        else
                                            Err "The pasted text should have been replaced with a file attachment placeholder in the draft"

                                    Types.NotLoggedIn _ ->
                                        Err "Expected admin to be logged in"

                            _ ->
                                Err "Expected admin frontend to be loaded"
                    )

                -- The Rust server tells the backend about the uploaded file (in tests the
                -- upload HTTP response is mocked so this notification is injected manually,
                -- like E2EHelper.uploadNonImageAttachment does).
                , T.backendUpdate
                    100
                    (Types.Rpc_GotFileUpload (FileStatus.fileHash "123123123") 2500 Nothing)
                , admin.keyDown 1000 (Dom.id "channel_textinput") "Enter" []
                , admin.checkView
                    100
                    (Test.Html.Query.has
                        [ Test.Html.Selector.id "guild_message_1" ]
                    )
                , admin.checkView
                    100
                    (Test.Html.Query.has [ Test.Html.Selector.text FrontendExtra.pastedMessageFileName ])
                ]
            )
        ]


{-| Hovering a message shows a popup above each of its reactions naming who reacted.
For a standard unicode emoji the popup also names the emoji itself, which it can only
do once the emoji data has been fetched and copied into LocalUser.
-}
reactionPopupNamesEmojiTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
reactionPopupNamesEmojiTest config =
    E2EHelper.startTest
        "Hovering a reaction names the emoji it was made with"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.writeMessage admin 1000 "Hello everyone"

                -- The first of the buttons that appear when hovering a message reacts
                -- with ❤️, the emoji `User.commonlyUsedEmojis` leads with.
                , admin.mouseEnter 100 (Dom.id "guild_message_1") ( 10, 10 ) []
                , admin.click 100 (Dom.id "miniView_emojiReact_0")
                , admin.checkView
                    100
                    (Test.Html.Query.has [ Test.Html.Selector.id "guild_removeReactionEmoji_0" ])
                , admin.mouseEnter 100 (Dom.id "guild_message_1") ( 10, 10 ) []
                , admin.checkView
                    100
                    (Test.Html.Query.has
                        [ Test.Html.Selector.class "emoji-popup"
                        , Test.Html.Selector.text ":heart:"
                        ]
                    )
                ]
            )
        ]


{-| Pressing "Export channel" in the member column asks the backend for a JSON
copy of the channel and downloads it. The JSON should contain every message plus
the publicly available data of everyone with access to the channel.
-}
exportChannelTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
exportChannelTest config =
    E2EHelper.startTest
        "Export a guild channel to a JSON file"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.writeMessage admin 1000 "Hello everyone"

                -- Something drawn on the message is part of the conversation, so it is exported
                -- along with it
                , admin.click 100 (Dom.id "channelHeader_drawOnMessages")
                , T.andThen
                    100
                    (\data ->
                        case E2EHelper.lastGuildChannelMessage data.backend of
                            Just ( _, messageId, _ ) ->
                                [ admin.mouseEnter
                                    100
                                    (Dom.id ("guild_message_" ++ Id.toString messageId))
                                    ( 10, 10 )
                                    []
                                , admin.custom
                                    100
                                    (Drawing.profileImageAnchorId messageId)
                                    "click"
                                    (E2EHelper.drawingAnchorClick 30 25)
                                , E2EHelper.drawZigzagStroke admin
                                ]

                            Nothing ->
                                [ T.checkState 100 (\_ -> Err "The message wasn't written") ]
                    )
                , admin.click 1000 (Dom.id "guild_showMembers")
                , admin.click 1000 (Dom.id "guild_exportChannel")
                , T.checkState
                    1000
                    (\data ->
                        case exportedChannel data of
                            Ok (ChannelExport.GuildChannelExport channel) ->
                                if List.any messageHasDrawing (IdArray.toList channel.messages) then
                                    Ok ()

                                else
                                    Err "The drawing on the message wasn't exported"

                            Ok _ ->
                                Err "A guild channel was exported as some other kind of channel"

                            Err error ->
                                Err error
                    )
                ]
            )
        ]


{-| The one file the export button handed the browser, read back as the channel it holds.
-}
exportedChannel :
    T.Data FrontendModel E2EHelper.BackendModel2
    -> Result String ChannelExport.ChannelExport
exportedChannel data =
    case data.downloads of
        [ download ] ->
            case download.content of
                T.StringFile content ->
                    case ChannelExport.decode content of
                        Ok channelExport ->
                            Ok channelExport

                        Err error ->
                            Err
                                ("The exported channel couldn't be read back: "
                                    ++ Json.Decode.errorToString error
                                )

                T.BytesFile _ ->
                    Err "The exported channel should be a text file"

        downloads ->
            Err ("Expected a single download, instead got " ++ String.fromInt (List.length downloads))


messageHasDrawing : Message.Message messageId userId channelId -> Bool
messageHasDrawing message =
    case message of
        Message.UserTextMessage data ->
            case data.drawings of
                Just drawings ->
                    List.isEmpty drawings.userIconDrawings.finished |> not

                Nothing ->
                    False

        _ ->
            False


{-| A guild owner can hand the guild settings a file the export channel button wrote and get a
channel out of it. The channel that arrives holds the same conversation as the one that was
exported, which is what makes an export a way of moving a channel rather than just reading it.
-}
importChannelTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
importChannelTest config =
    E2EHelper.startTest
        "Import a guild channel from an exported file"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.writeMessage admin 1000 "Hello everyone"
                , admin.click 1000 (Dom.id "guild_inviteLinkCreatorRoute")
                , E2EHelper.hasExactText admin [ Pages.Guild.importChannelText ]

                -- Nothing has been exported yet, so the file picker offers a file that isn't a
                -- channel export and it gets turned down instead of making a channel
                , admin.click 1000 (Dom.id "guild_importChannel")
                , E2EHelper.hasExactText admin [ Pages.Guild.importChannelFailedText NotAChannelExport ]
                , admin.click 1000 (Dom.id "guild_openChannel_0")
                , admin.click 1000 (Dom.id "guild_showMembers")
                , admin.click 1000 (Dom.id "guild_exportChannel")
                , admin.click 1000 (Dom.id "guild_inviteLinkCreatorRoute")
                , admin.click 1000 (Dom.id "guild_importChannel")
                , E2EHelper.hasExactText admin [ Pages.Guild.importedChannelText 0 ]
                , admin.click 1000 (Dom.id "guild_openChannel_1")
                , E2EHelper.hasExactText admin [ "Hello everyone" ]
                ]
            )
        ]


{-| DM channels have a member column too, listing the two people in the DM and
the same "Export channel" button that guild channels have.
-}
exportDmChannelTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
exportDmChannelTest config =
    E2EHelper.startTest
        "Export a DM channel to a JSON file"
        E2EHelper.startTime
        config
        [ T.connectFrontend
            100
            E2EHelper.sessionId0
            "/"
            E2EHelper.desktopWindow
            (\admin ->
                [ E2EHelper.handleLogin E2EHelper.firefoxDesktop E2EHelper.adminEmail admin
                , E2EHelper.inviteUser
                    admin
                    (\user ->
                        [ E2EHelper.openDm user 1000 "0"
                        , E2EHelper.writeMessage user 100 "Hello in a DM"
                        , user.checkView
                            100
                            (Test.Html.Query.hasNot [ Test.Html.Selector.exactText "Members (2)" ])
                        , user.click 100 (Dom.id "guild_showMembers")
                        , user.checkView
                            100
                            (Test.Html.Query.has [ Test.Html.Selector.exactText "Members (2)" ])
                        , user.click 1000 (Dom.id "guild_exportChannel")
                        , T.checkState
                            1000
                            (\data ->
                                case exportedChannel data of
                                    Ok (ChannelExport.DmChannelExport channel) ->
                                        if IdArray.length channel.messages == 1 then
                                            Ok ()

                                        else
                                            Err "The DM's message wasn't exported"

                                    Ok _ ->
                                        Err "A DM channel was exported as some other kind of channel"

                                    Err error ->
                                        Err error
                            )
                        ]
                    )
                ]
            )
        ]


{-| Simulates the browser moving focus into a search input. Unlike
`E2EHelper.focusEvent` this includes the `selectionDirection` field, which the
focus decoder requires before it will record the input as focused.
-}
focusSearchInput :
    Dom.HtmlId
    -> T.FrontendActions ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.Action ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
focusSearchInput htmlId client =
    client.portEvent
        100
        "focus_changed_from_js"
        (Json.Encode.object
            [ ( "id", Json.Encode.string (Dom.idToString htmlId) )
            , ( "selectionStart", Json.Encode.int 0 )
            , ( "selectionEnd", Json.Encode.int 0 )
            , ( "selectionDirection", Json.Encode.string "forward" )
            ]
        )


friendsSearchTest : T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2 -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
friendsSearchTest config =
    E2EHelper.startTest
        "Filter friends with the direct messages search input"
        E2EHelper.startTime
        config
        [ T.connectFrontend
            100
            E2EHelper.sessionId0
            "/"
            E2EHelper.desktopWindow
            (\admin ->
                [ E2EHelper.handleLogin E2EHelper.firefoxDesktop E2EHelper.adminEmail admin
                , E2EHelper.inviteUser
                    admin
                    (\user ->
                        [ E2EHelper.openDm user 1000 "0"
                        , E2EHelper.writeMessage user 100 "Hello admin!"
                        , admin.click 100 (Dom.id "guild_openChannel_0")
                        , E2EHelper.openDm admin 100 "2"

                        -- The search input is transparent until it gets focus, so its placeholder
                        -- text is used to detect whether it is shown or not.
                        , admin.checkView 100
                            (Test.Html.Query.has
                                [ Test.Html.Selector.id "guild_friendLabel_0"
                                , Test.Html.Selector.id "guild_friendLabel_2"
                                , Test.Html.Selector.exactText Pages.Guild.directMessagesText
                                ]
                            )
                        , admin.checkView 100
                            (Test.Html.Query.hasNot
                                [ Test.Html.Selector.attribute (Html.Attributes.placeholder "Filter friends") ]
                            )
                        , focusSearchInput Pages.Guild.friendsSearchInputId admin
                        , admin.checkView 100
                            (Test.Html.Query.has
                                [ Test.Html.Selector.attribute (Html.Attributes.placeholder "Filter friends") ]
                            )
                        , admin.snapshotView 100 { name = "Friends search input open" }
                        , admin.input 100 Pages.Guild.friendsSearchInputId "sven"
                        , admin.checkView 100
                            (Test.Html.Query.has [ Test.Html.Selector.id "guild_friendLabel_2" ])
                        , admin.checkView 100
                            (Test.Html.Query.hasNot [ Test.Html.Selector.id "guild_friendLabel_0" ])
                        , admin.snapshotView 100 { name = "Friends search input filters friends column" }
                        , admin.input 100 Pages.Guild.friendsSearchInputId "does not match anyone"
                        , admin.checkView 100
                            (Test.Html.Query.hasNot [ Test.Html.Selector.id "guild_friendLabel_2" ])

                        -- Clearing the text shows all friends again, and the input stays visible
                        -- because it still has focus.
                        , admin.click 100 (Dom.id "guild_clearFriendsSearch")
                        , admin.checkView 100
                            (Test.Html.Query.has
                                [ Test.Html.Selector.id "guild_friendLabel_0"
                                , Test.Html.Selector.id "guild_friendLabel_2"
                                , Test.Html.Selector.attribute (Html.Attributes.placeholder "Filter friends")
                                ]
                            )

                        -- Once the empty input loses focus it becomes transparent again.
                        , E2EHelper.focusEvent admin 100 Nothing Nothing
                        , admin.checkView 100
                            (Test.Html.Query.hasNot
                                [ Test.Html.Selector.attribute (Html.Attributes.placeholder "Filter friends") ]
                            )
                        ]
                    )
                ]
            )
        ]


channelSearchTest : T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2 -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
channelSearchTest config =
    E2EHelper.startTest
        "Filter channels with the channel column search input"
        E2EHelper.startTime
        config
        [ T.connectFrontend
            100
            E2EHelper.sessionId0
            "/"
            E2EHelper.desktopWindow
            (\admin ->
                [ E2EHelper.handleLogin E2EHelper.firefoxDesktop E2EHelper.adminEmail admin
                , admin.click 100 (Dom.id "guild_createGuild")
                , admin.input 100 (Dom.id "newGuildName") "My new guild!"
                , admin.click 100 (Dom.id "guild_createGuildSubmit")

                -- The search row only appears for guilds with more than 6 channels.
                , admin.checkView 100
                    (Test.Html.Query.hasNot
                        [ Test.Html.Selector.id (Dom.idToString Pages.Guild.channelSearchInputId) ]
                    )
                , List.map
                    (\channelName ->
                        T.group
                            [ admin.click 100 (Dom.id "guild_newChannel")
                            , admin.input 100 (Dom.id "newChannelName") channelName
                            , admin.click 100 (Dom.id "guild_createChannel")
                            ]
                    )
                    [ "alpha", "beta", "gamma", "delta", "epsilon", "zeta" ]
                    |> T.group

                -- With 7 channels the search row appears below the header.
                , admin.checkView 100
                    (Test.Html.Query.has
                        [ Test.Html.Selector.id (Dom.idToString Pages.Guild.channelSearchInputId)
                        , Test.Html.Selector.attribute (Html.Attributes.placeholder "Search channels")
                        ]
                    )
                , admin.snapshotView 100 { name = "Channel search row in channel column" }
                , admin.input 100 Pages.Guild.channelSearchInputId "zeta"
                , admin.checkView 100
                    (Test.Html.Query.has [ Test.Html.Selector.id "guild_openChannel_6" ])
                , admin.checkView 100
                    (Test.Html.Query.hasNot [ Test.Html.Selector.id "guild_openChannel_0" ])
                , admin.snapshotView 100 { name = "Channel search row filters channel column" }
                , admin.input 100 Pages.Guild.channelSearchInputId "does not match any channel"
                , admin.checkView 100
                    (Test.Html.Query.hasNot [ Test.Html.Selector.id "guild_openChannel_6" ])
                , admin.checkView 100
                    (Test.Html.Query.has [ Test.Html.Selector.exactText Pages.Guild.noMatchingChannelsText ])

                -- Clearing the text shows all channels again.
                , admin.click 100 (Dom.id "guild_clearChannelSearch")
                , admin.checkView 100
                    (Test.Html.Query.has
                        [ Test.Html.Selector.id "guild_openChannel_0"
                        , Test.Html.Selector.id "guild_openChannel_6"
                        , Test.Html.Selector.attribute (Html.Attributes.placeholder "Search channels")
                        ]
                    )
                ]
            )
        ]


inviteUserAndDmChat : T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2 -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
inviteUserAndDmChat config =
    E2EHelper.startTest
        "Invite user and then have DM chat"
        E2EHelper.startTime
        config
        [ T.connectFrontend
            100
            E2EHelper.sessionId0
            "/"
            E2EHelper.desktopWindow
            (\admin ->
                [ E2EHelper.handleLogin E2EHelper.firefoxDesktop E2EHelper.adminEmail admin
                , E2EHelper.inviteUser
                    admin
                    (\user ->
                        [ E2EHelper.openDm user 1000 "0"
                        , E2EHelper.writeMessage user 100 "Hello"
                        , admin.click 100 (Dom.id "guildsColumn_openDm_2")
                        , E2EHelper.writeMessage user 100 "Hello 2"
                        , E2EHelper.writeMessage admin 100 "Hello from *admin*"
                        , user.checkView
                            100
                            (\html ->
                                Test.Html.Query.findAll [ Test.Html.Selector.exactText "Sven" ] html
                                    -- Two Sven messages, Sven in the DM column, and Sven in the user options
                                    |> Test.Html.Query.count (Expect.equal 4)
                            )
                        , E2EHelper.createThread user (Id.fromInt 1)
                        , E2EHelper.writeMessage user 100 "Writing in thread"
                        , admin.checkView
                            100
                            (\html ->
                                Test.Html.Query.find [ Test.Html.Selector.id "guild_threadStarterIndicator_1" ] html
                                    |> Test.Html.Query.has
                                        [ Test.Html.Selector.containing [ Test.Html.Selector.exactText "Sven" ]
                                        ]
                            )
                        , admin.click 100 (Dom.id "guild_threadStarterIndicator_1")
                        ]
                    )
                ]
            )
        ]


{-| Clicking the profile image next to a message in a guild channel opens the DM channel
with whoever wrote it.
-}
profileImageOpensDm : T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2 -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
profileImageOpensDm config =
    E2EHelper.startTest
        "Clicking a profile image opens the DM channel with that user"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin user ->
                [ E2EHelper.writeMessage user 100 "Click on my profile image!"
                , T.andThen
                    100
                    (\data ->
                        case E2EHelper.lastGuildChannelMessage data.backend of
                            Just ( _, messageId, Message.UserTextMessage _ ) ->
                                [ admin.click 100 (Pages.Guild.profileImageButtonId messageId)
                                , E2EHelper.hasText admin [ Pages.Guild.chatWithText, "Stevie Steve" ]
                                ]

                            _ ->
                                [ T.checkState
                                    0
                                    (\_ -> Err "Expected the guild channel to contain the other user's message")
                                ]
                    )
                ]
            )
        ]


{-| Writing a message counts as reading it, so the thread it went into holds nothing
unread for the person who wrote it.
-}
checkDmThreadIsRead : Id.Id Id.UserId -> Id.Id Id.ChannelMessageId -> FrontendModel -> Result String ()
checkDmThreadIsRead otherUserId threadMessageIndex model =
    case Audio.userModel model of
        Types.Loaded loaded ->
            case loaded.loginStatus of
                Types.LoggedIn loggedIn ->
                    let
                        local : LocalState
                        local =
                            Local.model loggedIn.localState

                        newestMessageId : Maybe (Id.Id Id.ThreadMessageId)
                        newestMessageId =
                            SeqDict.get otherUserId local.dmChannels
                                |> Maybe.andThen (\dmChannel -> SeqDict.get threadMessageIndex dmChannel.threads)
                                |> Maybe.map DmChannel.latestFrontendThreadMessageId
                    in
                    if
                        SeqDict.get
                            ( Id.GuildOrDmId (Id.GuildOrDmId_Dm { otherUserId = otherUserId }), threadMessageIndex )
                            local.localUser.user.lastViewedThreadMessage
                            == newestMessageId
                    then
                        Ok ()

                    else
                        Err "Expected the newest message in the DM thread to have been read"

                Types.NotLoggedIn _ ->
                    Err "Expected the frontend to be logged in"

        Types.Loading _ ->
            Err "Expected the frontend to have finished loading"


{-| The connections section of the admin page names the channel each connection has open,
so a tab sitting in a guild channel is listed by that channel's name while the admin page's
own tab, which isn't in a conversation, is listed as viewing nothing.
-}
adminConnectionsShowWhatIsViewedTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
adminConnectionsShowWhatIsViewedTest config =
    E2EHelper.startTest
        "Admin page shows what each connection is viewing"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\_ _ ->
                [ T.connectFrontend
                    100
                    E2EHelper.sessionId0
                    "/admin"
                    E2EHelper.desktopWindow
                    (\adminPage ->
                        [ T.andThen
                            10
                            (\data ->
                                [ adminPage.portEvent
                                    10
                                    "load_startup_data_from_js"
                                    (E2EHelper.startupDataJson data.time E2EHelper.firefoxDesktop)
                                ]
                            )
                        , adminPage.click 100 (Pages.Admin.expandSectionButtonId Pages.Admin.ConnectionsSection)
                        , E2EHelper.hasExactText
                            adminPage
                            [ "Viewing: My new guild! #general", "Viewing: Nothing" ]
                        ]
                    )
                ]
            )
        ]


{-| The file attached to a message is in use, so only the uploads nothing refers to are listed
and deleted.
-}
orphanedFilesTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
orphanedFilesTest config =
    E2EHelper.startTest
        "Admin page lists uploaded files that nothing uses"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.uploadImageAttachment admin
                , E2EHelper.focusEvent admin 1000 (Just (Dom.id "channel_textinput")) (Just { start = 0, end = 0 })
                , admin.keyDown 100 (Dom.id "channel_textinput") "Enter" []
                , List.range 1 150
                    |> List.map
                        (\index ->
                            T.backendUpdate
                                0
                                (Types.Rpc_GotFileUpload (FileStatus.fileHash ("unusedFile" ++ String.fromInt index)) 5000 Nothing)
                        )
                    |> T.group
                , T.connectFrontend
                    100
                    E2EHelper.sessionId0
                    "/admin"
                    E2EHelper.desktopWindow
                    (\adminPage ->
                        [ T.andThen
                            10
                            (\data ->
                                [ adminPage.portEvent
                                    10
                                    "load_startup_data_from_js"
                                    (E2EHelper.startupDataJson data.time E2EHelper.firefoxDesktop)
                                ]
                            )
                        , adminPage.click 100 (Pages.Admin.expandSectionButtonId Pages.Admin.FilesSection)
                        , E2EHelper.hasExactText adminPage [ "unusedFile150", "Orphaned file count: 150, total size: 732.4kb" ]
                        , E2EHelper.hasNotExactText adminPage [ "123123123" ]
                        , adminPage.click 100 Pages.Admin.deleteOrphanedFilesButtonId
                        , E2EHelper.hasExactText adminPage [ "File count: 1", "No orphaned files" ]
                        , T.checkBackend
                            100
                            (\backend ->
                                if SeqDict.keys (E2EHelper.unwrapBackend backend).files == [ FileStatus.fileHash "123123123" ] then
                                    Ok ()

                                else
                                    Err "Only the file in use should be left"
                            )
                        ]
                    )
                ]
            )
        ]


{-| An upload that nothing uses is noticed on one hourly update and deleted on the next, while
one that only became unused since the last update is left for another hour.
-}
hourlyOrphanedFilesTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
hourlyOrphanedFilesTest config =
    E2EHelper.startTest
        "Hourly update deletes files that stayed orphaned for an hour"
        E2EHelper.startTime
        config
        [ T.backendUpdate 0 (Types.Rpc_GotFileUpload (FileStatus.fileHash "firstFile") 5000 Nothing)
        , T.andThen 100 (\data -> [ T.backendUpdate 0 (Types.HourlyUpdate data.time) ])
        , T.backendUpdate 100 (Types.Rpc_GotFileUpload (FileStatus.fileHash "secondFile") 5000 Nothing)
        , T.andThen 100 (\data -> [ T.backendUpdate 0 (Types.HourlyUpdate data.time) ])
        , T.checkBackend
            100
            (\backend ->
                if SeqDict.keys (E2EHelper.unwrapBackend backend).files == [ FileStatus.fileHash "secondFile" ] then
                    Ok ()

                else
                    Err "Only the file orphaned since the last hourly update should be left"
            )
        ]


{-| A message someone else writes into the conversation you are looking at, with nothing
unread in it, shouldn't leave you with something unread. The conversation stays caught up
while you sit in it, and it is still caught up once you leave.
-}
staysReadWhileViewingTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
staysReadWhileViewingTest config =
    E2EHelper.startTest
        "Messages arriving in the conversation you are looking at stay read"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin user ->
                [ -- The guild channel both of them are looking at
                  E2EHelper.writeMessage admin 100 "In the channel"
                , checkChannelIsCaughtUp guildChannelId user
                , user.click 100 (Dom.id "guildIcon_showFriends")
                , E2EHelper.hasExactText user [ Pages.Guild.noUnreadMessagesText ]

                -- A message arriving while the reader is elsewhere is still unread
                , E2EHelper.writeMessage admin 100 "While away"
                , E2EHelper.hasNotExactText user [ Pages.Guild.noUnreadMessagesText ]
                , E2EHelper.hasExactText user [ "While away" ]

                -- Opening the channel catches them up again, and a thread started from a
                -- message counts separately from the channel it hangs off
                , user.click 100 (Dom.id "guild_openGuild_1")
                , user.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.exactText Pages.Guild.newMessagesBadgeText ])
                , E2EHelper.createThread admin (Id.fromInt 1)
                , E2EHelper.writeMessage admin 100 "Starting a thread"
                , user.click 100 (Dom.id "guild_viewThread_0_1")
                , user.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.exactText Pages.Guild.newMessagesBadgeText ])
                , E2EHelper.writeMessage admin 100 "In the thread"
                , checkThreadIsCaughtUp guildChannelId (Id.fromInt 1) user
                , user.click 100 (Dom.id "guildIcon_showFriends")
                , E2EHelper.hasExactText user [ Pages.Guild.noUnreadMessagesText ]

                -- The same in a DM, where the reader knows the conversation by the person
                -- writing to them
                , E2EHelper.openDm admin 100 "2"
                , E2EHelper.writeMessage admin 100 "Hello in a DM!"
                , user.click 100 (Dom.id "guild_friendLabel_0")
                , user.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.exactText Pages.Guild.newMessagesBadgeText ])
                , E2EHelper.writeMessage admin 100 "And another one"
                , checkChannelIsCaughtUp dmWithAdminId user
                , user.click 100 (Dom.id "guildIcon_showFriends")
                , E2EHelper.hasExactText user [ Pages.Guild.noUnreadMessagesText ]

                -- A reader who is behind stays where they are. Marking "While away" as
                -- unread puts them one message back, and a message arriving while they are
                -- still looking at the channel leaves that where it is instead of catching
                -- them up over the top of it.
                , user.click 100 (Dom.id "guild_openGuild_1")
                , user.click 100 (Dom.id "guild_openChannel_0")
                , markAsUnread user (Id.fromInt 2)
                , user.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.exactText Pages.Guild.newMessagesBadgeText ])
                , user.checkModel 100 (checkLastViewedMessageIs guildChannelId (Id.fromInt 1))
                , admin.click 100 (Dom.id "guild_openGuild_1")
                , admin.click 100 (Dom.id "guild_openChannel_0")
                , E2EHelper.writeMessage admin 100 "After the mark"
                , E2EHelper.hasExactText user [ "After the mark" ]
                , user.checkModel 100 (checkLastViewedMessageIs guildChannelId (Id.fromInt 1))
                , E2EHelper.tallSnapshot user 100 { name = "Unread divider stays put while viewing" }

                -- Opening one of the channel's tabs isn't arriving in a conversation they
                -- weren't already in, so that leaves the mark where it is too
                , user.click 100 (Dom.id "guild_openGamesTab")
                , user.checkModel 100 (checkLastViewedMessageIs guildChannelId (Id.fromInt 1))

                -- Which is what the unread overview shows once they leave: everything from
                -- the message they marked onwards, and nothing from before it
                , user.click 100 (Dom.id "guildIcon_showFriends")
                , E2EHelper.hasExactText user [ "While away", "After the mark" ]
                , E2EHelper.hasNotExactText user [ "In the channel" ]
                , E2EHelper.tallSnapshot user 100 { name = "Unread overview after a message marked as unread" }
                ]
            )
        ]


{-| Landing in a conversation because the page was loaded on its url isn't the reader having
read what turned up while they were away. The unread divider is still there for them to look
at rather than the messages being marked as read on their behalf as the page loads.
-}
reloadingAConversationLeavesItUnreadTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
reloadingAConversationLeavesItUnreadTest config =
    E2EHelper.startTest
        "Loading the url of a conversation leaves what's unread in it unread"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin user ->
                [ E2EHelper.writeMessage admin 100 "In the channel"
                , checkChannelIsCaughtUp guildChannelId user

                -- The reader goes elsewhere and a message turns up without them
                , user.click 100 (Dom.id "guildIcon_showFriends")
                , E2EHelper.writeMessage admin 100 "While away"
                , E2EHelper.hasNotExactText user [ Pages.Guild.noUnreadMessagesText ]

                -- Loading the channel's url puts them back in it with that message still
                -- unread, so the divider above it is what they see
                , T.connectFrontend
                    100
                    E2EHelper.sessionId1
                    (Route.encode
                        (Route.GuildRoute
                            (Id.fromInt 1)
                            (Route.ChannelRoute
                                (Id.fromInt 0)
                                (Route.NoThreadWithFriends Nothing Route.HideChannelSettings)
                                Nothing
                            )
                            ChannelsHiddenOnMobile
                            Nothing
                        )
                    )
                    E2EHelper.desktopWindow
                    (\reloaded ->
                        [ T.andThen
                            10
                            (\data ->
                                [ reloaded.portEvent
                                    10
                                    "load_startup_data_from_js"
                                    (E2EHelper.startupDataJson data.time E2EHelper.firefoxDesktop)
                                ]
                            )
                        , reloaded.checkView
                            500
                            (Test.Html.Query.has [ Test.Html.Selector.exactText "While away" ])
                        , reloaded.checkView
                            100
                            (Test.Html.Query.has [ Test.Html.Selector.exactText Pages.Guild.newMessagesBadgeText ])
                        , reloaded.checkModel 100 (checkLastViewedMessageIs guildChannelId (Id.fromInt 1))
                        ]
                    )
                ]
            )
        ]


{-| Swiping the conversation view off screen on mobile has to tell the backend the reader
isn't looking at it any more, otherwise messages written while they sit on the channel list
are marked as read on their behalf.
-}
swipedAwayConversationStopsBeingViewedTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
swipedAwayConversationStopsBeingViewedTest config =
    E2EHelper.startTest
        "Swiping the conversation view closed on mobile stops it counting as viewed"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.iphone14Window
            (\admin user ->
                [ -- A guild channel is what the reader lands in, so that's what the backend
                  -- has them looking at
                  T.checkState 100 checkBackendIsViewingTheChannel
                , E2EHelper.writeMessageMobile admin "While looking at the channel"
                , user.checkModel 100 (checkLastViewedMessageIs guildChannelId (Id.fromInt 1))

                -- Swiping it away leaves them on the guild's channel list, which the
                -- backend has to hear about
                , user.click 100 (Dom.id "guild_headerBackButton")
                , T.checkState 500 checkBackendIsViewingNothing
                , E2EHelper.writeMessageMobile admin "While the channel is swiped away"
                , user.checkModel 100 (checkLastViewedMessageIs guildChannelId (Id.fromInt 1))

                -- The same goes for a DM, where swiping the conversation away leaves them
                -- on the friends list instead
                , E2EHelper.openDm admin 100 "2"
                , E2EHelper.writeMessageMobile admin "Starting a DM"
                , user.click 100 (Dom.id "guildIcon_showFriends")
                , user.click 100 (Dom.id "guild_friendLabel_0")
                , T.checkState 100 checkBackendIsViewingTheDm
                , E2EHelper.writeMessageMobile admin "While looking at the DM"
                , user.checkModel 100 (checkLastViewedMessageIs dmWithAdminId (Id.fromInt 1))
                , user.click 100 (Dom.id "guild_headerBackButton")
                , T.checkState 500 checkBackendIsViewingNothing
                , E2EHelper.writeMessageMobile admin "While the DM is swiped away"
                , user.checkModel 100 (checkLastViewedMessageIs dmWithAdminId (Id.fromInt 1))
                , E2EHelper.tallSnapshot user 100 { name = "DM left unread while swiped away" }
                ]
            )
        ]


checkBackendIsViewingTheChannel : T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
checkBackendIsViewingTheChannel data =
    case E2EHelper.backendViewing E2EHelper.sessionId1 data of
        Ok (UserSession.Viewing_Channel _) ->
            Ok ()

        Ok _ ->
            Err "Expected the backend to have the reader viewing the guild channel"

        Err error ->
            Err error


checkBackendIsViewingTheDm : T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
checkBackendIsViewingTheDm data =
    case E2EHelper.backendViewing E2EHelper.sessionId1 data of
        Ok (UserSession.Viewing_Dm _) ->
            Ok ()

        Ok _ ->
            Err "Expected the backend to have the reader viewing the DM"

        Err error ->
            Err error


checkBackendIsViewingNothing : T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
checkBackendIsViewingNothing data =
    case E2EHelper.backendViewing E2EHelper.sessionId1 data of
        Ok UserSession.Viewing_None ->
            Ok ()

        Ok _ ->
            Err "Expected the backend to hear that the swiped away conversation is no longer being viewed"

        Err error ->
            Err error


{-| The reader is behind by however far the given message sits from the newest one, which
is what the unread divider and the notification counts are drawn from.
-}
checkLastViewedMessageIs : Id.AnyGuildOrDmId -> Id.Id Id.ChannelMessageId -> FrontendModel -> Result String ()
checkLastViewedMessageIs guildOrDmId messageId model =
    withLocalState
        model
        (\local ->
            case SeqDict.get guildOrDmId local.localUser.user.lastViewedMessage of
                Just lastViewed ->
                    if lastViewed == messageId then
                        Ok ()

                    else
                        Err
                            ("Expected the last viewed message to be "
                                ++ Id.toString messageId
                                ++ " but it is "
                                ++ Id.toString lastViewed
                            )

                Nothing ->
                    Err "Expected the channel to have been viewed"
        )


guildChannelId : Id.AnyGuildOrDmId
guildChannelId =
    Id.GuildOrDmId (Id.GuildOrDmId_Guild { guildId = Id.fromInt 1, channelId = Id.fromInt 0 })


dmWithAdminId : Id.AnyGuildOrDmId
dmWithAdminId =
    Id.GuildOrDmId (Id.GuildOrDmId_Dm { otherUserId = Id.fromInt 0 })


{-| The reader has seen every message in the channel, which is what the notification counts
and the unread overview are worked out from.
-}
checkChannelIsCaughtUp :
    Id.AnyGuildOrDmId
    -> T.FrontendActions ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.Action ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
checkChannelIsCaughtUp guildOrDmId user =
    T.group
        [ user.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.exactText Pages.Guild.newMessagesBadgeText ])
        , user.checkModel
            0
            (\model ->
                withLocalState
                    model
                    (\local ->
                        case ( SeqDict.get guildOrDmId local.localUser.user.lastViewedMessage, latestChannelMessageId guildOrDmId local ) of
                            ( lastViewed, Just latest ) ->
                                if lastViewed == Just latest then
                                    Ok ()

                                else
                                    Err
                                        ("Expected the channel to be caught up at "
                                            ++ Id.toString latest
                                            ++ " but the last viewed message is "
                                            ++ (case lastViewed of
                                                    Just messageId ->
                                                        Id.toString messageId

                                                    Nothing ->
                                                        "nothing"
                                               )
                                        )

                            ( _, Nothing ) ->
                                Err "Expected the channel to exist"
                    )
            )
        ]


checkThreadIsCaughtUp :
    Id.AnyGuildOrDmId
    -> Id.Id Id.ChannelMessageId
    -> T.FrontendActions ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.Action ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
checkThreadIsCaughtUp guildOrDmId threadId user =
    T.group
        [ user.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.exactText Pages.Guild.newMessagesBadgeText ])
        , user.checkModel
            0
            (\model ->
                withLocalState
                    model
                    (\local ->
                        case ( SeqDict.get ( guildOrDmId, threadId ) local.localUser.user.lastViewedThreadMessage, latestThreadMessageId guildOrDmId threadId local ) of
                            ( lastViewed, Just latest ) ->
                                if lastViewed == Just latest then
                                    Ok ()

                                else
                                    Err "Expected the thread to be caught up with its newest message"

                            ( _, Nothing ) ->
                                Err "Expected the thread to exist"
                    )
            )
        ]


latestChannelMessageId : Id.AnyGuildOrDmId -> LocalState -> Maybe (Id.Id Id.ChannelMessageId)
latestChannelMessageId guildOrDmId local =
    case guildOrDmId of
        Id.GuildOrDmId (Id.GuildOrDmId_Guild id) ->
            LocalState.getGuildAndChannel id local
                |> Maybe.map (\( _, channel ) -> DmChannel.latestFrontendMessageId channel)

        Id.GuildOrDmId (Id.GuildOrDmId_Dm id) ->
            SeqDict.get id.otherUserId local.dmChannels
                |> Maybe.map DmChannel.latestFrontendMessageId

        Id.DiscordGuildOrDmId _ ->
            Nothing


latestThreadMessageId : Id.AnyGuildOrDmId -> Id.Id Id.ChannelMessageId -> LocalState -> Maybe (Id.Id Id.ThreadMessageId)
latestThreadMessageId guildOrDmId threadId local =
    case guildOrDmId of
        Id.GuildOrDmId (Id.GuildOrDmId_Guild id) ->
            LocalState.getGuildAndChannel id local
                |> Maybe.andThen (\( _, channel ) -> SeqDict.get threadId channel.threads)
                |> Maybe.map DmChannel.latestFrontendThreadMessageId

        Id.GuildOrDmId (Id.GuildOrDmId_Dm id) ->
            SeqDict.get id.otherUserId local.dmChannels
                |> Maybe.andThen (\dmChannel -> SeqDict.get threadId dmChannel.threads)
                |> Maybe.map DmChannel.latestFrontendThreadMessageId

        Id.DiscordGuildOrDmId _ ->
            Nothing


withLocalState : FrontendModel -> (LocalState -> Result String ()) -> Result String ()
withLocalState model func =
    case Audio.userModel model of
        Types.Loaded loaded ->
            case loaded.loginStatus of
                Types.LoggedIn loggedIn ->
                    func (Local.model loggedIn.localState)

                Types.NotLoggedIn _ ->
                    Err "Expected the frontend to be logged in"

        Types.Loading _ ->
            Err "Expected the frontend to have finished loading"


markMessageAsUnreadTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
markMessageAsUnreadTest config =
    E2EHelper.startTest
        "Mark a message as unread"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin user ->
                let
                    -- "Two" and "Three" are unread, the message announcing that the user
                    -- joined and "One" are not
                    twoUnread : Test.Html.Selector.Selector
                    twoUnread =
                        Test.Html.Selector.attribute (Html.Attributes.attribute "aria-label" "2")
                in
                [ E2EHelper.writeMessage admin 100 "One"
                , E2EHelper.writeMessage admin 100 "Two"
                , E2EHelper.writeMessage admin 100 "Three"

                -- Leaving the channel marks everything in it as read
                , user.click 100 (Dom.id "guildIcon_showFriends")
                , E2EHelper.hasExactText user [ Pages.Guild.noUnreadMessagesText ]
                , user.checkView 100 (Test.Html.Query.hasNot [ twoUnread ])

                -- Marking the second of the three messages as unread leaves it and the
                -- message after it unread
                , user.click 100 (Dom.id "guild_openGuild_1")
                , markAsUnread user (Id.fromInt 2)
                , user.click 100 (Dom.id "guildIcon_showFriends")
                , user.checkView 100 (Test.Html.Query.has [ twoUnread ])
                , E2EHelper.hasExactText user [ "Two", "Three" ]
                , E2EHelper.hasNotExactText user [ "One" ]

                -- Hovering a message in the overview restarts the animations inside it and
                -- offers to react to it. Editing, replying and the full menu belong to the
                -- channel the message came from, so the menu here leaves them out
                , user.mouseEnter 100 (Dom.id "guild_message_2") ( 10, 10 ) []
                , user.checkView
                    100
                    (Test.Html.Query.has
                        [ Test.Html.Selector.id "miniView_showReactionEmojiSelector"
                        , Test.Html.Selector.id "miniView_emojiReact_0"
                        ]
                    )
                , user.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.id "miniView_showFullMenu" ])
                , user.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.id "miniView_reply" ])
                , user.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.id "miniView_editMessage" ])
                , E2EHelper.tallSnapshot user 100 { name = "Reaction menu on an unread overview message" }

                -- The shortcut reacts with the emoji drawn on it, without leaving the overview
                , user.click 100 (Dom.id "miniView_emojiReact_0")
                , user.checkView
                    100
                    (Test.Html.Query.has [ Test.Html.Selector.id "guild_removeReactionEmoji_0" ])

                -- And the button beside the shortcuts opens the emoji selector over the
                -- overview, for a reaction that isn't one of them
                , user.click 100 (Dom.id "miniView_showReactionEmojiSelector")
                , user.checkView
                    100
                    (Test.Html.Query.has
                        [ Test.Html.Selector.id (Dom.idToString Emoji.searchInputId) ]
                    )
                , user.click 100 (Dom.id "miniView_showReactionEmojiSelector")

                -- Reading the channel for real puts the unread count away again
                , user.click 100 (Dom.id "guild_openGuild_1")
                , user.click 100 (Dom.id "guildIcon_showFriends")
                , E2EHelper.hasExactText user [ Pages.Guild.noUnreadMessagesText ]
                , user.checkView 100 (Test.Html.Query.hasNot [ twoUnread ])
                ]
            )
        ]


markAsUnread :
    T.FrontendActions ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> Id.Id Id.ChannelMessageId
    -> T.Action ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
markAsUnread user messageId =
    T.group
        [ user.mouseEnter 100 (Dom.id ("guild_message_" ++ Id.toString messageId)) ( 10, 10 ) []
        , user.custom
            100
            (Dom.id "miniView_showFullMenu")
            "click"
            (Json.Encode.object
                [ ( "clientX", Json.Encode.int 500 )
                , ( "clientY", Json.Encode.int 300 )
                ]
            )
        , user.click 100 (Dom.id "messageMenu_markAsUnread")
        ]


inactiveThreadsAreHiddenTest : T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2 -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
inactiveThreadsAreHiddenTest config =
    T.start
        "Inactive threads are hidden"
        E2EHelper.startTime
        config
        [ T.connectFrontend
            100
            E2EHelper.sessionId0
            "/"
            E2EHelper.desktopWindow
            (\admin ->
                [ E2EHelper.handleLogin E2EHelper.firefoxDesktop E2EHelper.adminEmail admin
                , E2EHelper.inviteUser
                    admin
                    (\user ->
                        [ E2EHelper.writeMessage user 100 "Hello!"
                        , admin.click 100 (Dom.id "guild_openChannel_0")
                        , E2EHelper.writeMessage admin 100 "Hello from admin!"
                        , E2EHelper.createThread admin (Id.fromInt 0)
                        , E2EHelper.writeMessage admin 100 "Hello from admin in thread!"
                        , user.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.id "guild_viewThread_0_0" ])
                        , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.id "guild_viewThread_0_0" ])
                        , admin.click 100 (Dom.id "guild_openChannel_0")
                        , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.id "guild_viewThread_0_0" ])
                        ]
                    )
                ]
            )
        , T.connectFrontend
            (Duration.days 7.1 |> Duration.inMilliseconds)
            E2EHelper.sessionId0
            "/"
            E2EHelper.desktopWindow
            (\admin ->
                [ T.andThen
                    10
                    (\data -> [ admin.portEvent 10 "load_startup_data_from_js" (E2EHelper.startupDataJson data.time E2EHelper.firefoxDesktop) ])
                , admin.click 100 (Dom.id "guild_openGuild_0")
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.id "guild_viewThread_0_0" ])
                , admin.click 100 (Dom.id "guild_threadStarterIndicator_0")
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.id "guild_viewThread_0_0" ])
                , admin.navigateBack 100
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.id "guild_viewThread_0_0" ])
                , admin.click 100 (Dom.id "guild_threadStarterIndicator_0")
                , E2EHelper.writeMessage admin 100 "Hello again from thread!"
                , admin.navigateBack 100
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.id "guild_viewThread_0_0" ])
                ]
            )
        ]


openLastViewedGuildOnStartupTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
openLastViewedGuildOnStartupTest config =
    let
        loadStartupData : T.FrontendActions ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2 -> T.Action ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
        loadStartupData client =
            T.andThen
                10
                (\data -> [ client.portEvent 10 "load_startup_data_from_js" (E2EHelper.startupDataJson data.time E2EHelper.firefoxDesktop) ])
    in
    T.start
        "Opening the homepage on desktop goes to the session's last viewed guild"
        E2EHelper.startTime
        config
        [ T.connectFrontend
            100
            E2EHelper.sessionId0
            "/"
            E2EHelper.desktopWindow
            (\admin ->
                [ E2EHelper.handleLogin E2EHelper.firefoxDesktop E2EHelper.adminEmail admin
                , admin.checkModel 100 checkHomePageRoute
                , admin.click 100 (Dom.id "guild_openGuild_0")
                ]
            )
        , T.connectFrontend
            100
            E2EHelper.sessionId0
            "/"
            E2EHelper.desktopWindow
            (\admin ->
                [ loadStartupData admin
                , admin.checkModel 100 checkGuild0Route
                ]
            )
        , T.connectFrontend
            100
            E2EHelper.sessionId0
            "/"
            E2EHelper.iphone14Window
            (\admin ->
                [ loadStartupData admin
                , admin.checkModel 100 checkHomePageRoute
                ]
            )
        , T.connectFrontend
            100
            E2EHelper.sessionId1
            "/"
            E2EHelper.desktopWindow
            (\admin ->
                [ E2EHelper.handleLogin E2EHelper.firefoxDesktop E2EHelper.adminEmail admin
                , admin.checkModel 100 checkHomePageRoute
                ]
            )
        ]


checkHomePageRoute : FrontendModel -> Result String ()
checkHomePageRoute model =
    case Audio.userModel model of
        Types.Loaded loaded ->
            case loaded.route of
                Route.HomePageRoute _ ->
                    Ok ()

                _ ->
                    Err "Expected to stay on the homepage"

        Types.Loading _ ->
            Err "Expected the frontend to have finished loading"


checkGuild0Route : FrontendModel -> Result String ()
checkGuild0Route model =
    case Audio.userModel model of
        Types.Loaded loaded ->
            case loaded.route of
                Route.GuildRoute guildId _ _ _ ->
                    if guildId == Id.fromInt 0 then
                        Ok ()

                    else
                        Err "Opened the wrong guild"

                _ ->
                    Err "Expected to be sent to the last viewed guild"

        Types.Loading _ ->
            Err "Expected the frontend to have finished loading"


{-| DM threads carry their own route, are listed underneath the DM in the friends
column and notify with the red count that every unread DM message gets.
-}
dmThreadsTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
dmThreadsTest config =
    E2EHelper.startTest
        "DM threads"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin user ->
                let
                    -- The DM holds one unread message and the thread inside it two, so the
                    -- two notification counts can be told apart. The DM's own icon counts
                    -- both, which makes three.
                    threadNotification : Test.Html.Selector.Selector
                    threadNotification =
                        Test.Html.Selector.attribute (Html.Attributes.attribute "aria-label" "2")

                    dmNotification : Test.Html.Selector.Selector
                    dmNotification =
                        Test.Html.Selector.attribute (Html.Attributes.attribute "aria-label" "3")
                in
                [ -- The user waits on the friends page while the admin writes to them
                  user.click 100 (Dom.id "guildIcon_showFriends")
                , E2EHelper.openDm admin 100 "2"
                , E2EHelper.writeMessage admin 100 "Hello in a DM!"
                , E2EHelper.createThread admin (Id.fromInt 0)
                , E2EHelper.writeMessage admin 100 "First message in the DM thread"
                , E2EHelper.writeMessage admin 100 "Second message in the DM thread"

                -- Opening a thread from a DM message puts the thread on screen
                , E2EHelper.hasText admin [ Pages.Guild.startOfThreadText ]

                -- The admin wrote those two thread messages, so neither is unread for them
                , admin.checkModel 100 (checkDmThreadIsRead (Id.fromInt 2) (Id.fromInt 0))

                -- The thread is listed underneath the DM for both of them. The admin sees
                -- it under the user (id 2) and the user sees it under the admin (id 0).
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.id "guild_viewDmThread_2_0" ])
                , user.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.id "guild_viewDmThread_0_0" ])

                -- Unread thread messages notify the user, who has read none of them
                , user.checkView 100 (Test.Html.Query.has [ threadNotification, dmNotification ])

                -- The unread overview names the DM after the person on the other end of
                -- it, and the thread after the message it hangs off
                , user.checkView
                    100
                    (\html ->
                        Test.Html.Query.find
                            [ Test.Html.Selector.id "guild_unreadOverviewOpenChannel_dm_0" ]
                            html
                            |> Test.Html.Query.has
                                [ Test.Html.Selector.exactText Pages.Guild.chatWithText
                                , Test.Html.Selector.exactText E2EHelper.adminName
                                ]
                    )
                , user.checkView
                    100
                    (\html ->
                        Test.Html.Query.find
                            [ Test.Html.Selector.id "guild_unreadOverviewOpenChannel_dm_0_thread_0" ]
                            html
                            |> Test.Html.Query.has
                                [ Test.Html.Selector.exactText Pages.Guild.chatWithText
                                , Test.Html.Selector.exactText E2EHelper.adminName
                                , Test.Html.Selector.exactText "Hello in a DM!"
                                ]
                    )
                , user.snapshotView 100 { name = "User perspective" }
                , admin.snapshotView 100 { name = "Admin perspective" }

                -- Writing in the thread reaches the other user, and the thread's messages
                -- stay out of the DM itself
                , user.click 100 (Dom.id "guild_viewDmThread_0_0")
                , E2EHelper.hasExactText user [ "First message in the DM thread", "Second message in the DM thread" ]
                , E2EHelper.writeMessage user 100 "Reply from the user"
                , user.checkModel 100 (checkDmThreadIsRead (Id.fromInt 0) (Id.fromInt 0))
                , E2EHelper.hasExactText admin [ "Reply from the user" ]
                , user.click 100 (Dom.id "guild_friendLabel_0")
                , E2EHelper.hasExactText user [ "Hello in a DM!" ]
                , E2EHelper.hasNotExactText user [ "First message in the DM thread", "Reply from the user" ]

                -- Reading the thread took its notification away. The DM's own message is
                -- still unread until the DM itself is opened, so its icon drops from three
                -- to one instead of disappearing.
                , user.checkView 100 (Test.Html.Query.hasNot [ threadNotification, dmNotification ])

                -- The thread is stored on the backend, so loading its url from scratch
                -- shows the messages and lists the thread under the DM again
                , T.connectFrontend
                    100
                    E2EHelper.sessionId1
                    (Route.encode
                        (Route.DmRoute
                            { channelId = DmChannelId.fromUserIds (Id.fromInt 0) (Id.fromInt 2)
                            , threadRoute = Route.ViewThreadWithFriends (Id.fromInt 0) Nothing Route.HideChannelSettings
                            , tab = Nothing
                            , channelsVisible = ChannelsHiddenOnMobile
                            , overlay = Nothing
                            }
                        )
                    )
                    E2EHelper.desktopWindow
                    (\userReload ->
                        [ T.andThen
                            10
                            (\data ->
                                [ userReload.portEvent
                                    10
                                    "load_startup_data_from_js"
                                    (E2EHelper.startupDataJson data.time E2EHelper.firefoxDesktop)
                                ]
                            )
                        , E2EHelper.hasExactText
                            userReload
                            [ "First message in the DM thread", "Reply from the user" ]
                        , userReload.checkView
                            2000
                            (Test.Html.Query.has [ Test.Html.Selector.id "guild_viewDmThread_0_0" ])
                        ]
                    )
                ]
            )
        ]


{-| Just like a guild channel's threads, a DM thread drops out of the friends
column once it's been quiet for a week, and comes back as soon as it's opened
again.
-}
inactiveDmThreadsAreHiddenTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
inactiveDmThreadsAreHiddenTest config =
    E2EHelper.startTest
        "Inactive DM threads are hidden"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.openDm admin 100 "2"
                , E2EHelper.writeMessage admin 100 "Hello in a DM!"
                , E2EHelper.createThread admin (Id.fromInt 0)
                , E2EHelper.writeMessage admin 100 "Hello in the thread!"
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.id "guild_viewDmThread_2_0" ])
                ]
            )
        , T.connectFrontend
            (Duration.days 7.1 |> Duration.inMilliseconds)
            E2EHelper.sessionId0
            "/"
            E2EHelper.desktopWindow
            (\admin ->
                [ T.andThen
                    10
                    (\data ->
                        [ admin.portEvent
                            10
                            "load_startup_data_from_js"
                            (E2EHelper.startupDataJson data.time E2EHelper.firefoxDesktop)
                        ]
                    )
                , admin.click 100 (Dom.id "guildIcon_showFriends")

                -- A week without a message and nothing unread in it, so the thread is gone
                -- from the column
                , admin.checkView
                    2000
                    (Test.Html.Query.hasNot [ Test.Html.Selector.id "guild_viewDmThread_2_0" ])

                -- Opening it from the DM message it hangs off puts it back
                , admin.click 100 (Dom.id "guild_friendLabel_2")
                , admin.click 100 (Dom.id "guild_threadStarterIndicator_0")
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.id "guild_viewDmThread_2_0" ])
                , E2EHelper.hasExactText admin [ "Hello in the thread!" ]
                , admin.snapshotView 100 { name = "Inactive threads are hidden" }
                ]
            )
        ]


{-| Noon on the day `E2EHelper.startTime` falls on. The suggestions a timestamp dropdown makes
are relative to the time the test is running at, so starting at midday leaves room either side
of it for them to land on the same day and read as a time rather than a date.
-}
middayStartTime : Time.Posix
middayStartTime =
    Duration.addTo E2EHelper.startTime (Duration.hours 12)


{-| The minutes of every timestamp in the most recent message the backend has stored.
-}
lastMessageTimestamps : E2EHelper.BackendModel2 -> List Int
lastMessageTimestamps backend =
    case E2EHelper.lastGuildChannelMessage backend of
        Just ( _, _, Message.UserTextMessage data ) ->
            List.Nonempty.toList data.content.content
                |> List.filterMap
                    (\part ->
                        case part of
                            RichText.Timestamp time ->
                                TimeInMinutes.toSeconds time // 60 |> Just

                            _ ->
                                Nothing
                    )

        _ ->
            []


expectLastMessageTimestamps : List Int -> T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
expectLastMessageTimestamps expected data =
    if lastMessageTimestamps data.backend == expected then
        Ok ()

    else
        Err
            ("Expected the stored message to hold timestamps at "
                ++ String.join "," (List.map String.fromInt expected)
                ++ " minutes but it held "
                ++ String.join "," (List.map String.fromInt (lastMessageTimestamps data.backend))
            )


{-| Noon on the 15th of July 2026, read in `E2EHelper.testTimezone`, in minutes since the
epoch. The clocks are forward an hour by then, so it's 11:00 UTC.
-}
summerNoon : Int
summerNoon =
    29735220


{-| Noon on the 15th of December 2026, read in `E2EHelper.testTimezone`. The clocks have gone
back by then, so it's 12:00 UTC and an hour later in the day than `summerNoon` would suggest.
-}
winterNoon : Int
winterNoon =
    29955600


{-| Writing a time of day offers the times a clock shows it at, and the one that's picked is
still a timestamp by the time the backend has it.
-}
timeOfDaySuggestionTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
timeOfDaySuggestionTest config =
    E2EHelper.startTest
        "Writing a time of day suggests timestamps"
        middayStartTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.focusEvent admin 1000 (Just Pages.Guild.channelTextInputId) (Just { start = 0, end = 0 })
                , admin.click 100 Pages.Guild.channelTextInputId
                , admin.input 100 Pages.Guild.channelTextInputId "Meet at 18:00"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 13, end = 13 }
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text MessageDropdown.addTimestampText ])

                -- Suggestions that land on another day read as a date, which is the same text
                -- that picking them writes into the message.
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text "January 2, 1970 at 06:00" ])
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text "January 2, 1970 at 18:00" ])
                , admin.input 100 Pages.Guild.channelTextInputId "Meet at July 15, 2026 at 12:00"

                -- Enter picks a suggestion while the dropdown is open, so it has to be shut
                -- before the message can be sent. Writing out a whole timestamp shuts it,
                -- since the time of day on the end of one is already part of a timestamp.
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 30, end = 30 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.addTimestampText ])
                , admin.keyDown 100 Pages.Guild.channelTextInputId "Enter" []
                , T.checkState 100 (expectLastMessageTimestamps [ summerNoon ])
                , admin.input 100 Pages.Guild.channelTextInputId "Meet at December 15, 2026 at 12:00"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 34, end = 34 }
                , admin.keyDown 100 Pages.Guild.channelTextInputId "Enter" []
                , T.checkState 100 (expectLastMessageTimestamps [ winterNoon ])
                ]
            )
        ]


{-| Writing a time offset suggests times either side of now, and the timestamp that ends up in
the message is still there after the message has been round tripped through an edit.
-}
timeOffsetSuggestionTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
timeOffsetSuggestionTest config =
    E2EHelper.startTest
        "Writing a time offset suggests a timestamp that survives being sent and edited"
        middayStartTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                let
                    withTimestamp : String
                    withTimestamp =
                        "Remind me in January 1, 1970 at 17:00"
                in
                [ E2EHelper.focusEvent admin 1000 (Just Pages.Guild.channelTextInputId) (Just { start = 0, end = 0 })
                , admin.click 100 Pages.Guild.channelTextInputId
                , admin.input 100 Pages.Guild.channelTextInputId "Remind me in 5 hours"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 20, end = 20 }
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text MessageDropdown.addTimestampText ])

                -- Picking a suggestion closes the dropdown. What it writes into the message is
                -- put there by js, which these tests don't run, so the text it would have left
                -- behind is typed in its place.
                , admin.click 100 (Pages.Guild.dropdownButtonId 0)
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.addTimestampText ])
                , admin.input 100 Pages.Guild.channelTextInputId withTimestamp
                , admin.keyDown 100 Pages.Guild.channelTextInputId "Enter" []
                , T.checkState 100 (expectLastMessageTimestamps [ 17 * 60 ])

                -- The message shows the moment rather than the words that were typed.
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text "17:00" ])

                -- Editing turns the message back into text, which is where a timestamp that
                -- can't be read back would be lost.
                , E2EHelper.focusEvent admin 100 (Just Pages.Guild.channelTextInputId) (Just { start = 0, end = 0 })
                , admin.keyDown 100 Pages.Guild.channelTextInputId "ArrowUp" []
                , admin.checkView
                    100
                    (Test.Html.Query.has
                        [ Test.Html.Selector.id "editMessageTextInput"
                        , Test.Html.Selector.attribute (Html.Attributes.value withTimestamp)
                        ]
                    )
                , admin.keyDown 100 (Dom.id "editMessageTextInput") "Enter" []
                , T.checkState 100 (expectLastMessageTimestamps [ 17 * 60 ])
                ]
            )
        ]


{-| The dropdown stays out of the way of text that isn't asking for a timestamp, including the
time of day at the end of a timestamp that's already there.
-}
noTimestampSuggestionTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
noTimestampSuggestionTest config =
    E2EHelper.startTest
        "Text that isn't asking for a timestamp doesn't get one suggested"
        middayStartTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.focusEvent admin 1000 (Just Pages.Guild.channelTextInputId) (Just { start = 0, end = 0 })
                , admin.click 100 Pages.Guild.channelTextInputId
                , admin.input 100 Pages.Guild.channelTextInputId "see you later"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 13, end = 13 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.addTimestampText ])

                -- A unit needs a number in front of it to be an offset.
                , admin.input 100 Pages.Guild.channelTextInputId "later that day"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 14, end = 14 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.addTimestampText ])

                -- The 18:00 on the end of a timestamp is already part of one, so offering to
                -- turn it into another would nest a timestamp inside the one that's there.
                , admin.input 100 Pages.Guild.channelTextInputId "Meet at January 1, 1970 at 18:00"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 32, end = 32 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.addTimestampText ])

                -- Away from the end of the words it reads, there's nothing to replace.
                , admin.input 100 Pages.Guild.channelTextInputId "Remind me in 5 hours and also buy milk"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 38, end = 38 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.addTimestampText ])
                ]
            )
        ]


{-| How many people the most recent message the backend has stored mentions.
-}
lastMessageMentionCount : E2EHelper.BackendModel2 -> Int
lastMessageMentionCount backend =
    case E2EHelper.lastGuildChannelMessage backend of
        Just ( _, _, Message.UserTextMessage data ) ->
            List.Nonempty.toList data.content.content
                |> List.filter
                    (\part ->
                        case part of
                            RichText.UserMention _ ->
                                True

                            _ ->
                                False
                    )
                |> List.length

        _ ->
            0


{-| Writing an @ suggests the people who can be mentioned, and the one that's picked is a
mention rather than their name in text by the time the backend has it.
-}
mentionSuggestionTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
mentionSuggestionTest config =
    E2EHelper.startTest
        "Writing an @ suggests people to mention"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.focusEvent admin 1000 (Just Pages.Guild.channelTextInputId) (Just { start = 0, end = 0 })
                , admin.click 100 Pages.Guild.channelTextInputId

                -- Only the people in the guild whose name starts with what's been written so
                -- far, so the admin's own name isn't among them.
                , admin.input 100 Pages.Guild.channelTextInputId "Hey @S"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 6, end = 6 }
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text MessageDropdown.mentionUserText ])
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.exactText "Stevie Steve" ])

                -- A name nobody in the guild has leaves nothing to suggest.
                , admin.input 100 Pages.Guild.channelTextInputId "Hey @Zz"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 7, end = 7 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.mentionUserText ])

                -- Nor is anything suggested inside a code block or inline code.
                , admin.input 100 Pages.Guild.channelTextInputId "```\nHey @S"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 10, end = 10 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.mentionUserText ])
                , admin.input 100 Pages.Guild.channelTextInputId "```code```\nHey @S"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 17, end = 17 }
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text MessageDropdown.mentionUserText ])
                , admin.input 100 Pages.Guild.channelTextInputId "Hey `x @S"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 9, end = 9 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.mentionUserText ])
                , admin.input 100 Pages.Guild.channelTextInputId "`code` ```code``` @S"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 20, end = 20 }
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text MessageDropdown.mentionUserText ])

                -- Picking a suggestion closes the dropdown. The name it writes into the message
                -- is put there by js, which these tests don't run, so the text it would have
                -- left behind is typed in its place.
                , admin.input 100 Pages.Guild.channelTextInputId "Hey @S"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 6, end = 6 }
                , admin.click 100 (Pages.Guild.dropdownButtonId 0)
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.mentionUserText ])
                , admin.input 100 Pages.Guild.channelTextInputId "Hey @Stevie Steve"
                , admin.keyDown 100 Pages.Guild.channelTextInputId "Enter" []
                , T.checkState
                    100
                    (\data ->
                        if lastMessageMentionCount data.backend == 1 then
                            Ok ()

                        else
                            Err
                                ("Expected the stored message to mention one person but it mentioned "
                                    ++ String.fromInt (lastMessageMentionCount data.backend)
                                )
                    )
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.exactText "@Stevie Steve" ])
                ]
            )
        ]


{-| A mention is never split across lines, so one too long to fit on a phone is cut off
with an ellipsis rather than making the conversation scroll sideways.
-}
longMentionTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
longMentionTest config =
    E2EHelper.startTest
        "A long mention is cut off instead of scrolling sideways"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.iphone14Window
            (\_ user ->
                [ E2EHelper.writeMessageMobile user ("@" ++ E2EHelper.adminName)
                , E2EHelper.writeMessageMobile user ("Hello @" ++ E2EHelper.adminName ++ " how are you?")
                , E2EHelper.writeMessageMobile user "Hello @Stevie Steve how are you? Mentions that fit stay on the line they're written on."
                , user.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.exactText ("@" ++ E2EHelper.adminName) ])
                , user.snapshotView 100 { name = "Long mentions on a phone" }
                , user.click 100 (Dom.id "guild_headerBackButton")
                , user.snapshotView 100 { name = "Selected guild sidebar on a phone" }
                ]
            )
        ]


{-| Writing a colon and enough of a name to narrow it down suggests emoji to add.
-}
emojiSuggestionTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
emojiSuggestionTest config =
    E2EHelper.startTest
        "Writing a colon suggests emoji"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.focusEvent admin 1000 (Just Pages.Guild.channelTextInputId) (Just { start = 0, end = 0 })
                , admin.click 100 Pages.Guild.channelTextInputId

                -- One character matches too much of the emoji list to be worth showing.
                , admin.input 100 Pages.Guild.channelTextInputId "Party :t"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 8, end = 8 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.addStickerOrEmojiText ])
                , admin.input 100 Pages.Guild.channelTextInputId "Party :tada"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 11, end = 11 }
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text MessageDropdown.addStickerOrEmojiText ])
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.exactText ":tada:" ])
                , admin.input 100 Pages.Guild.channelTextInputId "```\nParty :tada"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 15, end = 15 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.addStickerOrEmojiText ])
                , admin.input 100 Pages.Guild.channelTextInputId "Party `x :tada"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 14, end = 14 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.addStickerOrEmojiText ])
                , admin.input 100 Pages.Guild.channelTextInputId "Party :tada"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 11, end = 11 }

                -- Picking a suggestion closes the dropdown. As with a mention, what it writes
                -- into the message is put there by js, so the emoji it would have left behind
                -- is typed in its place.
                , admin.click 100 (Pages.Guild.dropdownButtonId 0)
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.addStickerOrEmojiText ])
                , admin.input 100 Pages.Guild.channelTextInputId "Party 🎉"
                , admin.keyDown 100 Pages.Guild.channelTextInputId "Enter" []
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text "🎉" ])
                ]
            )
        ]


{-| Writing a # suggests the guild's channels, and the one that's picked links to that channel
once sent.
-}
channelSuggestionTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
channelSuggestionTest config =
    E2EHelper.startTest
        "Writing a # suggests channels"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.focusEvent admin 1000 (Just Pages.Guild.channelTextInputId) (Just { start = 0, end = 0 })
                , admin.click 100 Pages.Guild.channelTextInputId
                , admin.input 100 Pages.Guild.channelTextInputId "See #gen"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 8, end = 8 }
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text MessageDropdown.mentionChannelText ])
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.exactText "general" ])
                , admin.input 100 Pages.Guild.channelTextInputId "See #zz"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 7, end = 7 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.mentionChannelText ])
                , admin.input 100 Pages.Guild.channelTextInputId "```\nSee #gen"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 12, end = 12 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.mentionChannelText ])
                , admin.input 100 Pages.Guild.channelTextInputId "See `x #gen"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 11, end = 11 }
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.mentionChannelText ])
                , admin.input 100 Pages.Guild.channelTextInputId "See #gen"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 8, end = 8 }
                , admin.click 100 (Pages.Guild.dropdownButtonId 0)
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.text MessageDropdown.mentionChannelText ])
                , admin.input 100 Pages.Guild.channelTextInputId "See #general"
                , admin.keyDown 100 Pages.Guild.channelTextInputId "Enter" []
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.tag "a", Test.Html.Selector.exactText "#general" ])
                ]
            )
        ]


{-| Checks what's been written into the channel message input so far.
-}
checkDraft : Maybe String -> FrontendModel -> Result String ()
checkDraft expected model =
    let
        draft : Maybe String
        draft =
            case Audio.userModel model of
                Types.Loaded loaded ->
                    case loaded.loginStatus of
                        Types.LoggedIn loggedIn ->
                            SeqDict.values loggedIn.drafts
                                |> List.head
                                |> Maybe.map String.Nonempty.toString

                        Types.NotLoggedIn _ ->
                            Nothing

                Types.Loading _ ->
                    Nothing
    in
    if draft == expected then
        Ok ()

    else
        Err
            ("Expected the message input to contain "
                ++ Maybe.withDefault "nothing" expected
                ++ " but it contained "
                ++ Maybe.withDefault "nothing" draft
            )


{-| While the cursor is inside a \`\`\` code block, enter writes a line break instead of sending the
message and tab writes two spaces instead of moving the focus.
-}
codeBlockInputTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
codeBlockInputTest config =
    E2EHelper.startTest
        "Enter and tab behave differently inside a code block"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.focusEvent admin 1000 (Just Pages.Guild.channelTextInputId) (Just { start = 0, end = 0 })
                , admin.click 100 Pages.Guild.channelTextInputId

                -- The code block hasn't been closed yet so enter is left to the textarea to
                -- handle (which writes a line break) rather than sending the message.
                , admin.input 100 Pages.Guild.channelTextInputId "```"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 3, end = 3 }
                , admin.keyDown 100 Pages.Guild.channelTextInputId "Enter" []
                , admin.checkModel 100 (checkDraft (Just "```"))

                -- Tab writes two spaces. Normally js is what puts them in the text input but
                -- these tests don't run js, so only the model is checked here.
                , admin.keyDown 100 Pages.Guild.channelTextInputId "Tab" []
                , admin.checkModel 100 (checkDraft (Just "```  "))

                -- Once the code block is closed, the cursor is outside of it again and enter
                -- sends the message.
                , admin.input 100 Pages.Guild.channelTextInputId "```\nlet x = 1\n```"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 17, end = 17 }
                , admin.keyDown 100 Pages.Guild.channelTextInputId "Enter" []
                , admin.checkModel 100 (checkDraft Nothing)
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text "let x = 1" ])

                -- Tab is only special inside a code block. Everywhere else it's left alone so
                -- that it still moves the focus.
                , admin.input 100 Pages.Guild.channelTextInputId "no code block"
                , E2EHelper.selectionEvent admin 100 Pages.Guild.channelTextInputId { start = 13, end = 13 }
                , admin.keyDown 100 Pages.Guild.channelTextInputId "Tab" []
                , admin.checkModel 100 (checkDraft (Just "no code block"))
                ]
            )
        ]


{-| A code block naming a language comes out highlighted, and the button in its corner
copies the code without the \`\`\` or the language name around it.
-}
codeBlockCopyButtonTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
codeBlockCopyButtonTest config =
    E2EHelper.startTest
        "Pressing a code block's copy button copies its code"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin _ ->
                [ E2EHelper.writeMessage admin 1000 "```elm\nx = 1\n```"
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.class "elmsh1", Test.Html.Selector.text "1" ])
                , admin.click 100 (Dom.id "spoiler_1_copyCode_0")
                , T.checkState
                    100
                    (\data ->
                        case E2EHelper.copiedText admin.clientId data of
                            Just "x = 1\n" ->
                                Ok ()

                            Just copied ->
                                Err ("Expected the code to be copied but got " ++ copied)

                            Nothing ->
                                Err "Clipboard text not found"
                    )
                ]
            )
        ]


{-| A call or a game leaves a card behind in the conversation it was started from. It is
the starter's own doing, the same as a message they wrote, so it doesn't leave them with
something unread or with the card sitting under an unread divider.
-}
startingACallOrGameStaysReadTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
startingACallOrGameStaysReadTest config =
    E2EHelper.startTest
        "Starting a call or a game doesn't leave the starter with something unread"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin user ->
                [ -- The admin is caught up in the channel both of them are looking at
                  E2EHelper.writeMessage user 100 "In the channel"
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.exactText Pages.Guild.newMessagesBadgeText ])

                -- Starting a call from it leaves them caught up on the card it wrote
                , admin.click 100 (Dom.id "guild_voiceChat")
                , E2EVoiceChat.startCall admin
                , admin.navigateBack 100
                , admin.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text Pages.Guild.startedACallText ])
                , admin.checkModel 100 (checkChannelIsCaughtUpModel guildChannelId)
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.exactText Pages.Guild.newMessagesBadgeText ])

                -- And so does starting a game
                , admin.click 100 (Dom.id "guild_openGamesTab")
                , admin.click 100 (Dom.id "game_select_Go (Baduk)")
                , admin.click 100 (Dom.id "go_start")
                , admin.navigateBack 100
                , admin.checkModel 100 (checkChannelIsCaughtUpModel guildChannelId)
                , admin.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.exactText Pages.Guild.newMessagesBadgeText ])
                , E2EHelper.tallSnapshot admin 100 { name = "Started a call and a game without either turning up unread" }
                ]
            )
        ]


{-| The reader has seen every message in the channel, which is what the notification counts
and the unread overview are worked out from.
-}
checkChannelIsCaughtUpModel : Id.AnyGuildOrDmId -> FrontendModel -> Result String ()
checkChannelIsCaughtUpModel guildOrDmId model =
    withLocalState
        model
        (\local ->
            case ( SeqDict.get guildOrDmId local.localUser.user.lastViewedMessage, latestChannelMessageId guildOrDmId local ) of
                ( Just lastViewed, Just latest ) ->
                    if lastViewed == latest then
                        Ok ()

                    else
                        Err
                            ("Expected the channel to be caught up at "
                                ++ Id.toString latest
                                ++ " but the last viewed message is "
                                ++ Id.toString lastViewed
                            )

                _ ->
                    Err "Expected the channel to exist and to have been viewed"
        )


leaveGuildTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
leaveGuildTest config =
    E2EHelper.startTest
        "A member leaves a guild from the guild settings page"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin user ->
                let
                    guildId : Id.Id Id.GuildId
                    guildId =
                        Id.fromInt 1
                in
                [ -- The owner can delete the guild but has no way to leave it
                  admin.click 100 (Dom.id "guild_inviteLinkCreatorRoute")
                , E2EHelper.hasExactText admin [ Pages.Guild.deleteGuildText ]
                , E2EHelper.hasNotExactText admin [ Pages.Guild.leaveGuildText ]

                -- A member gets the leave button instead
                , user.click 100 (Dom.id "guild_inviteLinkCreatorRoute")
                , E2EHelper.hasExactText user [ Pages.Guild.leaveGuildText ]
                , E2EHelper.hasNotExactText user [ Pages.Guild.deleteGuildText ]
                , user.snapshotView 100 { name = "Guild settings for a member who isn't the owner" }

                -- The first press only asks for confirmation
                , user.click 100 (Dom.id "guild_leaveGuild")
                , E2EHelper.hasExactText user [ Pages.Guild.confirmLeaveGuildText ]
                , T.checkBackend 100 (checkGuildMemberCount guildId 1)

                -- The second press leaves the guild
                , user.click 100 (Dom.id "guild_leaveGuild")
                , T.checkBackend 100 (checkGuildMemberCount guildId 0)
                , E2EHelper.hasNotExactText user [ "My new guild!" ]
                , user.checkModel
                    100
                    (\model ->
                        withLocalState
                            model
                            (\local ->
                                if SeqDict.member guildId local.guilds then
                                    Err "The guild should be gone from the frontend of the user who left"

                                else
                                    Ok ()
                            )
                    )

                -- The owner sees the member disappear without losing the guild
                , admin.checkModel
                    100
                    (\model ->
                        withLocalState
                            model
                            (\local ->
                                case SeqDict.get guildId local.guilds of
                                    Just guild ->
                                        if SeqDict.isEmpty (MembersAndOwner.members guild.membersAndOwner) then
                                            Ok ()

                                        else
                                            Err "The owner should no longer see the member who left"

                                    Nothing ->
                                        Err "The owner should still be in the guild"
                            )
                    )
                ]
            )
        ]


banMemberTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
banMemberTest config =
    E2EHelper.startTest
        "The guild owner bans a member from the member table in the guild settings"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.desktopWindow
            (\admin user ->
                let
                    guildId : Id.Id Id.GuildId
                    guildId =
                        Id.fromInt 1

                    userId : Id.Id Id.UserId
                    userId =
                        Id.fromInt 2
                in
                [ -- The member hasn't written anything yet, so the table says so
                  admin.click 100 (Dom.id "guild_inviteLinkCreatorRoute")
                , E2EHelper.hasExactText
                    admin
                    [ Pages.Guild.guildMembersText
                    , E2EHelper.userName
                    , Pages.Guild.neverPostedText
                    , Pages.Guild.banMemberText
                    ]

                -- Once they write something the last posted column fills in
                , E2EHelper.writeMessage user 100 "Hello everyone"
                , E2EHelper.hasNotExactText admin [ Pages.Guild.neverPostedText ]
                , admin.snapshotView 100 { name = "Guild settings member table" }

                -- A member who isn't the owner doesn't get the table at all
                , user.click 100 (Dom.id "guild_inviteLinkCreatorRoute")
                , E2EHelper.hasNotExactText user [ Pages.Guild.guildMembersText, Pages.Guild.banMemberText ]

                -- Banning drops them from the guild and remembers them
                , admin.click 100 (Dom.id ("guild_banMember_" ++ Id.toString userId))
                , T.checkBackend 100 (checkGuildMemberCount guildId 0)
                , T.checkBackend 100 (checkUserIsBanned guildId userId)
                , E2EHelper.hasNotExactText admin [ E2EHelper.userName, Pages.Guild.banMemberText ]
                , user.checkModel
                    100
                    (\model ->
                        withLocalState
                            model
                            (\local ->
                                if SeqDict.member guildId local.guilds then
                                    Err "The guild should be gone from the banned member's frontend"

                                else
                                    Ok ()
                            )
                    )

                -- And the invite link they still have won't let them back in
                , admin.click 100 (Dom.id "guild_inviteLinkCopy_copy")
                , T.andThen
                    100
                    (\data ->
                        case E2EHelper.copiedText admin.clientId data of
                            Just inviteLink ->
                                [ T.connectFrontend
                                    100
                                    E2EHelper.sessionId1
                                    (String.replace Env.domain "" inviteLink)
                                    E2EHelper.desktopWindow
                                    (\bannedUser ->
                                        [ T.andThen
                                            10
                                            (\data2 ->
                                                [ bannedUser.portEvent
                                                    10
                                                    "load_startup_data_from_js"
                                                    (E2EHelper.startupDataJson data2.time E2EHelper.firefoxDesktop)
                                                ]
                                            )
                                        , T.checkBackend 100 (checkGuildMemberCount guildId 0)
                                        ]
                                    )
                                ]

                            Nothing ->
                                [ T.checkState 0 (\_ -> Err "Clipboard text not found") ]
                    )
                ]
            )
        ]


checkUserIsBanned : Id.Id Id.GuildId -> Id.Id Id.UserId -> E2EHelper.BackendModel2 -> Result String ()
checkUserIsBanned guildId userId backend =
    case SeqDict.get guildId (E2EHelper.unwrapBackend backend).guilds of
        Just guild ->
            if SeqSet.member userId guild.bannedUsers then
                Ok ()

            else
                Err "Expected the banned member to be on the guild's ban list"

        Nothing ->
            Err "The guild should still exist after a member is banned"


checkGuildMemberCount : Id.Id Id.GuildId -> Int -> E2EHelper.BackendModel2 -> Result String ()
checkGuildMemberCount guildId expected backend =
    case SeqDict.get guildId (E2EHelper.unwrapBackend backend).guilds of
        Just guild ->
            let
                count : Int
                count =
                    SeqDict.size (MembersAndOwner.members guild.membersAndOwner)
            in
            if count == expected then
                Ok ()

            else
                Err
                    ("Expected the guild to have "
                        ++ String.fromInt expected
                        ++ " members besides the owner but it has "
                        ++ String.fromInt count
                    )

        Nothing ->
            Err "The guild should still exist after a member leaves"


{-| The colour picker in the user options. The preview message is drawn on with the colour
that's selected, and nothing is saved until the submit button that turns up alongside it is
pressed.
-}
colorPickerTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
colorPickerTest config =
    E2EHelper.startTest
        "Pick a user colour"
        E2EHelper.startTime
        config
        [ T.connectFrontend
            100
            E2EHelper.sessionId0
            "/"
            E2EHelper.tallDesktopWindow
            (\admin ->
                [ E2EHelper.handleLogin E2EHelper.firefoxDesktop E2EHelper.adminEmail admin
                , admin.click 1000 (Dom.id "guild_showUserOptions")

                -- The grid is a lot of squares, so it stays put away until asked for, with
                -- the colour they already have shown beside the button.
                , admin.checkView
                    100
                    (Test.Html.Query.has
                        [ Test.Html.Selector.id "userOptions_selectColor"
                        , Test.Html.Selector.id "userOptions_currentColor"
                        ]
                    )
                , admin.checkView
                    100
                    (Test.Html.Query.hasNot [ Test.Html.Selector.id "userColor_lightness" ])
                , admin.click 100 (Dom.id "userOptions_selectColor")

                -- Out come the picker and an example message scrawled on in whatever is
                -- selected.
                , admin.checkView
                    100
                    (Test.Html.Query.has
                        [ Test.Html.Selector.id "userColor_lightness"
                        , Test.Html.Selector.text "Hello"
                        ]
                    )
                , admin.checkView 100 (hasStrokeColored RichText.defaultColor)

                -- Nothing to submit until the colour actually changes.
                , admin.checkView
                    100
                    (Test.Html.Query.hasNot [ Test.Html.Selector.id "userOptions_submitColor" ])

                -- Turning the brightness right down leaves the chosen square too dark to be
                -- used, so the preview holds onto the last colour that could be and there's
                -- still nothing to submit.
                , admin.input 100 (Dom.id "userColor_lightness") "3"
                , admin.checkView 100 (hasStrokeColored RichText.defaultColor)
                , admin.checkView
                    100
                    (Test.Html.Query.hasNot [ Test.Html.Selector.id "userOptions_submitColor" ])

                -- Back to a brightness that works and the colour moves again.
                , admin.input 100 (Dom.id "userColor_lightness") "10"
                , admin.checkView
                    100
                    (Test.Html.Query.has [ Test.Html.Selector.id "userOptions_submitColor" ])
                , admin.checkView 100 (Test.Html.Query.hasNot [ hasStrokeSelector RichText.defaultColor ])

                -- Resetting puts the grid away without having saved anything.
                , admin.click 100 (Dom.id "userOptions_resetColor")
                , admin.checkView
                    100
                    (Test.Html.Query.hasNot [ Test.Html.Selector.id "userColor_lightness" ])
                , T.checkState 100 (checkSavedColorIs RichText.defaultColor)

                -- Submitting saves it and puts the grid away too.
                , admin.click 100 (Dom.id "userOptions_selectColor")
                , admin.input 100 (Dom.id "userColor_lightness") "10"
                , admin.click 100 (Dom.id "userOptions_submitColor")
                , admin.checkView
                    100
                    (Test.Html.Query.hasNot [ Test.Html.Selector.id "userColor_lightness" ])
                , T.checkState 100 (checkSavedColorIsNot RichText.defaultColor)
                ]
            )
        ]


{-| Deleting an account only schedules it, so the same button cancels it again, and until then a
banner counts down the time left and leads back to the button. Once the time is up, the next
hourly update resets the account, renames it, replaces everything it wrote with deleted
messages and unlinks its Discord account. Admins can't delete their account.
-}
deleteAccountTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> String
    -> String
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
deleteAccountTest config discordOp0Ready discordOp0ReadySupplemental =
    T.start
        "Delete account"
        E2EHelper.startTime
        config
        [ T.connectFrontend
            100
            E2EHelper.sessionId0
            "/"
            E2EHelper.tallDesktopWindow
            (\admin ->
                [ E2EHelper.handleLogin E2EHelper.firefoxDesktop E2EHelper.adminEmail admin
                , admin.click 1000 (Dom.id "guild_showUserOptions")
                , admin.click 100 UserOptions.deleteAccountButtonId
                , T.checkState 100 (checkAccountDeletion E2EHelper.adminEmail False)
                , admin.click 100 (Dom.id "userOptions_closeUserOptions")
                , E2EHelper.inviteUser
                    admin
                    (\user ->
                        [ user.click 1000 (Dom.id "guild_openChannel_0")
                        , E2EHelper.writeMessage user 100 "In the guild"
                        , admin.click 100 (Dom.id "guild_openChannel_0")
                        , E2EHelper.writeMessage admin 100 "From the admin"
                        , E2EHelper.openDm user 100 "0"
                        , E2EHelper.writeMessage user 100 "In a DM"
                        , E2EHelper.createThread user (Id.fromInt 0)
                        , E2EHelper.writeMessage user 100 "In a thread"
                        , user.click 100 (Dom.id "guild_showUserOptions")
                        , user.click 100 UserOptions.deleteAccountButtonId
                        , T.checkState 100 (checkAccountDeletion E2EHelper.userEmail True)
                        , T.checkState 100 (checkAccountDeletionEmails 1)
                        , user.checkView 100 (Test.Html.Query.has [ Test.Html.Selector.text UserOptions.cancelAccountDeletionText ])

                        -- The banner leads to the user options, so it stays out of the way while they're open.
                        , user.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.id "accountDeletionBanner" ])
                        , user.click 100 (Dom.id "userOptions_settings")
                        , user.click 100 (Dom.id "userOptions_closeUserOptions")
                        , user.checkView
                            100
                            (\html ->
                                Test.Html.Query.find [ Test.Html.Selector.id "accountDeletionBanner" ] html
                                    |> Test.Html.Query.has [ Test.Html.Selector.containing [ Test.Html.Selector.text "14\u{00A0}days" ] ]
                            )

                        -- Pressing the banner opens the account settings, even though they were collapsed.
                        , user.click 100 (Dom.id "accountDeletionBanner")
                        , user.click 100 UserOptions.deleteAccountButtonId
                        , T.checkState 100 (checkAccountDeletion E2EHelper.userEmail False)
                        , user.click 100 (Dom.id "userOptions_closeUserOptions")
                        , user.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.id "accountDeletionBanner" ])

                        -- Once closed, the banner stays closed. Waiting an hour first makes the deletion
                        -- land on an hourly update that doesn't also start a backup export, as the two
                        -- take different paths through HourlyUpdate.
                        , user.click (Duration.hour |> Duration.inMilliseconds) (Dom.id "guild_showUserOptions")
                        , user.click 100 UserOptions.deleteAccountButtonId
                        , T.checkState 100 (checkAccountDeletionEmails 2)
                        , user.click 100 (Dom.id "userOptions_closeUserOptions")
                        , user.click 100 (Dom.id "accountDeletionBanner_close")
                        , user.checkView 100 (Test.Html.Query.hasNot [ Test.Html.Selector.id "accountDeletionBanner" ])
                        , E2EHelper.linkSecondDiscordAccount E2EHelper.sessionId1 discordOp0Ready discordOp0ReadySupplemental
                        , T.checkState 1000 (checkDiscordAccountIsLinked True)
                        ]
                    )
                ]
            )

        -- Nothing happens until the two weeks are up. The frontends are gone by now so that
        -- simulating two weeks of their timers doesn't slow the test down, which is also why
        -- this test uses T.start instead of E2EHelper.startTest and its attacker frontend.
        , T.andThen
            0
            (\data ->
                case userIdByEmail E2EHelper.userEmail data of
                    Just ( userId, _ ) ->
                        [ T.checkState (Duration.days 14 |> Quantity.minus Duration.hour |> Duration.inMilliseconds) (checkAccountIsStillScheduled userId)
                        , T.checkState 0 (checkDeletedMessages userId 0)
                        , T.checkState 0 (checkDiscordAccountIsLinked True)
                        , T.checkState (Duration.hours 2 |> Duration.inMilliseconds) (checkAccountWasDeleted userId)
                        , T.checkState 0 (checkDeletedMessages userId 3)
                        , T.checkState 0 (checkDiscordAccountIsLinked False)
                        ]

                    Nothing ->
                        [ T.checkState 0 (\_ -> Err "Expected the user to exist on the backend") ]
            )
        ]


{-| A linked Discord account has its gateway open, and deleting the at-chat account it's linked to
turns it back into basic data and closes the gateway.
-}
checkDiscordAccountIsLinked : Bool -> T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
checkDiscordAccountIsLinked isLinked data =
    case
        ( SeqDict.get E2EHelper.secondDiscordUserId (E2EHelper.unwrapBackend data.backend).discordUsers
        , E2EHelper.websocketByDiscordToken E2EHelper.secondDiscordToken data
        , isLinked
        )
    of
        ( Just (DiscordUserData.FullData _), Just _, True ) ->
            Ok ()

        ( Just (DiscordUserData.BasicData _), Nothing, False ) ->
            Ok ()

        ( Nothing, _, _ ) ->
            Err "Expected the Discord account to exist"

        _ ->
            if isLinked then
                Err "The Discord account should be linked with its gateway open"

            else
                Err "The Discord account should have been unlinked and its gateway closed"


userIdByEmail : EmailAddress -> T.Data FrontendModel E2EHelper.BackendModel2 -> Maybe ( Id.Id Id.UserId, User.BackendUser )
userIdByEmail email state =
    NonemptyDict.toList (E2EHelper.unwrapBackend state.backend).users
        |> List.filter (\( _, user ) -> user.email == User.UserHasEmail email)
        |> List.head


checkAccountDeletion : EmailAddress -> Bool -> T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
checkAccountDeletion email isScheduled state =
    case ( userIdByEmail email state |> Maybe.map (\( _, user ) -> user.deleteAccountAt), isScheduled ) of
        ( Just (Just deleteAt), True ) ->
            let
                weeksLeft : Float
                weeksLeft =
                    Duration.from state.time deleteAt |> Duration.inWeeks
            in
            if weeksLeft > 1.99 && weeksLeft <= 2 then
                Ok ()

            else
                Err "The account should be deleted 2 weeks after it was asked for"

        ( Just Nothing, False ) ->
            Ok ()

        ( Just (Just _), False ) ->
            Err "The account shouldn't be scheduled for deletion"

        ( Just Nothing, True ) ->
            Err "The account should be scheduled for deletion"

        ( Nothing, _ ) ->
            Err "Expected the user to exist on the backend"


checkAccountIsStillScheduled : Id.Id Id.UserId -> T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
checkAccountIsStillScheduled userId state =
    case NonemptyDict.get userId (E2EHelper.unwrapBackend state.backend).users of
        Just user ->
            if user.deleteAccountAt == Nothing then
                Err "The account should still be scheduled for deletion"

            else
                Ok ()

        Nothing ->
            Err "Expected the user to exist on the backend"


checkAccountWasDeleted : Id.Id Id.UserId -> T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
checkAccountWasDeleted userId state =
    let
        backend : Types.BackendModel
        backend =
            E2EHelper.unwrapBackend state.backend
    in
    case NonemptyDict.get userId backend.users of
        Just user ->
            if PersonName.toString user.name /= "<delete_user_" ++ Id.toString userId ++ ">" then
                Err ("The deleted account should have been renamed but is called " ++ PersonName.toString user.name)

            else if user.email /= User.DeletedUser then
                Err "The deleted account shouldn't have an email address"

            else if user.deleteAccountAt /= Nothing then
                Err "The deleted account shouldn't be scheduled for deletion again"

            else if List.any (\session -> session.userId == userId) (SeqDict.values backend.sessions) then
                Err "The deleted account shouldn't have any sessions left"

            else
                Ok ()

        Nothing ->
            Err "Expected the user to exist on the backend"


{-| Counts the deleted messages in every guild and DM, and checks nothing the invited user wrote
is left while the admin's message still is.
-}
checkDeletedMessages : Id.Id Id.UserId -> Int -> T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
checkDeletedMessages userId expectedDeleted state =
    let
        backend : Types.BackendModel
        backend =
            E2EHelper.unwrapBackend state.backend

        allMessages : List ( Maybe (Id.Id Id.UserId), Bool )
        allMessages =
            List.concatMap
                (\guild -> List.concatMap messagesInChannel (SeqDict.values guild.channels))
                (SeqDict.values backend.guilds)
                ++ List.concatMap messagesInChannel (SeqDict.values backend.dmChannels)

        deletedCount : Int
        deletedCount =
            List.filter Tuple.second allMessages |> List.length

        authors : List (Id.Id Id.UserId)
        authors =
            List.filterMap Tuple.first allMessages
    in
    if deletedCount /= expectedDeleted then
        Err ("Expected " ++ String.fromInt expectedDeleted ++ " deleted messages but got " ++ String.fromInt deletedCount)

    else if expectedDeleted > 0 && List.member userId authors then
        Err "A message written by the deleted account is left"

    else if not (List.member Broadcast.adminUserId authors) then
        Err "The admin's message should be left alone"

    else
        Ok ()


messagesInChannel :
    { a
        | messages : IdArray.IdArray Id.ChannelMessageId (Message.Message Id.ChannelMessageId (Id.Id Id.UserId) (Id.Id Id.ChannelId))
        , threads : SeqDict.SeqDict (Id.Id Id.ChannelMessageId) { b | messages : IdArray.IdArray Id.ThreadMessageId (Message.Message Id.ThreadMessageId (Id.Id Id.UserId) (Id.Id Id.ChannelId)) }
    }
    -> List ( Maybe (Id.Id Id.UserId), Bool )
messagesInChannel channel =
    List.map authorAndIsDeleted (IdArray.toList channel.messages)
        ++ List.concatMap
            (\thread -> List.map authorAndIsDeleted (IdArray.toList thread.messages))
            (SeqDict.values channel.threads)


authorAndIsDeleted : Message.Message messageId (Id.Id Id.UserId) channelId -> ( Maybe (Id.Id Id.UserId), Bool )
authorAndIsDeleted message =
    case message of
        Message.UserTextMessage data ->
            ( Just data.createdBy, False )

        Message.EncryptedUserTextMessage data ->
            ( Just data.createdBy, False )

        Message.DeletedMessage _ ->
            ( Nothing, True )

        Message.UserJoinedMessage _ _ _ _ ->
            ( Nothing, False )

        Message.CallStarted _ ->
            ( Nothing, False )

        Message.GameStarted _ ->
            ( Nothing, False )


checkAccountDeletionEmails : Int -> T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
checkAccountDeletionEmails expected state =
    let
        count : Int
        count =
            List.filterMap (E2EHelper.isAccountDeletionEmail E2EHelper.userEmail) state.httpRequests
                |> List.length
    in
    if count == expected then
        Ok ()

    else
        Err ("Expected " ++ String.fromInt expected ++ " account deletion emails but got " ++ String.fromInt count)


hasStrokeSelector : UserColor.UserColor -> Test.Html.Selector.Selector
hasStrokeSelector color =
    Test.Html.Selector.attribute
        (Html.Attributes.attribute "stroke" (UserColor.toStyle color))


hasStrokeColored : UserColor.UserColor -> Test.Html.Query.Single msg -> Expect.Expectation
hasStrokeColored color view =
    Test.Html.Query.findAll [ hasStrokeSelector color ] view
        |> Test.Html.Query.count (Expect.greaterThan 0)


checkSavedColorIs : UserColor.UserColor -> T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
checkSavedColorIs expected state =
    if savedColor state == Just expected then
        Ok ()

    else
        Err "The colour the backend has saved isn't the one it should be"


checkSavedColorIsNot : UserColor.UserColor -> T.Data FrontendModel E2EHelper.BackendModel2 -> Result String ()
checkSavedColorIsNot unexpected state =
    case savedColor state of
        Just color ->
            if color == unexpected then
                Err "The backend is still holding the colour the user started with"

            else
                Ok ()

        Nothing ->
            Err "Expected the admin to exist on the backend"


savedColor : T.Data FrontendModel E2EHelper.BackendModel2 -> Maybe UserColor.UserColor
savedColor state =
    NonemptyDict.get Broadcast.adminUserId (E2EHelper.unwrapBackend state.backend).users
        |> Maybe.map .color


{-| Touching a text input on mobile mustn't start a drag, because starting one hides the
virtual keyboard so that the sidebar isn't dragged out from behind it. Which touches count
used to be decided from a list holding the two message textareas, so the private key box
wasn't one of them and lost the keyboard the moment it was touched, which is what stopped a
key from being pasted into it on iOS.
-}
touchingTextInputDoesntStartDragTest :
    T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
    -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
touchingTextInputDoesntStartDragTest config =
    E2EHelper.startTest
        "Touching a text input on mobile doesn't start a drag"
        E2EHelper.startTime
        config
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            E2EHelper.iphone14Window
            (\_ user ->
                [ -- The private key box is an input rather than one of the message
                  -- textareas, and counts all the same.
                  user.custom 100 (Dom.id "elm-ui-root-id") "touchstart" (touchStartOnTag "INPUT")
                , user.checkModel 100 checkNoDragStarted
                , user.custom 100 (Dom.id "elm-ui-root-id") "touchstart" (touchStartOnTag "TEXTAREA")
                , user.checkModel 100 checkNoDragStarted

                -- Touching anything else still starts one, which is what drags the channel
                -- sidebar.
                , user.custom 100 (Dom.id "elm-ui-root-id") "touchstart" (touchStartOnTag "DIV")
                , user.checkModel 100 checkDragStarted
                ]
            )
        ]


{-| One touch that landed on an element with the given tag name, reported the way the
mobile frontend decodes touch events.
-}
touchStartOnTag : String -> Json.Encode.Value
touchStartOnTag tagName =
    Json.Encode.object
        [ ( "timeStamp", Json.Encode.float 1000 )
        , ( "touches"
          , Json.Encode.object
                [ ( "length", Json.Encode.int 1 )
                , ( "0"
                  , Json.Encode.object
                        [ ( "identifier", Json.Encode.int 0 )
                        , ( "clientX", Json.Encode.float 200 )
                        , ( "clientY", Json.Encode.float 700 )
                        , ( "target", Json.Encode.object [ ( "tagName", Json.Encode.string tagName ) ] )
                        ]
                  )
                ]
          )
        ]


checkNoDragStarted : FrontendModel -> Result String ()
checkNoDragStarted model =
    withDrag
        model
        (\drag ->
            case drag of
                Touch.NoDrag ->
                    Ok ()

                _ ->
                    Err "Touching a text input started a drag, which hides the virtual keyboard"
        )


checkDragStarted : FrontendModel -> Result String ()
checkDragStarted model =
    withDrag
        model
        (\drag ->
            case drag of
                Touch.NoDrag ->
                    Err "Touching something that isn't a text input should still start a drag"

                _ ->
                    Ok ()
        )


withDrag : FrontendModel -> (Touch.Drag -> Result String ()) -> Result String ()
withDrag model func =
    case Audio.userModel model of
        Types.Loaded loaded ->
            func loaded.drag

        Types.Loading _ ->
            Err "Expected the frontend to have finished loading"


richTextMessage : Bool -> T.Config ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2 -> T.EndToEndTest ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
richTextMessage isMobile normalConfig =
    E2EHelper.startTest
        ("Message with bullet points and rich text formatting"
            ++ (if isMobile then
                    " (mobile)"

                else
                    ""
               )
        )
        E2EHelper.startTime
        normalConfig
        [ E2EHelper.connectTwoUsersAndJoinNewGuild
            (if isMobile then
                E2EHelper.iphone14Window

             else
                E2EHelper.desktopWindow
            )
            (\admin _ ->
                let
                    -- Uses bullet points along with various other rich text formatting.
                    messageText : String
                    messageText =
                        "This line has *bold*, _italic_, __underline__, ~~strikethrough~~, ||spoiler|| and `inline code`.\n* First bullet point\n* Second bullet with *bold* text\n* Third bullet with a [link](https://elm-lang.org/)\n```elm\nadd a b =\n    a + b\n```\n```ascii\n════════════════════════════\n _,  ____ ____  ,-  \n¢ºº < Yo.│ No.> ··?\\\n/¥\\  ¯¯¯¯ ¯¯¯¯  /V\\ \n/¯|    ___      ´╥` \n░▒▓█``` ```trigger horizontal scroll trigger horizontal scroll trigger horizontal scroll trigger horizontal scroll trigger horizontal scroll trigger horizontal scroll trigger horizontal scroll```"

                    -- Selections are looked up by substring so that they stay on the text they are
                    -- meant to highlight when messageText is edited.
                    selectionAround : String -> Range
                    selectionAround substring =
                        case String.indexes substring messageText of
                            index :: _ ->
                                { start = index, end = index + String.length substring }

                            [] ->
                                Debug.todo (substring ++ " isn't part of messageText so it can't be selected")

                    -- On a phone the enter key writes a line break and the message is sent by
                    -- pressing the button next to the input, so there is nothing for the
                    -- keyboard shortcut to do there.
                    sendMessage : T.Action ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
                    sendMessage =
                        if isMobile then
                            admin.click 100 (Dom.id "messageMenu_channelInput_sendMessage")

                        else
                            admin.keyDown 100 (Dom.id "channel_textinput") "Enter" []

                    -- Replying to a message is reached differently on each: hovering a message
                    -- on a desktop brings up a row of buttons next to it, while on a phone there
                    -- is nothing to hover and the message is long pressed to open a menu
                    -- instead. The long press arrives as a contextmenu event, and the menu
                    -- slides in, which is what the wait before pressing reply is for.
                    replyToRichTextMessage : T.Action ToBackend FrontendMsg FrontendModel ToFrontend BackendMsg E2EHelper.BackendModel2
                    replyToRichTextMessage =
                        if isMobile then
                            T.group
                                [ admin.custom
                                    100
                                    (Dom.id "guild_message_2")
                                    "contextmenu"
                                    (Json.Encode.object
                                        [ ( "clientX", Json.Encode.float 50 )
                                        , ( "clientY", Json.Encode.float 150 )
                                        ]
                                    )
                                , admin.click 2000 (Dom.id "messageMenu_replyTo")
                                ]

                        else
                            T.group
                                [ admin.mouseEnter 100 (Dom.id "guild_message_2") ( 100, 100 ) []
                                , admin.click 100 (Dom.id "miniView_reply")
                                ]
                in
                [ -- Focus the channel text input and type the message.
                  E2EHelper.focusEvent admin 100 (Just (Dom.id "channel_textinput")) (Just { start = 0, end = 0 })
                , admin.click 100 (Dom.id "channel_textinput")
                , admin.input 100 (Dom.id "channel_textinput") "# Rich text demo"
                , sendMessage
                , admin.input 100 (Dom.id "channel_textinput") messageText

                -- Snapshot the formatted preview while the message is still in the text input.
                , E2EHelper.tallSnapshot admin 100 { name = "Rich text message in text input" }

                -- The textarea is drawn on top of the rich text so that the caret stays visible,
                -- which means the rich text draws the selection highlight itself. Check that the
                -- highlight lands on the right text for a few different selections.
                , E2EHelper.selectionEvent
                    admin
                    100
                    (Dom.id "channel_textinput")
                    (selectionAround "*bold*, _italic_, __underline__, ~~strikethrough~~, ||spoiler||")
                , E2EHelper.tallSnapshot admin 100 { name = "Rich text selection across inline formatting" }
                , E2EHelper.selectionEvent
                    admin
                    100
                    (Dom.id "channel_textinput")
                    (selectionAround "```elm\nadd a b =\n    a + b\n```")
                , E2EHelper.tallSnapshot admin 100 { name = "Rich text selection over a code block" }
                , E2EHelper.selectionEvent
                    admin
                    100
                    (Dom.id "channel_textinput")
                    (selectionAround "/)\n```el")
                , E2EHelper.tallSnapshot admin 100 { name = "Rich text partial selection over a code block" }
                , E2EHelper.selectionEvent
                    admin
                    100
                    (Dom.id "channel_textinput")
                    (selectionAround "First bullet point\n* Second bullet with *bold* text")
                , E2EHelper.tallSnapshot admin 100 { name = "Rich text selection across bullet points" }

                -- Send the message and snapshot how it renders in the channel.
                , sendMessage
                , E2EHelper.focusEvent admin 100 Nothing Nothing
                , E2EHelper.tallSnapshot admin 1000 { name = "Rich text message after being sent" }

                -- The bullet points should render in the same order they were written.
                , admin.checkView
                    100
                    (\html ->
                        html
                            |> Test.Html.Query.find [ Test.Html.Selector.tag "ul" ]
                            |> Test.Html.Query.findAll [ Test.Html.Selector.tag "li" ]
                            |> Test.Html.Query.index 0
                            |> Test.Html.Query.has [ Test.Html.Selector.text "First bullet point" ]
                    )
                , admin.checkView
                    100
                    (\html ->
                        html
                            |> Test.Html.Query.find [ Test.Html.Selector.tag "ul" ]
                            |> Test.Html.Query.findAll [ Test.Html.Selector.tag "li" ]
                            |> Test.Html.Query.index 2
                            |> Test.Html.Query.has [ Test.Html.Selector.text "Third bullet with a " ]
                    )
                , replyToRichTextMessage
                , admin.input 100 (Dom.id "channel_textinput") "Reply"
                , sendMessage

                -- The snapshot below is only worth looking at if there is a reply header in
                -- it, and the two ways of starting a reply are easy to break one at a time,
                -- so check the header is there rather than leaving it to the eye. The id
                -- names the message being replied to.
                , admin.checkView
                    1000
                    (Test.Html.Query.has [ Test.Html.Selector.id "guild_replyLink_2" ])
                , E2EHelper.tallSnapshot admin 1000 { name = "Rich text message previewed in reply" }

                -- Following the reply back to the message it points at marks that message. The mark
                -- is a layer inside the message rather than the message's own background, since its
                -- opacity fades.
                , admin.click 100 (Dom.id "guild_replyLink_2")
                , admin.checkView
                    100
                    (\html ->
                        Test.Html.Query.find [ Test.Html.Selector.id "guild_message_2" ] html
                            |> Test.Html.Query.findAll
                                [ Test.Html.Selector.style
                                    "background-color"
                                    (Color.toCssString MyUi.replyToColor)
                                ]
                            |> Test.Html.Query.count (Expect.equal 1)
                    )
                ]
            )
        ]
