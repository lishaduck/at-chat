module Pages.Guild exposing
    ( DmChannelSelection(..)
    , HighlightMessage(..)
    , IsHovered(..)
    , RepliedToView
    , banMemberText
    , channelDoesNotExistText
    , channelMessageHtmlId
    , channelSearchInputId
    , channelTextInputId
    , chatWithText
    , confirmLeaveGuildText
    , conversationContainerId
    , decodeMessageView
    , deleteGuildText
    , directMessagesText
    , discordGuildView
    , dropdownButtonId
    , e2eeSectionIsExpanded
    , editingText
    , encodeMessageView
    , friendLabel
    , friendsSearchInputId
    , guildMembersText
    , guildNotFoundText
    , guildView
    , homePageLoggedInView
    , importChannelFailedText
    , importChannelText
    , importedChannelText
    , leaveGuildText
    , neverPostedText
    , newGuildFormInit
    , newGuildFormView
    , newMessagesBadgeText
    , newMessagesId
    , noMatchingChannelsText
    , noUnreadMessagesText
    , olderUnreadMessagesText
    , profileImageButtonId
    , setImportChannelStatus
    , startOfThreadText
    , startedACallText
    , threadMessageHtmlId
    , typingDebouncerDelay
    , typingText
    , userTextMessageContent
    )

import Array
import AsciiArt exposing (AsciiArt)
import Bitwise
import Call
import ChannelDescription
import ChannelHeader
import ChannelName exposing (ChannelName)
import Coord
import CustomEmoji exposing (CustomEmojiData)
import Date exposing (Date)
import Discord
import DmChannel exposing (DiscordFrontendDmChannel, E2eeStatus(..), FrontendDmChannel)
import DmChannelId
import Drawing exposing (Drawing)
import Duration exposing (Duration)
import Effect.Browser.Dom as Dom exposing (HtmlId)
import Emoji exposing (CachedEmojiData, EmojiConfig, EmojiOrCustomEmoji)
import Encryption exposing (BytesHash)
import Env
import FileStatus exposing (FileHash, FileId, FileStatus)
import Game
import GuildColumn
import GuildIcon exposing (ChannelNotificationType(..))
import GuildName exposing (GuildName)
import Html exposing (Html)
import Html.Attributes
import Html.Events
import Icons
import Id exposing (AnyGuildOrDmId(..), ChannelId, ChannelMessageId, CustomEmojiId, DiscordGuildOrDmId(..), ExportChannelId(..), GuildId, GuildOrDmId(..), Id, StickerId, ThreadMessageId, ThreadRoute(..), ThreadRouteWithMaybeMessage(..), ThreadRouteWithMessage(..), UserId)
import ImageEditor
import Json.Decode
import LinkedAndOtherDiscordUsers
import List.Extra
import List.Nonempty exposing (Nonempty)
import LocalState exposing (DiscordFrontendChannel, DiscordFrontendGuild, FrontendChannel, FrontendGuild, LocalState)
import Maybe.Extra
import MembersAndOwner exposing (IsMember(..), MembersAndOwner)
import Message exposing (GameType(..), Message(..), MessageContent, RepliedTo(..), UserTextMessageDrawings)
import MessageArray exposing (MessageArray)
import MessageInput
import MessageMenu
import MessageView exposing (MessageViewMsg(..))
import MuteSettings exposing (IsMuted(..))
import MyUi
import NonemptyDict exposing (NonemptyDict)
import NonemptySet exposing (NonemptySet)
import OneOrGreater exposing (OneOrGreater)
import OneToOne
import PersonName exposing (PersonName)
import QRCode
import Quantity
import RichText exposing (RichText)
import Route exposing (ChannelRoute(..), ChannelsVisibleOnMobile(..), DiscordChannelRoute(..), DiscordDmRouteData, DiscordGuildRouteData, DmRouteData, Route(..), ShowChannelSettings(..), ThreadRouteWithFriends(..))
import Scroll
import SecretId
import SeqDict exposing (SeqDict)
import SeqSet exposing (SeqSet)
import SheepGame
import Sticker exposing (AnimationMode(..))
import String.Nonempty
import Thread exposing (DiscordFrontendThread, FrontendGenericThread, FrontendThread)
import Time
import Touch
import Types exposing (EditChannelForm, EditGuildForm, EditMessage, EmojiSelector(..), FrontendMsg_(..), ImportChannelError(..), ImportChannelStatus(..), LoadedFrontend, LoggedIn2, MessageHover(..), NewChannelForm, NewGuildForm)
import Ui exposing (Element)
import Ui.Anim
import Ui.Events
import Ui.Font
import Ui.Input
import Ui.Keyed
import Ui.Lazy
import Ui.Prose
import Ui.Table
import User exposing (EmbedVisibility(..), FrontendCurrentUser, FrontendUser, LocalUser, NotificationLevel(..))
import UserColor exposing (UserColor)
import UserSession exposing (ChannelHeaderTab(..), DiscordFrontendUser, PreviouslyLastViewedMessage(..), Viewing(..))
import VisibleMessages exposing (VisibleMessages)


newMessagesBadgeText : String
newMessagesBadgeText =
    "new"


noUnreadMessagesText : String
noUnreadMessagesText =
    "You have no unread messages!"


startedACallText : String
startedACallText =
    "started a call"


typingText : String
typingText =
    "Typing..."


editingText : String
editingText =
    "Editing..."


chatWithText : String
chatWithText =
    "Chat with"


deleteGuildText : String
deleteGuildText =
    "Delete guild"


leaveGuildText : String
leaveGuildText =
    "Leave guild"


confirmLeaveGuildText : String
confirmLeaveGuildText =
    "Yes, leave guild"


startOfThreadText : String
startOfThreadText =
    "Start of thread"


channelDoesNotExistText : String
channelDoesNotExistText =
    "Channel does not exist"


guildNotFoundText : String
guildNotFoundText =
    "Guild not found"


directMessagesText : String
directMessagesText =
    "Direct messages"


noMatchingChannelsText : String
noMatchingChannelsText =
    "No matching channels\u{00A0}found"


olderUnreadMessagesText : Int -> String
olderUnreadMessagesText count =
    if count == 1 then
        "1 older unread message"

    else
        String.fromInt count ++ " older unread messages"


loggedInAsView : LocalUser -> Element FrontendMsg_
loggedInAsView localUser =
    Ui.row
        [ Ui.Font.color MyUi.font2
        , Ui.borderColor MyUi.border1
        , Ui.borderWith { left = 0, bottom = 0, top = 1, right = 0 }
        , Ui.background MyUi.background1
        , Ui.paddingWith { left = 4, right = 4, top = 4, bottom = localUser.safeAreaInsetBottom + 4 }
        , Ui.spacing 8
        , Ui.clipWithEllipsis
        ]
        [ User.profileImage (Just localUser.user)
        , Ui.text (PersonName.toString localUser.user.name)
        , MyUi.elButton
            (Dom.id "guild_showUserOptions")
            PressedShowUserOption
            [ Ui.width (Ui.px 38)
            , Ui.height Ui.fill
            , Ui.contentCenterY
            , Ui.paddingXY 8 0
            , Ui.alignRight
            , MyUi.hoverText "User settings"
            ]
            (Ui.html Icons.gear)
        ]


type DmChannelSelection
    = SelectedDmChannel DmRouteData
    | SelectedDiscordDmChannel DiscordDmRouteData
    | NoDmChannelSelected


homePageLoggedInView :
    DmChannelSelection
    -> LoadedFrontend
    -> LoggedIn2
    -> LocalState
    -> Element FrontendMsg_
homePageLoggedInView maybeOtherUserId model loggedIn local =
    case loggedIn.showFileToUploadInfo of
        Just fileData ->
            FileStatus.imageInfoView local.localUser.safeAreaInsetTop local.localUser.safeAreaInsetBottom model.timezone PressedCloseImageInfo fileData

        Nothing ->
            if MyUi.isMobile model then
                let
                    canScroll2 : Bool
                    canScroll2 =
                        MyUi.canScroll True model.drag

                    showMembers : ( ShowChannelSettings, Bool )
                    showMembers =
                        Route.toShowMembersTabVisible loggedIn model.route

                    memberColumn : Element FrontendMsg_
                    memberColumn =
                        case showMembers of
                            ( ShowChannelSettings, isThread ) ->
                                case maybeOtherUserId of
                                    SelectedDmChannel dmRoute ->
                                        case DmChannelId.otherUserId local.localUser.session.userId dmRoute.channelId of
                                            Just otherUserId ->
                                                dmChannelSettingsMobile
                                                    canScroll2
                                                    local.localUser
                                                    otherUserId
                                                    isThread
                                                    (dmE2eeStatus otherUserId local)
                                                    (e2eeSectionIsExpanded otherUserId local loggedIn)
                                                    (e2eeKeyInput otherUserId loggedIn)
                                                    |> Ui.el
                                                        [ Ui.height Ui.fill
                                                        , Ui.background MyUi.background3
                                                        , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                                        , Ui.move
                                                            { x = Call.memberColumnOffset loggedIn.sidebarMode model
                                                            , y = 0
                                                            , z = 0
                                                            }
                                                        , Ui.heightMin 0
                                                        ]

                                            Nothing ->
                                                Ui.none

                                    SelectedDiscordDmChannel routeData ->
                                        case SeqDict.get routeData.channelId local.discordDmChannels of
                                            Just dmChannel ->
                                                discordDmChannelSettingsMobile
                                                    canScroll2
                                                    local.localUser
                                                    routeData.currentDiscordUserId
                                                    routeData.channelId
                                                    dmChannel
                                                    |> Ui.el
                                                        [ Ui.height Ui.fill
                                                        , Ui.background MyUi.background3
                                                        , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                                        , Ui.move
                                                            { x = Call.memberColumnOffset loggedIn.sidebarMode model
                                                            , y = 0
                                                            , z = 0
                                                            }
                                                        , Ui.heightMin 0
                                                        ]

                                            Nothing ->
                                                Ui.none

                                    NoDmChannelSelected ->
                                        Ui.none

                            ( HideChannelSettings, _ ) ->
                                Ui.none
                in
                Ui.row
                    [ Ui.height Ui.fill
                    , Ui.background MyUi.background1
                    ]
                    [ Ui.column
                        [ Ui.height Ui.fill
                        , Ui.inFront memberColumn
                        , case maybeOtherUserId of
                            SelectedDmChannel dmRoute ->
                                dmChannelView dmRoute loggedIn local model
                                    |> Ui.el
                                        [ Ui.height Ui.fill
                                        , Ui.background MyUi.background3
                                        , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                        , Ui.move
                                            { x = Call.conversationOffset loggedIn.sidebarMode model
                                            , y = 0
                                            , z = 0
                                            }
                                        , Ui.heightMin 0
                                        , Ui.borderColor MyUi.border1
                                        , Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
                                        ]
                                    |> Ui.inFront

                            SelectedDiscordDmChannel routeData ->
                                discordDmChannelView routeData loggedIn local model
                                    |> Ui.el
                                        [ Ui.height Ui.fill
                                        , Ui.background MyUi.background3
                                        , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                        , Ui.move
                                            { x = Call.conversationOffset loggedIn.sidebarMode model
                                            , y = 0
                                            , z = 0
                                            }
                                        , Ui.heightMin 0
                                        , Ui.borderColor MyUi.border1
                                        , Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
                                        ]
                                    |> Ui.inFront

                            NoDmChannelSelected ->
                                Ui.noAttr
                        ]
                        [ Ui.row
                            [ Ui.height Ui.fill, Ui.heightMin 0, MyUi.htmlStyle "isolation" "isolate" ]
                            [ GuildColumn.guildColumnLazy True model local
                            , friendsColumnLazy
                                canScroll2
                                True
                                model.time
                                maybeOtherUserId
                                loggedIn.friendsSearch
                                (Maybe.map .htmlId loggedIn.textInputFocus == Just friendsSearchInputId)
                                local
                            ]
                        , Ui.Lazy.lazy loggedInAsView local.localUser
                        ]
                    ]

            else
                Ui.row
                    [ Ui.height Ui.fill
                    , Ui.background MyUi.background1
                    ]
                    [ Ui.column
                        [ Ui.height Ui.fill, Ui.width (Ui.px (MyUi.channelAndGuildColumnWidth model.windowSize)) ]
                        [ Ui.row
                            [ Ui.height Ui.fill, Ui.heightMin 0 ]
                            [ GuildColumn.guildColumnLazy False model local
                            , friendsColumnLazy
                                (MyUi.canScroll False model.drag)
                                False
                                model.time
                                maybeOtherUserId
                                loggedIn.friendsSearch
                                (Maybe.map .htmlId loggedIn.textInputFocus == Just friendsSearchInputId)
                                local
                            ]
                        , Ui.Lazy.lazy loggedInAsView local.localUser
                        ]
                    , case maybeOtherUserId of
                        SelectedDmChannel dmRoute ->
                            dmChannelView dmRoute loggedIn local model
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.background MyUi.background3
                                    , Ui.heightMin 0
                                    , Ui.borderColor MyUi.border1
                                    , Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
                                    ]
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                    ]

                        SelectedDiscordDmChannel routeData ->
                            discordDmChannelView routeData loggedIn local model
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.background MyUi.background3
                                    , Ui.heightMin 0
                                    , Ui.borderColor MyUi.border1
                                    , Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
                                    ]
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                    ]

                        NoDmChannelSelected ->
                            unreadOverviewNotMobile local loggedIn model
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.background MyUi.background3
                                    , Ui.heightMin 0
                                    , Ui.borderColor MyUi.border1
                                    , Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
                                    ]
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                    ]
                    , case ( Route.toShowMembersTabVisible loggedIn model.route, maybeOtherUserId ) of
                        ( ( ShowChannelSettings, isThread ), SelectedDmChannel dmRoute ) ->
                            case DmChannelId.otherUserId local.localUser.session.userId dmRoute.channelId of
                                Just otherUserId ->
                                    dmChannelSettingsNotMobile
                                        local.localUser
                                        otherUserId
                                        isThread
                                        (dmE2eeStatus otherUserId local)
                                        (e2eeSectionIsExpanded otherUserId local loggedIn)
                                        (e2eeKeyInput otherUserId loggedIn)
                                        |> Ui.el
                                            [ Ui.width Ui.shrink
                                            , Ui.height Ui.fill
                                            , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                            ]

                                Nothing ->
                                    Ui.none

                        ( ( ShowChannelSettings, _ ), SelectedDiscordDmChannel routeData ) ->
                            case SeqDict.get routeData.channelId local.discordDmChannels of
                                Just dmChannel ->
                                    Ui.Lazy.lazy4
                                        discordDmMemberColumnNotMobile
                                        local.localUser
                                        routeData.currentDiscordUserId
                                        routeData.channelId
                                        dmChannel
                                        |> Ui.el
                                            [ Ui.width Ui.shrink
                                            , Ui.height Ui.fill
                                            , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                            ]

                                Nothing ->
                                    Ui.none

                        ( ( ShowChannelSettings, _ ), NoDmChannelSelected ) ->
                            Ui.none

                        ( ( HideChannelSettings, _ ), _ ) ->
                            Ui.none
                    ]


{-| The unread messages of one channel or thread, along with where they came from and how
many older unread messages of it aren't shown.
-}
type alias UnreadOverviewChannel =
    { source : Element FrontendMsg_
    , route : Route
    , guildOrDmId : AnyGuildOrDmId
    , threadRoute : ThreadRouteWithMessage
    , additionalUnread : Int
    , oldestAt : Time.Posix
    , messages : UnreadOverviewMessages
    }


{-| Discord messages are kept apart from the rest because they are written by Discord users
rather than our own users, and thread messages are kept apart from channel messages because
they are numbered separately.
-}
type UnreadOverviewMessages
    = UnreadOverviewMessages (List ( Id ChannelMessageId, Message ChannelMessageId (Id UserId) (Id ChannelId) ))
    | UnreadOverviewThreadMessages (Id ChannelMessageId) (List ( Id ThreadMessageId, Message ThreadMessageId (Id UserId) (Id ChannelId) ))
    | UnreadOverviewDiscordMessages (Discord.Id Discord.UserId) (List ( Id ChannelMessageId, Message ChannelMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId) ))
    | UnreadOverviewDiscordThreadMessages (Id ChannelMessageId) (Discord.Id Discord.UserId) (List ( Id ThreadMessageId, Message ThreadMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId) ))


unreadOverviewNotMobile : LocalState -> LoggedIn2 -> LoadedFrontend -> Element FrontendMsg_
unreadOverviewNotMobile local loggedIn model =
    let
        allDiscordUsers : SeqDict (Discord.Id Discord.UserId) DiscordFrontendUser
        allDiscordUsers =
            LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers

        allUsers : SeqDict (Id UserId) FrontendUser
        allUsers =
            User.allUsers local.localUser

        containerWidth : Int
        containerWidth =
            conversationWidth model

        unreads : List UnreadOverviewChannel
        unreads =
            unreadOverviewChannels local allDiscordUsers
    in
    Ui.column
        [ Ui.height Ui.fill
        , Ui.heightMin 0
        , Ui.Font.color MyUi.font1

        -- Reacting to a message from here opens the emoji selector over the overview, so it
        -- has to be drawn here as well as over a conversation
        , emojiSelector
            (MyUi.isMobile model)
            local.localUser.user.availableCustomEmojis
            local.localUser.user.availableStickers
            local
            loggedIn
            model
        ]
        [ Ui.row
            [ Ui.paddingXY 8 0
            , Ui.spacing 8
            , Ui.height (Ui.px MyUi.channelHeaderHeight)
            , Ui.contentCenterY
            , MyUi.noShrinking
            , Ui.borderWith { left = 0, right = 0, top = 0, bottom = 1 }
            , Ui.borderColor MyUi.border2
            ]
            [ Ui.el [ Ui.Font.bold ] (Ui.text "All unread messages")
            , case unreads of
                [] ->
                    Ui.none

                _ ->
                    MyUi.elButton
                        unreadOverviewMarkAllAsReadId
                        (List.map (\unread -> ( unread.guildOrDmId, unread.threadRoute )) unreads
                            |> PressedMarkAllChannelsAsRead
                        )
                        [ Ui.width Ui.shrink
                        , Ui.alignRight
                        , Ui.paddingXY 8 2
                        , Ui.rounded 4
                        , Ui.border 1
                        , Ui.borderColor MyUi.buttonBorder
                        , Ui.background MyUi.buttonBackground
                        , MyUi.noShrinking
                        ]
                        (Ui.text "Mark all as read")
            ]
        , case unreads of
            [] ->
                let
                    art : AsciiArt
                    art =
                        List.Nonempty.get
                            (Time.toDay model.timezone model.time - Time.toDay model.timezone local.localUser.user.createdAt)
                            AsciiArt.art

                    dpi =
                        model.startupData.devicePixelRatio

                    scaleFactor : Int
                    scaleFactor =
                        min
                            (round (dpi * toFloat containerWidth) // Coord.xRaw art.size)
                            (round (dpi * toFloat (Coord.yRaw model.windowSize - MyUi.channelHeaderHeight - 80)) // Coord.yRaw art.size)
                            |> clamp 1 (round (2 * dpi))
                in
                Ui.column
                    [ Ui.height Ui.fill
                    , Ui.inFront
                        (Ui.el
                            [ Ui.Font.center
                            , Ui.padding 16
                            , Ui.Font.color MyUi.font3
                            , Ui.Font.bold
                            , Ui.Font.size 20
                            ]
                            (Ui.text noUnreadMessagesText)
                        )
                    ]
                    [ Ui.image
                        [ MyUi.htmlStyle "image-rendering" "pixelated"
                        , Ui.centerX
                        , Ui.centerY
                        , Ui.paddingXY 0 16
                        , MyUi.htmlStyle
                            "width"
                            (String.fromFloat (toFloat (Coord.xRaw art.size * scaleFactor) / dpi) ++ "px")
                        , Ui.opacity 0.3
                        , MyUi.hover False [ Ui.Anim.opacity 0.6 ]
                        , Ui.linkNewTab
                            ("https://ascii-collab.app/?x="
                                ++ String.fromInt (Coord.xRaw art.coordinates)
                                ++ "&y="
                                ++ String.fromInt (Coord.yRaw art.coordinates)
                            )
                        ]
                        { source = "/cacheable/art/" ++ art.url ++ ".png"
                        , description = "Pleasing ascii art drawing"
                        , onLoad = Nothing
                        }
                    ]

            _ ->
                Ui.column
                    [ Ui.height Ui.fill
                    , Ui.heightMin 0
                    , MyUi.scrollable True
                    , Ui.paddingWith { left = 0, right = 0, top = 2, bottom = 0 }
                    ]
                    (List.map
                        (\unread ->
                            unreadOverviewContainer
                                unread
                                (case unread.messages of
                                    UnreadOverviewMessages messages ->
                                        let
                                            revealedSpoilers : SeqDict (Id ChannelMessageId) (NonemptySet Int)
                                            revealedSpoilers =
                                                revealedChannelSpoilers unread.guildOrDmId loggedIn
                                        in
                                        List.map
                                            (\( messageId, message ) ->
                                                ( Message.createdAt message |> Date.fromPosix local.localUser.timezone
                                                , messageView
                                                    model.time
                                                    False
                                                    containerWidth
                                                    False
                                                    revealedSpoilers
                                                    NoHighlight
                                                    (unreadOverviewMessageHover
                                                        unread.guildOrDmId
                                                        (NoThreadWithMessage messageId)
                                                        loggedIn
                                                    )
                                                    False
                                                    local.localUser.session.userId
                                                    allUsers
                                                    (case unread.guildOrDmId of
                                                        GuildOrDmId guildOrDmId ->
                                                            LocalState.guildChannels guildOrDmId local

                                                        DiscordGuildOrDmId _ ->
                                                            SeqDict.empty
                                                    )
                                                    local.localUser
                                                    Nothing
                                                    Nothing
                                                    messageId
                                                    message
                                                    |> Ui.map (UnreadOverviewChannelMsg unread.guildOrDmId messageId)
                                                )
                                            )
                                            messages

                                    UnreadOverviewThreadMessages threadId messages ->
                                        let
                                            revealedSpoilers : SeqDict (Id ThreadMessageId) (NonemptySet Int)
                                            revealedSpoilers =
                                                revealedThreadSpoilers unread.guildOrDmId threadId loggedIn
                                        in
                                        List.map
                                            (\( messageId, message ) ->
                                                ( Message.createdAt message |> Date.fromPosix local.localUser.timezone
                                                , threadMessageView
                                                    model.time
                                                    False
                                                    containerWidth
                                                    revealedSpoilers
                                                    NoHighlight
                                                    (unreadOverviewMessageHover
                                                        unread.guildOrDmId
                                                        (ViewThreadWithMessage threadId messageId)
                                                        loggedIn
                                                    )
                                                    False
                                                    allUsers
                                                    (case unread.guildOrDmId of
                                                        GuildOrDmId guildOrDmId ->
                                                            LocalState.channelMentions guildOrDmId local

                                                        DiscordGuildOrDmId _ ->
                                                            SeqDict.empty
                                                    )
                                                    local.localUser.session.userId
                                                    local.localUser
                                                    Nothing
                                                    messageId
                                                    message
                                                    |> Ui.map (UnreadOverviewThreadMsg unread.guildOrDmId threadId messageId)
                                                )
                                            )
                                            messages

                                    UnreadOverviewDiscordMessages currentDiscordUserId messages ->
                                        let
                                            revealedSpoilers : SeqDict (Id ChannelMessageId) (NonemptySet Int)
                                            revealedSpoilers =
                                                revealedChannelSpoilers unread.guildOrDmId loggedIn
                                        in
                                        List.map
                                            (\( messageId, message ) ->
                                                ( Message.createdAt message |> Date.fromPosix local.localUser.timezone
                                                , discordMessageView
                                                    model.time
                                                    False
                                                    containerWidth
                                                    False
                                                    revealedSpoilers
                                                    NoHighlight
                                                    (unreadOverviewMessageHover
                                                        unread.guildOrDmId
                                                        (NoThreadWithMessage messageId)
                                                        loggedIn
                                                    )
                                                    currentDiscordUserId
                                                    allDiscordUsers
                                                    (case unread.guildOrDmId of
                                                        GuildOrDmId _ ->
                                                            SeqDict.empty

                                                        DiscordGuildOrDmId guildOrDmId ->
                                                            LocalState.discordChannelMentions guildOrDmId local
                                                    )
                                                    local.localUser
                                                    Nothing
                                                    Nothing
                                                    messageId
                                                    message
                                                    |> Ui.map (UnreadOverviewChannelMsg unread.guildOrDmId messageId)
                                                )
                                            )
                                            messages

                                    UnreadOverviewDiscordThreadMessages threadId currentDiscordUserId messages ->
                                        let
                                            revealedSpoilers : SeqDict (Id ThreadMessageId) (NonemptySet Int)
                                            revealedSpoilers =
                                                revealedThreadSpoilers unread.guildOrDmId threadId loggedIn
                                        in
                                        List.map
                                            (\( messageId, message ) ->
                                                ( Message.createdAt message |> Date.fromPosix local.localUser.timezone
                                                , discordThreadMessageView
                                                    model.time
                                                    False
                                                    containerWidth
                                                    revealedSpoilers
                                                    NoHighlight
                                                    (unreadOverviewMessageHover
                                                        unread.guildOrDmId
                                                        (ViewThreadWithMessage threadId messageId)
                                                        loggedIn
                                                    )
                                                    allDiscordUsers
                                                    (case unread.guildOrDmId of
                                                        GuildOrDmId _ ->
                                                            SeqDict.empty

                                                        DiscordGuildOrDmId guildOrDmId ->
                                                            LocalState.discordChannelMentions guildOrDmId local
                                                    )
                                                    currentDiscordUserId
                                                    local.localUser
                                                    Nothing
                                                    messageId
                                                    message
                                                    |> Ui.map (UnreadOverviewThreadMsg unread.guildOrDmId threadId messageId)
                                                )
                                            )
                                            messages
                                )
                        )
                        unreads
                    )
        ]


{-| Every channel and thread with unread messages, ordered by the oldest unread message of
each. A channel's place in the overview is decided by when it started being unread, so
messages arriving while the overview is open add to a channel where it already is instead of
moving it.
-}
unreadOverviewChannels :
    LocalState
    -> SeqDict (Discord.Id Discord.UserId) DiscordFrontendUser
    -> List UnreadOverviewChannel
unreadOverviewChannels local allDiscordUsers =
    let
        currentUser : FrontendCurrentUser
        currentUser =
            local.localUser.user

        allUsers : SeqDict (Id UserId) FrontendUser
        allUsers =
            User.allUsers local.localUser
    in
    List.concatMap
        (\( guildId, guild ) ->
            List.concatMap
                (\( channelId, channel ) ->
                    let
                        guildOrDmId : AnyGuildOrDmId
                        guildOrDmId =
                            GuildOrDmId (GuildOrDmId_Guild { guildId = guildId, channelId = channelId })
                    in
                    (if showInUnreadOverview (MuteSettings.isChannelMuted currentUser.muteSettings guildId channelId NoThread) (SeqSet.member guildId currentUser.notifyOnAllMessages || isMentioned (SeqDict.get guildId currentUser.directMentions) ( channelId, NoThread )) then
                        unreadMessages (SeqDict.get guildOrDmId currentUser.lastViewedMessage) channel
                            |> Maybe.map
                                (\unread ->
                                    [ { source = channelSource guild.name channel.name
                                      , route =
                                            GuildRoute
                                                guildId
                                                (ChannelRoute channelId (NoThreadWithFriends Nothing HideChannelSettings) Nothing)
                                                ChannelsHiddenOnMobile
                                                Nothing
                                      , guildOrDmId = guildOrDmId
                                      , threadRoute = NoThreadWithMessage unread.newestMessageId
                                      , additionalUnread = unread.additionalUnread
                                      , oldestAt = unread.oldestAt
                                      , messages = UnreadOverviewMessages unread.messages
                                      }
                                    ]
                                )
                            |> Maybe.withDefault []

                     else
                        []
                    )
                        ++ List.filterMap
                            (\( threadId, thread ) ->
                                if showInUnreadOverview (MuteSettings.isChannelMuted currentUser.muteSettings guildId channelId (ViewThread threadId)) (SeqSet.member guildId currentUser.notifyOnAllMessages || isMentioned (SeqDict.get guildId currentUser.directMentions) ( channelId, ViewThread threadId )) then
                                    unreadMessages
                                        (SeqDict.get ( guildOrDmId, threadId ) currentUser.lastViewedThreadMessage)
                                        thread
                                        |> Maybe.map
                                            (\unread ->
                                                { source =
                                                    threadSource
                                                        guild.name
                                                        channel.name
                                                        (threadPreviewText local.localUser.timezone allUsers (LocalState.guildChannelMentions local.localUser guild.channels) threadId local.localUser.decryptedMessages channel)
                                                , route =
                                                    GuildRoute
                                                        guildId
                                                        (ChannelRoute
                                                            channelId
                                                            (ViewThreadWithFriends threadId Nothing HideChannelSettings)
                                                            Nothing
                                                        )
                                                        ChannelsHiddenOnMobile
                                                        Nothing
                                                , guildOrDmId = guildOrDmId
                                                , threadRoute =
                                                    ViewThreadWithMessage threadId unread.newestMessageId
                                                , additionalUnread = unread.additionalUnread
                                                , oldestAt = unread.oldestAt
                                                , messages = UnreadOverviewThreadMessages threadId unread.messages
                                                }
                                            )

                                else
                                    Nothing
                            )
                            (SeqDict.toList channel.threads)
                )
                (SeqDict.toList guild.channels)
        )
        (SeqDict.toList local.guilds)
        ++ List.concatMap
            (\( otherUserId, dmChannel ) ->
                let
                    guildOrDmId : AnyGuildOrDmId
                    guildOrDmId =
                        GuildOrDmId (GuildOrDmId_Dm { otherUserId = otherUserId })
                in
                (if MuteSettings.hidesRedDot (MuteSettings.isDmMuted currentUser.muteSettings otherUserId NoThread) then
                    []

                 else
                    unreadMessages (SeqDict.get guildOrDmId currentUser.lastViewedMessage) dmChannel
                        |> Maybe.map
                            (\unread ->
                                [ { source = dmSource otherUserId local.localUser
                                  , route =
                                        DmRoute
                                            { channelId = DmChannelId.fromUserIds local.localUser.session.userId otherUserId
                                            , threadRoute = NoThreadWithFriends Nothing HideChannelSettings
                                            , tab = Nothing
                                            , channelsVisible = ChannelsHiddenOnMobile
                                            , overlay = Nothing
                                            }
                                  , guildOrDmId = guildOrDmId
                                  , threadRoute = NoThreadWithMessage unread.newestMessageId
                                  , additionalUnread = unread.additionalUnread
                                  , oldestAt = unread.oldestAt
                                  , messages = UnreadOverviewMessages unread.messages
                                  }
                                ]
                            )
                        |> Maybe.withDefault []
                )
                    ++ List.filterMap
                        (\( threadId, thread ) ->
                            if MuteSettings.hidesRedDot (MuteSettings.isDmMuted currentUser.muteSettings otherUserId (ViewThread threadId)) then
                                Nothing

                            else
                                unreadMessages
                                    (SeqDict.get ( guildOrDmId, threadId ) currentUser.lastViewedThreadMessage)
                                    thread
                                    |> Maybe.map
                                        (\unread ->
                                            { source =
                                                dmThreadSource
                                                    otherUserId
                                                    local.localUser
                                                    (threadPreviewText local.localUser.timezone allUsers SeqDict.empty threadId local.localUser.decryptedMessages dmChannel)
                                            , route =
                                                DmRoute
                                                    { channelId = DmChannelId.fromUserIds local.localUser.session.userId otherUserId
                                                    , threadRoute = ViewThreadWithFriends threadId Nothing HideChannelSettings
                                                    , tab = Nothing
                                                    , channelsVisible = ChannelsHiddenOnMobile
                                                    , overlay = Nothing
                                                    }
                                            , guildOrDmId = guildOrDmId
                                            , threadRoute = ViewThreadWithMessage threadId unread.newestMessageId
                                            , additionalUnread = unread.additionalUnread
                                            , oldestAt = unread.oldestAt
                                            , messages = UnreadOverviewThreadMessages threadId unread.messages
                                            }
                                        )
                        )
                        (SeqDict.toList dmChannel.threads)
            )
            (SeqDict.toList local.dmChannels)
        ++ List.concatMap
            (\( guildId, guild ) ->
                case GuildColumn.discordGuildCurrentUserId local.localUser guild of
                    Just currentDiscordUserId ->
                        List.concatMap
                            (\( channelId, channel ) ->
                                let
                                    guildOrDmId : AnyGuildOrDmId
                                    guildOrDmId =
                                        DiscordGuildOrDmId
                                            (DiscordGuildOrDmId_Guild { currentUserId = currentDiscordUserId, guildId = guildId, channelId = channelId })
                                in
                                (if showInUnreadOverview (MuteSettings.isDiscordChannelMuted currentUser.muteSettings guildId channelId NoThread) (SeqSet.member guildId currentUser.discordNotifyOnAllMessages || isMentioned (SeqDict.get guildId currentUser.discordDirectMentions) ( channelId, NoThread )) then
                                    unreadMessages (SeqDict.get guildOrDmId currentUser.lastViewedMessage) channel
                                        |> Maybe.map
                                            (\unread ->
                                                [ { source = channelSource guild.name channel.name
                                                  , route =
                                                        DiscordGuildRoute
                                                            { currentDiscordUserId = currentDiscordUserId
                                                            , guildId = guildId
                                                            , channelRoute =
                                                                DiscordChannel_ChannelRoute
                                                                    channelId
                                                                    (NoThreadWithFriends Nothing HideChannelSettings)
                                                                    Nothing
                                                            , channelsVisible = ChannelsHiddenOnMobile
                                                            , overlay = Nothing
                                                            }
                                                  , guildOrDmId = guildOrDmId
                                                  , threadRoute = NoThreadWithMessage unread.newestMessageId
                                                  , additionalUnread = unread.additionalUnread
                                                  , oldestAt = unread.oldestAt
                                                  , messages =
                                                        UnreadOverviewDiscordMessages currentDiscordUserId unread.messages
                                                  }
                                                ]
                                            )
                                        |> Maybe.withDefault []

                                 else
                                    []
                                )
                                    ++ List.filterMap
                                        (\( threadId, thread ) ->
                                            if showInUnreadOverview (MuteSettings.isDiscordChannelMuted currentUser.muteSettings guildId channelId (ViewThread threadId)) (SeqSet.member guildId currentUser.discordNotifyOnAllMessages || isMentioned (SeqDict.get guildId currentUser.discordDirectMentions) ( channelId, ViewThread threadId )) then
                                                unreadMessages
                                                    (SeqDict.get ( guildOrDmId, threadId ) currentUser.lastViewedThreadMessage)
                                                    thread
                                                    |> Maybe.map
                                                        (\unread ->
                                                            { source =
                                                                threadSource
                                                                    guild.name
                                                                    channel.name
                                                                    (threadPreviewText
                                                                        local.localUser.timezone
                                                                        allDiscordUsers
                                                                        (LocalState.discordGuildChannelMentions local.localUser guild.channels)
                                                                        threadId
                                                                        SeqDict.empty
                                                                        channel
                                                                    )
                                                            , route =
                                                                DiscordGuildRoute
                                                                    { currentDiscordUserId = currentDiscordUserId
                                                                    , guildId = guildId
                                                                    , channelRoute =
                                                                        DiscordChannel_ChannelRoute
                                                                            channelId
                                                                            (ViewThreadWithFriends threadId Nothing HideChannelSettings)
                                                                            Nothing
                                                                    , channelsVisible = ChannelsHiddenOnMobile
                                                                    , overlay = Nothing
                                                                    }
                                                            , guildOrDmId = guildOrDmId
                                                            , threadRoute =
                                                                ViewThreadWithMessage threadId unread.newestMessageId
                                                            , additionalUnread = unread.additionalUnread
                                                            , oldestAt = unread.oldestAt
                                                            , messages =
                                                                UnreadOverviewDiscordThreadMessages
                                                                    threadId
                                                                    currentDiscordUserId
                                                                    unread.messages
                                                            }
                                                        )

                                            else
                                                Nothing
                                        )
                                        (SeqDict.toList channel.threads)
                            )
                            (SeqDict.toList guild.channels)

                    Nothing ->
                        []
            )
            (SeqDict.toList local.discordGuilds)
        ++ List.filterMap
            (\( channelId, dmChannel ) ->
                case
                    ( GuildColumn.discordDmCurrentUserId local.localUser dmChannel
                    , MuteSettings.hidesRedDot (MuteSettings.isDiscordDmMuted currentUser.muteSettings channelId)
                    )
                of
                    ( Just currentDiscordUserId, False ) ->
                        let
                            guildOrDmId : AnyGuildOrDmId
                            guildOrDmId =
                                DiscordGuildOrDmId
                                    (DiscordGuildOrDmId_Dm
                                        { currentUserId = currentDiscordUserId, channelId = channelId }
                                    )
                        in
                        unreadMessages (SeqDict.get guildOrDmId currentUser.lastViewedMessage) dmChannel
                            |> Maybe.map
                                (\unread ->
                                    { source = discordDmSource currentDiscordUserId allDiscordUsers dmChannel
                                    , route =
                                        DiscordDmRoute
                                            { currentDiscordUserId = currentDiscordUserId
                                            , channelId = channelId
                                            , viewingMessage = Nothing
                                            , showMembersTab = HideChannelSettings
                                            , tab = Nothing
                                            , channelsVisible = ChannelsHiddenOnMobile
                                            , overlay = Nothing
                                            }
                                    , guildOrDmId = guildOrDmId
                                    , threadRoute = NoThreadWithMessage unread.newestMessageId
                                    , additionalUnread = unread.additionalUnread
                                    , oldestAt = unread.oldestAt
                                    , messages =
                                        UnreadOverviewDiscordMessages currentDiscordUserId unread.messages
                                    }
                                )

                    _ ->
                        Nothing
            )
            (SeqDict.toList local.discordDmChannels)
        |> List.sortBy (\unread -> Time.posixToMillis unread.oldestAt)


channelSource : GuildName -> ChannelName -> Element msg
channelSource guildName channelName =
    Ui.row
        [ Ui.spacing 8
        , Ui.width Ui.shrink
        ]
        [ Ui.text (GuildName.toString guildName)
        , Ui.text "/"
        , Ui.row [ Ui.width Ui.shrink ] [ Ui.html Icons.hashtag, Ui.text (ChannelName.toString channelName) ]
        ]


{-| A thread is named after the message it started from, which can be arbitrarily long, so
the guild and channel it belongs to stay at their full width and the thread name is the part
that gets cut short when there isn't enough room.
-}
threadSource : GuildName -> ChannelName -> String -> Element msg
threadSource guildName channelName threadName =
    Ui.row
        [ Ui.spacing 8 ]
        [ Ui.row
            [ Ui.spacing 8, Ui.width Ui.shrink, MyUi.noShrinking ]
            [ Ui.text (GuildName.toString guildName)
            , Ui.text "/"
            , Ui.row [ Ui.width Ui.shrink ] [ Ui.html Icons.hashtag, Ui.text (ChannelName.toString channelName) ]
            , Ui.text "/"
            ]
        , Ui.el [ Ui.clipWithEllipsis, MyUi.hoverText threadName ] (Ui.text threadName)
        ]


{-| A DM is named after the person on the other end of it. Only their name is bold,
so it stands out from the words around it the way a guild and channel name does.
-}
dmSource : Id UserId -> LocalUser -> Element msg
dmSource otherUserId localUser =
    Ui.row
        [ Ui.spacing 4, Ui.width Ui.shrink, MyUi.noShrinking ]
        [ Ui.el [ Ui.Font.weight 400, Ui.width Ui.shrink ] (Ui.text chatWithText)
        , Ui.text (User.toStringAlt otherUserId localUser)
        ]


{-| A thread in a DM. Like threadSource, the DM keeps its full width and the thread
name is the part that gets cut short when there isn't enough room.
-}
dmThreadSource : Id UserId -> LocalUser -> String -> Element msg
dmThreadSource otherUserId localUser threadName =
    Ui.row
        [ Ui.spacing 8 ]
        [ Ui.row
            [ Ui.spacing 8, Ui.width Ui.shrink, MyUi.noShrinking ]
            [ dmSource otherUserId localUser
            , Ui.text "/"
            ]
        , Ui.el [ Ui.clipWithEllipsis, MyUi.hoverText threadName ] (Ui.text threadName)
        ]


{-| The people in a Discord DM channel, not counting the linked Discord account the user
is in the channel as. A DM channel with only us in it is named after us.
-}
discordDmSource :
    Discord.Id Discord.UserId
    -> SeqDict (Discord.Id Discord.UserId) DiscordFrontendUser
    -> DiscordFrontendDmChannel
    -> Element msg
discordDmSource currentDiscordUserId allDiscordUsers dmChannel =
    case NonemptyDict.remove currentDiscordUserId dmChannel.members |> SeqDict.keys of
        [] ->
            Ui.text (User.toString currentDiscordUserId allDiscordUsers)

        others ->
            List.map (\userId -> User.toString userId allDiscordUsers) others |> String.join ", " |> Ui.text


{-| One channel's worth of unread messages in the overview. The messages are shown the same
way the conversation view shows them, so a line and a header saying which channel they are
from is what separates one channel from the next.
-}
unreadOverviewContainer : UnreadOverviewChannel -> List ( Date, Element FrontendMsg_ ) -> Element FrontendMsg_
unreadOverviewContainer unread messageViews =
    Ui.column
        [ MyUi.noShrinking
        , Ui.borderColor MyUi.border2
        , Ui.borderWith { left = 0, right = 0, top = 0, bottom = 2 }
        ]
        (Ui.row
            [ Ui.spacing 8, Ui.paddingWith { left = 8, right = 8, top = 6, bottom = 0 }, Ui.contentCenterY ]
            [ GuildColumn.elLinkButton
                (unreadOverviewHtmlId "guild_unreadOverviewOpenChannel_" unread.guildOrDmId unread.threadRoute)
                unread.route
                [ Ui.Font.bold
                , Ui.Font.color MyUi.font3
                , Ui.clipWithEllipsis
                , MyUi.hover False [ Ui.Anim.fontColor MyUi.font1 ]
                ]
                unread.source
            , MyUi.elButton
                (unreadOverviewHtmlId "guild_unreadOverviewMarkAsRead_" unread.guildOrDmId unread.threadRoute)
                (PressedMarkChannelAsRead unread.guildOrDmId unread.threadRoute)
                [ Ui.width Ui.shrink
                , Ui.alignRight
                , Ui.paddingXY 8 2
                , Ui.rounded 4
                , Ui.border 1
                , Ui.borderColor MyUi.buttonBorder
                , Ui.background MyUi.buttonBackground
                , Ui.Font.color MyUi.font1
                , MyUi.noShrinking
                ]
                (Ui.text "Mark as read")
            ]
            :: (if unread.additionalUnread > 0 then
                    Ui.el
                        [ Ui.Font.color MyUi.font3
                        , Ui.Font.italic
                        , Ui.paddingWith { left = 8, right = 8, top = 0, bottom = 4 }
                        ]
                        (Ui.text (olderUnreadMessagesText unread.additionalUnread))

                else
                    Ui.none
               )
            :: unreadOverviewMessages messageViews
        )


{-| The messages of one channel in the overview, oldest first, with a date divider above the
oldest one and another wherever the messages pass into a new day. The oldest one gets a
divider too because the messages above it aren't shown, so there's nothing else saying which
day they were written on.
-}
unreadOverviewMessages : List ( Date, Element FrontendMsg_ ) -> List (Element FrontendMsg_)
unreadOverviewMessages messageViews =
    List.foldl
        (\( date, messageView2 ) ( maybeLastDate, list ) ->
            ( Just date
            , if maybeLastDate == Just date then
                messageView2 :: list

              else
                messageView2 :: unreadOverviewDateDivider date :: list
            )
        )
        ( Nothing, [] )
        messageViews
        |> Tuple.second
        |> List.reverse


{-| The day the messages below it were written on. The conversation view shows the day that
ended and the day that started on either side of its divider, but the overview leaves gaps
between the messages it shows, so only the day that starts is meaningful here.
-}
unreadOverviewDateDivider : Date -> Element msg
unreadOverviewDateDivider date =
    Ui.el
        [ Ui.paddingXY 8 0, Ui.height (Ui.px 20), Ui.contentCenterY, MyUi.noShrinking ]
        (Ui.el
            [ Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
            , Ui.borderColor MyUi.font3
            , Ui.inFront
                (Ui.el
                    [ Ui.centerX
                    , Ui.width Ui.shrink
                    , Ui.move { x = 0, y = -9, z = 0 }
                    , Ui.background MyUi.background3
                    , Ui.paddingXY 6 0
                    , Ui.Font.color MyUi.font3
                    , Ui.Font.size 14
                    , Ui.Font.bold
                    ]
                    (Ui.text (MyUi.datestampDate date))
                )
            ]
            Ui.none
        )


{-| Buttons in the overview are named after the channel or thread they belong to, since the
overview shows many of them at once.
-}
unreadOverviewHtmlId : String -> AnyGuildOrDmId -> ThreadRouteWithMessage -> HtmlId
unreadOverviewHtmlId prefix guildOrDmId threadRoute =
    (case guildOrDmId of
        GuildOrDmId (GuildOrDmId_Guild { guildId, channelId }) ->
            "guild_" ++ Id.toString guildId ++ "_" ++ Id.toString channelId

        GuildOrDmId (GuildOrDmId_Dm { otherUserId }) ->
            "dm_" ++ Id.toString otherUserId

        DiscordGuildOrDmId (DiscordGuildOrDmId_Guild { guildId, channelId }) ->
            "discord_" ++ Discord.idToString guildId ++ "_" ++ Discord.idToString channelId

        DiscordGuildOrDmId (DiscordGuildOrDmId_Dm data) ->
            "discordDm_" ++ Discord.idToString data.channelId
    )
        ++ (case threadRoute of
                NoThreadWithMessage _ ->
                    ""

                ViewThreadWithMessage threadId _ ->
                    "_thread_" ++ Id.toString threadId
           )
        |> (\suffix -> Dom.id (prefix ++ suffix))


{-| The button that marks every channel and thread the overview lists as read.
-}
unreadOverviewMarkAllAsReadId : HtmlId
unreadOverviewMarkAllAsReadId =
    Dom.id "guild_unreadOverviewMarkAllAsRead"


{-| A partially muted channel or thread only shows up when it has a red notification.
-}
showInUnreadOverview : IsMuted -> Bool -> Bool
showInUnreadOverview isMuted hasRedNotification =
    case isMuted of
        IsNotMuted ->
            True

        IsPartiallyMuted ->
            hasRedNotification

        IsFullyMuted ->
            False


isMentioned : Maybe (NonemptyDict key OneOrGreater) -> key -> Bool
isMentioned maybeDirectMentions key =
    case maybeDirectMentions of
        Just directMentions ->
            NonemptyDict.member key directMentions

        Nothing ->
            False


{-| The newest unread messages of a channel or thread, oldest first, plus how many older
unread messages aren't shown. The backend only sends `UserSession.unreadOverviewMessageLimit`
of them per channel, but messages that arrive while the overview is open are loaded too, so
the same limit is applied here.

`oldestAt` is when the oldest unread message we have was written, which is older than the
messages we show if some of them were left out. It doesn't change as new messages arrive,
which is what makes it usable for ordering the overview.

-}
unreadMessages :
    Maybe (Id messageId)
    -> { a | messages : MessageArray messageId userId channelId }
    ->
        Maybe
            { messages : List ( Id messageId, Message messageId userId channelId )
            , additionalUnread : Int
            , newestMessageId : Id messageId
            , oldestAt : Time.Posix
            }
unreadMessages maybeLastViewed channel =
    let
        messageCount : Int
        messageCount =
            MessageArray.length channel.messages

        unreadCount : Int
        unreadCount =
            GuildColumn.newMessageCount maybeLastViewed channel

        loaded : List ( Id messageId, Message messageId userId channelId )
        loaded =
            MessageArray.slice
                (messageCount - unreadCount |> Id.fromInt)
                (Id.fromInt messageCount)
                channel.messages
                |> MessageArray.toList

        shown : List ( Id messageId, Message messageId userId channelId )
        shown =
            List.drop (List.length loaded - UserSession.unreadOverviewMessageLimit) loaded
    in
    case ( List.head loaded, List.Extra.last shown ) of
        ( Just ( _, oldest ), Just ( newestMessageId, _ ) ) ->
            Just
                { messages = shown
                , additionalUnread = unreadCount - List.length shown
                , newestMessageId = newestMessageId
                , oldestAt = Message.createdAt oldest
                }

        _ ->
            Nothing


unreadDividerAt : Id messageId -> PreviouslyLastViewedMessage messageId -> Id messageId
unreadDividerAt lastViewed previouslyLastViewedMessage =
    case previouslyLastViewedMessage of
        PreviouslyLastViewedMessage messageId ->
            messageId

        DontCare ->
            lastViewed


dmChannelView : DmRouteData -> LoggedIn2 -> LocalState -> LoadedFrontend -> Element FrontendMsg_
dmChannelView dmRoute loggedIn local model =
    case DmChannelId.otherUserId local.localUser.session.userId dmRoute.channelId of
        Nothing ->
            Ui.el
                [ Ui.centerY
                , Ui.Font.center
                , Ui.Font.color MyUi.font1
                , Ui.Font.size 20
                ]
                (Ui.text "Conversation not found")

        Just otherUserId ->
            case User.getUser otherUserId local.localUser of
                Just otherUser ->
                    let
                        dmChannel : FrontendDmChannel
                        dmChannel =
                            SeqDict.get otherUserId local.dmChannels
                                |> Maybe.withDefault DmChannel.frontendInit

                        missingPrivateKey : Bool
                        missingPrivateKey =
                            case dmChannel.e2ee of
                                E2eeEnabled _ ->
                                    not (SeqSet.member otherUserId loggedIn.e2eeKeysOnThisDevice)

                                E2eeDisabled _ ->
                                    False

                                E2eeRequestedBy _ ->
                                    False

                                E2eeDeclinedBy _ ->
                                    False
                    in
                    case dmRoute.threadRoute of
                        ViewThreadWithFriends threadMessageIndex maybeUrlMessageId _ ->
                            SeqDict.get threadMessageIndex dmChannel.threads
                                |> Maybe.withDefault Thread.frontendInit
                                |> threadConversationView
                                    (let
                                        lastViewed : Id ThreadMessageId
                                        lastViewed =
                                            SeqDict.get
                                                ( GuildOrDmId (GuildOrDmId_Dm { otherUserId = otherUserId }), threadMessageIndex )
                                                local.localUser.user.lastViewedThreadMessage
                                                |> Maybe.withDefault (Id.fromInt -1)
                                     in
                                     case local.localUser.currentlyViewing of
                                        Viewing_DmThread data ->
                                            if data.id == { otherUserId = otherUserId, threadId = threadMessageIndex } then
                                                unreadDividerAt lastViewed data.previouslyLastViewedMessage

                                            else
                                                lastViewed

                                        _ ->
                                            lastViewed
                                    )
                                    (GuildOrDmId_Dm { otherUserId = otherUserId })
                                    maybeUrlMessageId
                                    threadMessageIndex
                                    loggedIn
                                    model
                                    local
                                    missingPrivateKey
                                    (PersonName.toString otherUser.name)
                                    (threadPreviewText
                                        local.localUser.timezone
                                        (User.allUsers local.localUser)
                                        SeqDict.empty
                                        threadMessageIndex
                                        local.localUser.decryptedMessages
                                        dmChannel
                                    )

                        NoThreadWithFriends maybeUrlMessageId _ ->
                            conversationView
                                (let
                                    lastViewed : Id ChannelMessageId
                                    lastViewed =
                                        SeqDict.get
                                            (GuildOrDmId (GuildOrDmId_Dm { otherUserId = otherUserId }))
                                            local.localUser.user.lastViewedMessage
                                            |> Maybe.withDefault (Id.fromInt -1)
                                 in
                                 case local.localUser.currentlyViewing of
                                    Viewing_Dm data ->
                                        if data.id == { otherUserId = otherUserId } then
                                            unreadDividerAt lastViewed data.previouslyLastViewedMessage

                                        else
                                            lastViewed

                                    _ ->
                                        lastViewed
                                )
                                (GuildOrDmId_Dm { otherUserId = otherUserId })
                                maybeUrlMessageId
                                loggedIn
                                model
                                local
                                missingPrivateKey
                                (PersonName.toString otherUser.name)
                                dmChannel

                Nothing ->
                    Ui.el
                        [ Ui.centerY
                        , Ui.Font.center
                        , Ui.Font.color MyUi.font1
                        , Ui.Font.size 20
                        ]
                        (Ui.text "User not found")


discordDmChannelView :
    DiscordDmRouteData
    -> LoggedIn2
    -> LocalState
    -> LoadedFrontend
    -> Element FrontendMsg_
discordDmChannelView routeData loggedIn local model =
    case SeqDict.get routeData.channelId local.discordDmChannels of
        Just dmChannel ->
            discordConversationView
                (let
                    lastViewed : Id ChannelMessageId
                    lastViewed =
                        SeqDict.get
                            (DiscordGuildOrDmId
                                (DiscordGuildOrDmId_Dm
                                    { currentUserId = routeData.currentDiscordUserId, channelId = routeData.channelId }
                                )
                            )
                            local.localUser.user.lastViewedMessage
                            |> Maybe.withDefault (Id.fromInt -1)
                 in
                 case local.localUser.currentlyViewing of
                    Viewing_DiscordDm data ->
                        if
                            data.id
                                == { currentUserId = routeData.currentDiscordUserId
                                   , channelId = routeData.channelId
                                   }
                        then
                            unreadDividerAt lastViewed data.previouslyLastViewedMessage

                        else
                            lastViewed

                    _ ->
                        lastViewed
                )
                routeData.currentDiscordUserId
                (DiscordGuildOrDmId_Dm
                    { currentUserId = routeData.currentDiscordUserId, channelId = routeData.channelId }
                )
                routeData.viewingMessage
                loggedIn
                model
                local
                (NonemptyDict.toSeqDict dmChannel.members
                    |> SeqDict.remove routeData.currentDiscordUserId
                    |> SeqDict.toList
                    |> List.filterMap
                        (\( userId, _ ) ->
                            case User.getDiscordUser userId local.localUser of
                                Just user ->
                                    PersonName.toString user.name |> Just

                                Nothing ->
                                    Nothing
                        )
                    |> String.join ", "
                )
                { messages = dmChannel.messages
                , isForum = False
                , visibleMessages = dmChannel.visibleMessages
                , threads = SeqDict.empty
                , dateDividerDrawings = dmChannel.dateDividerDrawings
                }
                SeqSet.empty
                SeqSet.empty

        Nothing ->
            Ui.el
                [ Ui.centerY
                , Ui.Font.center
                , Ui.Font.color MyUi.font1
                , Ui.Font.size 20
                ]
                (Ui.text "DM channel not found")


conversationWidth : LoadedFrontend -> Int
conversationWidth model =
    MyUi.conversationWidthIgnoreScrollbar
        model.windowSize
        (case Route.toShowMembersTab model.route of
            ( ShowChannelSettings, _ ) ->
                True

            ( HideChannelSettings, _ ) ->
                False
        )
        - model.startupData.scrollbarWidth
        - (User.profileImageSize + (messagePaddingX * 2) + MessageView.profileImagePaddingRight)


guildView : LoadedFrontend -> Id GuildId -> ChannelRoute -> LoggedIn2 -> LocalState -> Element FrontendMsg_
guildView model guildId channelRoute loggedIn local =
    case loggedIn.showFileToUploadInfo of
        Just fileData ->
            FileStatus.imageInfoView local.localUser.safeAreaInsetTop local.localUser.safeAreaInsetBottom model.timezone PressedCloseImageInfo fileData

        Nothing ->
            case SeqDict.get guildId local.guilds of
                Just guild ->
                    if MyUi.isMobile model then
                        let
                            canScroll2 =
                                MyUi.canScroll (MyUi.isMobile model) model.drag

                            showMembers : ( ShowChannelSettings, Bool )
                            showMembers =
                                Route.toShowMembersTabVisible loggedIn model.route
                        in
                        Ui.column
                            [ Ui.height Ui.fill
                            , Ui.background MyUi.background1
                            , Ui.heightMin 0
                            , Ui.clip
                            , (case showMembers of
                                ( ShowChannelSettings, isThread ) ->
                                    channelSettingsMobile
                                        canScroll2
                                        local.localUser
                                        guildId
                                        channelRoute
                                        guild
                                        loggedIn.editChannelForm
                                        isThread
                                        |> Ui.el
                                            [ Ui.height Ui.fill
                                            , Ui.background MyUi.background3
                                            , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                            , Ui.move
                                                { x = Call.memberColumnOffset loggedIn.sidebarMode model
                                                , y = 0
                                                , z = 0
                                                }
                                            , Ui.heightMin 0
                                            ]

                                ( HideChannelSettings, _ ) ->
                                    Ui.none
                              )
                                |> Ui.inFront
                            , channelView channelRoute guildId guild loggedIn local model
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.background MyUi.background3
                                    , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                    , Ui.move
                                        { x = Call.conversationOffset loggedIn.sidebarMode model
                                        , y = 0
                                        , z = 0
                                        }
                                    , Ui.heightMin 0
                                    ]
                                |> Ui.inFront
                            ]
                            [ Ui.row
                                [ Ui.height Ui.fill, Ui.heightMin 0, MyUi.htmlStyle "isolation" "isolate" ]
                                [ GuildColumn.guildColumnLazy True model local
                                , channelColumnLazy True canScroll2 model loggedIn local.localUser local.calls guildId guild channelRoute
                                ]
                            , Ui.Lazy.lazy loggedInAsView local.localUser
                            ]

                    else
                        Ui.row
                            [ Ui.height Ui.fill, Ui.background MyUi.background1 ]
                            [ Ui.column
                                [ Ui.height Ui.fill
                                , Ui.width (Ui.px (MyUi.channelAndGuildColumnWidth model.windowSize))
                                ]
                                [ Ui.row
                                    [ Ui.height Ui.fill, Ui.heightMin 0 ]
                                    [ GuildColumn.guildColumnLazy False model local
                                    , channelColumnLazy False True model loggedIn local.localUser local.calls guildId guild channelRoute
                                    ]
                                , Ui.Lazy.lazy loggedInAsView local.localUser
                                ]
                            , channelView channelRoute guildId guild loggedIn local model
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.background MyUi.background3
                                    , Ui.heightMin 0
                                    , Ui.borderColor MyUi.border1
                                    , Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
                                    ]
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                    ]
                            , case Route.toShowMembersTabVisible loggedIn model.route of
                                ( ShowChannelSettings, isThread ) ->
                                    channelSettingsNotMobile
                                        local.localUser
                                        guildId
                                        channelRoute
                                        guild
                                        loggedIn.editChannelForm
                                        isThread
                                        |> Ui.el
                                            [ Ui.width Ui.shrink
                                            , Ui.height Ui.fill
                                            , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                            ]

                                ( HideChannelSettings, _ ) ->
                                    Ui.none
                            ]

                Nothing ->
                    if MyUi.isMobile model then
                        Ui.column
                            [ Ui.height Ui.fill
                            , Ui.background MyUi.background1
                            , Ui.heightMin 0
                            , Ui.clip
                            ]
                            [ Ui.row
                                [ Ui.height Ui.fill, Ui.heightMin 0 ]
                                [ GuildColumn.guildColumnLazy True model local
                                , pageMissingMobile guildNotFoundText
                                ]
                            , Ui.Lazy.lazy loggedInAsView local.localUser
                            ]

                    else
                        Ui.row
                            [ Ui.height Ui.fill, Ui.background MyUi.background1 ]
                            [ Ui.column
                                [ Ui.height Ui.fill
                                , Ui.width (Ui.px (MyUi.channelAndGuildColumnWidth model.windowSize))
                                ]
                                [ GuildColumn.guildColumnLazy False model local
                                , Ui.Lazy.lazy loggedInAsView local.localUser
                                ]
                            , pageMissing guildNotFoundText
                            ]


nearestHour : Time.Posix -> Int
nearestHour time =
    Time.posixToMillis time // (60 * 60 * 1000) |> (*) (60 * 60 * 1000)


discordGuildView :
    LoadedFrontend
    -> DiscordGuildRouteData
    -> LoggedIn2
    -> LocalState
    -> Element FrontendMsg_
discordGuildView model routeData loggedIn local =
    case loggedIn.showFileToUploadInfo of
        Just fileData ->
            FileStatus.imageInfoView local.localUser.safeAreaInsetTop local.localUser.safeAreaInsetBottom model.timezone PressedCloseImageInfo fileData

        Nothing ->
            case
                ( SeqDict.get routeData.guildId local.discordGuilds
                , LinkedAndOtherDiscordUsers.getLinkedUser routeData.currentDiscordUserId local.localUser.discordUsers
                )
            of
                ( Just guild, Just currentDiscordUser ) ->
                    if MembersAndOwner.isMember routeData.currentDiscordUserId guild.membersAndOwner == IsNotMember then
                        guildErrorPage
                            ("Selected Discord user ("
                                ++ PersonName.toString currentDiscordUser.name
                                ++ ") is not a member of this guild"
                            )
                            local
                            model

                    else if MyUi.isMobile model then
                        let
                            canScroll2 =
                                MyUi.canScroll (MyUi.isMobile model) model.drag

                            showMembers : ( ShowChannelSettings, Bool )
                            showMembers =
                                Route.toShowMembersTabVisible loggedIn model.route
                        in
                        Ui.column
                            [ Ui.height Ui.fill
                            , Ui.background MyUi.background1
                            , Ui.heightMin 0
                            , Ui.clip
                            , (case showMembers of
                                ( ShowChannelSettings, _ ) ->
                                    case routeData.channelRoute of
                                        DiscordChannel_ChannelRoute channelId threadRoute _ ->
                                            Ui.Lazy.lazy6
                                                discordChannelSettingsMobile
                                                canScroll2
                                                local.localUser
                                                routeData
                                                guild
                                                channelId
                                                threadRoute
                                                |> Ui.el
                                                    [ Ui.height Ui.fill
                                                    , Ui.background MyUi.background3
                                                    , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                                    , Ui.move
                                                        { x = Call.memberColumnOffset loggedIn.sidebarMode model
                                                        , y = 0
                                                        , z = 0
                                                        }
                                                    , Ui.heightMin 0
                                                    ]

                                        DiscordChannel_NewChannelRoute ->
                                            discordMemberColumnContainer []

                                        DiscordChannel_GuildSettingsRoute ->
                                            discordMemberColumnContainer []

                                ( HideChannelSettings, _ ) ->
                                    Ui.none
                              )
                                |> Ui.inFront
                            , discordChannelView routeData guild loggedIn local model
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.background MyUi.background3
                                    , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                    , Ui.move
                                        { x = Call.conversationOffset loggedIn.sidebarMode model
                                        , y = 0
                                        , z = 0
                                        }
                                    , Ui.heightMin 0
                                    ]
                                |> Ui.inFront
                            ]
                            [ Ui.row
                                [ Ui.height Ui.fill, Ui.heightMin 0, MyUi.htmlStyle "isolation" "isolate" ]
                                [ GuildColumn.guildColumnLazy True model local
                                , discordChannelColumnLazy True canScroll2 model loggedIn local.localUser routeData guild
                                ]
                            , Ui.Lazy.lazy loggedInAsView local.localUser
                            ]

                    else
                        Ui.row
                            [ Ui.height Ui.fill, Ui.background MyUi.background1 ]
                            [ Ui.column
                                [ Ui.height Ui.fill
                                , Ui.width (Ui.px (MyUi.channelAndGuildColumnWidth model.windowSize))
                                ]
                                [ Ui.row
                                    [ Ui.height Ui.fill, Ui.heightMin 0 ]
                                    [ GuildColumn.guildColumnLazy False model local
                                    , discordChannelColumnLazy False True model loggedIn local.localUser routeData guild
                                    ]
                                , Ui.Lazy.lazy loggedInAsView local.localUser
                                ]
                            , discordChannelView routeData guild loggedIn local model
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.background MyUi.background3
                                    , Ui.heightMin 0
                                    , Ui.borderColor MyUi.border1
                                    , Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
                                    ]
                                |> Ui.el
                                    [ Ui.height Ui.fill
                                    , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                    ]
                            , case Route.toShowMembersTabVisible loggedIn model.route of
                                ( ShowChannelSettings, _ ) ->
                                    case routeData.channelRoute of
                                        DiscordChannel_ChannelRoute channelId threadRoute _ ->
                                            Ui.Lazy.lazy6
                                                discordChannelSettingsNotMobile
                                                local.localUser
                                                routeData.guildId
                                                routeData.currentDiscordUserId
                                                guild
                                                channelId
                                                threadRoute
                                                |> Ui.el
                                                    [ Ui.width Ui.shrink
                                                    , Ui.height Ui.fill
                                                    , Ui.paddingWith { left = 0, right = 0, top = local.localUser.safeAreaInsetTop, bottom = 0 }
                                                    ]

                                        DiscordChannel_NewChannelRoute ->
                                            Ui.none

                                        DiscordChannel_GuildSettingsRoute ->
                                            Ui.none

                                ( HideChannelSettings, _ ) ->
                                    Ui.none
                            ]

                ( Just _, Nothing ) ->
                    guildErrorPage "Discord user not found" local model

                ( Nothing, _ ) ->
                    guildErrorPage "Discord guild not found" local model


guildErrorPage : String -> LocalState -> LoadedFrontend -> Element FrontendMsg_
guildErrorPage error local model =
    if MyUi.isMobile model then
        Ui.column
            [ Ui.height Ui.fill
            , Ui.background MyUi.background1
            , Ui.heightMin 0
            , Ui.clip
            ]
            [ Ui.row
                [ Ui.height Ui.fill, Ui.heightMin 0 ]
                [ GuildColumn.guildColumnLazy True model local
                , pageMissingMobile error
                ]
            , Ui.Lazy.lazy loggedInAsView local.localUser
            ]

    else
        Ui.row
            [ Ui.height Ui.fill, Ui.background MyUi.background1 ]
            [ Ui.column
                [ Ui.height Ui.fill
                , Ui.width (Ui.px (MyUi.channelAndGuildColumnWidth model.windowSize))
                ]
                [ GuildColumn.guildColumnLazy False model local
                , Ui.Lazy.lazy loggedInAsView local.localUser
                ]
            , pageMissing error
            ]


{-| The channel the route has selected, and which of its threads (if any) is open.
The member column is also shown on routes where no channel is selected, hence the
`Maybe`.
-}
channelRouteToChannelIdAndThread : ChannelRoute -> Maybe ( Id ChannelId, ThreadRoute )
channelRouteToChannelIdAndThread channelRoute =
    case channelRoute of
        ChannelRoute channelId threadRoute _ ->
            Just ( channelId, threadRouteWithFriends threadRoute )

        NewChannelRoute ->
            Nothing

        GuildSettingsRoute ->
            Nothing

        JoinRoute _ ->
            Nothing


{-| The thread a route has open, without the extra bits the route carries around for
scrolling to a message and showing the member tab.
-}
threadRouteWithFriends : ThreadRouteWithFriends -> ThreadRoute
threadRouteWithFriends threadRoute =
    case threadRoute of
        ViewThreadWithFriends threadId _ _ ->
            ViewThread threadId

        NoThreadWithFriends _ _ ->
            NoThread


exportChannelButton : ExportChannelId -> Element FrontendMsg_
exportChannelButton exportChannelId =
    MyUi.elButton
        (Dom.id "guild_exportChannel")
        (PressedExportChannel exportChannelId)
        [ Ui.paddingXY 8 4
        , Ui.rounded 4
        , Ui.border 1
        , Ui.borderColor MyUi.buttonBorder
        , Ui.background MyUi.buttonBackground
        , Ui.Font.color MyUi.font1
        , Ui.Font.center
        , Ui.Font.weight 500
        , MyUi.noShrinking
        ]
        (Ui.text "Export channel")


memberColumnContainerNotMobile : Bool -> List (Element FrontendMsg_) -> Element FrontendMsg_
memberColumnContainerNotMobile isThread contents =
    Ui.column
        [ Ui.height Ui.fill
        , Ui.alignRight
        , Ui.background MyUi.background2
        , Ui.Font.color MyUi.font1
        , Ui.width (Ui.px MyUi.memberColumnWidth)
        , Ui.heightMin 0
        , Ui.borderWith { left = 1, right = 0, top = 0, bottom = 0 }
        , Ui.borderColor MyUi.border2
        ]
        [ Ui.row
            [ -- For some reason the bottom border isn't lining up with the ChannelHeader so we need to add a 1px offset
              Ui.height (Ui.px (MyUi.channelHeaderHeight + 1))
            , MyUi.noShrinking
            , Ui.borderWith { left = 0, right = 0, top = 0, bottom = 1 }
            , Ui.borderColor MyUi.border1
            ]
            [ Ui.el
                [ Ui.Font.color MyUi.font3, Ui.paddingXY 8 0 ]
                (if isThread then
                    Ui.text "Thread settings"

                 else
                    Ui.text "Channel settings"
                )
            , MyUi.elButton
                (Dom.id "guild_hideMembers")
                PressedHideMembers
                [ Ui.alignRight
                , Ui.width (Ui.px (24 + 24))
                , Ui.height Ui.fill
                , Ui.paddingXY 12 0
                , Ui.contentCenterY
                , Ui.Font.color MyUi.font3
                , MyUi.hover False [ Ui.Anim.fontColor MyUi.font1 ]
                , MyUi.hoverText "Hide members"
                ]
                (Ui.html Icons.x)
            ]
        , Ui.column
            [ Ui.height Ui.fill
            , Ui.scrollable
            , Ui.heightMin 0
            ]
            contents
        ]


{-| Only actual channels can be edited. Threads only get the mute setting, and the
other channel routes show nothing at all.
-}
channelSettingsForm :
    LocalUser
    -> Id GuildId
    -> ChannelRoute
    -> FrontendGuild
    -> SeqDict ( Id GuildId, Id ChannelId ) EditChannelForm
    -> Element FrontendMsg_
channelSettingsForm localUser guildId channelRoute guild editChannelForm =
    case channelRouteToChannelIdAndThread channelRoute of
        Just ( channelId, NoThread ) ->
            case SeqDict.get channelId guild.channels of
                Just channel ->
                    (if localUser.session.userId == MembersAndOwner.owner guild.membersAndOwner then
                        let
                            form : EditChannelForm
                            form =
                                SeqDict.get ( guildId, channelId ) editChannelForm
                                    |> Maybe.withDefault (editChannelFormInit channel)

                            isEmpty : Bool
                            isEmpty =
                                MessageArray.isEmpty channel.messages

                            channelNameString : String
                            channelNameString =
                                ChannelName.toString channel.name

                            channelDescriptionString : String
                            channelDescriptionString =
                                ChannelDescription.toString channel.description

                            hasChanges : Bool
                            hasChanges =
                                form.name /= channelNameString || form.description /= channelDescriptionString

                            confirmationMatches : Bool
                            confirmationMatches =
                                form.deleteConfirmation == channelNameString

                            ( deleteOnPress, deleteEnabled ) =
                                if isEmpty then
                                    ( PressedDeleteChannel guildId channelId, True )

                                else if not form.showDeleteConfirmation then
                                    ( EditChannelFormChanged guildId channelId { form | showDeleteConfirmation = True }, True )

                                else if confirmationMatches then
                                    ( PressedDeleteChannel guildId channelId, True )

                                else
                                    ( FrontendNoOp, False )
                        in
                        [ channelNameInput form |> Ui.map (EditChannelFormChanged guildId channelId)
                        , channelDescriptionInput form |> Ui.map (EditChannelFormChanged guildId channelId)
                        , MuteSettings.view
                            (PressedMuteChannel guildId channelId)
                            (MuteSettings.isChannelSpecificallyMuted localUser.user.muteSettings guildId channelId)
                        , if hasChanges then
                            Ui.row
                                [ Ui.spacing 8 ]
                                [ MyUi.secondaryButton
                                    (Dom.id "guild_resetEditChannel")
                                    (PressedResetEditChannelChanges guildId channelId)
                                    "Reset"
                                , submitButtonWide
                                    (Dom.id "guild_submitEditChannel")
                                    (PressedSubmitEditChannelChanges guildId channelId form)
                                    "Save changes"
                                ]

                          else
                            Ui.none
                        , exportChannelButton (ExportChannel_Guild guildId channelId)
                        , Ui.el [ Ui.height (Ui.px 1), Ui.background MyUi.border1 ] Ui.none
                        , if not isEmpty && form.showDeleteConfirmation then
                            deleteConfirmationInput channelNameString form
                                |> Ui.map (EditChannelFormChanged guildId channelId)

                          else
                            Ui.none
                        , MyUi.elButton
                            (Dom.id "guild_deleteChannel")
                            deleteOnPress
                            [ Ui.background
                                (if deleteEnabled then
                                    MyUi.deleteButtonBackground

                                 else
                                    MyUi.disabledButtonBackground
                                )
                            , Ui.paddingXY 8 4
                            , Ui.rounded 4
                            , Ui.Font.color MyUi.deleteButtonFont
                            , Ui.Font.weight 500
                            , Ui.Font.center
                            , Ui.borderColor
                                (if deleteEnabled then
                                    MyUi.deleteButtonBorder

                                 else
                                    MyUi.disabledButtonBorder
                                )
                            , Ui.border 1
                            ]
                            (Ui.text "Delete channel")
                        ]

                     else
                        [ MuteSettings.view
                            (PressedMuteChannel guildId channelId)
                            (MuteSettings.isChannelSpecificallyMuted localUser.user.muteSettings guildId channelId)
                        , exportChannelButton (ExportChannel_Guild guildId channelId)
                        ]
                    )
                        |> Ui.column [ Ui.Font.color MyUi.font1, Ui.padding 8, Ui.spacing 16 ]

                Nothing ->
                    Ui.none

        Just ( channelId, ViewThread threadId ) ->
            [ MuteSettings.view
                (PressedMuteThread guildId channelId threadId)
                (MuteSettings.isThreadSpecificallyMuted localUser.user.muteSettings guildId channelId threadId)
            ]
                |> Ui.column [ Ui.Font.color MyUi.font1, Ui.padding 8, Ui.spacing 16 ]

        Nothing ->
            Ui.none


memberListView : Bool -> LocalUser -> MembersAndOwner (Id UserId) LocalState.GuildMember -> Element FrontendMsg_
memberListView isMobile localUser membersAndOwner =
    let
        members : SeqDict (Id UserId) LocalState.GuildMember
        members =
            MembersAndOwner.members membersAndOwner
    in
    Ui.column
        []
        [ Ui.el [ Ui.paddingXY 8 4 ] (Ui.text ("Members (" ++ String.fromInt (SeqDict.size members + 1) ++ ")"))
        , Ui.column
            [ Ui.height Ui.fill ]
            (memberLabel isMobile localUser (MembersAndOwner.owner membersAndOwner)
                :: SeqDict.foldr (\userId _ list -> memberLabel isMobile localUser userId :: list) [] members
            )
        ]


discordMemberListView :
    Bool
    -> Discord.Id Discord.UserId
    -> LocalUser
    -> Discord.Id Discord.GuildId
    -> DiscordFrontendGuild
    -> Discord.Id Discord.ChannelId
    -> Element FrontendMsg_
discordMemberListView isMobile currentUserId localUser guildId guild channelId =
    case discordChannelViewers guildId guild channelId of
        Nothing ->
            Ui.none

        Just members ->
            Ui.column
                []
                [ Ui.el [ Ui.paddingXY 8 4 ] (Ui.text ("Members (" ++ String.fromInt (SeqDict.size members) ++ ")"))
                , Ui.column
                    [ Ui.height Ui.fill ]
                    (SeqDict.foldr
                        (\userId _ list -> discordMemberLabel isMobile localUser currentUserId userId :: list)
                        []
                        members
                    )
                ]


channelSettingsNotMobile :
    LocalUser
    -> Id GuildId
    -> ChannelRoute
    -> FrontendGuild
    -> SeqDict ( Id GuildId, Id ChannelId ) EditChannelForm
    -> Bool
    -> Element FrontendMsg_
channelSettingsNotMobile localUser guildId channelRoute guild editChannelForm isThread =
    memberColumnContainerNotMobile
        isThread
        [ channelSettingsForm localUser guildId channelRoute guild editChannelForm
        , Ui.Lazy.lazy3 memberListView False localUser guild.membersAndOwner
        ]


{-| Determine which guild members can view the given channel, following the
channel's permission overwrites. The owner is not included here since it's
shown separately and can always view every channel. Returns `Nothing` when no
channel is selected, so the member column can be hidden.
-}
discordChannelViewers :
    Discord.Id Discord.GuildId
    -> DiscordFrontendGuild
    -> Discord.Id Discord.ChannelId
    -> Maybe (SeqDict (Discord.Id Discord.UserId) { joinedAt : Maybe Time.Posix, roles : SeqSet (Discord.Id Discord.RoleId) })
discordChannelViewers guildId guild channelId =
    case SeqDict.get channelId guild.channels of
        Just channel ->
            MembersAndOwner.members guild.membersAndOwner
                |> SeqDict.filter (\userId _ -> LocalState.canViewDiscordChannel guildId channel guild userId)
                |> Just

        Nothing ->
            Nothing


discordChannelSettingsNotMobile :
    LocalUser
    -> Discord.Id Discord.GuildId
    -> Discord.Id Discord.UserId
    -> DiscordFrontendGuild
    -> Discord.Id Discord.ChannelId
    -> ThreadRouteWithFriends
    -> Element FrontendMsg_
discordChannelSettingsNotMobile localUser guildId currentDiscordUserId guild channelId threadRoute =
    memberColumnContainerNotMobile
        (case threadRoute of
            NoThreadWithFriends _ _ ->
                False

            ViewThreadWithFriends _ _ _ ->
                True
        )
        [ discordChannelSettingsForm localUser currentDiscordUserId guildId channelId threadRoute
        , Ui.Lazy.lazy6 discordMemberListView False currentDiscordUserId localUser guildId guild channelId
        ]


{-| Discord channels are managed on Discord, so the only thing to change here is whether
the channel (or the thread inside it) is muted.
-}
discordChannelSettingsForm :
    LocalUser
    -> Discord.Id Discord.UserId
    -> Discord.Id Discord.GuildId
    -> Discord.Id Discord.ChannelId
    -> ThreadRouteWithFriends
    -> Element FrontendMsg_
discordChannelSettingsForm localUser currentDiscordUserId guildId channelId threadRoute =
    (case threadRoute of
        NoThreadWithFriends _ _ ->
            [ MuteSettings.view
                (PressedMuteDiscordChannel currentDiscordUserId guildId channelId)
                (MuteSettings.isDiscordChannelSpecificallyMuted localUser.user.muteSettings guildId channelId)
            , exportChannelButton (ExportChannel_Discord currentDiscordUserId guildId channelId)
            ]

        ViewThreadWithFriends threadId _ _ ->
            [ MuteSettings.view
                (PressedMuteDiscordThread currentDiscordUserId guildId channelId threadId)
                (MuteSettings.isDiscordThreadSpecificallyMuted localUser.user.muteSettings guildId channelId threadId)
            ]
    )
        |> Ui.column [ Ui.Font.color MyUi.font1, Ui.padding 8, Ui.spacing 16 ]


discordMemberColumnContainer : List (Element msg) -> Element msg
discordMemberColumnContainer contents =
    Ui.column
        [ Ui.height Ui.fill
        , Ui.alignRight
        , Ui.background MyUi.background2
        , Ui.Font.color MyUi.font1
        , Ui.width (Ui.px MyUi.memberColumnWidth)
        , Ui.scrollable
        , Ui.heightMin 0
        , Ui.paddingXY 8 4
        ]
        contents


channelSettingsMobile :
    Bool
    -> LocalUser
    -> Id GuildId
    -> ChannelRoute
    -> FrontendGuild
    -> SeqDict ( Id GuildId, Id ChannelId ) EditChannelForm
    -> Bool
    -> Element FrontendMsg_
channelSettingsMobile canScroll2 localUser guildId channelRoute guild editChannelForm isThread =
    Ui.column
        [ Ui.height Ui.fill ]
        [ Ui.row
            [ Ui.contentCenterY
            , Ui.borderWith { left = 0, right = 0, top = 0, bottom = 1 }
            , Ui.borderColor MyUi.border2
            , Ui.background MyUi.background3
            , Ui.height (Ui.px MyUi.channelHeaderHeight)
            , MyUi.noShrinking
            ]
            [ ChannelHeader.headerBackButton (Dom.id "guild_memberColumnBack") PressedMemberListBack
            , if isThread then
                Ui.text "Thread settings"

              else
                Ui.text "Channel settings"
            ]
        , Ui.column
            [ Ui.height Ui.fill
            , Ui.background MyUi.background2
            , Ui.Font.color MyUi.font1
            , Ui.paddingWith { left = 0, right = 0, top = 16, bottom = localUser.safeAreaInsetBottom + 16 }
            , MyUi.scrollable canScroll2
            , Ui.heightMin 0
            ]
            [ channelSettingsForm localUser guildId channelRoute guild editChannelForm
            , Ui.Lazy.lazy3 memberListView True localUser guild.membersAndOwner
            ]
        ]


discordChannelSettingsMobile :
    Bool
    -> LocalUser
    -> DiscordGuildRouteData
    -> DiscordFrontendGuild
    -> Discord.Id Discord.ChannelId
    -> ThreadRouteWithFriends
    -> Element FrontendMsg_
discordChannelSettingsMobile canScroll2 localUser routeData guild channelId threadRoute =
    Ui.column
        [ Ui.height Ui.fill ]
        [ Ui.row
            [ Ui.contentCenterY
            , Ui.borderWith { left = 0, right = 0, top = 0, bottom = 1 }
            , Ui.borderColor MyUi.border2
            , Ui.background MyUi.background3
            , Ui.height (Ui.px MyUi.channelHeaderHeight)
            , MyUi.noShrinking
            ]
            [ ChannelHeader.headerBackButton (Dom.id "guild_memberColumnBack") PressedMemberListBack
            , case threadRoute of
                ViewThreadWithFriends _ _ _ ->
                    Ui.text "Thread members"

                NoThreadWithFriends _ _ ->
                    Ui.text "Channel members"
            ]
        , Ui.column
            [ Ui.height Ui.fill
            , Ui.background MyUi.background2
            , Ui.Font.color MyUi.font1
            , Ui.paddingWith { left = 0, right = 0, top = 16, bottom = localUser.safeAreaInsetBottom + 16 }
            , MyUi.scrollable canScroll2
            , Ui.heightMin 0
            ]
            [ discordChannelSettingsForm
                localUser
                routeData.currentDiscordUserId
                routeData.guildId
                channelId
                threadRoute
            , discordMemberListView True routeData.currentDiscordUserId localUser routeData.guildId guild channelId
            ]
        ]


{-| A DM only contains the user and the person they are talking to, unless
they're talking to themselves.
-}
dmMembers : LocalUser -> Id UserId -> List (Id UserId)
dmMembers localUser otherUserId =
    if localUser.session.userId == otherUserId then
        [ otherUserId ]

    else
        [ localUser.session.userId, otherUserId ]


{-| Whether the end-to-end encryption section of a DM's channel settings is open. It
opens on its own while the other person is waiting for an answer, up until the user opens
or closes it themselves.
-}
e2eeSectionIsExpanded : Id UserId -> LocalState -> LoggedIn2 -> Bool
e2eeSectionIsExpanded otherUserId local loggedIn =
    Maybe.withDefault
        (ChannelHeader.showDmSettingsRedDot otherUserId local loggedIn)
        (SeqDict.get otherUserId loggedIn.e2eeSectionsExpanded)


dmE2eeStatus : Id UserId -> LocalState -> E2eeStatus
dmE2eeStatus otherUserId local =
    case SeqDict.get otherUserId local.dmChannels of
        Just dmChannel ->
            dmChannel.e2ee

        Nothing ->
            DmChannel.E2eeDisabled Nothing


e2eeSectionView :
    LocalUser
    -> Id UserId
    -> E2eeStatus
    -> Bool
    -> E2eeKeyInput
    -> Element FrontendMsg_
e2eeSectionView localUser otherUserId e2ee isExpanded keyInput =
    let
        risksAccepted : Bool
        risksAccepted =
            localUser.user.e2eeRisksAccepted

        risksLabel : { element : Element FrontendMsg_, id : Ui.Input.Label }
        risksLabel =
            Ui.Input.label
                "guild_e2eeAcceptRisks"
                [ Ui.paddingWith { left = 16, right = 0, top = 0, bottom = 0 }, Ui.pointer, Ui.width Ui.shrink ]
                (Ui.text "I understand and accept the\u{00A0}risks")
    in
    MyUi.container
        16
        isExpanded
        (Dom.id "guild_e2eeSection")
        (PressedExpandE2eeSection otherUserId)
        MyUi.background2
        True
        Encryption.e2eeSectionTitle
        [ Ui.column
            [ Ui.paddingWith { left = 8, right = 8, top = 8, bottom = 0 }, Ui.spacing 16 ]
            [ case e2ee of
                DmChannel.E2eeRequestedBy ( requestedBy, _ ) ->
                    if requestedBy == localUser.session.userId then
                        Ui.none

                    else
                        Ui.column
                            [ Ui.spacing 16 ]
                            [ Ui.Prose.paragraph
                                [ MyUi.htmlStyle "word-wrap" "anywhere", Ui.paddingXY 0 4 ]
                                [ Ui.el [ Ui.Font.bold ] (Ui.text (User.toStringAlt otherUserId localUser))
                                , Ui.text " would like to enable E2EE. You can either:"
                                ]
                            , MyUi.simpleButton
                                (Dom.id "guild_declineE2ee")
                                (PressedDeclineE2eeRequest otherUserId)
                                (Ui.text Encryption.declineE2eeText)
                            , Ui.text "Or follow the instructions below to set it up."
                            , Ui.el [ Ui.height (Ui.px 1), Ui.background MyUi.font1 ] Ui.none
                            ]

                DmChannel.E2eeDisabled _ ->
                    Ui.none

                DmChannel.E2eeDeclinedBy _ ->
                    Ui.none

                DmChannel.E2eeEnabled _ ->
                    Ui.none
            , Ui.column
                [ Ui.attrIf risksAccepted (Ui.opacity 0.5), Ui.spacing 8 ]
                [ MyUi.warningHeader "Before you enable E2EE:"
                , Ui.text "You'll get a private key that you need to store in a password manager. If you lose it, you'll permanently lose access to all your encrypted messages."
                , Ui.el
                    [ Ui.Font.color MyUi.textLinkColorOnDarkBackground
                    , Ui.link
                        (Route.encode
                            (Route.DmRoute
                                { channelId = DmChannelId.fromUserIds otherUserId localUser.session.userId
                                , threadRoute = NoThreadWithFriends Nothing ShowChannelSettings
                                , tab = Nothing
                                , channelsVisible = ChannelsVisibleOnMobile
                                , overlay = Just Route.E2eeInfoOverlay
                                }
                            )
                        )
                    ]
                    (Ui.text "Read more about E2EE here")
                , Ui.row
                    []
                    [ Ui.Input.checkbox
                        []
                        { onChange = PressedE2eeRisksAccepted
                        , icon = Nothing
                        , checked = risksAccepted
                        , label = risksLabel.id
                        }
                    , risksLabel.element
                    ]
                ]
            , case localUser.user.publicKey of
                Just _ ->
                    Ui.text "1. Create private key: completed!"

                Nothing ->
                    Ui.none
            , case e2ee of
                DmChannel.E2eeDisabled disabledBy ->
                    Ui.column
                        [ Ui.spacing 16 ]
                        [ case disabledBy of
                            Just ( disabledBy2, disabledAt ) ->
                                Ui.Prose.paragraph
                                    [ MyUi.htmlStyle "word-wrap" "anywhere", Ui.paddingXY 0 4 ]
                                    [ Ui.text "2. "
                                    , if disabledBy2 == localUser.session.userId then
                                        Ui.text "You"

                                      else
                                        Ui.el [ Ui.Font.bold ] (Ui.text (User.toStringAlt disabledBy2 localUser))
                                    , Ui.text (" disabled E2EE on " ++ MyUi.datestampNoLineBreaks localUser.timezone disabledAt)
                                    ]

                            Nothing ->
                                Ui.none
                        , if not risksAccepted then
                            Ui.none

                          else
                            case localUser.user.publicKey of
                                Nothing ->
                                    createPrivateKeyButton

                                Just _ ->
                                    MyUi.simpleButton
                                        (Dom.id "guild_enableE2ee")
                                        (PressedEnableE2ee otherUserId)
                                        (Ui.text (Encryption.enableE2eeText disabledBy))
                        ]

                DmChannel.E2eeRequestedBy ( requestedBy, _ ) ->
                    if requestedBy /= localUser.session.userId then
                        Ui.column
                            [ Ui.spacing 16 ]
                            [ if not risksAccepted then
                                Ui.none

                              else
                                case localUser.user.publicKey of
                                    Nothing ->
                                        createPrivateKeyButton

                                    Just _ ->
                                        privateKeyInput
                                            otherUserId
                                            (Ui.text Encryption.enterPrivateKeyText)
                                            keyInput
                            ]

                    else if otherUserId == localUser.session.userId then
                        privateKeyInput
                            otherUserId
                            (Ui.text Encryption.enterPrivateKeyText)
                            keyInput

                    else
                        Ui.column
                            [ Ui.spacing 16 ]
                            [ Ui.Prose.paragraph
                                [ MyUi.htmlStyle "word-wrap" "anywhere", Ui.paddingXY 0 4 ]
                                [ Ui.text Encryption.waitingForE2eeText
                                , Ui.el [ Ui.Font.bold ] (Ui.text (User.toStringAlt otherUserId localUser))
                                , Ui.text Encryption.toAcceptE2eeText
                                ]
                            , MyUi.simpleButton
                                (Dom.id "guild_cancelE2ee")
                                (PressedCancelE2eeRequest otherUserId)
                                (Ui.text "Cancel")
                            ]

                DmChannel.E2eeDeclinedBy declinedBy ->
                    if declinedBy == localUser.session.userId then
                        Ui.column
                            [ Ui.spacing 16 ]
                            [ Ui.Prose.paragraph [] [ Ui.text Encryption.youDeclinedE2eeText ]
                            , if not risksAccepted then
                                Ui.none

                              else
                                case localUser.user.publicKey of
                                    Nothing ->
                                        createPrivateKeyButton

                                    Just _ ->
                                        MyUi.simpleButton
                                            (Dom.id "guild_enableE2ee")
                                            (PressedEnableE2ee otherUserId)
                                            (Ui.text (Encryption.enableE2eeText Nothing))
                            ]

                    else
                        Ui.Prose.paragraph
                            []
                            [ Ui.text (User.toStringAlt otherUserId localUser ++ " " ++ Encryption.e2eeDeclinedText) ]

                DmChannel.E2eeEnabled data ->
                    Ui.column
                        [ Ui.spacing 16 ]
                        [ Ui.text ("2. E2EE was enabled on " ++ MyUi.datestampNoLineBreaks localUser.timezone data.enabledAt)
                        , if keyInput.hasKeyOnThisDevice then
                            MyUi.simpleButton
                                (Dom.id "guild_disableE2ee")
                                (PressedDisableE2ee otherUserId)
                                (Ui.text Encryption.disableE2eeText)

                          else
                            privateKeyInput
                                otherUserId
                                (if data.requestedBy == ( localUser.session.userId, localUser.session.sessionIdHash ) then
                                    "3. "
                                        ++ User.toStringAlt otherUserId localUser
                                        ++ " "
                                        ++ Encryption.requestAcceptedText
                                        |> Ui.text

                                 else
                                    Ui.text Encryption.missingPrivateKeyText
                                )
                                keyInput
                        ]
            ]
        ]


{-| Pulls together what the private key box for one conversation needs from the model.
-}
e2eeKeyInput : Id UserId -> LoggedIn2 -> E2eeKeyInput
e2eeKeyInput otherUserId loggedIn =
    { text = loggedIn.e2eePrivateKeyText
    , error = loggedIn.e2eeError
    , hasKeyOnThisDevice = SeqSet.member otherUserId loggedIn.e2eeKeysOnThisDevice
    }


{-| What the private key box needs to draw itself: what has been typed so far, whether
anything went wrong with the last attempt, and whether this device already has a key and
so does not need to ask at all.
-}
type alias E2eeKeyInput =
    { text : String
    , error : Maybe String
    , hasKeyOnThisDevice : Bool
    }


privateKeyInput : Id UserId -> Element FrontendMsg_ -> E2eeKeyInput -> Element FrontendMsg_
privateKeyInput otherUserId prompt keyInput =
    let
        keyLabel : { element : Element FrontendMsg_, id : Ui.Input.Label }
        keyLabel =
            Ui.Input.label "guild_e2eePrivateKey" [ MyUi.htmlStyle "word-wrap" "anywhere" ] prompt
    in
    Ui.column
        [ Ui.spacing 4 ]
        [ keyLabel.element
        , Ui.Input.currentPassword
            [ Ui.background MyUi.inputBackground
            , Ui.paddingXY 8 8
            , Ui.borderColor MyUi.inputBorder
            ]
            { text = keyInput.text
            , onChange = TypedPrivateKey otherUserId
            , placeholder = Just "Your private key"
            , label = keyLabel.id
            , show = False
            }
            |> Ui.el [ ChannelHeader.e2eeRequestDot, Ui.widthMax 300 ]
        , case keyInput.error of
            Just error ->
                Ui.el [ Ui.Font.color MyUi.errorColor ] (Ui.text error)

            Nothing ->
                Ui.none
        ]


createPrivateKeyButton : Element FrontendMsg_
createPrivateKeyButton =
    MyUi.simpleButton
        (Dom.id "guild_addPrivateKey")
        PressedAddPrivateKeyToAccount
        (Ui.text "Create a private key")


dmChannelSettingsNotMobile :
    LocalUser
    -> Id UserId
    -> Bool
    -> E2eeStatus
    -> Bool
    -> E2eeKeyInput
    -> Element FrontendMsg_
dmChannelSettingsNotMobile localUser otherUserId isThread e2ee isExpanded keyInput =
    let
        members : List (Id UserId)
        members =
            dmMembers localUser otherUserId
    in
    memberColumnContainerNotMobile
        isThread
        [ if isThread then
            Ui.none

          else
            Ui.el [ Ui.paddingXY 8 8 ] (exportChannelButton (ExportChannel_Dm otherUserId))
        , Ui.column
            [ Ui.paddingWith { left = 0, right = 0, top = 4, bottom = 16 } ]
            [ Ui.el [ Ui.paddingXY 8 0 ] (Ui.text ("Members (" ++ String.fromInt (List.length members) ++ ")"))
            , Ui.column
                [ Ui.height Ui.fill ]
                (List.map (memberLabel False localUser) members)
            ]
        , if isThread then
            Ui.none

          else
            e2eeSectionView localUser otherUserId e2ee isExpanded keyInput
        ]


dmChannelSettingsMobile :
    Bool
    -> LocalUser
    -> Id UserId
    -> Bool
    -> E2eeStatus
    -> Bool
    -> E2eeKeyInput
    -> Element FrontendMsg_
dmChannelSettingsMobile canScroll2 localUser otherUserId isThread e2ee isExpanded keyInput =
    let
        members : List (Id UserId)
        members =
            dmMembers localUser otherUserId
    in
    Ui.column
        [ Ui.height Ui.fill ]
        [ Ui.row
            [ Ui.contentCenterY
            , Ui.borderWith { left = 0, right = 0, top = 0, bottom = 1 }
            , Ui.borderColor MyUi.border2
            , Ui.background MyUi.background3
            , Ui.height (Ui.px MyUi.channelHeaderHeight)
            , MyUi.noShrinking
            ]
            [ ChannelHeader.headerBackButton (Dom.id "guild_memberColumnBack") PressedMemberListBack
            , if isThread then
                Ui.text "Thread settings"

              else
                Ui.text "Channel settings"
            ]
        , Ui.column
            [ Ui.height Ui.fill
            , Ui.background MyUi.background2
            , Ui.Font.color MyUi.font1
            , Ui.paddingWith { left = 0, right = 0, top = 16, bottom = localUser.safeAreaInsetBottom + 16 }
            , MyUi.scrollable canScroll2
            , Ui.heightMin 0
            ]
            [ if isThread then
                Ui.none

              else
                Ui.el [ Ui.paddingXY 8 4 ] (exportChannelButton (ExportChannel_Dm otherUserId))
            , Ui.column
                [ Ui.paddingXY 8 4 ]
                [ Ui.text ("Members (" ++ String.fromInt (List.length members) ++ ")")
                , Ui.column
                    [ Ui.height Ui.fill ]
                    (List.map (memberLabel True localUser) members)
                ]
            , if isThread then
                Ui.none

              else
                e2eeSectionView localUser otherUserId e2ee isExpanded keyInput
            ]
        ]


discordDmMemberColumnNotMobile :
    LocalUser
    -> Discord.Id Discord.UserId
    -> Discord.Id Discord.PrivateChannelId
    -> DiscordFrontendDmChannel
    -> Element FrontendMsg_
discordDmMemberColumnNotMobile localUser currentDiscordUserId channelId dmChannel =
    let
        members : List (Discord.Id Discord.UserId)
        members =
            NonemptyDict.keys dmChannel.members |> List.Nonempty.toList
    in
    memberColumnContainerNotMobile
        False
        [ Ui.column
            [ Ui.paddingXY 8 4 ]
            [ Ui.el
                [ Ui.paddingXY 0 4 ]
                (exportChannelButton (ExportChannel_DiscordDm currentDiscordUserId channelId))
            , Ui.text ("Members (" ++ String.fromInt (List.length members) ++ ")")
            , Ui.column
                [ Ui.height Ui.fill ]
                (List.map (discordMemberLabel False localUser currentDiscordUserId) members)
            ]
        ]


discordDmChannelSettingsMobile :
    Bool
    -> LocalUser
    -> Discord.Id Discord.UserId
    -> Discord.Id Discord.PrivateChannelId
    -> DiscordFrontendDmChannel
    -> Element FrontendMsg_
discordDmChannelSettingsMobile canScroll2 localUser currentDiscordUserId channelId dmChannel =
    let
        members : List (Discord.Id Discord.UserId)
        members =
            NonemptyDict.keys dmChannel.members |> List.Nonempty.toList
    in
    Ui.column
        [ Ui.height Ui.fill ]
        [ Ui.row
            [ Ui.contentCenterY
            , Ui.borderWith { left = 0, right = 0, top = 0, bottom = 1 }
            , Ui.borderColor MyUi.border2
            , Ui.background MyUi.background3
            , Ui.height (Ui.px MyUi.channelHeaderHeight)
            , MyUi.noShrinking
            ]
            [ ChannelHeader.headerBackButton (Dom.id "guild_memberColumnBack") PressedMemberListBack
            , Ui.text "Channel settings"
            ]
        , Ui.column
            [ Ui.height Ui.fill
            , Ui.background MyUi.background2
            , Ui.Font.color MyUi.font1
            , Ui.paddingWith { left = 0, right = 0, top = 16, bottom = localUser.safeAreaInsetBottom + 16 }
            , MyUi.scrollable canScroll2
            , Ui.heightMin 0
            ]
            [ Ui.el
                [ Ui.paddingXY 8 4 ]
                (exportChannelButton (ExportChannel_DiscordDm currentDiscordUserId channelId))
            , Ui.column
                [ Ui.paddingXY 8 4 ]
                [ Ui.text ("Members (" ++ String.fromInt (List.length members) ++ ")")
                , Ui.column
                    [ Ui.height Ui.fill ]
                    (List.map (discordMemberLabel True localUser currentDiscordUserId) members)
                ]
            ]
        ]


memberLabel : Bool -> LocalUser -> Id UserId -> Element FrontendMsg_
memberLabel isMobile localUser userId =
    GuildColumn.rowLinkButton
        (Dom.id ("guild_openDm_" ++ Id.toString userId))
        (DmRoute
            { channelId = DmChannelId.fromUserIds localUser.session.userId userId
            , threadRoute = NoThreadWithFriends Nothing HideChannelSettings
            , tab = Nothing
            , channelsVisible = ChannelsHiddenOnMobile
            , overlay = Nothing
            }
        )
        [ Ui.spacing 8
        , Ui.paddingXY 8 4
        , MyUi.hover
            isMobile
            [ Ui.Anim.backgroundColor MyUi.weakHoverHighlight
            , Ui.Anim.fontColor MyUi.font1
            ]
        , Ui.Font.color MyUi.font3
        , Ui.clipWithEllipsis
        ]
        (case User.getUser userId localUser of
            Just user ->
                [ User.profileImage (Just user), Ui.text (PersonName.toString user.name) ]

            Nothing ->
                []
        )


discordMemberLabel :
    Bool
    -> LocalUser
    -> Discord.Id Discord.UserId
    -> Discord.Id Discord.UserId
    -> Element FrontendMsg_
discordMemberLabel isMobile localUser currentUserId userId =
    MyUi.rowButton
        (Dom.id ("guild_openDiscordDm_" ++ Discord.idToString userId))
        (PressedDiscordGuildMemberLabel { currentUserId = currentUserId, otherUserId = userId })
        [ Ui.spacing 8
        , Ui.paddingXY 8 4
        , MyUi.hover
            isMobile
            [ Ui.Anim.backgroundColor MyUi.weakHoverHighlight
            , Ui.Anim.fontColor MyUi.font1
            ]
        , Ui.Font.color MyUi.font3
        , Ui.clipWithEllipsis
        ]
        (case User.getDiscordUser userId localUser of
            Just user ->
                [ User.discordProfileImage userId user.icon, Ui.text (PersonName.toString user.name) ]

            Nothing ->
                []
        )


pageMissing : String -> Element msg
pageMissing text =
    Ui.el
        [ Ui.height Ui.fill
        , Ui.contentCenterY
        , Ui.Font.center
        , Ui.Font.color MyUi.font1
        , Ui.Font.size 20
        , Ui.background MyUi.background3
        ]
        (Ui.text text)


pageMissingMobile : String -> Element msg
pageMissingMobile text =
    Ui.el
        [ Ui.height Ui.fill
        , Ui.contentCenterY
        , Ui.Font.center
        , Ui.Font.color MyUi.font1
        , Ui.Font.size 20
        , Ui.background MyUi.background2
        ]
        (Ui.text text)


threadPreviewText :
    Time.Zone
    -> SeqDict userId { a | name : PersonName }
    -> SeqDict ( channelId, Maybe (Id ChannelMessageId) ) { c | name : String }
    -> Id ChannelMessageId
    -> SeqDict BytesHash (Result () (MessageContent userId channelId))
    -> { b | messages : MessageArray ChannelMessageId userId channelId }
    -> String
threadPreviewText timezone allUsers channels threadMessageIndex decrypted channel =
    case MessageArray.get threadMessageIndex channel.messages of
        Just message ->
            LocalState.messageToString timezone allUsers channels decrypted message

        _ ->
            "Thread not found"


channelView : ChannelRoute -> Id GuildId -> FrontendGuild -> LoggedIn2 -> LocalState -> LoadedFrontend -> Element FrontendMsg_
channelView channelRoute guildId guild loggedIn local model =
    case channelRoute of
        ChannelRoute channelId threadRoute _ ->
            case SeqDict.get channelId guild.channels of
                Just channel ->
                    case threadRoute of
                        ViewThreadWithFriends threadMessageIndex maybeUrlMessageId _ ->
                            SeqDict.get threadMessageIndex channel.threads
                                |> Maybe.withDefault Thread.frontendInit
                                |> threadConversationView
                                    (let
                                        lastViewed : Id ThreadMessageId
                                        lastViewed =
                                            SeqDict.get
                                                ( GuildOrDmId (GuildOrDmId_Guild { guildId = guildId, channelId = channelId }), threadMessageIndex )
                                                local.localUser.user.lastViewedThreadMessage
                                                |> Maybe.withDefault (Id.fromInt -1)
                                     in
                                     case local.localUser.currentlyViewing of
                                        Viewing_ChannelThread data ->
                                            if
                                                data.id
                                                    == { guildId = guildId
                                                       , channelId = channelId
                                                       , threadId = threadMessageIndex
                                                       }
                                            then
                                                unreadDividerAt lastViewed data.previouslyLastViewedMessage

                                            else
                                                lastViewed

                                        _ ->
                                            lastViewed
                                    )
                                    (GuildOrDmId_Guild { guildId = guildId, channelId = channelId })
                                    maybeUrlMessageId
                                    threadMessageIndex
                                    loggedIn
                                    model
                                    local
                                    False
                                    (ChannelName.toString channel.name)
                                    (threadPreviewText
                                        local.localUser.timezone
                                        (User.allUsers local.localUser)
                                        (LocalState.channelMentions (GuildOrDmId_Guild { guildId = guildId, channelId = channelId }) local)
                                        threadMessageIndex
                                        local.localUser.decryptedMessages
                                        channel
                                    )

                        NoThreadWithFriends maybeUrlMessageId _ ->
                            conversationView
                                (let
                                    lastViewed : Id ChannelMessageId
                                    lastViewed =
                                        SeqDict.get
                                            (GuildOrDmId (GuildOrDmId_Guild { guildId = guildId, channelId = channelId }))
                                            local.localUser.user.lastViewedMessage
                                            |> Maybe.withDefault (Id.fromInt -1)
                                 in
                                 case local.localUser.currentlyViewing of
                                    Viewing_Channel data ->
                                        if data.id == { guildId = guildId, channelId = channelId } then
                                            unreadDividerAt lastViewed data.previouslyLastViewedMessage

                                        else
                                            lastViewed

                                    _ ->
                                        lastViewed
                                )
                                (GuildOrDmId_Guild { guildId = guildId, channelId = channelId })
                                maybeUrlMessageId
                                loggedIn
                                model
                                local
                                False
                                (ChannelName.toString channel.name)
                                channel

                Nothing ->
                    pageMissing channelDoesNotExistText

        NewChannelRoute ->
            SeqDict.get guildId loggedIn.newChannelForm
                |> Maybe.withDefault newChannelFormInit
                |> newChannelFormView (MyUi.isMobile model) guildId

        GuildSettingsRoute ->
            guildSettingsView model loggedIn local guildId guild

        JoinRoute _ ->
            Ui.none


discordChannelView : DiscordGuildRouteData -> DiscordFrontendGuild -> LoggedIn2 -> LocalState -> LoadedFrontend -> Element FrontendMsg_
discordChannelView routeData guild loggedIn local model =
    case routeData.channelRoute of
        DiscordChannel_ChannelRoute channelId threadRoute _ ->
            case SeqDict.get channelId guild.channels of
                Just channel ->
                    let
                        ( availableCustomEmojis, availableStickers ) =
                            LocalState.discordGuildAvailableStickersAndCustomEmojis local.localUser guild
                    in
                    case threadRoute of
                        ViewThreadWithFriends threadMessageIndex maybeUrlMessageId _ ->
                            SeqDict.get threadMessageIndex channel.threads
                                |> Maybe.withDefault Thread.discordFrontendInit
                                |> discordThreadConversationView
                                    (let
                                        lastViewed : Id ThreadMessageId
                                        lastViewed =
                                            SeqDict.get
                                                ( DiscordGuildOrDmId
                                                    (DiscordGuildOrDmId_Guild { currentUserId = routeData.currentDiscordUserId, guildId = routeData.guildId, channelId = channelId })
                                                , threadMessageIndex
                                                )
                                                local.localUser.user.lastViewedThreadMessage
                                                |> Maybe.withDefault (Id.fromInt -1)
                                     in
                                     case local.localUser.currentlyViewing of
                                        Viewing_DiscordChannelThread data ->
                                            if
                                                data.id
                                                    == { guildId = routeData.guildId
                                                       , channelId = channelId
                                                       , currentUserId = routeData.currentDiscordUserId
                                                       , threadId = threadMessageIndex
                                                       }
                                            then
                                                unreadDividerAt lastViewed data.previouslyLastViewedMessage

                                            else
                                                lastViewed

                                        _ ->
                                            lastViewed
                                    )
                                    routeData.currentDiscordUserId
                                    (DiscordGuildOrDmId_Guild { currentUserId = routeData.currentDiscordUserId, guildId = routeData.guildId, channelId = channelId })
                                    maybeUrlMessageId
                                    threadMessageIndex
                                    loggedIn
                                    model
                                    local
                                    (ChannelName.toString channel.name
                                        ++ " / "
                                        ++ threadPreviewText
                                            local.localUser.timezone
                                            (LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers)
                                            (LocalState.discordChannelMentions
                                                (DiscordGuildOrDmId_Guild
                                                    { currentUserId = routeData.currentDiscordUserId
                                                    , guildId = routeData.guildId
                                                    , channelId = channelId
                                                    }
                                                )
                                                local
                                            )
                                            threadMessageIndex
                                            SeqDict.empty
                                            channel
                                    )
                                    availableCustomEmojis
                                    availableStickers

                        NoThreadWithFriends maybeUrlMessageId _ ->
                            discordConversationView
                                (let
                                    lastViewed : Id ChannelMessageId
                                    lastViewed =
                                        SeqDict.get
                                            (DiscordGuildOrDmId
                                                (DiscordGuildOrDmId_Guild { currentUserId = routeData.currentDiscordUserId, guildId = routeData.guildId, channelId = channelId })
                                            )
                                            local.localUser.user.lastViewedMessage
                                            |> Maybe.withDefault (Id.fromInt -1)
                                 in
                                 case local.localUser.currentlyViewing of
                                    Viewing_DiscordChannel data ->
                                        if
                                            data.id
                                                == { guildId = routeData.guildId
                                                   , channelId = channelId
                                                   , currentUserId = routeData.currentDiscordUserId
                                                   }
                                        then
                                            unreadDividerAt lastViewed data.previouslyLastViewedMessage

                                        else
                                            lastViewed

                                    _ ->
                                        lastViewed
                                )
                                routeData.currentDiscordUserId
                                (DiscordGuildOrDmId_Guild { currentUserId = routeData.currentDiscordUserId, guildId = routeData.guildId, channelId = channelId })
                                maybeUrlMessageId
                                loggedIn
                                model
                                local
                                (ChannelName.toString channel.name)
                                channel
                                availableCustomEmojis
                                availableStickers

                Nothing ->
                    pageMissing channelDoesNotExistText

        DiscordChannel_NewChannelRoute ->
            pageMissing "Adding Discord channels not supported yet"

        DiscordChannel_GuildSettingsRoute ->
            discordGuildSettingsView model routeData.currentDiscordUserId routeData.guildId guild local


discordGuildSettingsView :
    LoadedFrontend
    -> Discord.Id Discord.UserId
    -> Discord.Id Discord.GuildId
    -> DiscordFrontendGuild
    -> LocalState
    -> Element FrontendMsg_
discordGuildSettingsView model currentUserId guildId guild local =
    let
        isMobile =
            MyUi.isMobile model
    in
    Ui.column
        [ Ui.height Ui.fill, Ui.Font.color MyUi.font1 ]
        [ ChannelHeader.channelHeader isMobile (Ui.text "Guild settings") Nothing
        , Ui.column
            [ Ui.spacing 16
            , Ui.height Ui.fill
            , Ui.heightMin 0
            , MyUi.scrollable (MyUi.canScroll isMobile model.drag)
            , Ui.paddingWith { left = 0, right = 0, top = 16, bottom = local.localUser.safeAreaInsetBottom + 16 }
            ]
            [ Ui.column
                [ Ui.paddingXY 8 0 ]
                [ Ui.el [ Ui.paddingXY 8 0, Ui.Font.bold ] (Ui.text "Owner")
                , discordMemberLabel isMobile local.localUser currentUserId (MembersAndOwner.owner guild.membersAndOwner)
                ]
            , Ui.el
                [ Ui.paddingXY 16 0 ]
                (MyUi.radioColumn
                    (Dom.id "guild_discordNotificationLevel")
                    (PressedDiscordGuildNotificationLevel currentUserId guildId)
                    (if SeqSet.member guildId local.localUser.user.discordNotifyOnAllMessages then
                        Just NotifyOnEveryMessage

                     else
                        Just NotifyOnMention
                    )
                    (Ui.text "Guild notifications")
                    [ ( NotifyOnMention, "Only when mentioned" )
                    , ( NotifyOnEveryMessage, "On every message" )
                    ]
                )
            , Ui.el
                [ Ui.paddingXY 16 0 ]
                (MuteSettings.view
                    (PressedMuteDiscordGuild currentUserId guildId)
                    (MuteSettings.isDiscordGuildSpecificallyMute local.localUser.user.muteSettings guildId)
                )
            ]
        ]


guildSettingsView : LoadedFrontend -> LoggedIn2 -> LocalState -> Id GuildId -> FrontendGuild -> Element FrontendMsg_
guildSettingsView model loggedIn local guildId guild =
    let
        isMobile =
            MyUi.isMobile model

        owner =
            MembersAndOwner.owner guild.membersAndOwner

        isOwner : Bool
        isOwner =
            owner == local.localUser.session.userId

        editGuildForm : EditGuildForm
        editGuildForm =
            SeqDict.get guildId loggedIn.editGuildForm
                |> Maybe.withDefault (editGuildFormInit guild)

        guildIconEditor : ImageEditor.Model
        guildIconEditor =
            case loggedIn.guildIconEditor of
                Just ( existingGuildId, editor ) ->
                    if existingGuildId == guildId then
                        editor

                    else
                        ImageEditor.init

                Nothing ->
                    ImageEditor.init
    in
    Ui.column
        [ Ui.height Ui.fill, Ui.Font.color MyUi.font1 ]
        [ ChannelHeader.channelHeader isMobile (Ui.text "Guild settings") Nothing
        , Ui.column
            [ Ui.spacing 16
            , Ui.height Ui.fill
            , Ui.heightMin 0
            , MyUi.scrollable (MyUi.canScroll isMobile model.drag)
            , Ui.paddingWith { left = 0, right = 0, top = 16, bottom = local.localUser.safeAreaInsetBottom + 16 }
            ]
            [ Ui.column
                [ Ui.paddingXY 8 0 ]
                [ Ui.el [ Ui.paddingXY 8 0, Ui.Font.bold ] (Ui.text "Owner")
                , memberLabel isMobile local.localUser owner
                ]
            , if isOwner then
                guildMemberTable local.localUser guildId guild

              else
                Ui.none
            , if isOwner then
                editGuildNameSection guildId guild editGuildForm

              else
                Ui.none
            , if isOwner then
                Ui.column
                    [ Ui.spacing 8, Ui.paddingXY 16 0 ]
                    [ Ui.el [ Ui.Font.bold ] (Ui.text "Guild icon")
                    , Ui.row
                        [ Ui.spacing 12, Ui.alignLeft ]
                        [ GuildIcon.view (GuildIcon.Normal NoNotification) guild
                        , ImageEditor.view model.windowSize (guild.icon /= Nothing) guildIconEditor
                            |> Ui.map (GuildIconEditorMsg guildId)
                        ]
                    ]

              else
                Ui.none
            , Ui.el
                [ Ui.paddingXY 16 0 ]
                (submitButton (Dom.id "guild_createInviteLink") (PressedCreateInviteLink guildId) "Create invite link")
            , if SeqDict.isEmpty guild.invites then
                Ui.none

              else
                Ui.el [ Ui.Font.bold, Ui.paddingXY 16 0 ] (Ui.text "Existing invites")
            , Ui.column
                [ Ui.spacing 8, Ui.paddingXY 16 0 ]
                (SeqDict.toList guild.invites
                    |> List.sortBy (\( _, data ) -> -(Time.posixToMillis data.createdAt))
                    |> List.map
                        (\( inviteId, data ) ->
                            let
                                url : String
                                url =
                                    Route.encode (GuildRoute guildId (JoinRoute inviteId) ChannelsHiddenOnMobile Nothing)

                                inviteLink : String
                                inviteLink =
                                    Env.domain ++ url

                                showQrCode : Bool
                                showQrCode =
                                    Just inviteId == loggedIn.showInviteLinkQrCode
                            in
                            Ui.column
                                [ Ui.spacing 8 ]
                                [ Ui.row
                                    [ Ui.spacing 16 ]
                                    [ Ui.el
                                        [ Ui.widthMax 300 ]
                                        (MyUi.copyBox
                                            (Dom.id "guild_inviteLinkCopy")
                                            Nothing
                                            PressedCopyText
                                            FrontendNoOp
                                            model
                                            inviteLink
                                        )
                                    , MyUi.elButton
                                        (Dom.id ("guild_inviteLinkQrCode_" ++ SecretId.toString inviteId))
                                        (PressedToggleInviteLinkQrCode inviteId)
                                        [ Ui.Font.color MyUi.font2
                                        , Ui.rounded 4
                                        , Ui.border 1
                                        , Ui.borderColor MyUi.inputBorder
                                        , Ui.paddingXY 6 0
                                        , Ui.width Ui.shrink
                                        , Ui.height Ui.fill
                                        , Ui.contentCenterY
                                        , Ui.Font.size 14
                                        ]
                                        (Ui.text
                                            (if showQrCode then
                                                "Hide QR code"

                                             else
                                                "Show QR code"
                                            )
                                        )
                                    , if isOwner then
                                        MyUi.deleteButton
                                            (Dom.id ("guild_deleteInviteLink_" ++ SecretId.toString inviteId))
                                            (PressedDeleteInviteLink guildId inviteId)

                                      else
                                        Ui.none
                                    , if Duration.from data.createdAt model.time |> Quantity.lessThan (Duration.minutes 5) then
                                        Ui.text "Created just now!"

                                      else
                                        Ui.none
                                    ]
                                , if showQrCode then
                                    Ui.Lazy.lazy2 inviteLinkQrCodeView (conversationWidth model) inviteLink

                                  else
                                    Ui.none
                                ]
                        )
                )
            , Ui.el
                [ Ui.paddingXY 16 0 ]
                (MyUi.radioColumn
                    (Dom.id "guild_notificationLevel")
                    (PressedGuildNotificationLevel guildId)
                    (if SeqSet.member guildId local.localUser.user.notifyOnAllMessages then
                        Just NotifyOnEveryMessage

                     else
                        Just NotifyOnMention
                    )
                    (Ui.text "Guild notifications")
                    [ ( NotifyOnMention, "Only when mentioned" )
                    , ( NotifyOnEveryMessage, "On every message" )
                    ]
                )
            , Ui.el
                [ Ui.paddingXY 16 0 ]
                (MuteSettings.view
                    (PressedMuteGuild guildId)
                    (MuteSettings.isGuildSpecificallyMute local.localUser.user.muteSettings guildId)
                )
            , if isOwner then
                importChannelSection guildId editGuildForm

              else
                Ui.none
            , if isOwner then
                deleteGuildSection guildId guild editGuildForm

              else
                leaveGuildSection guildId editGuildForm
            ]
        ]


guildMembersText : String
guildMembersText =
    "Members"


banMemberText : String
banMemberText =
    "Ban"


neverPostedText : String
neverPostedText =
    "Never"


{-| The owner isn't listed here. They are shown on their own further up the page and
there's nothing on this table they could do to themselves.
-}
guildMemberTable : LocalUser -> Id GuildId -> FrontendGuild -> Element FrontendMsg_
guildMemberTable localUser guildId guild =
    Ui.column
        [ Ui.spacing 8, Ui.paddingXY 16 0 ]
        [ Ui.el [ Ui.Font.bold ] (Ui.text guildMembersText)
        , Ui.Table.view
            [ Ui.Font.size 14 ]
            (Ui.Table.columns
                [ Ui.Table.column
                    { header = Ui.Table.header "Name"
                    , view =
                        \( userId, _ ) ->
                            Ui.Table.cell
                                guildMemberCellPadding
                                (Ui.text
                                    (case User.getUser userId localUser of
                                        Just user ->
                                            PersonName.toString user.name

                                        Nothing ->
                                            User.missingName
                                    )
                                )
                    }
                , Ui.Table.column
                    { header = Ui.Table.header "Joined"
                    , view =
                        \( _, member ) ->
                            Ui.Table.cell
                                guildMemberCellPadding
                                (Ui.text (MyUi.datestamp localUser.timezone member.joinedAt))
                    }
                , Ui.Table.column
                    { header = Ui.Table.header "Last posted"
                    , view =
                        \( _, member ) ->
                            Ui.Table.cell
                                guildMemberCellPadding
                                (Ui.text
                                    (case member.lastPostedAt of
                                        Just lastPostedAt ->
                                            MyUi.datestamp localUser.timezone lastPostedAt

                                        Nothing ->
                                            neverPostedText
                                    )
                                )
                    }
                , Ui.Table.column
                    { header = Ui.Table.header ""
                    , view =
                        \( userId, _ ) ->
                            Ui.Table.cell
                                guildMemberCellPadding
                                (MyUi.elButton
                                    (Dom.id ("guild_banMember_" ++ Id.toString userId))
                                    (PressedBanMember guildId userId)
                                    [ Ui.paddingXY 8 2
                                    , Ui.background MyUi.deleteButtonBackground
                                    , Ui.width Ui.shrink
                                    , Ui.rounded 4
                                    , Ui.Font.color MyUi.deleteButtonFont
                                    , Ui.Font.weight 500
                                    , Ui.borderColor MyUi.deleteButtonBorder
                                    , Ui.border 1
                                    ]
                                    (Ui.text banMemberText)
                                )
                    }
                ]
            )
            (MembersAndOwner.members guild.membersAndOwner |> SeqDict.toList)
        ]


guildMemberCellPadding : List (Ui.Attribute msg)
guildMemberCellPadding =
    [ Ui.paddingXY 8 4, Ui.contentCenterY ]


importChannelText : String
importChannelText =
    "Import channel"


importChannelFailedText : ImportChannelError -> String
importChannelFailedText error =
    case error of
        NotAChannelExport ->
            "That file isn't a channel export"


{-| Turns a file that the export channel button wrote into a channel in this guild. Whatever
was encrypted in the channel it came from can't be read here, so those messages arrive as
deleted ones and the owner is told how many there were.
-}
importChannelSection : Id GuildId -> EditGuildForm -> Element FrontendMsg_
importChannelSection guildId form =
    Ui.column
        [ Ui.spacing 8, Ui.paddingXY 16 0 ]
        [ Ui.el [ Ui.Font.bold ] (Ui.text importChannelText)
        , Ui.row
            [ Ui.spacing 8 ]
            [ submitButton (Dom.id "guild_importChannel") (PressedImportChannel guildId) importChannelText
            , case form.importChannel of
                NotImportingChannel ->
                    Ui.none

                ImportingChannel ->
                    Ui.text "Importing..."

                ImportChannelFailed error ->
                    Ui.el [ Ui.Font.color MyUi.errorColor ] (Ui.text (importChannelFailedText error))

                ImportedChannel { encryptedMessages } ->
                    Ui.text (importedChannelText encryptedMessages)
            ]
        ]


importedChannelText : Int -> String
importedChannelText encryptedMessages =
    if encryptedMessages == 0 then
        "Imported!"

    else if encryptedMessages == 1 then
        "Imported! 1 encrypted message was left out"

    else
        "Imported! " ++ String.fromInt encryptedMessages ++ " encrypted messages were left out"


{-| The import status is kept with the rest of the guild settings form, which might not exist
yet when an import starts, so it gets filled in from the guild the same way opening the settings
would have.
-}
setImportChannelStatus : Id GuildId -> ImportChannelStatus -> LocalState -> LoggedIn2 -> LoggedIn2
setImportChannelStatus guildId status local loggedIn =
    { loggedIn
        | editGuildForm =
            SeqDict.update
                guildId
                (\maybeForm ->
                    case ( maybeForm, SeqDict.get guildId local.guilds ) of
                        ( Just form, _ ) ->
                            Just { form | importChannel = status }

                        ( Nothing, Just guild ) ->
                            Just
                                { name = GuildName.toString guild.name
                                , deleteConfirmation = ""
                                , showDeleteConfirmation = False
                                , showLeaveConfirmation = False
                                , pressedSubmit = False
                                , importChannel = status
                                }

                        ( Nothing, Nothing ) ->
                            Nothing
                )
                loggedIn.editGuildForm
    }


editGuildFormInit : FrontendGuild -> EditGuildForm
editGuildFormInit guild =
    { name = GuildName.toString guild.name
    , deleteConfirmation = ""
    , showDeleteConfirmation = False
    , showLeaveConfirmation = False
    , pressedSubmit = False
    , importChannel = NotImportingChannel
    }


editGuildNameSection : Id GuildId -> FrontendGuild -> EditGuildForm -> Element FrontendMsg_
editGuildNameSection guildId guild form =
    let
        nameLabel =
            Ui.Input.label
                "editGuildName"
                [ Ui.Font.bold, Ui.paddingXY 2 0 ]
                (Ui.text "Guild name")

        hasChanges : Bool
        hasChanges =
            form.name /= GuildName.toString guild.name
    in
    Ui.column
        [ Ui.spacing 8, Ui.paddingXY 16 0 ]
        [ nameLabel.element
        , Ui.Input.text
            [ Ui.padding 6
            , Ui.background MyUi.inputBackground
            , Ui.borderColor MyUi.inputBorder
            , Ui.widthMax 500
            ]
            { onChange = \text -> EditGuildFormChanged guildId { form | name = text }
            , text = form.name
            , placeholder = Nothing
            , label = nameLabel.id
            }
        , case ( form.pressedSubmit, GuildName.fromString form.name ) of
            ( True, Err error ) ->
                Ui.el [ Ui.paddingXY 2 0, Ui.Font.color MyUi.errorColor ] (Ui.text error)

            _ ->
                Ui.none
        , if hasChanges then
            Ui.row
                [ Ui.spacing 8 ]
                [ MyUi.secondaryButton
                    (Dom.id "guild_resetEditGuild")
                    (PressedResetEditGuildChanges guildId)
                    "Reset"
                , submitButton
                    (Dom.id "guild_submitEditGuild")
                    (PressedSubmitEditGuildChanges guildId form)
                    "Save changes"
                ]

          else
            Ui.none
        ]


deleteGuildSection : Id GuildId -> FrontendGuild -> EditGuildForm -> Element FrontendMsg_
deleteGuildSection guildId guild form =
    let
        guildNameString : String
        guildNameString =
            GuildName.toString guild.name

        confirmationMatches : Bool
        confirmationMatches =
            form.deleteConfirmation == guildNameString

        ( deleteOnPress, deleteEnabled ) =
            if not form.showDeleteConfirmation then
                ( EditGuildFormChanged guildId { form | showDeleteConfirmation = True }, True )

            else if confirmationMatches then
                ( PressedDeleteGuild guildId, True )

            else
                ( FrontendNoOp, False )
    in
    Ui.column
        [ Ui.spacing 12, Ui.paddingXY 16 0 ]
        [ Ui.el [ Ui.height (Ui.px 1), Ui.background MyUi.border2 ] Ui.none
        , if form.showDeleteConfirmation then
            deleteGuildConfirmationInput guildId guildNameString form

          else
            Ui.none
        , MyUi.elButton
            (Dom.id "guild_deleteGuild")
            deleteOnPress
            [ Ui.paddingXY 16 4
            , Ui.background
                (if deleteEnabled then
                    MyUi.deleteButtonBackground

                 else
                    MyUi.disabledButtonBackground
                )
            , Ui.width Ui.shrink
            , Ui.rounded 4
            , Ui.Font.color MyUi.deleteButtonFont
            , Ui.Font.bold
            , Ui.borderColor
                (if deleteEnabled then
                    MyUi.deleteButtonBorder

                 else
                    MyUi.disabledButtonBorder
                )
            , Ui.border 1
            ]
            (Ui.text deleteGuildText)
        ]


leaveGuildSection : Id GuildId -> EditGuildForm -> Element FrontendMsg_
leaveGuildSection guildId form =
    Ui.column
        [ Ui.spacing 12, Ui.paddingXY 16 0 ]
        [ Ui.el [ Ui.height (Ui.px 1), Ui.background MyUi.border2 ] Ui.none
        , if form.showLeaveConfirmation then
            Ui.el
                [ Ui.Font.color MyUi.font2 ]
                (Ui.text "You'll need to use an invite link to rejoin. Are you sure?")

          else
            Ui.none
        , MyUi.elButton
            (Dom.id "guild_leaveGuild")
            (if form.showLeaveConfirmation then
                PressedLeaveGuild guildId

             else
                EditGuildFormChanged guildId { form | showLeaveConfirmation = True }
            )
            [ Ui.paddingXY 16 4
            , Ui.background MyUi.deleteButtonBackground
            , Ui.width Ui.shrink
            , Ui.rounded 4
            , Ui.Font.color MyUi.deleteButtonFont
            , Ui.Font.bold
            , Ui.borderColor MyUi.deleteButtonBorder
            , Ui.border 1
            ]
            (Ui.text
                (if form.showLeaveConfirmation then
                    confirmLeaveGuildText

                 else
                    leaveGuildText
                )
            )
        ]


deleteGuildConfirmationInput : Id GuildId -> String -> EditGuildForm -> Element FrontendMsg_
deleteGuildConfirmationInput guildId guildNameString form =
    let
        confirmLabel =
            Ui.Input.label
                "deleteGuildConfirmation"
                [ Ui.Font.color MyUi.font2, Ui.paddingXY 2 0 ]
                (Ui.text ("Type \"" ++ guildNameString ++ "\" to confirm deletion"))
    in
    Ui.column
        []
        [ confirmLabel.element
        , Ui.Input.text
            [ Ui.padding 6
            , Ui.background MyUi.inputBackground
            , Ui.borderColor MyUi.inputBorder
            , Ui.widthMax 500
            ]
            { onChange = \text -> EditGuildFormChanged guildId { form | deleteConfirmation = text }
            , text = form.deleteConfirmation
            , placeholder = Nothing
            , label = confirmLabel.id
            }
        ]


inviteLinkQrCodeView : Int -> String -> Element msg
inviteLinkQrCodeView containerWidth inviteLink =
    case QRCode.fromString inviteLink of
        Ok qrCode ->
            let
                size =
                    min 300 containerWidth
            in
            Ui.el
                [ Ui.background MyUi.white
                , Ui.padding 12
                , Ui.rounded 8
                , Ui.width Ui.shrink
                ]
                (QRCode.toSvgWithoutQuietZone
                    [ MyUi.widthAttr size, MyUi.heightAttr size ]
                    qrCode
                    |> Ui.html
                )

        Err _ ->
            Ui.none


channelTextInputId : HtmlId
channelTextInputId =
    "channel_textinput" |> Dom.id


messageHover : AnyGuildOrDmId -> ThreadRouteWithMessage -> LoggedIn2 -> LoadedFrontend -> IsHovered
messageHover guildOrDmId threadRoute loggedIn model =
    case loggedIn.messageHover of
        MessageMenu messageMenu ->
            if guildOrDmId == messageMenu.guildOrDmId && threadRoute == messageMenu.threadRoute then
                IsHoveredButNoMenu

            else
                IsNotHovered

        MessageHover guildOrDmIdA threadRouteA ->
            if guildOrDmId == guildOrDmIdA then
                if threadRouteA == threadRoute then
                    if drawingIsSelectingAnchor loggedIn model then
                        IsHoveredWhileSelectingAnchor

                    else
                        IsHovered

                else
                    notHoveredWhileSelectingAnchor loggedIn model

            else
                notHoveredWhileSelectingAnchor loggedIn model

        _ ->
            notHoveredWhileSelectingAnchor loggedIn model


{-| Hovering a message in the unread overview restarts the animated emojis, stickers and
embeds inside it, the same as it does in a channel, and brings up a menu offering to react
to it. That's all the menu offers: editing and replying belong to the channel the message
came from.
-}
unreadOverviewMessageHover : AnyGuildOrDmId -> ThreadRouteWithMessage -> LoggedIn2 -> IsHovered
unreadOverviewMessageHover guildOrDmId threadRoute loggedIn =
    case loggedIn.messageHover of
        MessageHover hoveredGuildOrDmId hoveredThreadRoute ->
            if guildOrDmId == hoveredGuildOrDmId && threadRoute == hoveredThreadRoute then
                IsHoveredReactionsOnly

            else
                IsNotHovered

        _ ->
            IsNotHovered


{-| A message the pointer isn't hovering over. Fingers can't hover, so on mobile
every message offers up its drawing anchors while the drawing tab waits for one
to be picked, instead of only the hovered message.
-}
notHoveredWhileSelectingAnchor : LoggedIn2 -> LoadedFrontend -> IsHovered
notHoveredWhileSelectingAnchor loggedIn model =
    if MyUi.isMobile model && drawingIsSelectingAnchor loggedIn model then
        IsHoveredWhileSelectingAnchor

    else
        IsNotHovered


revealedChannelSpoilers : AnyGuildOrDmId -> LoggedIn2 -> SeqDict (Id ChannelMessageId) (NonemptySet Int)
revealedChannelSpoilers guildOrDmId loggedIn =
    case SeqDict.get guildOrDmId loggedIn.revealedSpoilers of
        Just revealed ->
            revealed.messages

        Nothing ->
            SeqDict.empty


revealedThreadSpoilers : AnyGuildOrDmId -> Id ChannelMessageId -> LoggedIn2 -> SeqDict (Id ThreadMessageId) (NonemptySet Int)
revealedThreadSpoilers guildOrDmId threadId loggedIn =
    case SeqDict.get guildOrDmId loggedIn.revealedSpoilers of
        Just revealedSpoilers2 ->
            SeqDict.get threadId revealedSpoilers2.threadMessages |> Maybe.withDefault SeqDict.empty

        Nothing ->
            SeqDict.empty


conversationViewHelper :
    Id ChannelMessageId
    -> GuildOrDmId
    -> Maybe (Id ChannelMessageId)
    ->
        { a
            | messages : MessageArray ChannelMessageId (Id UserId) (Id ChannelId)
            , visibleMessages : VisibleMessages ChannelMessageId
            , threads : SeqDict (Id ChannelMessageId) FrontendThread
            , dateDividerDrawings : SeqDict Date (Drawing (Id UserId))
            , games : SeqDict (Id ChannelMessageId) Game.MatchData
        }
    -> LoggedIn2
    -> LocalState
    -> LoadedFrontend
    -> List ( String, Element FrontendMsg_ )
conversationViewHelper lastViewedIndex guildOrDmIdNoThread maybeUrlMessageId channel loggedIn local model =
    let
        channels : SeqDict (Id ChannelId) FrontendChannel
        channels =
            LocalState.guildChannels guildOrDmIdNoThread local

        guildOrDmId : ( AnyGuildOrDmId, ThreadRoute )
        guildOrDmId =
            ( GuildOrDmId guildOrDmIdNoThread, NoThread )

        maybeEditing : Maybe EditMessage
        maybeEditing =
            SeqDict.get guildOrDmId loggedIn.editMessage

        othersEditing : SeqSet (Id ChannelMessageId)
        othersEditing =
            SeqDict.remove local.localUser.session.userId (LocalState.typingIn guildOrDmIdNoThread local)
                |> SeqDict.values
                |> List.filterMap
                    (\a ->
                        case a.threadRoute of
                            NoThreadWithMaybeMessage maybeMessageId ->
                                if Duration.from a.time model.time |> Quantity.lessThan (Duration.seconds 3) then
                                    maybeMessageId

                                else
                                    Nothing

                            ViewThreadWithMaybeMessage _ _ ->
                                Nothing
                    )
                |> SeqSet.fromList

        replyToIndex : Maybe (Id ChannelMessageId)
        replyToIndex =
            SeqDict.get guildOrDmId loggedIn.replyTo |> Maybe.andThen Message.replyToMaybe

        revealedSpoilers : SeqDict (Id ChannelMessageId) (NonemptySet Int)
        revealedSpoilers =
            revealedChannelSpoilers (GuildOrDmId guildOrDmIdNoThread) loggedIn

        containerWidth : Int
        containerWidth =
            conversationWidth model

        isMobile : Bool
        isMobile =
            MyUi.isMobile model

        isSelectingAnchor =
            drawingIsSelectingAnchor loggedIn model
    in
    MessageArray.foldr
        (\messageId maybeMessage ( maybeLastDate, list ) ->
            let
                index : Int
                index =
                    Id.toInt messageId
            in
            case maybeMessage of
                Just message ->
                    let
                        threadRoute2 : ThreadRouteWithMessage
                        threadRoute2 =
                            NoThreadWithMessage messageId

                        threadId : Id ChannelMessageId
                        threadId =
                            Id.fromInt index

                        messageHover2 : IsHovered
                        messageHover2 =
                            messageHover (GuildOrDmId guildOrDmIdNoThread) threadRoute2 loggedIn model

                        otherUserIsEditing : Bool
                        otherUserIsEditing =
                            SeqSet.member (Id.changeType messageId) othersEditing

                        isEditing : Maybe EditMessage
                        isEditing =
                            case maybeEditing of
                                Just editing ->
                                    if editing.messageIndex == messageId then
                                        Just editing

                                    else
                                        Nothing

                                Nothing ->
                                    Nothing

                        highlight : HighlightMessage
                        highlight =
                            if maybeUrlMessageId == Just messageId then
                                UrlHighlight

                            else if replyToIndex == Just messageId then
                                ReplyToHighlight

                            else
                                NoHighlight

                        maybeRepliedTo2 : Maybe (RepliedToView ChannelMessageId (Id UserId) (Id ChannelId) msg)
                        maybeRepliedTo2 =
                            channelMessageRepliedTo local.localUser channel.games message channel

                        date : Date
                        date =
                            Message.createdAt message |> Date.fromPosix local.localUser.timezone
                    in
                    ( Just date
                    , ( String.fromInt index
                      , case isEditing of
                            Just edit ->
                                if MyUi.isMobile model then
                                    -- On mobile, we show the editor at the bottom instead
                                    messageView
                                        model.time
                                        isMobile
                                        containerWidth
                                        False
                                        revealedSpoilers
                                        highlight
                                        messageHover2
                                        otherUserIsEditing
                                        local.localUser.session.userId
                                        (User.allUsers local.localUser)
                                        channels
                                        local.localUser
                                        maybeRepliedTo2
                                        (SeqDict.get threadId channel.threads)
                                        messageId
                                        message
                                        |> Ui.map (MessageViewMsg (GuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                else
                                    let
                                        allUsers =
                                            User.allUsers local.localUser

                                        channelMentions : SeqDict ( Id ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
                                        channelMentions =
                                            LocalState.guildChannelMentions local.localUser channels

                                        editRichText : Maybe (Nonempty (RichText (Id UserId) (Id ChannelId)))
                                        editRichText =
                                            case String.Nonempty.fromString edit.text of
                                                Just nonempty ->
                                                    RichText.fromNonemptyString local.localUser.timezone allUsers channelMentions nonempty |> Just

                                                Nothing ->
                                                    Nothing

                                        charsLeft =
                                            RichText.maxLength - String.length edit.text
                                    in
                                    messageEditingView
                                        containerWidth
                                        model.time
                                        isMobile
                                        guildOrDmId
                                        threadRoute2
                                        message
                                        maybeRepliedTo2
                                        (SeqDict.get threadId channel.threads)
                                        revealedSpoilers
                                        charsLeft
                                        edit
                                        editRichText
                                        loggedIn
                                        local.localUser.decryptedMessages
                                        local.localUser.session.userId
                                        allUsers
                                        channelMentions
                                        local

                            Nothing ->
                                case SeqDict.get threadId channel.threads of
                                    Nothing ->
                                        case ( maybeRepliedTo2, Message.mentionsChannel message ) of
                                            ( Nothing, False ) ->
                                                Ui.Lazy.lazy5
                                                    messageViewNotThreadStarter
                                                    (encodeMessageView isMobile messageHover2 containerWidth otherUserIsEditing highlight model.time)
                                                    revealedSpoilers
                                                    local.localUser
                                                    index
                                                    message
                                                    |> Ui.map (MessageViewMsg (GuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                            ( Nothing, True ) ->
                                                Ui.Lazy.lazy6
                                                    messageViewNotThreadStarterWithChannelMention
                                                    (encodeMessageView isMobile messageHover2 containerWidth otherUserIsEditing highlight model.time)
                                                    revealedSpoilers
                                                    local.localUser
                                                    index
                                                    message
                                                    channels
                                                    |> Ui.map (MessageViewMsg (GuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                            _ ->
                                                messageView
                                                    model.time
                                                    isMobile
                                                    containerWidth
                                                    False
                                                    revealedSpoilers
                                                    highlight
                                                    messageHover2
                                                    otherUserIsEditing
                                                    local.localUser.session.userId
                                                    (User.allUsers local.localUser)
                                                    channels
                                                    local.localUser
                                                    maybeRepliedTo2
                                                    Nothing
                                                    messageId
                                                    message
                                                    |> Ui.map (MessageViewMsg (GuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                    Just thread ->
                                        case ( maybeRepliedTo2, Message.mentionsChannel message ) of
                                            ( Nothing, False ) ->
                                                Ui.Lazy.lazy6
                                                    messageViewThreadStarter
                                                    (encodeMessageView isMobile messageHover2 containerWidth otherUserIsEditing highlight model.time)
                                                    revealedSpoilers
                                                    local.localUser
                                                    index
                                                    thread
                                                    message
                                                    |> Ui.map (MessageViewMsg (GuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                            _ ->
                                                messageView
                                                    model.time
                                                    isMobile
                                                    containerWidth
                                                    False
                                                    revealedSpoilers
                                                    highlight
                                                    messageHover2
                                                    otherUserIsEditing
                                                    local.localUser.session.userId
                                                    (User.allUsers local.localUser)
                                                    channels
                                                    local.localUser
                                                    maybeRepliedTo2
                                                    (Just thread)
                                                    messageId
                                                    message
                                                    |> Ui.map (MessageViewMsg (GuildOrDmId guildOrDmIdNoThread) threadRoute2)
                      )
                        |> (\keyedMessage ->
                                keyedMessage
                                    :: List.map
                                        (Tuple.mapSecond (Ui.map (MessageViewMsg (GuildOrDmId guildOrDmIdNoThread) threadRoute2)))
                                        (newMessageLine
                                            (User.userColor local.localUser)
                                            isSelectingAnchor
                                            channel.dateDividerDrawings
                                            maybeLastDate
                                            date
                                            lastViewedIndex
                                            index
                                            messageId
                                        )
                                    ++ list
                           )
                    )

                Nothing ->
                    ( maybeLastDate, ( String.fromInt index, unloadedMessageView index ) :: list )
        )
        ( Nothing, [] )
        (VisibleMessages.slice channel)
        |> Tuple.second
        |> prependUnreadDivider lastViewedIndex channel.visibleMessages.oldest


userTextMessageRepliedTo :
    { a | repliedTo : RepliedTo messageId }
    -> { b | messages : MessageArray messageId userId channelId }
    -> Maybe (RepliedToView messageId userId channelId msg)
userTextMessageRepliedTo data channel =
    case data.repliedTo of
        RepliedToMessage repliedToIndex ->
            case MessageArray.get repliedToIndex channel.messages of
                Just message2 ->
                    RepliedToView_Message repliedToIndex message2 |> Just

                Nothing ->
                    Nothing

        RepliedToGame matchId game ->
            RepliedToView_Game matchId game Ui.none |> Just

        NoReply ->
            Nothing


{-| What a message replied to, for drawing the line above it. A reply to something inside a
game has no message behind it, only the game's card, what in the game it points at and, once
the match has been loaded, what that move or answer was.
-}
type RepliedToView messageId userId channelId msg
    = RepliedToView_Message (Id messageId) (Message messageId userId channelId)
    | RepliedToView_Game (Id ChannelMessageId) Message.RepliedToGame (Element msg)


channelMessageRepliedTo :
    LocalUser
    -> SeqDict (Id ChannelMessageId) Game.MatchData
    -> Message ChannelMessageId (Id UserId) (Id ChannelId)
    -> { b | messages : MessageArray ChannelMessageId (Id UserId) (Id ChannelId) }
    -> Maybe (RepliedToView ChannelMessageId (Id UserId) (Id ChannelId) msg)
channelMessageRepliedTo localUser games message channel =
    case maybeRepliedTo message channel of
        Just (RepliedToView_Game matchId game a) ->
            case SeqDict.get matchId games of
                Just matchData ->
                    Ui.Lazy.lazy3 gameReplyPreview localUser game matchData
                        |> RepliedToView_Game matchId game
                        |> Just

                Nothing ->
                    Just (RepliedToView_Game matchId game a)

        maybeRepliedTo2 ->
            maybeRepliedTo2


{-| Working out a word spelling game move replays the whole match, so this only runs again
when the match or the users change.
-}
gameReplyPreview : LocalUser -> Message.RepliedToGame -> Game.MatchData -> Element msg
gameReplyPreview localUser game matchData =
    Game.replyPreview (User.allUsers localUser) game matchData


maybeRepliedTo : Message messageId userId channelId -> { a | messages : MessageArray messageId userId channelId } -> Maybe (RepliedToView messageId userId channelId msg)
maybeRepliedTo message channel =
    case message of
        UserTextMessage data ->
            userTextMessageRepliedTo data channel

        EncryptedUserTextMessage data ->
            userTextMessageRepliedTo data channel

        UserJoinedMessage _ _ _ _ ->
            Nothing

        DeletedMessage _ ->
            Nothing

        CallStarted _ ->
            Nothing

        GameStarted _ ->
            Nothing


drawingIsSelectingAnchor : LoggedIn2 -> LoadedFrontend -> Bool
drawingIsSelectingAnchor loggedIn model =
    loggedIn.drawingMode == Drawing.NoSelectedAnchor && Route.toChannelHeaderTab model.route == Just ChannelHeaderTab_Draw


discordConversationViewHelper :
    Id ChannelMessageId
    -> Discord.Id Discord.UserId
    -> DiscordGuildOrDmId
    -> Maybe (Id ChannelMessageId)
    ->
        { a
            | messages : MessageArray ChannelMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)
            , visibleMessages : VisibleMessages ChannelMessageId
            , threads : SeqDict (Id ChannelMessageId) DiscordFrontendThread
            , dateDividerDrawings : SeqDict Date (Drawing (Discord.Id Discord.UserId))
        }
    -> LoggedIn2
    -> LocalState
    -> LoadedFrontend
    -> List ( String, Element FrontendMsg_ )
discordConversationViewHelper lastViewedIndex currentDiscordUserId guildOrDmIdNoThread maybeUrlMessageId channel loggedIn local model =
    let
        channels : SeqDict ( Discord.Id Discord.ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
        channels =
            LocalState.discordChannelMentions guildOrDmIdNoThread local

        guildOrDmId : ( AnyGuildOrDmId, ThreadRoute )
        guildOrDmId =
            ( DiscordGuildOrDmId guildOrDmIdNoThread, NoThread )

        maybeEditing : Maybe EditMessage
        maybeEditing =
            SeqDict.get guildOrDmId loggedIn.editMessage

        othersEditing : SeqSet (Id ChannelMessageId)
        othersEditing =
            SeqDict.remove currentDiscordUserId (LocalState.discordTypingIn guildOrDmIdNoThread local)
                |> SeqDict.values
                |> List.filterMap
                    (\a ->
                        case a.threadRoute of
                            NoThreadWithMaybeMessage maybeMessageId ->
                                if Duration.from a.time model.time |> Quantity.lessThan (Duration.seconds 3) then
                                    maybeMessageId

                                else
                                    Nothing

                            ViewThreadWithMaybeMessage _ _ ->
                                Nothing
                    )
                |> SeqSet.fromList

        replyToIndex : Maybe (Id ChannelMessageId)
        replyToIndex =
            SeqDict.get guildOrDmId loggedIn.replyTo |> Maybe.andThen Message.replyToMaybe

        revealedSpoilers : SeqDict (Id ChannelMessageId) (NonemptySet Int)
        revealedSpoilers =
            revealedChannelSpoilers (DiscordGuildOrDmId guildOrDmIdNoThread) loggedIn

        containerWidth : Int
        containerWidth =
            conversationWidth model

        isMobile : Bool
        isMobile =
            MyUi.isMobile model

        isSelectingAnchor =
            drawingIsSelectingAnchor loggedIn model
    in
    MessageArray.foldr
        (\messageId maybeMessage ( maybeLastDate, list ) ->
            let
                index : Int
                index =
                    Id.toInt messageId
            in
            case maybeMessage of
                Just message ->
                    let
                        threadRoute2 : ThreadRouteWithMessage
                        threadRoute2 =
                            NoThreadWithMessage messageId

                        threadId : Id ChannelMessageId
                        threadId =
                            Id.fromInt index

                        messageHover2 : IsHovered
                        messageHover2 =
                            messageHover (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2 loggedIn model

                        otherUserIsEditing : Bool
                        otherUserIsEditing =
                            SeqSet.member (Id.changeType messageId) othersEditing

                        isEditing : Maybe EditMessage
                        isEditing =
                            case maybeEditing of
                                Just editing ->
                                    if editing.messageIndex == messageId then
                                        Just editing

                                    else
                                        Nothing

                                Nothing ->
                                    Nothing

                        highlight : HighlightMessage
                        highlight =
                            if maybeUrlMessageId == Just messageId then
                                UrlHighlight

                            else if replyToIndex == Just messageId then
                                ReplyToHighlight

                            else
                                NoHighlight

                        maybeRepliedTo2 =
                            maybeRepliedTo message channel

                        date : Date
                        date =
                            Message.createdAt message |> Date.fromPosix local.localUser.timezone
                    in
                    ( Just date
                    , ( String.fromInt index
                      , case isEditing of
                            Just edit ->
                                if MyUi.isMobile model then
                                    -- On mobile, we show the editor at the bottom instead
                                    discordMessageView
                                        model.time
                                        isMobile
                                        containerWidth
                                        False
                                        revealedSpoilers
                                        highlight
                                        messageHover2
                                        currentDiscordUserId
                                        (LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers)
                                        channels
                                        local.localUser
                                        maybeRepliedTo2
                                        (SeqDict.get threadId channel.threads)
                                        messageId
                                        message
                                        |> Ui.map (MessageViewMsg (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                else
                                    let
                                        allUsers =
                                            LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers

                                        editRichText : Maybe (Nonempty (RichText (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)))
                                        editRichText =
                                            case String.Nonempty.fromString edit.text of
                                                Just nonempty ->
                                                    RichText.fromNonemptyString local.localUser.timezone allUsers channels nonempty |> Just

                                                Nothing ->
                                                    Nothing
                                    in
                                    messageEditingView
                                        containerWidth
                                        model.time
                                        isMobile
                                        guildOrDmId
                                        threadRoute2
                                        message
                                        maybeRepliedTo2
                                        (SeqDict.get threadId channel.threads)
                                        revealedSpoilers
                                        (RichText.discordCharsLeft OneToOne.empty editRichText)
                                        edit
                                        editRichText
                                        loggedIn
                                        SeqDict.empty
                                        currentDiscordUserId
                                        allUsers
                                        channels
                                        local

                            Nothing ->
                                case SeqDict.get threadId channel.threads of
                                    Nothing ->
                                        case ( maybeRepliedTo2, Message.mentionsChannel message ) of
                                            ( Nothing, False ) ->
                                                Ui.Lazy.lazy6
                                                    discordMessageViewNotThreadStarter
                                                    (encodeMessageView isMobile messageHover2 containerWidth otherUserIsEditing highlight model.time)
                                                    revealedSpoilers
                                                    currentDiscordUserId
                                                    local.localUser
                                                    index
                                                    message
                                                    |> Ui.map (MessageViewMsg (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                            _ ->
                                                discordMessageView
                                                    model.time
                                                    isMobile
                                                    containerWidth
                                                    False
                                                    revealedSpoilers
                                                    highlight
                                                    messageHover2
                                                    currentDiscordUserId
                                                    (LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers)
                                                    channels
                                                    local.localUser
                                                    maybeRepliedTo2
                                                    Nothing
                                                    messageId
                                                    message
                                                    |> Ui.map (MessageViewMsg (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                    Just thread ->
                                        case ( maybeRepliedTo2, Message.mentionsChannel message ) of
                                            ( Nothing, False ) ->
                                                discordMessageViewThreadStarter
                                                    (encodeMessageView isMobile messageHover2 containerWidth otherUserIsEditing highlight model.time)
                                                    revealedSpoilers
                                                    currentDiscordUserId
                                                    local.localUser
                                                    index
                                                    thread
                                                    message
                                                    |> Ui.map (MessageViewMsg (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                            _ ->
                                                discordMessageView
                                                    model.time
                                                    isMobile
                                                    containerWidth
                                                    False
                                                    revealedSpoilers
                                                    highlight
                                                    messageHover2
                                                    currentDiscordUserId
                                                    (LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers)
                                                    channels
                                                    local.localUser
                                                    maybeRepliedTo2
                                                    (Just thread)
                                                    messageId
                                                    message
                                                    |> Ui.map (MessageViewMsg (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2)
                      )
                        |> (\keyedMessage ->
                                keyedMessage
                                    :: List.map
                                        (Tuple.mapSecond (Ui.map (MessageViewMsg (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2)))
                                        (newMessageLine
                                            (User.discordUserColor local.localUser)
                                            isSelectingAnchor
                                            channel.dateDividerDrawings
                                            maybeLastDate
                                            date
                                            lastViewedIndex
                                            index
                                            messageId
                                        )
                                    ++ list
                           )
                    )

                Nothing ->
                    ( maybeLastDate, ( String.fromInt index, unloadedMessageView index ) :: list )
        )
        ( Nothing, [] )
        (VisibleMessages.slice channel)
        |> Tuple.second
        |> prependUnreadDivider lastViewedIndex channel.visibleMessages.oldest


newMessageLine :
    (userId -> UserColor)
    -> Bool
    -> SeqDict Date (Drawing userId)
    -> Maybe Date
    -> Date
    -> Id messageId
    -> Int
    -> Id messageId
    -> List ( String, Element MessageViewMsg )
newMessageLine drawingUserColor isSelectingAnchor dateDividerDrawings maybeLastDate date lastViewedIndex index messageId =
    case maybeLastDate of
        Just lastDate ->
            case ( lastViewedIndex == messageId, date == lastDate ) of
                ( True, True ) ->
                    [ ( "n" ++ String.fromInt index
                      , Ui.el
                            ([ Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
                             , Ui.borderColor MyUi.alertColor
                             ]
                                ++ newContentLabel
                            )
                            Ui.none
                      )
                    ]

                ( False, False ) ->
                    [ ( "n" ++ String.fromInt index
                      , Ui.el
                            [ Ui.paddingXY 8 0
                            , Ui.height (Ui.px 36)
                            , Ui.contentCenterY
                            , MyUi.noShrinking
                            ]
                            (Ui.el
                                [ Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
                                , Ui.borderColor MyUi.font3
                                , dateDivider drawingUserColor isSelectingAnchor dateDividerDrawings date lastDate
                                ]
                                Ui.none
                            )
                      )
                    ]

                ( True, False ) ->
                    [ ( "n" ++ String.fromInt index
                      , Ui.el
                            [ Ui.height (Ui.px 36), Ui.contentCenterY, MyUi.noShrinking ]
                            (Ui.el
                                ([ Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
                                 , Ui.borderColor MyUi.alertColor
                                 , dateDivider drawingUserColor isSelectingAnchor dateDividerDrawings date lastDate
                                 ]
                                    ++ newContentLabel
                                )
                                Ui.none
                            )
                      )
                    ]

                ( False, True ) ->
                    []

        Nothing ->
            []


{-| The divider is drawn underneath the last message the reader had already seen, so a
reader who hasn't seen anything yet has no message to hang it off and newMessageLine draws
nothing. The same happens further up a long conversation, where the message they stopped at
is older than anything loaded. Both mean everything on screen is new, which is the divider
sitting above all of it.
-}
prependUnreadDivider :
    Id messageId
    -> Id messageId
    -> List ( String, Element msg )
    -> List ( String, Element msg )
prependUnreadDivider lastViewedIndex oldestVisibleMessage list =
    if List.isEmpty list || Id.toInt lastViewedIndex >= Id.toInt oldestVisibleMessage then
        list

    else
        ( "nTop"
        , Ui.el
            ([ Ui.borderWith { left = 0, right = 0, top = 1, bottom = 0 }
             , Ui.borderColor MyUi.alertColor
             ]
                ++ newContentLabel
            )
            Ui.none
        )
            :: list


threadConversationViewHelper :
    Id ThreadMessageId
    -> GuildOrDmId
    -> Id ChannelMessageId
    -> Maybe (Id ThreadMessageId)
    -> FrontendThread
    -> LoggedIn2
    -> LocalState
    -> LoadedFrontend
    -> List ( String, Element FrontendMsg_ )
threadConversationViewHelper lastViewedIndex guildOrDmIdNoThread threadId maybeUrlMessageId thread loggedIn local model =
    let
        channels : SeqDict ( Id ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
        channels =
            LocalState.channelMentions guildOrDmIdNoThread local

        guildOrDmId : ( AnyGuildOrDmId, ThreadRoute )
        guildOrDmId =
            ( GuildOrDmId guildOrDmIdNoThread, ViewThread threadId )

        maybeEditing : Maybe EditMessage
        maybeEditing =
            SeqDict.get guildOrDmId loggedIn.editMessage

        othersEditing : SeqSet (Id ThreadMessageId)
        othersEditing =
            SeqDict.remove local.localUser.session.userId (LocalState.typingIn guildOrDmIdNoThread local)
                |> SeqDict.values
                |> List.filterMap
                    (\a ->
                        case a.threadRoute of
                            ViewThreadWithMaybeMessage typingThreadId maybeMessageId ->
                                if typingThreadId == threadId && (Duration.from a.time model.time |> Quantity.lessThan (Duration.seconds 3)) then
                                    maybeMessageId

                                else
                                    Nothing

                            NoThreadWithMaybeMessage _ ->
                                Nothing
                    )
                |> SeqSet.fromList

        replyToIndex : Maybe (Id ThreadMessageId)
        replyToIndex =
            SeqDict.get guildOrDmId loggedIn.replyTo
                |> Maybe.andThen Message.replyToMaybe
                |> Maybe.map Id.changeType

        revealedSpoilers : SeqDict (Id ThreadMessageId) (NonemptySet Int)
        revealedSpoilers =
            revealedThreadSpoilers (GuildOrDmId guildOrDmIdNoThread) threadId loggedIn

        containerWidth : Int
        containerWidth =
            conversationWidth model

        isMobile : Bool
        isMobile =
            MyUi.isMobile model

        isSelectingAnchor =
            drawingIsSelectingAnchor loggedIn model
    in
    MessageArray.foldr
        (\messageId maybeMessage ( maybeLastDate, list ) ->
            let
                index : Int
                index =
                    Id.toInt messageId
            in
            case maybeMessage of
                Just message ->
                    let
                        threadRoute2 =
                            ViewThreadWithMessage threadId messageId

                        messageHover2 : IsHovered
                        messageHover2 =
                            messageHover (GuildOrDmId guildOrDmIdNoThread) threadRoute2 loggedIn model

                        otherUserIsEditing : Bool
                        otherUserIsEditing =
                            SeqSet.member messageId othersEditing

                        isEditing : Maybe EditMessage
                        isEditing =
                            case maybeEditing of
                                Just editing ->
                                    if editing.messageIndex == Id.changeType messageId then
                                        Just editing

                                    else
                                        Nothing

                                Nothing ->
                                    Nothing

                        highlight : HighlightMessage
                        highlight =
                            if maybeUrlMessageId == Just messageId then
                                UrlHighlight

                            else if replyToIndex == Just messageId then
                                ReplyToHighlight

                            else
                                NoHighlight

                        maybeRepliedTo2 : Maybe (RepliedToView ThreadMessageId (Id UserId) (Id ChannelId) msg)
                        maybeRepliedTo2 =
                            maybeRepliedTo message thread

                        date : Date
                        date =
                            Message.createdAt message |> Date.fromPosix local.localUser.timezone
                    in
                    ( Just date
                    , ( String.fromInt index
                      , case isEditing of
                            Just editing ->
                                if MyUi.isMobile model then
                                    -- On mobile, we show the editor at the bottom instead
                                    threadMessageView
                                        model.time
                                        isMobile
                                        containerWidth
                                        revealedSpoilers
                                        highlight
                                        messageHover2
                                        otherUserIsEditing
                                        (User.allUsers local.localUser)
                                        channels
                                        local.localUser.session.userId
                                        local.localUser
                                        maybeRepliedTo2
                                        messageId
                                        message
                                        |> Ui.map (MessageViewMsg (GuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                else
                                    let
                                        allUsers =
                                            User.allUsers local.localUser

                                        editRichText : Maybe (Nonempty (RichText (Id UserId) (Id ChannelId)))
                                        editRichText =
                                            case String.Nonempty.fromString editing.text of
                                                Just text ->
                                                    Just (RichText.fromNonemptyString local.localUser.timezone allUsers channels text)

                                                Nothing ->
                                                    Nothing
                                    in
                                    threadMessageEditingView
                                        containerWidth
                                        model.time
                                        isMobile
                                        guildOrDmId
                                        threadId
                                        (Id.fromInt index)
                                        message
                                        maybeRepliedTo2
                                        revealedSpoilers
                                        (RichText.maxLength - String.length editing.text)
                                        editing
                                        editRichText
                                        loggedIn
                                        local.localUser.decryptedMessages
                                        local.localUser.session.userId
                                        allUsers
                                        channels
                                        local

                            Nothing ->
                                case ( maybeRepliedTo2, Message.mentionsChannel message ) of
                                    ( Nothing, False ) ->
                                        Ui.Lazy.lazy5
                                            threadMessageViewLazy
                                            (encodeMessageView isMobile messageHover2 containerWidth otherUserIsEditing highlight model.time)
                                            revealedSpoilers
                                            local.localUser
                                            index
                                            message
                                            |> Ui.map (MessageViewMsg (GuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                    _ ->
                                        threadMessageView
                                            model.time
                                            isMobile
                                            containerWidth
                                            revealedSpoilers
                                            highlight
                                            messageHover2
                                            otherUserIsEditing
                                            (User.allUsers local.localUser)
                                            channels
                                            local.localUser.session.userId
                                            local.localUser
                                            maybeRepliedTo2
                                            messageId
                                            message
                                            |> Ui.map (MessageViewMsg (GuildOrDmId guildOrDmIdNoThread) threadRoute2)
                      )
                        :: List.map
                            (Tuple.mapSecond (Ui.map (MessageViewMsg (GuildOrDmId guildOrDmIdNoThread) threadRoute2)))
                            (newMessageLine
                                (User.userColor local.localUser)
                                isSelectingAnchor
                                thread.dateDividerDrawings
                                maybeLastDate
                                date
                                lastViewedIndex
                                index
                                messageId
                            )
                        ++ list
                    )

                Nothing ->
                    ( maybeLastDate, ( String.fromInt index, unloadedMessageView index ) :: list )
        )
        ( Nothing, [] )
        (VisibleMessages.slice thread)
        |> Tuple.second
        |> prependUnreadDivider lastViewedIndex thread.visibleMessages.oldest


discordThreadConversationViewHelper :
    Id ThreadMessageId
    -> Discord.Id Discord.UserId
    -> DiscordGuildOrDmId
    -> Id ChannelMessageId
    -> Maybe (Id ThreadMessageId)
    -> DiscordFrontendThread
    -> LoggedIn2
    -> LocalState
    -> LoadedFrontend
    -> List ( String, Element FrontendMsg_ )
discordThreadConversationViewHelper lastViewedIndex currentDiscordUserId guildOrDmIdNoThread threadId maybeUrlMessageId thread loggedIn local model =
    let
        channels : SeqDict ( Discord.Id Discord.ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
        channels =
            LocalState.discordChannelMentions guildOrDmIdNoThread local

        guildOrDmId : ( AnyGuildOrDmId, ThreadRoute )
        guildOrDmId =
            ( DiscordGuildOrDmId guildOrDmIdNoThread, ViewThread threadId )

        maybeEditing : Maybe EditMessage
        maybeEditing =
            SeqDict.get guildOrDmId loggedIn.editMessage

        othersEditing : SeqSet (Id ThreadMessageId)
        othersEditing =
            SeqDict.remove currentDiscordUserId (LocalState.discordTypingIn guildOrDmIdNoThread local)
                |> SeqDict.values
                |> List.filterMap
                    (\a ->
                        case a.threadRoute of
                            ViewThreadWithMaybeMessage typingThreadId maybeMessageId ->
                                if typingThreadId == threadId && (Duration.from a.time model.time |> Quantity.lessThan (Duration.seconds 3)) then
                                    maybeMessageId

                                else
                                    Nothing

                            NoThreadWithMaybeMessage _ ->
                                Nothing
                    )
                |> SeqSet.fromList

        replyToIndex : Maybe (Id ThreadMessageId)
        replyToIndex =
            SeqDict.get guildOrDmId loggedIn.replyTo
                |> Maybe.andThen Message.replyToMaybe
                |> Maybe.map Id.changeType

        revealedSpoilers : SeqDict (Id ThreadMessageId) (NonemptySet Int)
        revealedSpoilers =
            revealedThreadSpoilers (DiscordGuildOrDmId guildOrDmIdNoThread) threadId loggedIn

        containerWidth : Int
        containerWidth =
            conversationWidth model

        isMobile : Bool
        isMobile =
            MyUi.isMobile model

        isSelectingAnchor =
            drawingIsSelectingAnchor loggedIn model
    in
    MessageArray.foldr
        (\messageId maybeMessage ( maybeLastDate, list ) ->
            let
                index : Int
                index =
                    Id.toInt messageId
            in
            case maybeMessage of
                Just message ->
                    let
                        threadRoute2 =
                            ViewThreadWithMessage threadId messageId

                        messageHover2 : IsHovered
                        messageHover2 =
                            messageHover (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2 loggedIn model

                        otherUserIsEditing : Bool
                        otherUserIsEditing =
                            SeqSet.member messageId othersEditing

                        isEditing : Maybe EditMessage
                        isEditing =
                            case maybeEditing of
                                Just editing ->
                                    if editing.messageIndex == Id.changeType messageId then
                                        Just editing

                                    else
                                        Nothing

                                Nothing ->
                                    Nothing

                        highlight : HighlightMessage
                        highlight =
                            if maybeUrlMessageId == Just messageId then
                                UrlHighlight

                            else if replyToIndex == Just messageId then
                                ReplyToHighlight

                            else
                                NoHighlight

                        maybeRepliedTo2 : Maybe (RepliedToView ThreadMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId) msg)
                        maybeRepliedTo2 =
                            maybeRepliedTo message thread

                        date : Date
                        date =
                            Message.createdAt message |> Date.fromPosix local.localUser.timezone
                    in
                    ( Just date
                    , ( String.fromInt index
                      , case isEditing of
                            Just editing ->
                                if MyUi.isMobile model then
                                    -- On mobile, we show the editor at the bottom instead
                                    discordThreadMessageView
                                        model.time
                                        isMobile
                                        containerWidth
                                        revealedSpoilers
                                        highlight
                                        messageHover2
                                        (LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers)
                                        channels
                                        currentDiscordUserId
                                        local.localUser
                                        maybeRepliedTo2
                                        messageId
                                        message
                                        |> Ui.map (MessageViewMsg (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                else
                                    let
                                        allUsers =
                                            LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers

                                        editRichText : Maybe (Nonempty (RichText (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)))
                                        editRichText =
                                            case String.Nonempty.fromString editing.text of
                                                Just text ->
                                                    Just (RichText.fromNonemptyString local.localUser.timezone allUsers channels text)

                                                Nothing ->
                                                    Nothing
                                    in
                                    threadMessageEditingView
                                        containerWidth
                                        model.time
                                        isMobile
                                        guildOrDmId
                                        threadId
                                        (Id.fromInt index)
                                        message
                                        maybeRepliedTo2
                                        revealedSpoilers
                                        (RichText.discordCharsLeft OneToOne.empty editRichText)
                                        editing
                                        editRichText
                                        loggedIn
                                        SeqDict.empty
                                        currentDiscordUserId
                                        allUsers
                                        channels
                                        local

                            Nothing ->
                                case ( maybeRepliedTo2, Message.mentionsChannel message ) of
                                    ( Nothing, False ) ->
                                        Ui.Lazy.lazy6
                                            discordThreadMessageViewLazy
                                            (encodeMessageView isMobile messageHover2 containerWidth otherUserIsEditing highlight model.time)
                                            revealedSpoilers
                                            currentDiscordUserId
                                            local.localUser
                                            index
                                            message
                                            |> Ui.map (MessageViewMsg (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2)

                                    _ ->
                                        discordThreadMessageView
                                            model.time
                                            isMobile
                                            containerWidth
                                            revealedSpoilers
                                            highlight
                                            messageHover2
                                            (LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers)
                                            channels
                                            currentDiscordUserId
                                            local.localUser
                                            maybeRepliedTo2
                                            messageId
                                            message
                                            |> Ui.map (MessageViewMsg (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2)
                      )
                        :: List.map
                            (Tuple.mapSecond (Ui.map (MessageViewMsg (DiscordGuildOrDmId guildOrDmIdNoThread) threadRoute2)))
                            (newMessageLine
                                (User.discordUserColor local.localUser)
                                isSelectingAnchor
                                thread.dateDividerDrawings
                                maybeLastDate
                                date
                                lastViewedIndex
                                index
                                messageId
                            )
                        ++ list
                    )

                Nothing ->
                    ( maybeLastDate, ( String.fromInt index, unloadedMessageView index ) :: list )
        )
        ( Nothing, [] )
        (VisibleMessages.slice thread)
        |> Tuple.second
        |> prependUnreadDivider lastViewedIndex thread.visibleMessages.oldest


unloadedMessageView : Int -> Element msg
unloadedMessageView index =
    Ui.el
        [ Ui.paddingXY 8 8
        , Ui.background MyUi.alertColor
        , Ui.Font.italic
        ]
        (Ui.text ("Something went wrong when loading message " ++ String.fromInt index))


dateDivider : (userId -> UserColor) -> Bool -> SeqDict Date (Drawing userId) -> Date -> Date -> Ui.Attribute MessageViewMsg
dateDivider userIdToColor isSelectingAnchor dateDividerDrawings laterDate newDate =
    Ui.inFront
        (Ui.column
            ([ Ui.Font.color MyUi.font3
             , Ui.centerX
             , Ui.Font.size 14
             , Ui.Font.bold
             , Ui.move { x = 0, y = -20, z = 0 }
             , Ui.rounded 4
             , Ui.paddingXY 4 0
             ]
                ++ Drawing.anchorHighlight
                    (("guild_dateDivider_" ++ Date.toIsoString newDate) |> Dom.id)
                    userIdToColor
                    (MessageView_PressedDateDivider newDate)
                    isSelectingAnchor
                    (SeqDict.get newDate dateDividerDrawings
                        |> Maybe.withDefault Drawing.emptyDrawing
                    )
            )
            [ Ui.el [ MyUi.noPointerEvents, Ui.Font.center ] (Ui.text (MyUi.datestampDate laterDate))
            , Ui.el [ MyUi.noPointerEvents, Ui.Font.center ] (Ui.text (MyUi.datestampDate newDate))
            ]
        )


newContentLabel : List (Ui.Attribute msg)
newContentLabel =
    [ Ui.inFront
        (Ui.el
            [ Ui.move { x = -6, y = -11, z = 0 }
            , Ui.alignRight
            , Ui.width Ui.shrink
            , Ui.Font.bold
            , Ui.Font.size 14
            ]
            (Ui.text newMessagesBadgeText)
        )
    , Ui.inFront
        (Ui.el
            [ Ui.Font.color MyUi.font1
            , Ui.background MyUi.alertColor
            , Ui.width (Ui.px 42)
            , Ui.alignRight
            , Ui.height (Ui.px 15)
            , Ui.roundedWith
                { bottomLeft = 8, bottomRight = 0, topLeft = 8, topRight = 0 }
            , Ui.move { x = 0, y = -8, z = 0 }
            ]
            Ui.none
        )
    ]


{-| The lazy wrappers a message view goes through take six arguments at most, so the flags,
the container width and the current time travel as a single Int. The time is what the
timestamps in a message count down from, rounded to the minute so that a message is only
redrawn when that countdown would read differently. It is multiplied in rather than shifted
in because Bitwise only reaches 32 bits.
-}
encodeMessageView : Bool -> IsHovered -> Int -> Bool -> HighlightMessage -> Time.Posix -> Int
encodeMessageView isMobile isHovered containerWidth otherUserIsEditing highlight time =
    (if otherUserIsEditing then
        1

     else
        0
    )
        + Bitwise.shiftLeftBy
            1
            (case isHovered of
                IsNotHovered ->
                    0

                IsHovered ->
                    1

                IsHoveredButNoMenu ->
                    2

                IsHoveredWhileSelectingAnchor ->
                    3

                IsHoveredReactionsOnly ->
                    4
            )
        + Bitwise.shiftLeftBy
            4
            (case highlight of
                NoHighlight ->
                    0

                ReplyToHighlight ->
                    1

                MentionHighlight ->
                    2

                UrlHighlight ->
                    3
            )
        + Bitwise.shiftLeftBy
            6
            (if isMobile then
                1

             else
                0
            )
        + Bitwise.shiftLeftBy 7 containerWidth
        + (Time.posixToMillis time // msInMinute * timePackingOffset)


{-| Where the time starts in the Int `encodeMessageView` packs. The flags take the bottom
seven bits and the container width sits above them, so this leaves room for a container up to
65535px wide, and the whole packed number stays well inside the range integers are exact in.
-}
timePackingOffset : Int
timePackingOffset =
    2 ^ 23


msInMinute : Int
msInMinute =
    1000 * 60


decodeMessageView :
    Int
    ->
        { containerWidth : Int
        , isEditing : Bool
        , highlight : HighlightMessage
        , isHovered : IsHovered
        , isMobile : Bool
        , time : Time.Posix
        }
decodeMessageView packed =
    let
        value : Int
        value =
            modBy timePackingOffset packed
    in
    { isEditing = Bitwise.and 0x01 value == 1
    , isHovered =
        case Bitwise.shiftRightBy 1 value |> Bitwise.and 0x07 of
            1 ->
                IsHovered

            2 ->
                IsHoveredButNoMenu

            3 ->
                IsHoveredWhileSelectingAnchor

            4 ->
                IsHoveredReactionsOnly

            _ ->
                IsNotHovered
    , highlight =
        case Bitwise.shiftRightBy 4 value |> Bitwise.and 0x03 of
            1 ->
                ReplyToHighlight

            2 ->
                MentionHighlight

            3 ->
                UrlHighlight

            _ ->
                NoHighlight
    , isMobile = Bitwise.shiftRightBy 6 value |> Bitwise.and 0x01 |> (==) 1
    , containerWidth = Bitwise.shiftRightBy 7 value
    , time = packed // timePackingOffset * msInMinute |> Time.millisToPosix
    }


encodeFriendsColumn : Bool -> Int -> Int
encodeFriendsColumn canScroll time =
    (if canScroll then
        1

     else
        0
    )
        + (time // msInMinute * 2)


decodeFriendsColumn : Int -> { canScroll : Bool, time : Int }
decodeFriendsColumn packed =
    { canScroll = modBy 2 packed == 1
    , time = packed // 2 * msInMinute
    }


encodeFriendLabel : Bool -> Int -> Int
encodeFriendLabel isSelected time =
    (if isSelected then
        1

     else
        0
    )
        + (time // msInMinute * 2)


decodeFriendLabel : Int -> { isSelected : Bool, time : Time.Posix }
decodeFriendLabel packed =
    { isSelected = modBy 2 packed == 1
    , time = packed // 2 * msInMinute |> Time.millisToPosix
    }


conversationContainerId : HtmlId
conversationContainerId =
    Dom.id "conversationContainer"


emojiSelectorPaddingX : Int
emojiSelectorPaddingX =
    4


emojiSelectorX : Bool -> LoadedFrontend -> Int
emojiSelectorX isMobile model =
    if isMobile then
        Coord.xRaw model.windowSize - emojiSelectorPaddingX * 2

    else
        Coord.xRaw model.windowSize - MyUi.channelAndGuildColumnWidth model.windowSize - emojiSelectorPaddingX * 2


emojiSelector :
    Bool
    -> SeqSet (Id CustomEmojiId)
    -> SeqSet (Id StickerId)
    -> LocalState
    -> LoggedIn2
    -> LoadedFrontend
    -> Ui.Attribute FrontendMsg_
emojiSelector isMobile availableCustomEmojis availableStickers local loggedIn model =
    let
        emojiConfig : EmojiConfig
        emojiConfig =
            local.localUser.user.emojiConfig

        availableHeight : Int
        availableHeight =
            model.visualViewportHeight - 54 - model.startupData.safeAreaInsetTop
    in
    case loggedIn.showEmojiSelector of
        EmojiSelectorHidden ->
            Ui.noAttr

        EmojiSelectorForReaction _ _ ->
            emojiSelectorAtBottomOfTheConversation isMobile availableHeight availableCustomEmojis availableStickers local loggedIn model

        EmojiSelectorForSheepGameReaction _ _ _ ->
            emojiSelectorAtBottomOfTheConversation isMobile availableHeight availableCustomEmojis availableStickers local loggedIn model

        EmojiSelectorForWordSpellingGameReaction _ _ _ ->
            emojiSelectorAtBottomOfTheConversation isMobile availableHeight availableCustomEmojis availableStickers local loggedIn model

        EmojiSelectorForMessage _ ->
            Ui.inFront
                (Emoji.selector
                    isMobile
                    availableHeight
                    model.startupData.scrollbarWidth
                    (emojiSelectorX isMobile model)
                    loggedIn.emojiSelector
                    emojiConfig
                    model.emojiData
                    availableCustomEmojis
                    local.localUser.customEmojis
                    availableStickers
                    local.localUser.stickers
                    |> Ui.el
                        [ Ui.alignBottom
                        , Ui.paddingXY emojiSelectorPaddingX 0
                        , if isMobile then
                            Ui.width Ui.fill

                          else
                            Ui.width Ui.shrink
                        , emojiSelectorZIndex
                        ]
                    |> Ui.map EmojiSelectorMsg
                )

        EmojiSelectorForEditMessage position _ ->
            let
                y =
                    Coord.yRaw position - Emoji.selectorHeight availableHeight - MyUi.channelHeaderHeight
            in
            Ui.inFront
                (Emoji.selector
                    isMobile
                    availableHeight
                    model.startupData.scrollbarWidth
                    (emojiSelectorX isMobile model)
                    loggedIn.emojiSelector
                    emojiConfig
                    model.emojiData
                    availableCustomEmojis
                    local.localUser.customEmojis
                    availableStickers
                    local.localUser.stickers
                    |> Ui.el
                        [ Ui.paddingXY emojiSelectorPaddingX 0
                        , Ui.move
                            { x = 0
                            , y =
                                if y < 0 then
                                    Coord.yRaw position

                                else
                                    y
                            , z = 0
                            }
                        , emojiSelectorZIndex
                        ]
                    |> Ui.map EmojiSelectorMsg
                )

        EmojiSelectorForSheepGameInput _ position _ ->
            let
                y : Int
                y =
                    Coord.yRaw position
                        - MyUi.channelHeaderHeight
                        |> min (Coord.yRaw model.windowSize - MyUi.channelHeaderHeight - Emoji.selectorHeight availableHeight)
                        |> max 0
            in
            Ui.inFront
                (Emoji.selector
                    isMobile
                    availableHeight
                    model.startupData.scrollbarWidth
                    (emojiSelectorX isMobile model)
                    loggedIn.emojiSelector
                    emojiConfig
                    model.emojiData
                    availableCustomEmojis
                    local.localUser.customEmojis
                    availableStickers
                    local.localUser.stickers
                    |> Ui.el
                        [ Ui.paddingXY emojiSelectorPaddingX 0
                        , Ui.move { x = 0, y = y, z = 0 }
                        , emojiSelectorZIndex
                        ]
                    |> Ui.map EmojiSelectorMsg
                )


emojiSelectorAtBottomOfTheConversation :
    Bool
    -> Int
    -> SeqSet (Id CustomEmojiId)
    -> SeqSet (Id StickerId)
    -> LocalState
    -> LoggedIn2
    -> LoadedFrontend
    -> Ui.Attribute FrontendMsg_
emojiSelectorAtBottomOfTheConversation isMobile availableHeight availableCustomEmojis availableStickers local loggedIn model =
    Ui.inFront
        (Emoji.selector
            isMobile
            availableHeight
            model.startupData.scrollbarWidth
            (emojiSelectorX isMobile model)
            loggedIn.emojiSelector
            local.localUser.user.emojiConfig
            model.emojiData
            availableCustomEmojis
            local.localUser.customEmojis
            availableStickers
            local.localUser.stickers
            |> Ui.el
                [ Ui.alignBottom
                , Ui.paddingXY emojiSelectorPaddingX 0
                , if isMobile then
                    Ui.width Ui.fill

                  else
                    Ui.width Ui.shrink
                , emojiSelectorZIndex
                ]
            |> Ui.map EmojiSelectorMsg
        )


emojiSelectorZIndex : Ui.Attribute msg
emojiSelectorZIndex =
    MyUi.htmlStyle "z-index" "30"


{-| The channel's own reply header. Only a message written straight into a channel can reply
into one of the channel's matches, so only this one can say what the move or answer it points
at was.
-}
channelReplyToHeader :
    Bool
    -> ( AnyGuildOrDmId, ThreadRoute )
    -> Maybe (RepliedTo messageId)
    -> SeqDict (Id UserId) { a | name : PersonName, color : UserColor }
    -> SeqDict (Id ChannelMessageId) Game.MatchData
    -> { b | messages : MessageArray messageId2 (Id UserId) (Id ChannelId) }
    -> Element FrontendMsg_
channelReplyToHeader isMobile guildOrDmIdNoThread replyTo allUsers games channel =
    case replyTo of
        Just (RepliedToGame matchId game) ->
            replyToHeaderRow
                isMobile
                (PressedCloseReplyTo guildOrDmIdNoThread)
                (case SeqDict.get matchId games of
                    Just matchData ->
                        [ Game.replyPreview allUsers game matchData ]

                    Nothing ->
                        []
                )

        _ ->
            replyToHeader isMobile guildOrDmIdNoThread replyTo allUsers channel


replyToHeader :
    Bool
    -> ( AnyGuildOrDmId, ThreadRoute )
    -> Maybe (RepliedTo messageId)
    -> SeqDict userId { a | name : PersonName, color : UserColor }
    -> { b | messages : MessageArray messageId2 userId channelId }
    -> Element FrontendMsg_
replyToHeader isMobile guildOrDmIdNoThread replyTo allUsers channel =
    case replyTo of
        Just (RepliedToMessage messageIndex) ->
            case MessageArray.get (Id.changeType messageIndex) channel.messages of
                Just message ->
                    case message of
                        UserTextMessage data ->
                            replyToHeaderHelper isMobile (PressedCloseReplyTo guildOrDmIdNoThread) (Just data.createdBy) allUsers

                        EncryptedUserTextMessage data ->
                            replyToHeaderHelper isMobile (PressedCloseReplyTo guildOrDmIdNoThread) (Just data.createdBy) allUsers

                        UserJoinedMessage _ userId _ _ ->
                            replyToHeaderHelper isMobile (PressedCloseReplyTo guildOrDmIdNoThread) (Just userId) allUsers

                        DeletedMessage _ ->
                            Ui.none

                        CallStarted { startedBy } ->
                            replyToHeaderHelper isMobile (PressedCloseReplyTo guildOrDmIdNoThread) (Just startedBy) allUsers

                        GameStarted { startedBy } ->
                            replyToHeaderHelper isMobile (PressedCloseReplyTo guildOrDmIdNoThread) (Just startedBy) allUsers

                _ ->
                    Ui.none

        Just (RepliedToGame _ _) ->
            Ui.none

        Just NoReply ->
            Ui.none

        Nothing ->
            Ui.none


replyToHeaderHelper : Bool -> msg -> Maybe userId -> SeqDict userId { a | name : PersonName, color : UserColor } -> Element msg
replyToHeaderHelper isMobile onPress userId allUsers =
    replyToHeaderRow
        isMobile
        onPress
        [ Ui.text "Reply to "
        , case userId of
            Just userId2 ->
                User.toColoredString userId2 allUsers

            Nothing ->
                Ui.text "message"
        ]


replyToHeaderRow : Bool -> msg -> List (Element msg) -> Element msg
replyToHeaderRow isMobile onPress content =
    Ui.row
        [ Ui.id (Dom.idToString replyToHeaderId)
        , Ui.Font.color MyUi.font2
        , Ui.background MyUi.background2
        , Ui.paddingWith { left = 12, right = 32, top = 8, bottom = 8 }
        , Ui.roundedWith { topLeft = 8, topRight = 8, bottomLeft = 0, bottomRight = 0 }
        , Ui.borderWith { left = 1, right = 1, top = 1, bottom = 0 }
        , Ui.borderColor MyUi.border1
        , Ui.spacing 5
        , Ui.inFront
            (MyUi.elButton
                (Dom.id "guild_closeReplyToHeader")
                onPress
                [ Ui.width (Ui.px 32)
                , Ui.paddingXY 4 0
                , Ui.height Ui.fill
                , Ui.contentCenterY
                , Ui.alignRight
                , MyUi.hoverText "Cancel reply"
                ]
                (Ui.html Icons.x)
            )
        ]
        ((if isMobile then
            Ui.none

          else
            Ui.el [ Ui.width Ui.shrink, Ui.move { x = 0, y = 2, z = 0 } ] (Ui.html (Icons.reply 18))
         )
            :: content
        )
        |> Ui.el [ Ui.paddingWith MessageInput.textareaPadding, Ui.move { x = 0, y = 1, z = 0 } ]


replyToHeaderId : HtmlId
replyToHeaderId =
    Dom.id "guild_replyToHeader"


newMessagesId : HtmlId
newMessagesId =
    Dom.id "guild_newMessages"


{-| Messages that arrived without the conversation scrolling to the bottom, either
because the user had scrolled up or because the drawing tab held the scroll
position, are counted in this warning above the message input. Pressing it
deselects the drawing anchor and scrolls to the bottom.
-}
newMessagesView : LoadedFrontend -> LoggedIn2 -> Element FrontendMsg_
newMessagesView model loggedIn =
    if loggedIn.newMessagesWhileNotScrolledToBottom > 0 then
        MyUi.elButton
            newMessagesId
            PressedNewMessagesWarning
            [ Ui.Font.color MyUi.font1
            , Ui.background MyUi.buttonBackground
            , Ui.paddingXY 12 8
            , Ui.roundedWith { topLeft = 8, topRight = 8, bottomLeft = 0, bottomRight = 0 }
            , Ui.borderWith { left = 1, right = 1, top = 1, bottom = 0 }
            , Ui.borderColor MyUi.buttonBorder
            , Ui.pointer
            , MyUi.hover (MyUi.isMobile model) [ Ui.Anim.backgroundColor MyUi.highlightedBorder ]
            ]
            (Ui.Prose.paragraph
                []
                [ Ui.text
                    ((if loggedIn.newMessagesWhileNotScrolledToBottom == 1 then
                        "1 new message"

                      else
                        String.fromInt loggedIn.newMessagesWhileNotScrolledToBottom ++ " new messages"
                     )
                        ++ ". Click here to jump to the bottom."
                    )
                ]
            )

    else
        Ui.none


{-| Attributes added to the conversation while the drawing tab is open. Until
an anchor is picked, valid anchor elements are highlighted when hovering over
them. Once an anchor is picked an overlay captures mouse events for freehand
drawing.
-}
drawingModeAttributes : Route -> Drawing.Model -> List (Ui.Attribute FrontendMsg_)
drawingModeAttributes route drawingMode =
    if Route.toChannelHeaderTab route == Just ChannelHeaderTab_Draw then
        case drawingMode of
            Drawing.NoSelectedAnchor ->
                []

            Drawing.SelectedAnchor selected ->
                Ui.inFront (Drawing.inputOverlay (selected.stroke /= Nothing) DrawingMsg)
                    :: (if selected.zoom /= 1 then
                            -- Keep the magnified conversation clipped to its normal
                            -- area so zooming in doesn't push the rest of the page around.
                            [ Ui.clip ]

                        else
                            []
                       )

    else
        []


drawingZoomAttributes : Route -> Drawing.Model -> List (Ui.Attribute FrontendMsg_)
drawingZoomAttributes route drawingMode =
    case ( Route.toChannelHeaderTab route, drawingMode ) of
        ( Just ChannelHeaderTab_Draw, Drawing.SelectedAnchor selected ) ->
            case ( selected.zoom /= 1, Drawing.zoomCssOrigin selected ) of
                ( True, Just ( originX, originY ) ) ->
                    [ MyUi.htmlStyle "transform" ("scale(" ++ String.fromFloat selected.zoom ++ ")")
                    , MyUi.htmlStyle
                        "transform-origin"
                        (String.fromFloat originX ++ "px " ++ String.fromFloat originY ++ "px")
                    ]

                _ ->
                    []

        _ ->
            []


missingPrivateKeyPlaceholder : Html msg
missingPrivateKeyPlaceholder =
    Html.span
        [ Html.Attributes.style "color" (MyUi.colorToStyle MyUi.errorColor) ]
        [ Html.text "Private key missing. Goto\u{00A0}"
        , Html.span [ Html.Attributes.style "position" "absolute" ] [ Icons.gear ]
        ]


conversationView :
    Id ChannelMessageId
    -> GuildOrDmId
    -> Maybe (Id ChannelMessageId)
    -> LoggedIn2
    -> LoadedFrontend
    -> LocalState
    -> Bool
    -> String
    ->
        { a
            | messages : MessageArray ChannelMessageId (Id UserId) (Id ChannelId)
            , visibleMessages : VisibleMessages ChannelMessageId
            , threads : SeqDict (Id ChannelMessageId) FrontendThread
            , dateDividerDrawings : SeqDict Date (Drawing (Id UserId))
            , games : SeqDict (Id ChannelMessageId) Game.MatchData
        }
    -> Element FrontendMsg_
conversationView lastViewedIndex guildOrDmIdNoThread maybeUrlMessageId loggedIn model local missingPrivateKey name channel =
    let
        channels : SeqDict ( Id ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
        channels =
            LocalState.channelMentions guildOrDmIdNoThread local

        allUsers : SeqDict (Id UserId) FrontendUser
        allUsers =
            User.allUsers local.localUser

        replyTo : Maybe (RepliedTo ChannelMessageId)
        replyTo =
            SeqDict.get ( GuildOrDmId guildOrDmIdNoThread, NoThread ) loggedIn.replyTo

        isMobile : Bool
        isMobile =
            MyUi.isMobile model

        draft : String
        draft =
            case SeqDict.get ( GuildOrDmId guildOrDmIdNoThread, NoThread ) loggedIn.drafts of
                Just text ->
                    String.Nonempty.toString text

                Nothing ->
                    ""

        draftRichText : Maybe (Nonempty (RichText (Id UserId) (Id ChannelId)))
        draftRichText =
            case SeqDict.get ( GuildOrDmId guildOrDmIdNoThread, NoThread ) loggedIn.drafts of
                Just text ->
                    Just (RichText.fromNonemptyString local.localUser.timezone allUsers channels text)

                Nothing ->
                    Nothing
    in
    Ui.column
        [ Ui.height Ui.fill
        , Ui.heightMin 0
        ]
        [ ChannelHeader.channel isMobile name guildOrDmIdNoThread local loggedIn model
        , Ui.el
            ([ emojiSelector
                isMobile
                local.localUser.user.availableCustomEmojis
                local.localUser.user.availableStickers
                local
                loggedIn
                model
             , Ui.heightMin 0
             , Ui.height Ui.fill
             ]
                ++ drawingModeAttributes model.route loggedIn.drawingMode
            )
            (Ui.Keyed.column
                ([ Ui.height Ui.fill
                 , Ui.width Ui.fill
                 , Ui.paddingWith { left = 0, right = 0, top = 200, bottom = 16 }
                 , MyUi.scrollable (MyUi.canScroll (MyUi.isMobile model) model.drag)
                 , MyUi.htmlStyle "overflow-wrap" "break-word"
                 , Ui.id (Dom.idToString conversationContainerId)
                 , Ui.Events.on
                    "scroll"
                    (Scroll.decodeScrollToBottom
                        (UserScrolled (GuildOrDmId guildOrDmIdNoThread) NoThread)
                        loggedIn.channelScrollPosition
                    )
                 , Ui.heightMin 0
                 , MyUi.bounceScroll isMobile
                 , MyUi.htmlStyle "background-image" "url(/cacheable/grid1.png)"
                 ]
                    ++ drawingZoomAttributes model.route loggedIn.drawingMode
                )
                (( "a"
                 , Ui.el
                    [ Ui.Font.color MyUi.font2, Ui.paddingXY 8 4, Ui.alignBottom, Ui.Font.size 20 ]
                    (if VisibleMessages.startIsVisible channel.visibleMessages then
                        case guildOrDmIdNoThread of
                            GuildOrDmId_Guild _ ->
                                Ui.text ("This is the start of #" ++ name)

                            GuildOrDmId_Dm { otherUserId } ->
                                Ui.text
                                    (if otherUserId == local.localUser.session.userId then
                                        "This is the start of a conversation with yourself"

                                     else
                                        "This is the start of your conversation with " ++ name
                                    )

                     else
                        Ui.none
                    )
                 )
                    :: conversationViewHelper
                        lastViewedIndex
                        guildOrDmIdNoThread
                        maybeUrlMessageId
                        channel
                        loggedIn
                        local
                        model
                )
            )
        , Ui.column
            [ Ui.paddingXY 2 0
            , Ui.heightMin 0
            , MyUi.noShrinking
            , case SeqDict.get ( GuildOrDmId guildOrDmIdNoThread, NoThread ) loggedIn.filesToUpload of
                Just filesToUpload2 ->
                    fileUploadPreview
                        (PressedDeleteAttachedFile ( GuildOrDmId guildOrDmIdNoThread, NoThread ))
                        (PressedViewAttachedFileInfo ( GuildOrDmId guildOrDmIdNoThread, NoThread ))
                        (PressedToggleAttachedFileSpoiler ( GuildOrDmId guildOrDmIdNoThread, NoThread ))
                        draftRichText
                        filesToUpload2
                        |> Ui.inFront

                Nothing ->
                    Ui.noAttr
            ]
            [ newMessagesView model loggedIn
            , channelReplyToHeader isMobile ( GuildOrDmId guildOrDmIdNoThread, NoThread ) replyTo allUsers channel.games channel
            , MessageInput.view
                (Dom.id "messageMenu_channelInput")
                (replyTo == Nothing)
                (MyUi.isMobile model)
                channelTextInputId
                (if missingPrivateKey then
                    missingPrivateKeyPlaceholder

                 else
                    (case guildOrDmIdNoThread of
                        GuildOrDmId_Guild _ ->
                            "Write a message in #" ++ name

                        GuildOrDmId_Dm { otherUserId } ->
                            "Write a message to "
                                ++ (if otherUserId == local.localUser.session.userId then
                                        "yourself"

                                    else
                                        name
                                   )
                    )
                        |> MessageInput.textPlaceholder
                )
                (RichText.maxLength - String.length draft)
                draft
                draftRichText
                (case SeqDict.get ( GuildOrDmId guildOrDmIdNoThread, NoThread ) loggedIn.filesToUpload of
                    Just attachedFiles ->
                        NonemptyDict.toSeqDict attachedFiles

                    Nothing ->
                        SeqDict.empty
                )
                local.localUser
                loggedIn
                (User.allUsers local.localUser)
                channels
                |> Ui.map (MessageInputMsg (GuildOrDmId guildOrDmIdNoThread) NoThread)
            , peopleAreTypingView allUsers NoThread (LocalState.typingIn guildOrDmIdNoThread local) local.localUser.session.userId model
            ]
        ]


discordConversationView :
    Id ChannelMessageId
    -> Discord.Id Discord.UserId
    -> DiscordGuildOrDmId
    -> Maybe (Id ChannelMessageId)
    -> LoggedIn2
    -> LoadedFrontend
    -> LocalState
    -> String
    ->
        { a
            | messages : MessageArray ChannelMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)
            , isForum : Bool
            , visibleMessages : VisibleMessages ChannelMessageId
            , threads : SeqDict (Id ChannelMessageId) DiscordFrontendThread
            , dateDividerDrawings : SeqDict Date (Drawing (Discord.Id Discord.UserId))
        }
    -> SeqSet (Id CustomEmojiId)
    -> SeqSet (Id StickerId)
    -> Element FrontendMsg_
discordConversationView lastViewedIndex currentDiscordUserId guildOrDmIdNoThread maybeUrlMessageId loggedIn model local name channel availableCustomEmojis availableStickers =
    let
        channels : SeqDict ( Discord.Id Discord.ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
        channels =
            LocalState.discordChannelMentions guildOrDmIdNoThread local

        guildOrDmId : ( AnyGuildOrDmId, ThreadRoute )
        guildOrDmId =
            ( DiscordGuildOrDmId guildOrDmIdNoThread, NoThread )

        allUsers : SeqDict (Discord.Id Discord.UserId) DiscordFrontendUser
        allUsers =
            LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers

        replyTo : Maybe (RepliedTo ChannelMessageId)
        replyTo =
            SeqDict.get guildOrDmId loggedIn.replyTo

        isMobile : Bool
        isMobile =
            MyUi.isMobile model

        draft : String
        draft =
            case SeqDict.get guildOrDmId loggedIn.drafts of
                Just text ->
                    String.Nonempty.toString text

                Nothing ->
                    ""

        draftRichText : Maybe (Nonempty (RichText (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)))
        draftRichText =
            case SeqDict.get guildOrDmId loggedIn.drafts of
                Just text ->
                    Just (RichText.fromNonemptyString local.localUser.timezone allUsers channels text)

                Nothing ->
                    Nothing
    in
    Ui.column
        [ Ui.height Ui.fill
        , Ui.heightMin 0
        ]
        [ ChannelHeader.discordChannel isMobile name guildOrDmIdNoThread local loggedIn model
        , Ui.el
            ([ emojiSelector isMobile availableCustomEmojis availableStickers local loggedIn model
             , Ui.heightMin 0
             , Ui.height Ui.fill
             ]
                ++ drawingModeAttributes model.route loggedIn.drawingMode
            )
            (Ui.Keyed.column
                ([ Ui.height Ui.fill
                 , Ui.width Ui.fill
                 , Ui.paddingWith { left = 0, right = 0, top = 200, bottom = 16 }
                 , MyUi.scrollable (MyUi.canScroll (MyUi.isMobile model) model.drag)
                 , MyUi.htmlStyle "overflow-wrap" "break-word"
                 , Ui.id (Dom.idToString conversationContainerId)
                 , Ui.Events.on
                    "scroll"
                    (Scroll.decodeScrollToBottom
                        (UserScrolled (DiscordGuildOrDmId guildOrDmIdNoThread) NoThread)
                        loggedIn.channelScrollPosition
                    )
                 , Ui.heightMin 0
                 , MyUi.bounceScroll isMobile
                 , MyUi.htmlStyle "background-image" "url(/cacheable/grid1.png)"
                 ]
                    ++ drawingZoomAttributes model.route loggedIn.drawingMode
                )
                (( "a"
                 , Ui.el
                    [ Ui.Font.color MyUi.font2, Ui.paddingXY 8 4, Ui.alignBottom, Ui.Font.size 20 ]
                    (if VisibleMessages.startIsVisible channel.visibleMessages then
                        case guildOrDmIdNoThread of
                            DiscordGuildOrDmId_Guild _ ->
                                Ui.text ("This is the start of #" ++ name)

                            DiscordGuildOrDmId_Dm data ->
                                Ui.text
                                    (if ChannelHeader.chattingWithYourself data local then
                                        "This is the start of a conversation with yourself"

                                     else
                                        "This is the start of your conversation with " ++ name
                                    )

                     else
                        Ui.none
                    )
                 )
                    :: discordConversationViewHelper
                        lastViewedIndex
                        currentDiscordUserId
                        guildOrDmIdNoThread
                        maybeUrlMessageId
                        channel
                        loggedIn
                        local
                        model
                )
            )
        , Ui.column
            [ Ui.paddingXY 2 0
            , Ui.heightMin 0
            , MyUi.noShrinking
            , case SeqDict.get guildOrDmId loggedIn.filesToUpload of
                Just filesToUpload2 ->
                    fileUploadPreview
                        (PressedDeleteAttachedFile guildOrDmId)
                        (PressedViewAttachedFileInfo guildOrDmId)
                        (PressedToggleAttachedFileSpoiler guildOrDmId)
                        draftRichText
                        filesToUpload2
                        |> Ui.inFront

                Nothing ->
                    Ui.noAttr
            ]
            [ newMessagesView model loggedIn
            , replyToHeader isMobile ( DiscordGuildOrDmId guildOrDmIdNoThread, NoThread ) replyTo allUsers channel
            , case ( LocalState.canSendDiscordMessage local guildOrDmIdNoThread, channel.isForum ) of
                ( Ok (), False ) ->
                    MessageInput.view
                        (Dom.id "messageMenu_channelInput")
                        (replyTo == Nothing)
                        (MyUi.isMobile model)
                        channelTextInputId
                        ((case guildOrDmIdNoThread of
                            DiscordGuildOrDmId_Guild _ ->
                                "Write a message in #" ++ name

                            DiscordGuildOrDmId_Dm data ->
                                "Write a message to "
                                    ++ (if ChannelHeader.chattingWithYourself data local then
                                            "yourself"

                                        else
                                            name
                                       )
                         )
                            |> MessageInput.textPlaceholder
                        )
                        (RichText.discordCharsLeft OneToOne.empty draftRichText)
                        draft
                        draftRichText
                        (case SeqDict.get guildOrDmId loggedIn.filesToUpload of
                            Just attachedFiles ->
                                NonemptyDict.toSeqDict attachedFiles

                            Nothing ->
                                SeqDict.empty
                        )
                        local.localUser
                        loggedIn
                        (LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers)
                        channels
                        |> Ui.map (MessageInputMsg (DiscordGuildOrDmId guildOrDmIdNoThread) NoThread)

                ( Err error, _ ) ->
                    MessageInput.disabledView
                        (replyTo == Nothing)
                        error
                        (case SeqDict.get guildOrDmId loggedIn.drafts of
                            Just text ->
                                String.Nonempty.toString text

                            Nothing ->
                                ""
                        )
                        (case SeqDict.get guildOrDmId loggedIn.filesToUpload of
                            Just attachedFiles ->
                                NonemptyDict.toSeqDict attachedFiles

                            Nothing ->
                                SeqDict.empty
                        )
                        local.localUser

                ( _, True ) ->
                    MessageInput.disabledView
                        (replyTo == Nothing)
                        "Forum channel posting is unsupported"
                        (case SeqDict.get guildOrDmId loggedIn.drafts of
                            Just text ->
                                String.Nonempty.toString text

                            Nothing ->
                                ""
                        )
                        (case SeqDict.get guildOrDmId loggedIn.filesToUpload of
                            Just attachedFiles ->
                                NonemptyDict.toSeqDict attachedFiles

                            Nothing ->
                                SeqDict.empty
                        )
                        local.localUser
            , peopleAreTypingView allUsers NoThread (LocalState.discordTypingIn guildOrDmIdNoThread local) currentDiscordUserId model
            ]
        ]


typingDebouncerDelay : Duration
typingDebouncerDelay =
    Duration.seconds 7


peopleAreTypingView :
    SeqDict userId { a | name : PersonName }
    -> ThreadRoute
    -> SeqDict userId { b | threadRoute : ThreadRouteWithMaybeMessage, time : Time.Posix }
    -> userId
    -> LoadedFrontend
    -> Element msg
peopleAreTypingView allUsers threadRoute typing currentUserId model =
    (case
        SeqDict.filter
            (\_ a ->
                (Duration.from a.time model.time |> Quantity.lessThan (Quantity.plus Duration.second typingDebouncerDelay))
                    && (a.threadRoute == Id.threadRouteWithNoMessage threadRoute)
            )
            (SeqDict.remove currentUserId typing)
            |> SeqDict.keys
     of
        [] ->
            " "

        [ single ] ->
            User.toString single allUsers ++ " is typing..."

        [ one, two ] ->
            User.toString one allUsers ++ " and " ++ User.toString two allUsers ++ " are typing..."

        [ one, two, three ] ->
            User.toString one allUsers
                ++ ", "
                ++ User.toString two allUsers
                ++ ", and "
                ++ User.toString three allUsers
                ++ " are typing..."

        _ :: _ :: _ :: _ ->
            "Several people are typing..."
    )
        |> Ui.text
        |> Ui.el
            [ Ui.Font.bold
            , Ui.Font.size 13
            , Ui.Font.color MyUi.font3
            , MyUi.prewrap
            , MyUi.noShrinking
            , Ui.contentCenterY
            , MyUi.htmlStyle "user-select" "none"
            , Ui.paddingWith
                { left = 12 + model.startupData.safeAreaInsetBottom // 2
                , right = 12 + model.startupData.safeAreaInsetBottom // 2
                , top = 0
                , bottom =
                    if MyUi.virtualKeyboardOpen model then
                        0

                    else
                        model.startupData.safeAreaInsetBottom
                }
            ]


threadConversationView :
    Id ThreadMessageId
    -> GuildOrDmId
    -> Maybe (Id ThreadMessageId)
    -> Id ChannelMessageId
    -> LoggedIn2
    -> LoadedFrontend
    -> LocalState
    -> Bool
    -> String
    -> String
    -> FrontendThread
    -> Element FrontendMsg_
threadConversationView lastViewedIndex guildOrDmIdNoThread maybeUrlMessageId threadId loggedIn model local missingPrivateKey name threadName channel =
    let
        channels : SeqDict ( Id ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
        channels =
            LocalState.channelMentions guildOrDmIdNoThread local

        guildOrDmId : ( AnyGuildOrDmId, ThreadRoute )
        guildOrDmId =
            ( GuildOrDmId guildOrDmIdNoThread, ViewThread threadId )

        allUsers : SeqDict (Id UserId) FrontendUser
        allUsers =
            User.allUsers local.localUser

        replyTo : Maybe (RepliedTo ChannelMessageId)
        replyTo =
            SeqDict.get guildOrDmId loggedIn.replyTo

        isMobile : Bool
        isMobile =
            MyUi.isMobile model

        draft : String
        draft =
            case SeqDict.get guildOrDmId loggedIn.drafts of
                Just text ->
                    String.Nonempty.toString text

                Nothing ->
                    ""

        draftRichText : Maybe (Nonempty (RichText (Id UserId) (Id ChannelId)))
        draftRichText =
            case SeqDict.get guildOrDmId loggedIn.drafts of
                Just text ->
                    Just (RichText.fromNonemptyString local.localUser.timezone allUsers channels text)

                Nothing ->
                    Nothing
    in
    Ui.column
        [ Ui.height Ui.fill
        , Ui.heightMin 0
        ]
        [ ChannelHeader.thread isMobile name threadName guildOrDmIdNoThread local loggedIn model
        , Ui.el
            ([ emojiSelector
                isMobile
                local.localUser.user.availableCustomEmojis
                local.localUser.user.availableStickers
                local
                loggedIn
                model
             , Ui.heightMin 0
             , Ui.height Ui.fill
             ]
                ++ drawingModeAttributes model.route loggedIn.drawingMode
            )
            (Ui.Keyed.column
                ([ Ui.height Ui.fill
                 , Ui.width Ui.fill
                 , Ui.paddingWith { left = 0, right = 0, top = 200, bottom = 16 }
                 , MyUi.scrollable (MyUi.canScroll (MyUi.isMobile model) model.drag)
                 , MyUi.htmlStyle "overflow-wrap" "break-word"
                 , Ui.id (Dom.idToString conversationContainerId)
                 , Ui.Events.on
                    "scroll"
                    (Scroll.decodeScrollToBottom
                        (UserScrolled (GuildOrDmId guildOrDmIdNoThread) (ViewThread threadId))
                        loggedIn.channelScrollPosition
                    )
                 , Ui.heightMin 0
                 , MyUi.bounceScroll isMobile
                 , MyUi.htmlStyle "background-image" "url(/cacheable/grid1.png)"
                 ]
                    ++ drawingZoomAttributes model.route loggedIn.drawingMode
                )
                (( "a"
                 , Ui.column
                    [ Ui.alignBottom ]
                    (if VisibleMessages.startIsVisible channel.visibleMessages then
                        [ Ui.el
                            [ Ui.Font.color MyUi.font2, Ui.paddingXY 8 4, Ui.alignBottom, Ui.Font.size 20 ]
                            (Ui.text startOfThreadText)
                        , case guildOrDmIdNoThread of
                            GuildOrDmId_Guild { guildId, channelId } ->
                                case LocalState.getGuildAndChannel { guildId = guildId, channelId = channelId } local of
                                    Just ( _, channel2 ) ->
                                        threadStarterMessage
                                            isMobile
                                            guildOrDmIdNoThread
                                            threadId
                                            channel2
                                            loggedIn
                                            local
                                            model

                                    Nothing ->
                                        Ui.none

                            GuildOrDmId_Dm { otherUserId } ->
                                case SeqDict.get otherUserId local.dmChannels of
                                    Just dmChannel2 ->
                                        threadStarterMessage
                                            isMobile
                                            guildOrDmIdNoThread
                                            threadId
                                            dmChannel2
                                            loggedIn
                                            local
                                            model

                                    Nothing ->
                                        Ui.none
                        ]

                     else
                        []
                    )
                 )
                    :: threadConversationViewHelper
                        lastViewedIndex
                        guildOrDmIdNoThread
                        threadId
                        maybeUrlMessageId
                        channel
                        loggedIn
                        local
                        model
                )
            )
        , Ui.column
            [ Ui.paddingXY 2 0
            , Ui.heightMin 0
            , MyUi.noShrinking
            , case SeqDict.get guildOrDmId loggedIn.filesToUpload of
                Just filesToUpload2 ->
                    fileUploadPreview
                        (PressedDeleteAttachedFile guildOrDmId)
                        (PressedViewAttachedFileInfo guildOrDmId)
                        (PressedToggleAttachedFileSpoiler guildOrDmId)
                        draftRichText
                        filesToUpload2
                        |> Ui.inFront

                Nothing ->
                    Ui.noAttr
            ]
            [ newMessagesView model loggedIn
            , replyToHeader isMobile guildOrDmId replyTo allUsers channel
            , MessageInput.view
                (Dom.id "messageMenu_channelInput")
                (replyTo == Nothing)
                (MyUi.isMobile model)
                channelTextInputId
                (if missingPrivateKey then
                    missingPrivateKeyPlaceholder

                 else
                    (case guildOrDmIdNoThread of
                        GuildOrDmId_Guild _ ->
                            "Write a message in this thread"

                        GuildOrDmId_Dm _ ->
                            "Write a message in this thread"
                    )
                        |> MessageInput.textPlaceholder
                )
                (RichText.maxLength - String.length draft)
                draft
                draftRichText
                (case SeqDict.get guildOrDmId loggedIn.filesToUpload of
                    Just attachedFiles ->
                        NonemptyDict.toSeqDict attachedFiles

                    Nothing ->
                        SeqDict.empty
                )
                local.localUser
                loggedIn
                (User.allUsers local.localUser)
                channels
                |> Ui.map (MessageInputMsg (GuildOrDmId guildOrDmIdNoThread) (ViewThread threadId))
            , peopleAreTypingView allUsers (ViewThread threadId) (LocalState.typingIn guildOrDmIdNoThread local) local.localUser.session.userId model
            ]
        ]


discordThreadConversationView :
    Id ThreadMessageId
    -> Discord.Id Discord.UserId
    -> DiscordGuildOrDmId
    -> Maybe (Id ThreadMessageId)
    -> Id ChannelMessageId
    -> LoggedIn2
    -> LoadedFrontend
    -> LocalState
    -> String
    -> SeqSet (Id CustomEmojiId)
    -> SeqSet (Id StickerId)
    -> DiscordFrontendThread
    -> Element FrontendMsg_
discordThreadConversationView lastViewedIndex currentDiscordUserId guildOrDmIdNoThread maybeUrlMessageId threadId loggedIn model local name availableCustomEmojis availableStickers channel =
    let
        channels : SeqDict ( Discord.Id Discord.ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
        channels =
            LocalState.discordChannelMentions guildOrDmIdNoThread local

        guildOrDmId : ( AnyGuildOrDmId, ThreadRoute )
        guildOrDmId =
            ( DiscordGuildOrDmId guildOrDmIdNoThread, ViewThread threadId )

        allUsers : SeqDict (Discord.Id Discord.UserId) DiscordFrontendUser
        allUsers =
            LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers

        replyTo : Maybe (RepliedTo ChannelMessageId)
        replyTo =
            SeqDict.get guildOrDmId loggedIn.replyTo

        isMobile : Bool
        isMobile =
            MyUi.isMobile model

        draft : String
        draft =
            case SeqDict.get guildOrDmId loggedIn.drafts of
                Just text ->
                    String.Nonempty.toString text

                Nothing ->
                    ""

        draftRichText : Maybe (Nonempty (RichText (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)))
        draftRichText =
            case SeqDict.get guildOrDmId loggedIn.drafts of
                Just text ->
                    Just (RichText.fromNonemptyString local.localUser.timezone allUsers channels text)

                Nothing ->
                    Nothing
    in
    Ui.column
        [ Ui.height Ui.fill
        , Ui.heightMin 0
        ]
        [ ChannelHeader.discordThread isMobile name guildOrDmIdNoThread local loggedIn model
        , Ui.el
            ([ emojiSelector isMobile availableCustomEmojis availableStickers local loggedIn model
             , Ui.heightMin 0
             , Ui.height Ui.fill
             ]
                ++ drawingModeAttributes model.route loggedIn.drawingMode
            )
            (Ui.Keyed.column
                ([ Ui.height Ui.fill
                 , Ui.width Ui.fill
                 , Ui.paddingWith { left = 0, right = 0, top = 200, bottom = 16 }
                 , MyUi.scrollable (MyUi.canScroll (MyUi.isMobile model) model.drag)
                 , MyUi.htmlStyle "overflow-wrap" "break-word"
                 , Ui.id (Dom.idToString conversationContainerId)
                 , Ui.Events.on
                    "scroll"
                    (Scroll.decodeScrollToBottom
                        (UserScrolled (DiscordGuildOrDmId guildOrDmIdNoThread) (ViewThread threadId))
                        loggedIn.channelScrollPosition
                    )
                 , Ui.heightMin 0
                 , MyUi.bounceScroll isMobile
                 , MyUi.htmlStyle "background-image" "url(/cacheable/grid1.png)"
                 ]
                    ++ drawingZoomAttributes model.route loggedIn.drawingMode
                )
                (( "a"
                 , Ui.column
                    [ Ui.alignBottom ]
                    (if VisibleMessages.startIsVisible channel.visibleMessages then
                        [ Ui.el
                            [ Ui.Font.color MyUi.font2, Ui.paddingXY 8 4, Ui.alignBottom, Ui.Font.size 20 ]
                            (Ui.text startOfThreadText)
                        , case guildOrDmIdNoThread of
                            DiscordGuildOrDmId_Guild { guildId, channelId } ->
                                case LocalState.getDiscordGuildAndChannel guildId channelId local of
                                    Just ( _, channel2 ) ->
                                        discordThreadStarterMessage
                                            isMobile
                                            guildOrDmIdNoThread
                                            threadId
                                            channel2
                                            loggedIn
                                            local
                                            model

                                    Nothing ->
                                        Ui.none

                            DiscordGuildOrDmId_Dm _ ->
                                Ui.none
                        ]

                     else
                        []
                    )
                 )
                    :: discordThreadConversationViewHelper
                        lastViewedIndex
                        currentDiscordUserId
                        guildOrDmIdNoThread
                        threadId
                        maybeUrlMessageId
                        channel
                        loggedIn
                        local
                        model
                )
            )
        , Ui.column
            [ Ui.paddingXY 2 0
            , Ui.heightMin 0
            , MyUi.noShrinking
            , case SeqDict.get guildOrDmId loggedIn.filesToUpload of
                Just filesToUpload2 ->
                    fileUploadPreview
                        (PressedDeleteAttachedFile guildOrDmId)
                        (PressedViewAttachedFileInfo guildOrDmId)
                        (PressedToggleAttachedFileSpoiler guildOrDmId)
                        draftRichText
                        filesToUpload2
                        |> Ui.inFront

                Nothing ->
                    Ui.noAttr
            ]
            [ newMessagesView model loggedIn
            , replyToHeader isMobile guildOrDmId replyTo allUsers channel
            , MessageInput.view
                (Dom.id "messageMenu_channelInput")
                (replyTo == Nothing)
                (MyUi.isMobile model)
                channelTextInputId
                ((case guildOrDmIdNoThread of
                    DiscordGuildOrDmId_Guild _ ->
                        "Write a message in this thread"

                    DiscordGuildOrDmId_Dm _ ->
                        "Write a message in this thread"
                 )
                    |> MessageInput.textPlaceholder
                )
                (RichText.discordCharsLeft OneToOne.empty draftRichText)
                draft
                draftRichText
                (case SeqDict.get guildOrDmId loggedIn.filesToUpload of
                    Just attachedFiles ->
                        NonemptyDict.toSeqDict attachedFiles

                    Nothing ->
                        SeqDict.empty
                )
                local.localUser
                loggedIn
                (LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers)
                channels
                |> Ui.map (MessageInputMsg (DiscordGuildOrDmId guildOrDmIdNoThread) (ViewThread threadId))
            , peopleAreTypingView allUsers (ViewThread threadId) (LocalState.discordTypingIn guildOrDmIdNoThread local) currentDiscordUserId model
            ]
        ]


threadStarterMessage :
    Bool
    -> GuildOrDmId
    -> Id ChannelMessageId
    -> { a | messages : MessageArray ChannelMessageId (Id UserId) (Id ChannelId) }
    -> LoggedIn2
    -> LocalState
    -> LoadedFrontend
    -> Element FrontendMsg_
threadStarterMessage isMobile normalGuildOrDmIdNoThread threadMessageIndex channel loggedIn local model =
    let
        channels : SeqDict (Id ChannelId) FrontendChannel
        channels =
            LocalState.guildChannels normalGuildOrDmIdNoThread local

        guildOrDmIdNoThread : AnyGuildOrDmId
        guildOrDmIdNoThread =
            GuildOrDmId normalGuildOrDmIdNoThread

        guildOrDmId : ( AnyGuildOrDmId, ThreadRoute )
        guildOrDmId =
            ( guildOrDmIdNoThread, NoThread )

        threadRoute : ThreadRouteWithMessage
        threadRoute =
            NoThreadWithMessage threadMessageIndex

        revealedSpoilers : SeqDict (Id ChannelMessageId) (NonemptySet Int)
        revealedSpoilers =
            revealedChannelSpoilers (GuildOrDmId normalGuildOrDmIdNoThread) loggedIn

        containerWidth : Int
        containerWidth =
            conversationWidth model
    in
    case MessageArray.get threadMessageIndex channel.messages of
        Just message ->
            case SeqDict.get guildOrDmId loggedIn.editMessage of
                Just edit ->
                    if edit.messageIndex == threadMessageIndex then
                        let
                            allUsers : SeqDict (Id UserId) FrontendUser
                            allUsers =
                                User.allUsers local.localUser

                            channelMentions : SeqDict ( Id ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
                            channelMentions =
                                LocalState.guildChannelMentions local.localUser channels

                            editRichText : Maybe (Nonempty (RichText (Id UserId) (Id ChannelId)))
                            editRichText =
                                case String.Nonempty.fromString edit.text of
                                    Just nonempty ->
                                        RichText.fromNonemptyString local.localUser.timezone allUsers channelMentions nonempty |> Just

                                    Nothing ->
                                        Nothing

                            charsLeft =
                                RichText.maxLength - String.length edit.text
                        in
                        messageEditingView
                            containerWidth
                            model.time
                            isMobile
                            guildOrDmId
                            (NoThreadWithMessage threadMessageIndex)
                            message
                            Nothing
                            Nothing
                            SeqDict.empty
                            charsLeft
                            edit
                            editRichText
                            loggedIn
                            local.localUser.decryptedMessages
                            local.localUser.session.userId
                            allUsers
                            channelMentions
                            local

                    else
                        messageView
                            model.time
                            isMobile
                            (conversationWidth model)
                            True
                            revealedSpoilers
                            NoHighlight
                            (messageHover guildOrDmIdNoThread threadRoute loggedIn model)
                            False
                            local.localUser.session.userId
                            (User.allUsers local.localUser)
                            channels
                            local.localUser
                            Nothing
                            Nothing
                            threadMessageIndex
                            message
                            |> Ui.map (MessageViewMsg guildOrDmIdNoThread threadRoute)

                Nothing ->
                    messageView
                        model.time
                        isMobile
                        (conversationWidth model)
                        True
                        revealedSpoilers
                        NoHighlight
                        (messageHover guildOrDmIdNoThread threadRoute loggedIn model)
                        False
                        local.localUser.session.userId
                        (User.allUsers local.localUser)
                        channels
                        local.localUser
                        Nothing
                        Nothing
                        threadMessageIndex
                        message
                        |> Ui.map (MessageViewMsg guildOrDmIdNoThread threadRoute)

        _ ->
            Ui.none


discordThreadStarterMessage :
    Bool
    -> DiscordGuildOrDmId
    -> Id ChannelMessageId
    -> { a | messages : MessageArray ChannelMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId) }
    -> LoggedIn2
    -> LocalState
    -> LoadedFrontend
    -> Element FrontendMsg_
discordThreadStarterMessage isMobile discordGuildOrDmId threadMessageIndex channel loggedIn local model =
    let
        channels : SeqDict ( Discord.Id Discord.ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
        channels =
            LocalState.discordChannelMentions discordGuildOrDmId local

        currentUserId : Discord.Id Discord.UserId
        currentUserId =
            case discordGuildOrDmId of
                DiscordGuildOrDmId_Guild id ->
                    id.currentUserId

                DiscordGuildOrDmId_Dm data ->
                    data.currentUserId

        guildOrDmIdNoThread : AnyGuildOrDmId
        guildOrDmIdNoThread =
            DiscordGuildOrDmId discordGuildOrDmId

        guildOrDmId : ( AnyGuildOrDmId, ThreadRoute )
        guildOrDmId =
            ( guildOrDmIdNoThread, NoThread )

        threadRoute : ThreadRouteWithMessage
        threadRoute =
            NoThreadWithMessage threadMessageIndex

        revealedSpoilers : SeqDict (Id ChannelMessageId) (NonemptySet Int)
        revealedSpoilers =
            revealedChannelSpoilers guildOrDmIdNoThread loggedIn

        containerWidth : Int
        containerWidth =
            conversationWidth model
    in
    case MessageArray.get threadMessageIndex channel.messages of
        Just message ->
            case SeqDict.get guildOrDmId loggedIn.editMessage of
                Just edit ->
                    if edit.messageIndex == threadMessageIndex then
                        let
                            allUsers =
                                LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers

                            editRichText : Maybe (Nonempty (RichText (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)))
                            editRichText =
                                case String.Nonempty.fromString edit.text of
                                    Just nonempty ->
                                        RichText.fromNonemptyString local.localUser.timezone allUsers channels nonempty |> Just

                                    Nothing ->
                                        Nothing
                        in
                        messageEditingView
                            containerWidth
                            model.time
                            isMobile
                            guildOrDmId
                            (NoThreadWithMessage threadMessageIndex)
                            message
                            Nothing
                            Nothing
                            SeqDict.empty
                            (RichText.discordCharsLeft OneToOne.empty editRichText)
                            edit
                            editRichText
                            loggedIn
                            SeqDict.empty
                            currentUserId
                            allUsers
                            channels
                            local

                    else
                        discordMessageView
                            model.time
                            isMobile
                            (conversationWidth model)
                            True
                            revealedSpoilers
                            NoHighlight
                            (messageHover guildOrDmIdNoThread threadRoute loggedIn model)
                            currentUserId
                            (LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers)
                            channels
                            local.localUser
                            Nothing
                            Nothing
                            threadMessageIndex
                            message
                            |> Ui.map (MessageViewMsg guildOrDmIdNoThread threadRoute)

                Nothing ->
                    discordMessageView
                        model.time
                        isMobile
                        (conversationWidth model)
                        True
                        revealedSpoilers
                        NoHighlight
                        (messageHover guildOrDmIdNoThread threadRoute loggedIn model)
                        currentUserId
                        (LinkedAndOtherDiscordUsers.allDiscordUsers local.localUser.discordUsers)
                        channels
                        local.localUser
                        Nothing
                        Nothing
                        threadMessageIndex
                        message
                        |> Ui.map (MessageViewMsg guildOrDmIdNoThread threadRoute)

        _ ->
            Ui.none


dropdownButtonId : Int -> HtmlId
dropdownButtonId index =
    Dom.id ("dropdown_button" ++ String.fromInt index)


messageEditingView :
    Int
    -> Time.Posix
    -> Bool
    -> ( AnyGuildOrDmId, ThreadRoute )
    -> ThreadRouteWithMessage
    -> Message ChannelMessageId userId channelId
    -> Maybe (RepliedToView ChannelMessageId userId channelId MessageViewMsg)
    -> Maybe (FrontendGenericThread userId channelId)
    -> SeqDict (Id ChannelMessageId) (NonemptySet Int)
    -> Int
    -> EditMessage
    -> Maybe (Nonempty (RichText userId channelId))
    -> LoggedIn2
    -> SeqDict BytesHash (Result () (MessageContent userId channelId))
    -> userId
    -> SeqDict userId { a | name : PersonName, icon : Maybe FileHash }
    -> SeqDict ( channelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> LocalState
    -> Element FrontendMsg_
messageEditingView containerWidth time isMobile guildOrDmId threadRouteWithMessage message maybeRepliedTo2 maybeThread revealedSpoilers charsLeft editing editingRichText loggedIn decrypted currentUserId allUsers channels local =
    let
        -- An encrypted message is edited the same way a plain one is. Only who wrote it and
        -- what it was reacted to with are read off the message here, and both kinds say.
        editingView : userId -> SeqDict EmojiOrCustomEmoji (NonemptySet userId) -> Element FrontendMsg_
        editingView createdBy reactions =
            let
                maybeReactions : Maybe (Element MessageViewMsg)
                maybeReactions =
                    MessageView.reactionEmojiView local.localUser.emojiData MessageView.ReactionsHovered currentUserId local.localUser.customEmojis allUsers LoopAFewTimesOnLoad containerWidth reactions

                ( guildOrDmIdNoThread, threadRoute ) =
                    guildOrDmId

                messageInput =
                    MessageInput.view
                        (Dom.id "messageMenu_editDesktop")
                        True
                        False
                        MessageMenu.editMessageTextInputId
                        MessageInput.emptyPlaceholder
                        charsLeft
                        editing.text
                        editingRichText
                        editing.attachedFiles
                        local.localUser
                        loggedIn
                        allUsers
                        channels
            in
            Ui.column
                [ Ui.Font.color MyUi.font1
                , Ui.background MyUi.hoverHighlight
                , Ui.paddingWith
                    { left = 0
                    , right = 0
                    , top = 4
                    , bottom =
                        if maybeReactions == Nothing then
                            8

                        else
                            4
                    }
                , Ui.spacing 4
                , (case threadRouteWithMessage of
                    ViewThreadWithMessage _ messageId ->
                        Id.changeType messageId

                    NoThreadWithMessage messageId ->
                        messageId
                  )
                    |> channelMessageHtmlId
                    |> Dom.idToString
                    |> Ui.id
                ]
                [ replyToHeaderAboveMessage
                    isMobile
                    local.localUser.timezone
                    time
                    maybeRepliedTo2
                    revealedSpoilers
                    local.localUser.emojiData
                    local.localUser.customEmojis
                    decrypted
                    allUsers
                    channels
                    |> Ui.el [ Ui.paddingXY 8 0 ]
                    |> Ui.map (MessageViewMsg guildOrDmIdNoThread threadRouteWithMessage)
                , User.toString createdBy allUsers
                    ++ " "
                    |> Ui.text
                    |> Ui.el [ Ui.Font.bold, Ui.paddingXY 8 0 ]
                , Ui.column
                    [ case NonemptyDict.fromSeqDict editing.attachedFiles of
                        Just filesToUpload ->
                            fileUploadPreview
                                (EditMessage_PressedDeleteAttachedFile guildOrDmId)
                                (EditMessage_PressedViewAttachedFileInfo guildOrDmId)
                                (EditMessage_PressedToggleAttachedFileSpoiler guildOrDmId)
                                editingRichText
                                filesToUpload
                                |> Ui.inFront

                        Nothing ->
                            Ui.noAttr
                    ]
                    [ messageInput
                        |> Ui.map (EditMessage_MessageInputMsg guildOrDmIdNoThread threadRoute)
                        |> Ui.el [ Ui.paddingXY 5 0 ]
                    , Ui.row
                        [ Ui.Font.size 14
                        , Ui.Font.color MyUi.font3
                        , Ui.paddingXY 12 0
                        , MyUi.prewrap
                        ]
                        [ Ui.text "Press "
                        , MyUi.elButton
                            (Dom.id "guild_exitEditMessage")
                            (PressedCancelMessageEdit guildOrDmId)
                            [ Ui.Font.color MyUi.font1
                            , Ui.width Ui.shrink
                            ]
                            (Ui.text "escape")
                        , Ui.text " to cancel edit"
                        ]
                    ]
                , case maybeReactions of
                    Just reactionView ->
                        Ui.el [ Ui.paddingXY 8 0 ] reactionView
                            |> Ui.map (MessageViewMsg guildOrDmIdNoThread threadRouteWithMessage)

                    Nothing ->
                        Ui.none
                , case ( threadRouteWithMessage, maybeThread ) of
                    ( NoThreadWithMessage messageId, Just thread ) ->
                        previewThreadLastMessage
                            local.localUser.timezone
                            time
                            local.localUser.emojiData
                            local.localUser.customEmojis
                            allUsers
                            channels
                            decrypted
                            messageId
                            thread
                            |> Ui.el [ Ui.paddingXY 8 0 ]
                            |> Ui.map (MessageViewMsg guildOrDmIdNoThread threadRouteWithMessage)

                    _ ->
                        Ui.none
                ]
    in
    case message of
        UserTextMessage data ->
            editingView data.createdBy data.reactions

        EncryptedUserTextMessage data ->
            editingView data.createdBy data.reactions

        UserJoinedMessage _ _ _ _ ->
            Ui.none

        DeletedMessage _ ->
            Ui.none

        CallStarted _ ->
            Ui.none

        GameStarted _ ->
            Ui.none


threadMessageEditingView :
    Int
    -> Time.Posix
    -> Bool
    -> ( AnyGuildOrDmId, ThreadRoute )
    -> Id ChannelMessageId
    -> Id ThreadMessageId
    -> Message ThreadMessageId userId channelId
    -> Maybe (RepliedToView ThreadMessageId userId channelId MessageViewMsg)
    -> SeqDict (Id ThreadMessageId) (NonemptySet Int)
    -> Int
    -> EditMessage
    -> Maybe (Nonempty (RichText userId channelId))
    -> LoggedIn2
    -> SeqDict BytesHash (Result () (MessageContent userId channelId))
    -> userId
    -> SeqDict userId { a | name : PersonName, icon : Maybe FileHash }
    -> SeqDict ( channelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> LocalState
    -> Element FrontendMsg_
threadMessageEditingView containerWidth time isMobile guildOrDmId threadId messageId message maybeRepliedTo2 revealedSpoilers charsLeft editing editingRichText loggedIn decrypted currentUserId allUsers channels local =
    let
        -- An encrypted message is edited the same way a plain one is. Only who wrote it and
        -- what it was reacted to with are read off the message here, and both kinds say.
        editingView : userId -> SeqDict EmojiOrCustomEmoji (NonemptySet userId) -> Element FrontendMsg_
        editingView createdBy reactions =
            let
                maybeReactions =
                    MessageView.reactionEmojiView local.localUser.emojiData MessageView.ReactionsHovered currentUserId local.localUser.customEmojis allUsers LoopAFewTimesOnLoad containerWidth reactions

                ( guildOrDmIdNoThread, _ ) =
                    guildOrDmId

                threadRouteWithMessage =
                    ViewThreadWithMessage threadId messageId

                messageInput =
                    MessageInput.view
                        (Dom.id "messageMenu_editDesktop")
                        True
                        False
                        MessageMenu.editMessageTextInputId
                        MessageInput.emptyPlaceholder
                        charsLeft
                        editing.text
                        editingRichText
                        editing.attachedFiles
                        local.localUser
                        loggedIn
                        allUsers
                        channels
            in
            Ui.column
                [ Ui.Font.color MyUi.font1
                , Ui.background MyUi.hoverHighlight
                , Ui.paddingWith
                    { left = 0
                    , right = 0
                    , top = 4
                    , bottom =
                        if maybeReactions == Nothing then
                            8

                        else
                            4
                    }
                , Ui.spacing 4
                , threadMessageHtmlId messageId |> Dom.idToString |> Ui.id
                ]
                [ replyToHeaderAboveMessage
                    isMobile
                    local.localUser.timezone
                    time
                    maybeRepliedTo2
                    revealedSpoilers
                    local.localUser.emojiData
                    local.localUser.customEmojis
                    decrypted
                    allUsers
                    channels
                    |> Ui.el [ Ui.paddingXY 8 0 ]
                    |> Ui.map (MessageViewMsg guildOrDmIdNoThread threadRouteWithMessage)
                , User.toString createdBy allUsers
                    ++ " "
                    |> Ui.text
                    |> Ui.el [ Ui.Font.bold, Ui.paddingXY 8 0 ]
                , Ui.column
                    [ case NonemptyDict.fromSeqDict editing.attachedFiles of
                        Just filesToUpload ->
                            fileUploadPreview
                                (EditMessage_PressedDeleteAttachedFile guildOrDmId)
                                (EditMessage_PressedViewAttachedFileInfo guildOrDmId)
                                (EditMessage_PressedToggleAttachedFileSpoiler guildOrDmId)
                                editingRichText
                                filesToUpload
                                |> Ui.inFront

                        Nothing ->
                            Ui.noAttr
                    ]
                    [ messageInput
                        |> Ui.map (EditMessage_MessageInputMsg guildOrDmIdNoThread (ViewThread threadId))
                        |> Ui.el [ Ui.paddingXY 5 0 ]
                    , Ui.row
                        [ Ui.Font.size 14
                        , Ui.Font.color MyUi.font3
                        , Ui.paddingXY 12 0
                        , MyUi.prewrap
                        ]
                        [ Ui.text "Press "
                        , MyUi.elButton
                            (Dom.id "guild_exitEditMessage")
                            (PressedCancelMessageEdit guildOrDmId)
                            [ Ui.Font.color MyUi.font1
                            , Ui.width Ui.shrink
                            ]
                            (Ui.text "escape")
                        , Ui.text " to cancel edit"
                        ]
                    ]
                , case maybeReactions of
                    Just reactionView ->
                        Ui.el [ Ui.paddingXY 8 0 ] reactionView
                            |> Ui.map (MessageViewMsg guildOrDmIdNoThread threadRouteWithMessage)

                    Nothing ->
                        Ui.none
                ]
    in
    case message of
        UserTextMessage data ->
            editingView data.createdBy data.reactions

        EncryptedUserTextMessage data ->
            editingView data.createdBy data.reactions

        UserJoinedMessage _ _ _ _ ->
            Ui.none

        DeletedMessage _ ->
            Ui.none

        CallStarted _ ->
            Ui.none

        GameStarted _ ->
            Ui.none


type IsHovered
    = IsNotHovered
    | IsHovered
    | IsHoveredButNoMenu
    | IsHoveredReactionsOnly
    | IsHoveredWhileSelectingAnchor


messageViewNotThreadStarter :
    Int
    -> SeqDict (Id ChannelMessageId) (NonemptySet Int)
    -> LocalUser
    -> Int
    -> Message ChannelMessageId (Id UserId) (Id ChannelId)
    -> Element MessageViewMsg
messageViewNotThreadStarter data revealedSpoilers localUser messageIndex message =
    let
        { containerWidth, isEditing, highlight, isHovered, isMobile, time } =
            decodeMessageView data
    in
    messageView
        time
        isMobile
        containerWidth
        False
        revealedSpoilers
        highlight
        isHovered
        isEditing
        localUser.session.userId
        (User.allUsers localUser)
        SeqDict.empty
        localUser
        Nothing
        Nothing
        (Id.fromInt messageIndex)
        message


messageViewNotThreadStarterWithChannelMention :
    Int
    -> SeqDict (Id ChannelMessageId) (NonemptySet Int)
    -> LocalUser
    -> Int
    -> Message ChannelMessageId (Id UserId) (Id ChannelId)
    -> SeqDict (Id ChannelId) FrontendChannel
    -> Element MessageViewMsg
messageViewNotThreadStarterWithChannelMention data revealedSpoilers localUser messageIndex message channels =
    let
        { containerWidth, isEditing, highlight, isHovered, isMobile, time } =
            decodeMessageView data
    in
    messageView
        time
        isMobile
        containerWidth
        False
        revealedSpoilers
        highlight
        isHovered
        isEditing
        localUser.session.userId
        (User.allUsers localUser)
        channels
        localUser
        Nothing
        Nothing
        (Id.fromInt messageIndex)
        message


messageViewThreadStarter :
    Int
    -> SeqDict (Id ChannelMessageId) (NonemptySet Int)
    -> LocalUser
    -> Int
    -> FrontendGenericThread (Id UserId) (Id ChannelId)
    -> Message ChannelMessageId (Id UserId) (Id ChannelId)
    -> Element MessageViewMsg
messageViewThreadStarter data revealedSpoilers localUser messageIndex thread message =
    let
        { containerWidth, isEditing, highlight, isHovered, isMobile, time } =
            decodeMessageView data
    in
    messageView
        time
        isMobile
        containerWidth
        False
        revealedSpoilers
        highlight
        isHovered
        isEditing
        localUser.session.userId
        (User.allUsers localUser)
        SeqDict.empty
        localUser
        Nothing
        (Just thread)
        (Id.fromInt messageIndex)
        message


discordMessageViewNotThreadStarter :
    Int
    -> SeqDict (Id ChannelMessageId) (NonemptySet Int)
    -> Discord.Id Discord.UserId
    -> LocalUser
    -> Int
    -> Message ChannelMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)
    -> Element MessageViewMsg
discordMessageViewNotThreadStarter data revealedSpoilers currentDiscordUserId localUser messageIndex message =
    let
        { containerWidth, highlight, isHovered, isMobile, time } =
            decodeMessageView data
    in
    --Ui.el
    --    [ Ui.inFront (MyUi.lazyChangedValue "revealedSpoilers" revealedSpoilers)
    --    , Ui.inFront (MyUi.lazyChangedValue "localUser" localUser)
    --    , Ui.inFront (MyUi.lazyChangedValue "messageIndex" messageIndex)
    --    , Ui.inFront (MyUi.lazyChangedValue "message" message)
    --    , Ui.inFront (MyUi.lazyChangedValue "data" data)
    --    , Ui.inFront (MyUi.lazyChangedValue "currentDiscordUserId" currentDiscordUserId)
    --    ]
    discordMessageView
        time
        isMobile
        containerWidth
        False
        revealedSpoilers
        highlight
        isHovered
        currentDiscordUserId
        (LinkedAndOtherDiscordUsers.allDiscordUsers localUser.discordUsers)
        SeqDict.empty
        localUser
        Nothing
        Nothing
        (Id.fromInt messageIndex)
        message


discordMessageViewThreadStarter :
    Int
    -> SeqDict (Id ChannelMessageId) (NonemptySet Int)
    -> Discord.Id Discord.UserId
    -> LocalUser
    -> Int
    -> DiscordFrontendThread
    -> Message ChannelMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)
    -> Element MessageViewMsg
discordMessageViewThreadStarter data revealedSpoilers currentDiscordUserId localUser messageIndex thread message =
    let
        { containerWidth, highlight, isHovered, isMobile, time } =
            decodeMessageView data
    in
    discordMessageView
        time
        isMobile
        containerWidth
        False
        revealedSpoilers
        highlight
        isHovered
        currentDiscordUserId
        (LinkedAndOtherDiscordUsers.allDiscordUsers localUser.discordUsers)
        SeqDict.empty
        localUser
        Nothing
        (Just thread)
        (Id.fromInt messageIndex)
        message


threadMessageViewLazy :
    Int
    -> SeqDict (Id ThreadMessageId) (NonemptySet Int)
    -> LocalUser
    -> Int
    -> Message ThreadMessageId (Id UserId) (Id ChannelId)
    -> Element MessageViewMsg
threadMessageViewLazy data revealedSpoilers localUser messageIndex message =
    let
        { containerWidth, isEditing, highlight, isHovered, isMobile, time } =
            decodeMessageView data
    in
    threadMessageView
        time
        isMobile
        containerWidth
        revealedSpoilers
        highlight
        isHovered
        isEditing
        (User.allUsers localUser)
        SeqDict.empty
        localUser.session.userId
        localUser
        Nothing
        (Id.fromInt messageIndex)
        message


discordThreadMessageViewLazy :
    Int
    -> SeqDict (Id ThreadMessageId) (NonemptySet Int)
    -> Discord.Id Discord.UserId
    -> LocalUser
    -> Int
    -> Message ThreadMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)
    -> Element MessageViewMsg
discordThreadMessageViewLazy data revealedSpoilers currentDiscordUserId localUser messageIndex message =
    let
        { containerWidth, highlight, isHovered, isMobile, time } =
            decodeMessageView data
    in
    discordThreadMessageView
        time
        isMobile
        containerWidth
        revealedSpoilers
        highlight
        isHovered
        (LinkedAndOtherDiscordUsers.allDiscordUsers localUser.discordUsers)
        SeqDict.empty
        currentDiscordUserId
        localUser
        Nothing
        (Id.fromInt messageIndex)
        message


highlightLayer : HighlightMessage -> Ui.Attribute msg
highlightLayer highlight =
    case highlight of
        NoHighlight ->
            Ui.behindContent Ui.none

        ReplyToHighlight ->
            MyUi.highlightFadeOut MyUi.replyToColor

        MentionHighlight ->
            MyUi.highlightFadeOut MyUi.mentionColor

        UrlHighlight ->
            MyUi.highlightFadeOut MyUi.replyToColor


type HighlightMessage
    = NoHighlight
    | ReplyToHighlight
    | MentionHighlight
    | UrlHighlight


{-| Which custom emojis the one-click reactions on a Discord message may offer.

Discord only accepts a reaction with an emoji it knows about, so a custom emoji picked
up from an at-chat guild is rejected when it's used on a Discord message. Narrowing the
offer to the current Discord guild's own emojis would mean carrying that guild's emoji
set into the message view, and the Discord message views have already spent every
argument `Ui.Lazy` has room for. The reactions offered up front are therefore unicode
emojis, which Discord always takes; the emoji selector still offers the guild's custom
emojis.

-}
discordQuickReactionCustomEmojis : SeqSet (Id CustomEmojiId)
discordQuickReactionCustomEmojis =
    SeqSet.empty


messageView :
    Time.Posix
    -> Bool
    -> Int
    -> Bool
    -> SeqDict (Id ChannelMessageId) (NonemptySet Int)
    -> HighlightMessage
    -> IsHovered
    -> Bool
    -> Id UserId
    -> SeqDict (Id UserId) FrontendUser
    -> SeqDict (Id ChannelId) FrontendChannel
    -> LocalUser
    -> Maybe (RepliedToView ChannelMessageId (Id UserId) (Id ChannelId) MessageViewMsg)
    -> Maybe (FrontendGenericThread (Id UserId) (Id ChannelId))
    -> Id ChannelMessageId
    -> Message ChannelMessageId (Id UserId) (Id ChannelId)
    -> Element MessageViewMsg
messageView time isMobile containerWidth isThreadStarter revealedSpoilers highlight isHovered isBeingEdited currentUserId allUsers channels localUser maybeRepliedTo2 maybeThreadStarter messageId message =
    let
        decrypted : SeqDict BytesHash (Result () (MessageContent (Id UserId) (Id ChannelId)))
        decrypted =
            localUser.decryptedMessages

        channelMentions : SeqDict ( Id ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
        channelMentions =
            LocalState.guildChannelMentions localUser channels
    in
    case message of
        UserTextMessage data ->
            messageContainer
                containerWidth
                isThreadStarter
                localUser.timezone
                time
                localUser.user.availableCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                channelMentions
                (case highlight of
                    NoHighlight ->
                        if SeqSet.member currentUserId (RichText.mentionsUser data.content.content) then
                            MentionHighlight

                        else
                            highlight

                    _ ->
                        highlight
                )
                messageId
                (currentUserId == data.createdBy)
                currentUserId
                localUser.user
                data.reactions
                maybeThreadStarter
                decrypted
                isHovered
                (userTextMessageContent
                    time
                    (Dom.id "spoiler")
                    containerWidth
                    isBeingEdited
                    isMobile
                    maybeRepliedTo2
                    localUser
                    revealedSpoilers
                    allUsers
                    channelMentions
                    (User.userColor localUser)
                    isHovered
                    messageId
                    data.content
                    False
                    data
                )

        EncryptedUserTextMessage data ->
            case SeqDict.get (Encryption.hash data.content) decrypted of
                Just result ->
                    messageContainer
                        containerWidth
                        isThreadStarter
                        localUser.timezone
                        time
                        localUser.user.availableCustomEmojis
                        localUser.customEmojis
                        localUser.emojiData
                        allUsers
                        channelMentions
                        highlight
                        messageId
                        (currentUserId == data.createdBy)
                        currentUserId
                        localUser.user
                        data.reactions
                        maybeThreadStarter
                        decrypted
                        isHovered
                        (userTextMessageContent
                            time
                            (Dom.id "spoiler")
                            containerWidth
                            isBeingEdited
                            isMobile
                            maybeRepliedTo2
                            localUser
                            revealedSpoilers
                            allUsers
                            channelMentions
                            (User.userColor localUser)
                            isHovered
                            messageId
                            (Result.withDefault
                                { content = RichText.failedToDecryptMessage, embeds = Array.empty, attachedFiles = SeqDict.empty }
                                result
                            )
                            True
                            data
                        )

                Nothing ->
                    Ui.none

        UserJoinedMessage joinedAt userId reactions drawings ->
            messageContainer
                containerWidth
                isThreadStarter
                localUser.timezone
                time
                localUser.user.availableCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                channelMentions
                highlight
                messageId
                False
                currentUserId
                localUser.user
                reactions
                maybeThreadStarter
                decrypted
                isHovered
                (Ui.row
                    []
                    [ userJoinedContent userId allUsers
                    , messageTimestamp
                        (User.userColor localUser)
                        drawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        joinedAt
                        localUser.timezone
                    , messageIdView messageId
                    ]
                )

        DeletedMessage createdAt ->
            messageContainer
                containerWidth
                isThreadStarter
                localUser.timezone
                time
                localUser.user.availableCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                channelMentions
                highlight
                messageId
                False
                currentUserId
                localUser.user
                SeqDict.empty
                maybeThreadStarter
                decrypted
                isHovered
                (deletedMessageContent
                    messageId
                    (isHovered == IsHoveredWhileSelectingAnchor)
                    createdAt
                    localUser.timezone
                )

        CallStarted callStartedData ->
            messageContainer
                containerWidth
                isThreadStarter
                localUser.timezone
                time
                localUser.user.availableCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                channelMentions
                highlight
                messageId
                False
                currentUserId
                localUser.user
                callStartedData.reactions
                maybeThreadStarter
                decrypted
                isHovered
                (Ui.row
                    [ Ui.contentTop ]
                    [ callStartedCard
                        (User.userColor localUser)
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        callStartedData.cardDrawings
                        callStartedData.startedBy
                        callStartedData.startedAt
                        callStartedData.endedAt
                        allUsers
                    , messageTimestamp
                        (User.userColor localUser)
                        callStartedData.timestampDrawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        callStartedData.startedAt
                        localUser.timezone
                    , messageIdView messageId
                    ]
                )

        GameStarted gameStarted ->
            messageContainer
                containerWidth
                isThreadStarter
                localUser.timezone
                time
                localUser.user.availableCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                channelMentions
                highlight
                messageId
                False
                currentUserId
                localUser.user
                gameStarted.reactions
                maybeThreadStarter
                decrypted
                isHovered
                (Ui.row
                    [ Ui.contentTop ]
                    [ goMatchStartedCard
                        (User.userColor localUser)
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        gameStarted.cardDrawings
                        messageId
                        gameStarted.startedBy
                        allUsers
                        gameStarted.gameType
                    , messageTimestamp
                        (User.userColor localUser)
                        gameStarted.timestampDrawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        gameStarted.startedAt
                        localUser.timezone
                    , messageIdView messageId
                    ]
                )


discordMessageView :
    Time.Posix
    -> Bool
    -> Int
    -> Bool
    -> SeqDict (Id ChannelMessageId) (NonemptySet Int)
    -> HighlightMessage
    -> IsHovered
    -> Discord.Id Discord.UserId
    -> SeqDict (Discord.Id Discord.UserId) DiscordFrontendUser
    -> SeqDict ( Discord.Id Discord.ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> LocalUser
    -> Maybe (RepliedToView ChannelMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId) MessageViewMsg)
    -> Maybe (FrontendGenericThread (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId))
    -> Id ChannelMessageId
    -> Message ChannelMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)
    -> Element MessageViewMsg
discordMessageView time isMobile containerWidth isThreadStarter revealedSpoilers highlight isHovered currentUserId allUsers channels localUser maybeRepliedTo2 maybeThreadStarter messageId message =
    case message of
        UserTextMessage data ->
            messageContainer
                containerWidth
                isThreadStarter
                localUser.timezone
                time
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                channels
                (case highlight of
                    NoHighlight ->
                        if SeqSet.member currentUserId (RichText.mentionsUser data.content.content) then
                            MentionHighlight

                        else
                            highlight

                    _ ->
                        highlight
                )
                messageId
                (currentUserId == data.createdBy)
                currentUserId
                localUser.user
                data.reactions
                maybeThreadStarter
                SeqDict.empty
                isHovered
                (discordUserTextMessageContent
                    time
                    (Dom.id "spoiler")
                    containerWidth
                    isMobile
                    maybeRepliedTo2
                    localUser
                    revealedSpoilers
                    allUsers
                    channels
                    isHovered
                    messageId
                    data.content
                    data
                )

        EncryptedUserTextMessage data ->
            messageContainer
                containerWidth
                isThreadStarter
                localUser.timezone
                time
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                channels
                highlight
                messageId
                (currentUserId == data.createdBy)
                currentUserId
                localUser.user
                data.reactions
                maybeThreadStarter
                SeqDict.empty
                isHovered
                (discordUserTextMessageContent
                    time
                    (Dom.id "spoiler")
                    containerWidth
                    isMobile
                    maybeRepliedTo2
                    localUser
                    revealedSpoilers
                    allUsers
                    channels
                    isHovered
                    messageId
                    { content = RichText.failedToDecryptMessage, embeds = Array.empty, attachedFiles = SeqDict.empty }
                    data
                )

        UserJoinedMessage joinedAt userId reactions drawings ->
            messageContainer
                containerWidth
                isThreadStarter
                localUser.timezone
                time
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                channels
                highlight
                messageId
                False
                currentUserId
                localUser.user
                reactions
                maybeThreadStarter
                SeqDict.empty
                isHovered
                (Ui.row
                    []
                    [ userJoinedContent userId allUsers
                    , messageTimestamp
                        (User.discordUserColor localUser)
                        drawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        joinedAt
                        localUser.timezone
                    , messageIdView messageId
                    ]
                )

        DeletedMessage createdAt ->
            messageContainer
                containerWidth
                isThreadStarter
                localUser.timezone
                time
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                channels
                highlight
                messageId
                False
                currentUserId
                localUser.user
                SeqDict.empty
                maybeThreadStarter
                SeqDict.empty
                isHovered
                (deletedMessageContent
                    messageId
                    (isHovered == IsHoveredWhileSelectingAnchor)
                    createdAt
                    localUser.timezone
                )

        CallStarted callStartedData ->
            messageContainer
                containerWidth
                isThreadStarter
                localUser.timezone
                time
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                channels
                highlight
                messageId
                False
                currentUserId
                localUser.user
                callStartedData.reactions
                maybeThreadStarter
                SeqDict.empty
                isHovered
                (Ui.row
                    [ Ui.contentTop ]
                    [ callStartedCard
                        (User.discordUserColor localUser)
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        callStartedData.cardDrawings
                        callStartedData.startedBy
                        callStartedData.startedAt
                        callStartedData.endedAt
                        allUsers
                    , messageTimestamp
                        (User.discordUserColor localUser)
                        callStartedData.timestampDrawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        callStartedData.startedAt
                        localUser.timezone
                    , messageIdView messageId
                    ]
                )

        GameStarted gameStarted ->
            messageContainer
                containerWidth
                isThreadStarter
                localUser.timezone
                time
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                channels
                highlight
                messageId
                False
                currentUserId
                localUser.user
                gameStarted.reactions
                maybeThreadStarter
                SeqDict.empty
                isHovered
                (Ui.row
                    [ Ui.contentTop ]
                    [ goMatchStartedCard
                        (User.discordUserColor localUser)
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        gameStarted.cardDrawings
                        messageId
                        gameStarted.startedBy
                        allUsers
                        gameStarted.gameType
                    , messageTimestamp
                        (User.discordUserColor localUser)
                        gameStarted.timestampDrawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        gameStarted.startedAt
                        localUser.timezone
                    , messageIdView messageId
                    ]
                )


threadMessageView :
    Time.Posix
    -> Bool
    -> Int
    -> SeqDict (Id ThreadMessageId) (NonemptySet Int)
    -> HighlightMessage
    -> IsHovered
    -> Bool
    -> SeqDict (Id UserId) FrontendUser
    -> SeqDict ( Id ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> Id UserId
    -> LocalUser
    -> Maybe (RepliedToView ThreadMessageId (Id UserId) (Id ChannelId) MessageViewMsg)
    -> Id ThreadMessageId
    -> Message ThreadMessageId (Id UserId) (Id ChannelId)
    -> Element MessageViewMsg
threadMessageView time isMobile containerWidth revealedSpoilers highlight isHovered isBeingEdited allUsers channels currentUserId localUser maybeRepliedTo2 messageId message =
    let
        decrypted : SeqDict BytesHash (Result () (MessageContent (Id UserId) (Id ChannelId)))
        decrypted =
            localUser.decryptedMessages
    in
    case message of
        UserTextMessage message2 ->
            threadMessageContainer
                containerWidth
                (case highlight of
                    NoHighlight ->
                        if SeqSet.member currentUserId (RichText.mentionsUser message2.content.content) then
                            MentionHighlight

                        else
                            highlight

                    _ ->
                        highlight
                )
                messageId
                (currentUserId == message2.createdBy)
                currentUserId
                localUser.user
                message2.reactions
                localUser.user.availableCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                isHovered
                (userTextMessageContent
                    time
                    (Dom.id "threadSpoiler")
                    containerWidth
                    isBeingEdited
                    isMobile
                    maybeRepliedTo2
                    localUser
                    revealedSpoilers
                    allUsers
                    channels
                    (User.userColor localUser)
                    isHovered
                    messageId
                    message2.content
                    False
                    message2
                )

        EncryptedUserTextMessage message2 ->
            case SeqDict.get (Encryption.hash message2.content) decrypted of
                Just result ->
                    threadMessageContainer
                        containerWidth
                        highlight
                        messageId
                        (currentUserId == message2.createdBy)
                        currentUserId
                        localUser.user
                        message2.reactions
                        localUser.user.availableCustomEmojis
                        localUser.customEmojis
                        localUser.emojiData
                        allUsers
                        isHovered
                        (userTextMessageContent
                            time
                            (Dom.id "threadSpoiler")
                            containerWidth
                            isBeingEdited
                            isMobile
                            maybeRepliedTo2
                            localUser
                            revealedSpoilers
                            allUsers
                            channels
                            (User.userColor localUser)
                            isHovered
                            messageId
                            (Result.withDefault
                                { content = RichText.failedToDecryptMessage, embeds = Array.empty, attachedFiles = SeqDict.empty }
                                result
                            )
                            True
                            message2
                        )

                Nothing ->
                    Ui.none

        UserJoinedMessage joinedAt userId reactions drawings ->
            threadMessageContainer
                containerWidth
                highlight
                messageId
                False
                currentUserId
                localUser.user
                reactions
                localUser.user.availableCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                isHovered
                (Ui.row
                    []
                    [ userJoinedContent userId allUsers
                    , messageTimestamp
                        (User.userColor localUser)
                        drawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        joinedAt
                        localUser.timezone
                    ]
                )

        DeletedMessage createdAt ->
            threadMessageContainer
                containerWidth
                highlight
                messageId
                False
                currentUserId
                localUser.user
                SeqDict.empty
                localUser.user.availableCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                isHovered
                (deletedMessageContent
                    messageId
                    (isHovered == IsHoveredWhileSelectingAnchor)
                    createdAt
                    localUser.timezone
                )

        CallStarted callStartedData ->
            threadMessageContainer
                containerWidth
                highlight
                messageId
                False
                currentUserId
                localUser.user
                callStartedData.reactions
                localUser.user.availableCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                isHovered
                (Ui.row
                    []
                    [ callStartedCard
                        (User.userColor localUser)
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        callStartedData.cardDrawings
                        callStartedData.startedBy
                        callStartedData.startedAt
                        callStartedData.endedAt
                        allUsers
                    , messageTimestamp
                        (User.userColor localUser)
                        callStartedData.timestampDrawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        callStartedData.startedAt
                        localUser.timezone
                    ]
                )

        GameStarted gameStarted ->
            threadMessageContainer
                containerWidth
                highlight
                messageId
                False
                currentUserId
                localUser.user
                gameStarted.reactions
                localUser.user.availableCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                isHovered
                (Ui.row
                    []
                    [ goMatchStartedCard
                        (User.userColor localUser)
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        gameStarted.cardDrawings
                        messageId
                        gameStarted.startedBy
                        allUsers
                        gameStarted.gameType
                    , messageTimestamp
                        (User.userColor localUser)
                        gameStarted.timestampDrawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        gameStarted.startedAt
                        localUser.timezone
                    ]
                )


discordThreadMessageView :
    Time.Posix
    -> Bool
    -> Int
    -> SeqDict (Id ThreadMessageId) (NonemptySet Int)
    -> HighlightMessage
    -> IsHovered
    -> SeqDict (Discord.Id Discord.UserId) DiscordFrontendUser
    -> SeqDict ( Discord.Id Discord.ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> Discord.Id Discord.UserId
    -> LocalUser
    -> Maybe (RepliedToView ThreadMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId) MessageViewMsg)
    -> Id ThreadMessageId
    -> Message ThreadMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)
    -> Element MessageViewMsg
discordThreadMessageView time isMobile containerWidth revealedSpoilers highlight isHovered allUsers channels currentUserId localUser maybeRepliedTo2 messageId message =
    case message of
        UserTextMessage message2 ->
            threadMessageContainer
                containerWidth
                (case highlight of
                    NoHighlight ->
                        if SeqSet.member currentUserId (RichText.mentionsUser message2.content.content) then
                            MentionHighlight

                        else
                            highlight

                    _ ->
                        highlight
                )
                messageId
                (currentUserId == message2.createdBy)
                currentUserId
                localUser.user
                message2.reactions
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                isHovered
                (discordUserTextMessageContent
                    time
                    (Dom.id "threadSpoiler")
                    containerWidth
                    isMobile
                    maybeRepliedTo2
                    localUser
                    revealedSpoilers
                    allUsers
                    channels
                    isHovered
                    messageId
                    message2.content
                    message2
                )

        EncryptedUserTextMessage message2 ->
            threadMessageContainer
                containerWidth
                highlight
                messageId
                (currentUserId == message2.createdBy)
                currentUserId
                localUser.user
                message2.reactions
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                isHovered
                (discordUserTextMessageContent
                    time
                    (Dom.id "threadSpoiler")
                    containerWidth
                    isMobile
                    maybeRepliedTo2
                    localUser
                    revealedSpoilers
                    allUsers
                    channels
                    isHovered
                    messageId
                    { content = RichText.failedToDecryptMessage, embeds = Array.empty, attachedFiles = SeqDict.empty }
                    message2
                )

        UserJoinedMessage joinedAt userId reactions drawings ->
            threadMessageContainer
                containerWidth
                highlight
                messageId
                False
                currentUserId
                localUser.user
                reactions
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                isHovered
                (Ui.row
                    []
                    [ userJoinedContent userId allUsers
                    , messageTimestamp
                        (User.discordUserColor localUser)
                        drawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        joinedAt
                        localUser.timezone
                    ]
                )

        DeletedMessage createdAt ->
            threadMessageContainer
                containerWidth
                highlight
                messageId
                False
                currentUserId
                localUser.user
                SeqDict.empty
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                isHovered
                (deletedMessageContent
                    messageId
                    (isHovered == IsHoveredWhileSelectingAnchor)
                    createdAt
                    localUser.timezone
                )

        CallStarted callStartedData ->
            threadMessageContainer
                containerWidth
                highlight
                messageId
                False
                currentUserId
                localUser.user
                callStartedData.reactions
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                isHovered
                (Ui.row
                    []
                    [ callStartedCard
                        (User.discordUserColor localUser)
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        callStartedData.cardDrawings
                        callStartedData.startedBy
                        callStartedData.startedAt
                        callStartedData.endedAt
                        allUsers
                    , messageTimestamp
                        (User.discordUserColor localUser)
                        callStartedData.timestampDrawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        callStartedData.startedAt
                        localUser.timezone
                    ]
                )

        GameStarted gameStarted ->
            threadMessageContainer
                containerWidth
                highlight
                messageId
                False
                currentUserId
                localUser.user
                gameStarted.reactions
                discordQuickReactionCustomEmojis
                localUser.customEmojis
                localUser.emojiData
                allUsers
                isHovered
                (Ui.row
                    []
                    [ goMatchStartedCard
                        (User.discordUserColor localUser)
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        gameStarted.cardDrawings
                        messageId
                        gameStarted.startedBy
                        allUsers
                        gameStarted.gameType
                    , messageTimestamp
                        (User.discordUserColor localUser)
                        gameStarted.timestampDrawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        gameStarted.startedAt
                        localUser.timezone
                    ]
                )


{-| A reaction's popup comes up while whatever it is attached to is hovered, which for a
message means the menu is up too. The places that show a message without its menu don't bring
popups up either.
-}
reactionsHover : IsHovered -> MessageView.ReactionsHover
reactionsHover isHovered =
    case isHovered of
        IsHovered ->
            MessageView.ReactionsHovered

        IsNotHovered ->
            MessageView.ReactionsNotHovered

        IsHoveredButNoMenu ->
            MessageView.ReactionsNotHovered

        IsHoveredReactionsOnly ->
            MessageView.ReactionsNotHovered

        IsHoveredWhileSelectingAnchor ->
            MessageView.ReactionsNotHovered


isHoveredToAnimationMode : IsHovered -> AnimationMode
isHoveredToAnimationMode isHovered =
    case isHovered of
        IsNotHovered ->
            Sticker.LoopAFewTimesOnLoad

        IsHovered ->
            Sticker.ResetAndLoopAFewTimes

        IsHoveredButNoMenu ->
            Sticker.ResetAndLoopAFewTimes

        IsHoveredReactionsOnly ->
            Sticker.ResetAndLoopAFewTimes

        IsHoveredWhileSelectingAnchor ->
            Sticker.ResetAndLoopAFewTimes


profileImageButtonId : Id messageId -> HtmlId
profileImageButtonId messageId =
    Dom.id ("guild_profileImage_" ++ Id.toString messageId)


openDmButton : Id messageId -> MessageViewMsg -> List (Ui.Attribute MessageViewMsg)
openDmButton messageId onPress =
    [ Ui.pointer
    , profileImageButtonId messageId |> Dom.idToString |> Ui.id
    , Ui.Events.onClick onPress
    , MyUi.hoverText "Go to direct messages"
    ]


userTextMessageContent :
    Time.Posix
    -> HtmlId
    -> Int
    -> Bool
    -> Bool
    -> Maybe (RepliedToView messageId (Id UserId) (Id ChannelId) MessageViewMsg)
    -> LocalUser
    -> SeqDict (Id messageId) (NonemptySet Int)
    -> SeqDict (Id UserId) FrontendUser
    -> SeqDict ( Id ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> (Id UserId -> UserColor)
    -> IsHovered
    -> Id messageId
    -> MessageContent (Id UserId) (Id ChannelId)
    -> Bool
    ->
        { a
            | createdAt : Time.Posix
            , createdBy : Id UserId
            , reactions : SeqDict EmojiOrCustomEmoji (NonemptySet (Id UserId))
            , editedAt : Maybe Time.Posix
            , repliedTo : RepliedTo messageId
            , drawings : Maybe (UserTextMessageDrawings (Id UserId))
        }
    -> Element MessageViewMsg
userTextMessageContent time spoilerHtmlId containerWidth isBeingEdited isMobile maybeRepliedTo2 localUser revealedSpoilers allUsers channels drawingColor isHovered messageId { content, embeds, attachedFiles } showEncryptionIcon message2 =
    let
        decrypted : SeqDict BytesHash (Result () (MessageContent (Id UserId) (Id ChannelId)))
        decrypted =
            localUser.decryptedMessages

        drawings : UserTextMessageDrawings (Id UserId)
        drawings =
            Maybe.withDefault Message.noDrawings message2.drawings
    in
    Ui.column
        []
        [ replyToHeaderAboveMessage
            isMobile
            localUser.timezone
            time
            maybeRepliedTo2
            revealedSpoilers
            localUser.emojiData
            localUser.customEmojis
            decrypted
            allUsers
            channels
            |> indentPastProfileImage maybeRepliedTo2
        , Ui.row
            []
            [ User.profileImage (SeqDict.get message2.createdBy allUsers)
                |> Ui.el
                    (Drawing.anchorHighlight
                        (Drawing.profileImageAnchorId messageId)
                        drawingColor
                        MessageView_PressedUserIconAnchor
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        drawings.userIconDrawings
                        ++ (if isHovered == IsHoveredWhileSelectingAnchor then
                                [ Ui.rounded User.profileImageRounding ]

                            else
                                openDmButton messageId (MessageView_PressedUserIconButton message2.createdBy)
                           )
                    )
                |> Ui.el
                    [ Ui.paddingWith
                        { left = 0
                        , right = MessageView.profileImagePaddingRight
                        , top = 2
                        , bottom = 0
                        }
                    , Ui.width Ui.shrink
                    , Ui.alignTop
                    ]
            , Ui.column
                []
                [ Ui.row
                    []
                    [ User.toColoredString message2.createdBy allUsers
                    , if showEncryptionIcon then
                        Ui.html Icons.lockClosed

                      else
                        Ui.none
                    , messageTimestamp
                        drawingColor
                        drawings.timestampDrawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        message2.createdAt
                        localUser.timezone
                    ]
                , Html.div
                    [ Html.Attributes.style "white-space" "pre-wrap" ]
                    (RichText.view
                        (Dom.id (Dom.idToString spoilerHtmlId ++ "_" ++ Id.toString messageId))
                        containerWidth
                        MessageView_PressedNonWhitelistLink
                        MessageView_PressedSpoiler
                        MessageView_PressedImage
                        { revealedSpoilers =
                            case SeqDict.get messageId revealedSpoilers of
                                Just nonempty ->
                                    NonemptySet.toSeqSet nonempty

                                Nothing ->
                                    SeqSet.empty
                        , users = allUsers
                        , channels = channels
                        , attachedFiles = attachedFiles
                        , domainWhitelist = localUser.user.domainWhitelist
                        , customEmojis = localUser.customEmojis
                        , emojiData = localUser.emojiData
                        , stickers = localUser.stickers
                        , animationMode = isHoveredToAnimationMode isHovered
                        , timezone = localUser.timezone
                        , time = time
                        , drawings = drawings.imageAttachmentDrawings
                        , embedDrawings = drawings.embedDrawings
                        , drawingUserColor = drawingColor
                        , isSelectingAnchor = isHovered == IsHoveredWhileSelectingAnchor
                        , devicePixelRatio = localUser.devicePixelRatio
                        , isHovered =
                            case isHovered of
                                IsNotHovered ->
                                    False

                                IsHovered ->
                                    True

                                IsHoveredButNoMenu ->
                                    True

                                IsHoveredReactionsOnly ->
                                    True

                                IsHoveredWhileSelectingAnchor ->
                                    False
                        , noOp = MessageView_NoOp
                        , onPressChannelMention = MessageView_PressedChannelMention
                        , onPressCopyCode = MessageView_PressedCopyCode
                        }
                        (case localUser.user.embedVisibility of
                            ShowEmbeds ->
                                embeds

                            HideEmbeds ->
                                Array.empty
                        )
                        content
                        ++ (if isBeingEdited then
                                [ Html.span
                                    [ Html.Attributes.style "color" (MyUi.colorToStyle MyUi.dimFont)
                                    , Html.Attributes.style "font-size" "12px"
                                    ]
                                    [ Html.text " (editing...)" ]
                                ]

                            else
                                case message2.editedAt of
                                    Just editedAt ->
                                        [ Html.span
                                            [ Html.Attributes.style "color" (MyUi.colorToStyle MyUi.dimFont)
                                            , Html.Attributes.style "font-size" "12px"
                                            , MyUi.datestamp localUser.timezone editedAt |> Html.Attributes.title
                                            ]
                                            [ Html.text " (edited)" ]
                                        ]

                                    Nothing ->
                                        []
                           )
                    )
                    |> Ui.html
                ]
            ]
        ]


discordUserTextMessageContent :
    Time.Posix
    -> HtmlId
    -> Int
    -> Bool
    -> Maybe (RepliedToView messageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId) MessageViewMsg)
    -> LocalUser
    -> SeqDict (Id messageId) (NonemptySet Int)
    -> SeqDict (Discord.Id Discord.UserId) DiscordFrontendUser
    -> SeqDict ( Discord.Id Discord.ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> IsHovered
    -> Id messageId
    -> MessageContent (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId)
    ->
        { a
            | createdAt : Time.Posix
            , createdBy : Discord.Id Discord.UserId
            , reactions : SeqDict EmojiOrCustomEmoji (NonemptySet (Discord.Id Discord.UserId))
            , editedAt : Maybe Time.Posix
            , repliedTo : RepliedTo messageId
            , drawings : Maybe (UserTextMessageDrawings (Discord.Id Discord.UserId))
        }
    -> Element MessageViewMsg
discordUserTextMessageContent time spoilerHtmlId containerWidth isMobile maybeRepliedTo2 localUser revealedSpoilers allUsers channels isHovered messageId { content, embeds, attachedFiles } message2 =
    let
        drawings : UserTextMessageDrawings (Discord.Id Discord.UserId)
        drawings =
            Maybe.withDefault Message.noDrawings message2.drawings
    in
    Ui.column
        []
        [ replyToHeaderAboveMessage
            isMobile
            localUser.timezone
            time
            maybeRepliedTo2
            revealedSpoilers
            localUser.emojiData
            localUser.customEmojis
            SeqDict.empty
            allUsers
            channels
            |> indentPastProfileImage maybeRepliedTo2
        , Ui.row
            []
            [ (case SeqDict.get message2.createdBy allUsers of
                Just user ->
                    User.discordProfileImage message2.createdBy user.icon

                Nothing ->
                    User.discordProfileImage message2.createdBy Nothing
              )
                |> Ui.el
                    (Drawing.anchorHighlight
                        (Drawing.profileImageAnchorId messageId)
                        (User.discordUserColor localUser)
                        MessageView_PressedUserIconAnchor
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        drawings.userIconDrawings
                        ++ (if isHovered == IsHoveredWhileSelectingAnchor then
                                [ Ui.rounded User.profileImageRounding ]

                            else
                                openDmButton messageId (MessageView_PressedDiscordUserIconButton message2.createdBy)
                           )
                    )
                |> Ui.el
                    [ Ui.paddingWith
                        { left = 0
                        , right = MessageView.profileImagePaddingRight
                        , top = 2
                        , bottom = 0
                        }
                    , Ui.width Ui.shrink
                    , Ui.alignTop
                    ]
            , Ui.column
                []
                [ Ui.row
                    []
                    [ User.toColoredString message2.createdBy allUsers
                    , messageTimestamp
                        (User.discordUserColor localUser)
                        drawings.timestampDrawings
                        (isHovered == IsHoveredWhileSelectingAnchor)
                        messageId
                        message2.createdAt
                        localUser.timezone
                    , messageIdView messageId
                    ]
                , Html.div
                    [ Html.Attributes.style "white-space" "pre-wrap" ]
                    (RichText.view
                        (Dom.id (Dom.idToString spoilerHtmlId ++ "_" ++ Id.toString messageId))
                        containerWidth
                        MessageView_PressedNonWhitelistLink
                        MessageView_PressedSpoiler
                        MessageView_PressedImage
                        { revealedSpoilers =
                            case SeqDict.get messageId revealedSpoilers of
                                Just nonempty ->
                                    NonemptySet.toSeqSet nonempty

                                Nothing ->
                                    SeqSet.empty
                        , users = allUsers
                        , channels = channels
                        , attachedFiles = attachedFiles
                        , domainWhitelist = localUser.user.domainWhitelist
                        , customEmojis = localUser.customEmojis
                        , emojiData = localUser.emojiData
                        , stickers = localUser.stickers
                        , animationMode = isHoveredToAnimationMode isHovered
                        , timezone = localUser.timezone
                        , time = time
                        , drawings = drawings.imageAttachmentDrawings
                        , embedDrawings = drawings.embedDrawings
                        , drawingUserColor = User.discordUserColor localUser
                        , isSelectingAnchor = isHovered == IsHoveredWhileSelectingAnchor
                        , devicePixelRatio = localUser.devicePixelRatio
                        , isHovered =
                            case isHovered of
                                IsNotHovered ->
                                    False

                                IsHovered ->
                                    True

                                IsHoveredButNoMenu ->
                                    True

                                IsHoveredReactionsOnly ->
                                    True

                                IsHoveredWhileSelectingAnchor ->
                                    False
                        , noOp = MessageView_NoOp
                        , onPressChannelMention = MessageView_PressedDiscordChannelMention
                        , onPressCopyCode = MessageView_PressedCopyCode
                        }
                        (case localUser.user.embedVisibility of
                            ShowEmbeds ->
                                embeds

                            HideEmbeds ->
                                Array.empty
                        )
                        content
                        ++ (case message2.editedAt of
                                Just editedAt ->
                                    [ Html.span
                                        [ Html.Attributes.style "color" (MyUi.colorToStyle MyUi.dimFont)
                                        , Html.Attributes.style "font-size" "12px"
                                        , MyUi.datestamp localUser.timezone editedAt |> Html.Attributes.title
                                        ]
                                        [ Html.text " (edited)" ]
                                    ]

                                Nothing ->
                                    []
                           )
                    )
                    |> Ui.html
                ]
            ]
        ]


messageIdView : Id messageId -> Element msg
messageIdView _ =
    Ui.none



--if Env.isProduction then
--    Ui.none
--
--else
--    Ui.el [ Ui.Font.size 14, Ui.width Ui.shrink, Ui.paddingLeft 4 ] (Ui.text (Id.toString messageId))


deletedMessageContent : Id messageId -> Bool -> Time.Posix -> Time.Zone -> Element MessageViewMsg
deletedMessageContent messageId isSelectingAnchor createdAt timezone =
    Ui.row
        [ Ui.paddingWith { left = 4, right = 0, top = 4, bottom = 0 } ]
        [ Ui.el
            [ Ui.Font.color MyUi.font3
            , Ui.Font.italic
            , Ui.Font.size 14
            ]
            (Ui.text LocalState.messageDeleted)
        , messageTimestamp (\_ -> RichText.defaultColor) Drawing.emptyDrawing isSelectingAnchor messageId createdAt timezone
        ]


messageTimestamp : (userId -> UserColor) -> Drawing userId -> Bool -> Id messageId -> Time.Posix -> Time.Zone -> Element MessageViewMsg
messageTimestamp userIdToColor drawings isSelectingAnchor messageId createdAt timezone =
    Ui.el
        ([ Ui.Font.size 14
         , Ui.Font.color MyUi.font3
         , Ui.paddingXY 4 0
         , Ui.rounded 4
         ]
            ++ Drawing.anchorHighlight
                ("guild_messageTimestamp_" ++ Id.toString messageId |> Dom.id)
                userIdToColor
                MessageView_PressedTimestamp
                isSelectingAnchor
                drawings
        )
        (Ui.el [ MyUi.noPointerEvents ] (Ui.text (MyUi.timestamp createdAt timezone)))


messagePreviewTimestamp : Time.Posix -> Time.Zone -> Html msg
messagePreviewTimestamp createdAt timezone =
    Html.span
        [ Html.Attributes.style "font-size" "14px"
        , Html.Attributes.style "color" (MyUi.colorToStyle MyUi.font3)
        ]
        [ MyUi.timestamp createdAt timezone |> Html.text ]


replyToHeaderAboveMessage_userTextMessage :
    Bool
    -> Id messageId
    -> Time.Zone
    -> Time.Posix
    -> Maybe CachedEmojiData
    -> SeqDict (Id CustomEmojiId) CustomEmojiData
    -> SeqDict userId { a | name : PersonName }
    -> SeqDict ( channelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> SeqDict (Id messageId) (NonemptySet Int)
    -> MessageContent userId channelId
    -> userId
    -> Element MessageViewMsg
replyToHeaderAboveMessage_userTextMessage isMobile repliedToIndex timezone time emojiData customEmojis allUsers channels revealedSpoilers contentAndEmbeds createdBy =
    replyToHeaderAboveMessageHelper
        isMobile
        repliedToIndex
        (userTextMessagePreview
            timezone
            time
            emojiData
            customEmojis
            allUsers
            channels
            (case SeqDict.get repliedToIndex revealedSpoilers of
                Just set ->
                    NonemptySet.toSeqSet set

                Nothing ->
                    SeqSet.empty
            )
            contentAndEmbeds
            createdBy
        )


replyToHeaderAboveMessage :
    Bool
    -> Time.Zone
    -> Time.Posix
    -> Maybe (RepliedToView messageId userId channelId MessageViewMsg)
    -> SeqDict (Id messageId) (NonemptySet Int)
    -> Maybe CachedEmojiData
    -> SeqDict (Id CustomEmojiId) CustomEmojiData
    -> SeqDict BytesHash (Result () (MessageContent userId channelId))
    -> SeqDict userId { a | name : PersonName, icon : Maybe FileHash }
    -> SeqDict ( channelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> Element MessageViewMsg
replyToHeaderAboveMessage isMobile timezone time maybeRepliedTo2 revealedSpoilers emojiData customEmojis decrypted allUsers channels =
    case maybeRepliedTo2 of
        Just (RepliedToView_Message repliedToIndex (UserTextMessage repliedToData)) ->
            replyToHeaderAboveMessage_userTextMessage
                isMobile
                repliedToIndex
                timezone
                time
                emojiData
                customEmojis
                allUsers
                channels
                revealedSpoilers
                repliedToData.content
                repliedToData.createdBy

        Just (RepliedToView_Message repliedToIndex (EncryptedUserTextMessage repliedToData)) ->
            case SeqDict.get (Encryption.hash repliedToData.content) decrypted of
                Just result ->
                    replyToHeaderAboveMessage_userTextMessage
                        isMobile
                        repliedToIndex
                        timezone
                        time
                        emojiData
                        customEmojis
                        allUsers
                        channels
                        revealedSpoilers
                        (Result.withDefault
                            { content = RichText.failedToDecryptMessage, embeds = Array.empty, attachedFiles = SeqDict.empty }
                            result
                        )
                        repliedToData.createdBy

                Nothing ->
                    Ui.none

        Just (RepliedToView_Message repliedToIndex (UserJoinedMessage _ userId _ _)) ->
            replyToHeaderAboveMessageHelper isMobile repliedToIndex (userJoinedContent userId allUsers)

        Just (RepliedToView_Message repliedToIndex (DeletedMessage _)) ->
            replyToHeaderAboveMessageHelper
                isMobile
                repliedToIndex
                (Ui.el
                    [ Ui.Font.italic, Ui.Font.color MyUi.font3 ]
                    (Ui.text LocalState.messageDeleted)
                )

        Just (RepliedToView_Message repliedToIndex (CallStarted { startedAt, endedAt, startedBy })) ->
            replyToHeaderAboveMessageHelper isMobile repliedToIndex (callStarted startedBy startedAt endedAt allUsers)

        Just (RepliedToView_Message repliedToIndex (GameStarted { startedBy, gameType })) ->
            replyToHeaderAboveMessageHelper isMobile repliedToIndex (gameStartedContent startedBy gameType allUsers)

        Just (RepliedToView_Game matchId _ preview) ->
            MyUi.rowButton
                (Dom.id ("guild_gameReplyLink_" ++ Id.toString matchId))
                MessageView_PressedReplyLink
                (replyToHeaderAboveMessageAttributes isMobile)
                [ replyToHeaderAboveMessageIcon, Ui.el [ Ui.width (Ui.px 4) ] Ui.none, preview ]

        Nothing ->
            Ui.none


userTextMessagePreview :
    Time.Zone
    -> Time.Posix
    -> Maybe CachedEmojiData
    -> SeqDict (Id CustomEmojiId) CustomEmojiData
    -> SeqDict userId { a | name : PersonName }
    -> SeqDict ( channelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> SeqSet Int
    -> MessageContent userId channelId
    -> userId
    -> Element MessageViewMsg
userTextMessagePreview timezone time emojiData customEmojis allUsers channels revealedSpoilers contentAndEmbeds createdBy =
    Html.div
        [ Html.Attributes.style "white-space" "nowrap"
        , Html.Attributes.style "overflow" "hidden"
        , Html.Attributes.style "text-overflow" "ellipsis"
        ]
        (Html.span
            [ Html.Attributes.style "color" (MyUi.colorToStyle MyUi.dimFont)
            , Html.Attributes.style "padding" "0 6px 0 2px"
            ]
            [ Html.text (User.toString createdBy allUsers) ]
            :: RichText.preview
                MessageView_NoOp
                (\_ -> MessageView_NoOp)
                (\_ _ -> MessageView_NoOp)
                { revealedSpoilers = revealedSpoilers
                , users = allUsers
                , channels = channels
                , attachedFiles = contentAndEmbeds.attachedFiles
                , customEmojis = customEmojis
                , emojiData = emojiData
                , domainWhitelist = SeqSet.empty
                , timezone = timezone
                , time = time
                }
                contentAndEmbeds.content
        )
        |> Ui.html


channelMessageHtmlId : Id ChannelMessageId -> HtmlId
channelMessageHtmlId messageIndex =
    "guild_message_" ++ Id.toString messageIndex |> Dom.id


threadMessageHtmlId : Id ThreadMessageId -> HtmlId
threadMessageHtmlId messageIndex =
    "thread_message_" ++ Id.toString messageIndex |> Dom.id


{-| The reply header is drawn above the message it belongs to, but starting where the
message text starts rather than where the profile image does, so it needs the profile
image's column of space skipped past. It sits outside the row holding the image and the
message so that the image lines up with the author's name on its own, without anything
having to know how tall the header came out.

`Ui.none` is left alone rather than padded, so a message without a reply is laid out
exactly as it was before.

-}
indentPastProfileImage : Maybe repliedTo -> Element msg -> Element msg
indentPastProfileImage maybeRepliedTo2 header =
    case maybeRepliedTo2 of
        Just _ ->
            Ui.el
                [ Ui.paddingLeft (User.profileImageSize + MessageView.profileImagePaddingRight) ]
                header

        Nothing ->
            header


replyToHeaderAboveMessageHelper : Bool -> Id messageId -> Element MessageViewMsg -> Element MessageViewMsg
replyToHeaderAboveMessageHelper isMobile messageId content =
    MyUi.rowButton
        (Dom.id ("guild_replyLink_" ++ Id.toString messageId))
        MessageView_PressedReplyLink
        (replyToHeaderAboveMessageAttributes isMobile)
        [ replyToHeaderAboveMessageIcon
        , content
        ]


replyToHeaderAboveMessageAttributes : Bool -> List (Ui.Attribute MessageViewMsg)
replyToHeaderAboveMessageAttributes isMobile =
    [ Ui.Font.size 14
    , Ui.paddingWith { left = 0, right = 8, top = 2, bottom = 0 }
    , Ui.Font.color MyUi.font3
    , MyUi.hover isMobile [ Ui.Anim.fontColor MyUi.font1 ]
    ]


replyToHeaderAboveMessageIcon : Element msg
replyToHeaderAboveMessageIcon =
    Ui.el [ Ui.width Ui.shrink, Ui.move { x = 0, y = 3, z = 0 }, MyUi.noShrinking ] (Ui.html (Icons.reply 18))


userJoinedContent : userId -> SeqDict userId { a | name : PersonName } -> Element msg
userJoinedContent userId allUsers =
    Ui.Prose.paragraph
        [ Ui.paddingXY 0 4 ]
        [ User.toString userId allUsers |> Ui.text |> Ui.el [ Ui.Font.bold ]
        , Ui.el [] (Ui.text " joined!")
        ]


callStarted : userId -> Time.Posix -> Maybe Time.Posix -> SeqDict userId { a | name : PersonName } -> Element msg
callStarted userId startedAt endedAt allUsers =
    Ui.Prose.paragraph
        [ Ui.paddingXY 0 4 ]
        [ User.toString userId allUsers
            |> Ui.text
            |> Ui.el [ Ui.Font.bold ]
        , " started a call" ++ eventDurationText startedAt endedAt |> Ui.text |> Ui.el []
        ]


eventDurationText : Time.Posix -> Maybe Time.Posix -> String
eventDurationText start end =
    case end of
        Just endedAt2 ->
            ", lasted " ++ MyUi.timeElapsed start endedAt2

        Nothing ->
            ""


gameStartedContent : userId -> GameType -> SeqDict userId { a | name : PersonName } -> Element msg
gameStartedContent userId game allUsers =
    Ui.Prose.paragraph
        [ Ui.paddingXY 0 4 ]
        [ User.toString userId allUsers
            |> Ui.text
            |> Ui.el [ Ui.Font.bold ]
        , Ui.text (" " ++ startedGameText game) |> Ui.el []
        ]


callStartedCard :
    (userId -> UserColor)
    -> Bool
    -> Id messageId
    -> Drawing userId
    -> userId
    -> Time.Posix
    -> Maybe Time.Posix
    -> SeqDict userId { a | name : PersonName }
    -> Element MessageViewMsg
callStartedCard userIdToColor isSelectingAnchor messageId drawings userId startedAt endedAt allUsers =
    eventCard
        userIdToColor
        isSelectingAnchor
        messageId
        drawings
        (Dom.id "guild_callStartedCard")
        MessageViewMsg_PressedCallStartedCard
        (Ui.html Icons.phone)
        (User.toString userId allUsers)
        (startedACallText ++ eventDurationText startedAt endedAt)


goMatchStartedCard :
    (userId -> UserColor)
    -> Bool
    -> Drawing userId
    -> Id messageId
    -> userId
    -> SeqDict userId { a | name : PersonName }
    -> GameType
    -> Element MessageViewMsg
goMatchStartedCard userIdToColor isSelectingAnchor drawings messageId userId allUsers game =
    eventCard
        userIdToColor
        isSelectingAnchor
        messageId
        drawings
        (Dom.id ("guild_gameStartedCard_" ++ Id.toString messageId))
        MessageViewMsg_PressedGameStartedCard
        (Ui.html Icons.go)
        (User.toString userId allUsers)
        (startedGameText game)


startedGameText : GameType -> String
startedGameText game =
    case game of
        GameType_Go ->
            "started a Go match"

        GameType_WordSpellingGame ->
            "started a Word Spelling game"

        GameType_SheepGame ->
            "started a Sheep Game"


eventCard :
    (userId -> UserColor)
    -> Bool
    -> Id messageId
    -> Drawing userId
    -> HtmlId
    -> MessageViewMsg
    -> Element MessageViewMsg
    -> String
    -> String
    -> Element MessageViewMsg
eventCard userIdToColor isSelectingAnchor messageId drawings htmlId onPress icon userName action =
    Ui.el
        []
        (Ui.el
            (Ui.rounded 6
                :: Drawing.anchorHighlight
                    ("guild_eventCard_" ++ Id.toString messageId |> Dom.id)
                    userIdToColor
                    MessageView_PressedCardAnchor
                    isSelectingAnchor
                    drawings
            )
            (MyUi.rowButton
                htmlId
                onPress
                [ Ui.spacing 12
                , Ui.paddingXY 16 6
                , Ui.background MyUi.background2
                , Ui.border 1
                , Ui.borderColor MyUi.border1
                , Ui.rounded 6
                , Ui.width Ui.shrink
                , Ui.Font.color MyUi.font3
                , MyUi.hover False [ Ui.Anim.fontColor MyUi.font1 ]
                , if isSelectingAnchor then
                    MyUi.noPointerEvents

                  else
                    Ui.noAttr
                ]
                [ icon
                , Ui.column
                    [ Ui.spacing 2, Ui.width Ui.shrink ]
                    [ Ui.el
                        [ Ui.Font.bold, Ui.Font.color MyUi.font1, Ui.widthMax 200, Ui.clipWithEllipsis ]
                        (Ui.text userName)
                    , Ui.el [ Ui.Font.size 13 ] (Ui.text action)
                    ]
                ]
            )
        )


messagePaddingX : number
messagePaddingX =
    8


{-| Decodes a "contextmenu" event into a message that opens the message menu.
If the right-click landed on an image attachment or a hyperlink we also grab
their urls (exposed via the "data-image-url"/"data-link-url" attributes) so that
the menu can offer "Copy image"/"Copy image link"/"Copy link" options.
-}
decodeMessageContextMenu : Bool -> Json.Decode.Decoder ( MessageViewMsg, Bool )
decodeMessageContextMenu isThreadStarter =
    Json.Decode.map3
        (\x y target ->
            ( MessageView_AltPressedMessage isThreadStarter target.imageUrl target.linkUrl (Coord.xy (round x) (round y))
            , True
            )
        )
        (Json.Decode.field "clientX" Json.Decode.float)
        (Json.Decode.field "clientY" Json.Decode.float)
        decodeEventTarget


{-| Reads the "data-image-url"/"data-link-url" off the event's target (walking up
its ancestors). Falls back to no urls when there is no target (e.g. in tests).
-}
decodeEventTarget : Json.Decode.Decoder ContextMenuTarget
decodeEventTarget =
    Json.Decode.oneOf
        [ Json.Decode.field "target" (decodeContextMenuTarget 20)
        , Json.Decode.succeed emptyContextMenuTarget
        ]


type alias ContextMenuTarget =
    { imageUrl : Maybe String, linkUrl : Maybe String }


emptyContextMenuTarget : ContextMenuTarget
emptyContextMenuTarget =
    { imageUrl = Nothing, linkUrl = Nothing }


{-| Walks up from the event target through its ancestors looking for the nearest
"data-image-url"/"data-link-url" attributes. We have to climb the tree because
the element actually under the cursor is often a descendant of the one carrying
the attribute (e.g. the <canvas>/<img> that an animated-image-player web
component appends inside itself, or the favicon/label inside a link).
-}
decodeContextMenuTarget : Int -> Json.Decode.Decoder ContextMenuTarget
decodeContextMenuTarget depth =
    Json.Decode.map2
        (\here parent ->
            { imageUrl = orElseMaybe here.imageUrl parent.imageUrl
            , linkUrl = orElseMaybe here.linkUrl parent.linkUrl
            }
        )
        (Json.Decode.map2 ContextMenuTarget
            (Json.Decode.maybe (Json.Decode.at [ "dataset", "imageUrl" ] Json.Decode.string))
            (Json.Decode.maybe (Json.Decode.at [ "dataset", "linkUrl" ] Json.Decode.string))
        )
        (if depth <= 0 then
            Json.Decode.succeed emptyContextMenuTarget

         else
            Json.Decode.oneOf
                [ Json.Decode.field "parentElement" (Json.Decode.lazy (\() -> decodeContextMenuTarget (depth - 1)))
                , Json.Decode.succeed emptyContextMenuTarget
                ]
        )


orElseMaybe : Maybe a -> Maybe a -> Maybe a
orElseMaybe first second =
    case first of
        Just _ ->
            first

        Nothing ->
            second


messageContainer :
    Int
    -> Bool
    -> Time.Zone
    -> Time.Posix
    -> SeqSet (Id CustomEmojiId)
    -> SeqDict (Id CustomEmojiId) CustomEmojiData
    -> Maybe CachedEmojiData
    -> SeqDict userId { a | name : PersonName }
    -> SeqDict ( channelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> HighlightMessage
    -> Id ChannelMessageId
    -> Bool
    -> userId
    -> FrontendCurrentUser
    -> SeqDict EmojiOrCustomEmoji (NonemptySet userId)
    -> Maybe (FrontendGenericThread userId channelId)
    -> SeqDict BytesHash (Result () (MessageContent userId channelId))
    -> IsHovered
    -> Element MessageViewMsg
    -> Element MessageViewMsg
messageContainer containerWidth isThreadStarter timezone currentTime availableCustomEmojis customEmojis emojiData allUsers channels highlight messageIndex canEdit currentUserId currentUser reactions maybeThread decrypted isHovered messageContent =
    let
        maybeReactions : Maybe (Element MessageViewMsg)
        maybeReactions =
            MessageView.reactionEmojiView emojiData (reactionsHover isHovered) currentUserId customEmojis allUsers (isHoveredToAnimationMode isHovered) containerWidth reactions
    in
    Ui.column
        ([ Ui.Font.color MyUi.font1
         , Ui.Events.onMouseEnter MessageView_MouseEnteredMessage
         , Ui.Events.onMouseLeave MessageView_MouseExitedMessage
         , Ui.Events.on
            "touchstart"
            (Json.Decode.map2
                (\toMsg target -> toMsg target.imageUrl target.linkUrl)
                (Touch.decodeTouchEvent
                    (\time touches imageUrl linkUrl ->
                        MessageView_TouchStart time isThreadStarter imageUrl linkUrl touches
                    )
                )
                decodeEventTarget
            )
         , Ui.Events.preventDefaultOn "contextmenu" (decodeMessageContextMenu isThreadStarter)
         , Ui.paddingWith
            { left = messagePaddingX
            , right = messagePaddingX
            , top = 4
            , bottom =
                if maybeReactions == Nothing then
                    8

                else
                    4
            }
         , Ui.spacing 4
         , channelMessageHtmlId messageIndex |> Dom.idToString |> Ui.id
         ]
            ++ (case isHovered of
                    IsNotHovered ->
                        [ Ui.behindContent Ui.none ]

                    IsHovered ->
                        [ MyUi.hoverHighlightLayer
                        , MessageView.miniView currentUser isThreadStarter canEdit availableCustomEmojis emojiData customEmojis |> Ui.inFront
                        ]

                    IsHoveredButNoMenu ->
                        [ MyUi.hoverHighlightLayer ]

                    IsHoveredReactionsOnly ->
                        [ MyUi.hoverHighlightLayer
                        , MessageView.reactionsMiniView currentUser availableCustomEmojis emojiData customEmojis |> Ui.inFront
                        ]

                    IsHoveredWhileSelectingAnchor ->
                        [ Ui.behindContent Ui.none ]
               )
            ++ [ highlightLayer highlight ]
        )
        (messageContent
            :: Maybe.Extra.toList maybeReactions
            ++ (case maybeThread of
                    Just thread ->
                        [ previewThreadLastMessage timezone currentTime emojiData customEmojis allUsers channels decrypted messageIndex thread
                        ]

                    Nothing ->
                        []
               )
        )


threadMessageContainer :
    Int
    -> HighlightMessage
    -> Id ThreadMessageId
    -> Bool
    -> userId
    -> FrontendCurrentUser
    -> SeqDict EmojiOrCustomEmoji (NonemptySet userId)
    -> SeqSet (Id CustomEmojiId)
    -> SeqDict (Id CustomEmojiId) CustomEmojiData
    -> Maybe CachedEmojiData
    -> SeqDict userId { a | name : PersonName }
    -> IsHovered
    -> Element MessageViewMsg
    -> Element MessageViewMsg
threadMessageContainer containerWidth highlight messageIndex canEdit currentUserId currentUser reactions availableCustomEmojis customEmojis emojiData allUsers isHovered messageContent =
    let
        maybeReactions : Maybe (Element MessageViewMsg)
        maybeReactions =
            MessageView.reactionEmojiView emojiData (reactionsHover isHovered) currentUserId customEmojis allUsers (isHoveredToAnimationMode isHovered) containerWidth reactions
    in
    Ui.column
        ([ Ui.Font.color MyUi.font1
         , Ui.Events.onMouseEnter MessageView_MouseEnteredMessage
         , Ui.Events.onMouseLeave MessageView_MouseExitedMessage
         , Ui.Events.on
            "touchstart"
            (Json.Decode.map2
                (\toMsg target -> toMsg target.imageUrl target.linkUrl)
                (Touch.decodeTouchEvent
                    (\time touches imageUrl linkUrl ->
                        MessageView_TouchStart time False imageUrl linkUrl touches
                    )
                )
                decodeEventTarget
            )
         , Ui.Events.preventDefaultOn "contextmenu" (decodeMessageContextMenu False)
         , Ui.paddingWith
            { left = messagePaddingX
            , right = messagePaddingX
            , top = 4
            , bottom =
                if maybeReactions == Nothing then
                    8

                else
                    4
            }
         , Ui.spacing 4
         , threadMessageHtmlId messageIndex |> Dom.idToString |> Ui.id
         ]
            ++ (case isHovered of
                    IsNotHovered ->
                        [ Ui.behindContent Ui.none ]

                    IsHovered ->
                        [ MyUi.hoverHighlightLayer
                        , MessageView.miniView currentUser False canEdit availableCustomEmojis emojiData customEmojis |> Ui.inFront
                        ]

                    IsHoveredButNoMenu ->
                        [ MyUi.hoverHighlightLayer ]

                    IsHoveredReactionsOnly ->
                        [ MyUi.hoverHighlightLayer
                        , MessageView.reactionsMiniView currentUser availableCustomEmojis emojiData customEmojis |> Ui.inFront
                        ]

                    IsHoveredWhileSelectingAnchor ->
                        [ Ui.behindContent Ui.none ]
               )
            ++ [ highlightLayer highlight ]
        )
        (messageContent :: Maybe.Extra.toList maybeReactions)


previewThreadLastMessage_userTextMessage :
    Time.Posix
    -> Time.Zone
    -> Maybe CachedEmojiData
    -> SeqDict (Id CustomEmojiId) CustomEmojiData
    -> SeqDict userId { a | name : PersonName }
    -> SeqDict ( channelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> MessageContent userId channelId
    -> userId
    -> List (Html MessageViewMsg)
previewThreadLastMessage_userTextMessage time timezone emojiData customEmojis allUsers channels contentAndEmbeds createdBy =
    Html.span
        [ Html.Attributes.style "color" (MyUi.colorToStyle MyUi.font3)
        , Html.Attributes.style "padding" "0 6px 0 2px"
        ]
        [ Html.text (User.toString createdBy allUsers) ]
        :: RichText.preview
            MessageView_NoOp
            (\_ -> MessageView_NoOp)
            (\_ _ -> MessageView_NoOp)
            { revealedSpoilers = SeqSet.empty
            , users = allUsers
            , channels = channels
            , attachedFiles = contentAndEmbeds.attachedFiles
            , customEmojis = customEmojis
            , emojiData = emojiData
            , domainWhitelist = SeqSet.empty
            , timezone = timezone
            , time = time
            }
            contentAndEmbeds.content


previewThreadLastMessage :
    Time.Zone
    -> Time.Posix
    -> Maybe CachedEmojiData
    -> SeqDict (Id CustomEmojiId) CustomEmojiData
    -> SeqDict userId { a | name : PersonName }
    -> SeqDict ( channelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> SeqDict BytesHash (Result () (MessageContent userId channelId))
    -> Id ChannelMessageId
    -> FrontendGenericThread userId channelId
    -> Element MessageViewMsg
previewThreadLastMessage timezone time emojiData customEmojis allUsers channels decrypted messageId thread =
    let
        lastMessage =
            MessageArray.last thread.messages
    in
    Html.button
        [ Html.Attributes.style "white-space" "nowrap"
        , Html.Attributes.style "text-overflow" "ellipsis"
        , Html.Attributes.style "overflow" "hidden"
        , Html.Attributes.style "background-color" (MyUi.colorToStyle MyUi.background1)
        , Html.Attributes.style "border" ("1px solid " ++ MyUi.colorToStyle MyUi.border1)
        , Html.Attributes.style "padding" "4px 8px 4px 8px"
        , Html.Attributes.style "width" "fit-content"
        , Html.Attributes.style "max-width" "calc(min(100% - 16px, 800px))"
        , Html.Attributes.style "min-width" "250px"
        , Html.Attributes.style "margin" "0"
        , Html.Attributes.style "color" "inherit"
        , Html.Attributes.style "font-size" "inherit"
        , Html.Attributes.style "text-align" "left"
        , Html.Attributes.id ("guild_threadStarterIndicator_" ++ Id.toString messageId)
        , Html.Events.onClick MessageView_PressedViewThreadLink
        , Html.Attributes.style "cursor" "pointer"
        , Html.Attributes.style "position" "relative"
        ]
        (Html.div
            [ Html.Attributes.style "display" "flex"
            , Html.Attributes.style "align-content" "center"
            , Html.Attributes.style "color" (MyUi.colorToStyle MyUi.font3)
            ]
            [ Icons.hashtag
            , case MessageArray.length thread.messages of
                1 ->
                    Html.text "1 message"

                count ->
                    Html.text (String.fromInt count ++ " messages")
            , Html.div [ Html.Attributes.style "flex-grow" "1" ] []
            , case lastMessage of
                Just message ->
                    messagePreviewTimestamp (Message.createdAt message) timezone

                _ ->
                    Html.text ""
            ]
            :: (case lastMessage of
                    Just last ->
                        case last of
                            UserTextMessage data ->
                                previewThreadLastMessage_userTextMessage
                                    time
                                    timezone
                                    emojiData
                                    customEmojis
                                    allUsers
                                    channels
                                    data.content
                                    data.createdBy

                            EncryptedUserTextMessage data ->
                                case SeqDict.get (Encryption.hash data.content) decrypted of
                                    Just result ->
                                        previewThreadLastMessage_userTextMessage
                                            time
                                            timezone
                                            emojiData
                                            customEmojis
                                            allUsers
                                            channels
                                            (Result.withDefault
                                                { content = RichText.failedToDecryptMessage, embeds = Array.empty, attachedFiles = SeqDict.empty }
                                                result
                                            )
                                            data.createdBy

                                    Nothing ->
                                        []

                            UserJoinedMessage _ userId _ _ ->
                                [ Html.span
                                    []
                                    [ Html.b [] [ User.toString userId allUsers |> Html.text ]
                                    , Html.text " joined!"
                                    ]
                                ]

                            DeletedMessage _ ->
                                [ Html.i
                                    [ Html.Attributes.style "color" (MyUi.colorToStyle MyUi.font3) ]
                                    [ Html.text LocalState.messageDeleted ]
                                ]

                            CallStarted { endedAt, startedBy } ->
                                [ Html.span
                                    []
                                    [ Html.b [] [ User.toString startedBy allUsers |> Html.text ]
                                    , case endedAt of
                                        Just _ ->
                                            Html.text "'s call ended"

                                        Nothing ->
                                            Html.text " started a call"
                                    ]
                                ]

                            GameStarted { startedBy, gameType } ->
                                [ Html.span
                                    []
                                    [ Html.b [] [ User.toString startedBy allUsers |> Html.text ]
                                    , Html.text (" " ++ startedGameText gameType)
                                    ]
                                ]

                    _ ->
                        []
               )
        )
        |> Ui.html


channelColumnLazy :
    Bool
    -> Bool
    -> LoadedFrontend
    -> LoggedIn2
    -> LocalUser
    -> Call.Local
    -> Id GuildId
    -> FrontendGuild
    -> ChannelRoute
    -> Element FrontendMsg_
channelColumnLazy isMobile canScroll2 model loggedIn localUser calls guildId guild channelRoute =
    if loggedIn.channelSearch /= "" then
        -- The search text changes too often for laziness to be worth it here
        channelColumn
            isMobile
            (Time.millisToPosix (nearestHour model.time))
            localUser
            calls
            guildId
            guild
            channelRoute
            canScroll2
            loggedIn.channelSearch

    else
        Ui.Lazy.lazy6
            (if isMobile then
                if canScroll2 then
                    channelColumnCanScrollMobile

                else
                    channelColumnCannotScrollMobile

             else
                channelColumnNotMobile
            )
            localUser
            calls
            (nearestHour model.time)
            guildId
            guild
            channelRoute


discordChannelColumnLazy :
    Bool
    -> Bool
    -> LoadedFrontend
    -> LoggedIn2
    -> LocalUser
    -> DiscordGuildRouteData
    -> DiscordFrontendGuild
    -> Element FrontendMsg_
discordChannelColumnLazy isMobile canScroll2 model loggedIn localUser routeData guild =
    if loggedIn.channelSearch /= "" then
        -- The search text changes too often for laziness to be worth it here
        discordChannelColumn
            isMobile
            (Time.millisToPosix (nearestHour model.time))
            localUser
            routeData
            guild
            canScroll2
            loggedIn.channelSearch

    else
        Ui.Lazy.lazy4
            (if isMobile then
                if canScroll2 then
                    discordChannelColumnCanScrollMobile

                else
                    discordChannelColumnCannotScrollMobile

             else
                discordChannelColumnNotMobile
            )
            (nearestHour model.time)
            localUser
            routeData
            guild


channelColumnNotMobile :
    LocalUser
    -> Call.Local
    -> Int
    -> Id GuildId
    -> FrontendGuild
    -> ChannelRoute
    -> Element FrontendMsg_
channelColumnNotMobile localUser calls time guildId guild channelRoute =
    channelColumn False (Time.millisToPosix time) localUser calls guildId guild channelRoute True ""


discordChannelColumnNotMobile :
    Int
    -> LocalUser
    -> DiscordGuildRouteData
    -> DiscordFrontendGuild
    -> Element FrontendMsg_
discordChannelColumnNotMobile time localUser routeData guild =
    discordChannelColumn False (Time.millisToPosix time) localUser routeData guild True ""


channelColumnCanScrollMobile :
    LocalUser
    -> Call.Local
    -> Int
    -> Id GuildId
    -> FrontendGuild
    -> ChannelRoute
    -> Element FrontendMsg_
channelColumnCanScrollMobile localUser calls time guildId guild channelRoute =
    channelColumn True (Time.millisToPosix time) localUser calls guildId guild channelRoute True ""


channelColumnCannotScrollMobile :
    LocalUser
    -> Call.Local
    -> Int
    -> Id GuildId
    -> FrontendGuild
    -> ChannelRoute
    -> Element FrontendMsg_
channelColumnCannotScrollMobile localUser calls time guildId guild channelRoute =
    channelColumn True (Time.millisToPosix time) localUser calls guildId guild channelRoute False ""


discordChannelColumnCanScrollMobile :
    Int
    -> LocalUser
    -> DiscordGuildRouteData
    -> DiscordFrontendGuild
    -> Element FrontendMsg_
discordChannelColumnCanScrollMobile time localUser guildId guild =
    discordChannelColumn True (Time.millisToPosix time) localUser guildId guild True ""


discordChannelColumnCannotScrollMobile :
    Int
    -> LocalUser
    -> DiscordGuildRouteData
    -> DiscordFrontendGuild
    -> Element FrontendMsg_
discordChannelColumnCannotScrollMobile time localUser guildId guild =
    discordChannelColumn True (Time.millisToPosix time) localUser guildId guild False ""


channelColumnContainer : Int -> List (Element msg) -> Element msg -> Element msg -> Element msg
channelColumnContainer safeAreaInsetTop header subHeader content =
    Ui.el
        [ Ui.height Ui.fill, Ui.paddingWith { left = 0, right = 0, top = safeAreaInsetTop, bottom = 0 } ]
        (Ui.column
            [ Ui.height Ui.fill
            , Ui.background MyUi.background2
            , MyUi.htmlStyle "border-radius" (String.fromInt (safeAreaInsetTop // 2) ++ "px 0 0 0")
            , MyUi.htmlStyle "border-width" (String.fromInt (min safeAreaInsetTop 1) ++ "px 0 0 1px")

            --Ui.borderWith { left = 1, right = 0, bottom = 0, top = 1 }
            , Ui.borderColor MyUi.guildColumnBorder
            ]
            [ Ui.row
                [ Ui.Font.bold
                , Ui.paddingWith { left = max (safeAreaInsetTop // 4) 8, right = 4, top = 0, bottom = 0 }
                , Ui.spacing 8
                , Ui.Font.color MyUi.font1
                , Ui.borderWith { left = 0, right = 0, top = 0, bottom = 1 }
                , Ui.borderColor MyUi.border1
                , Ui.height (Ui.px MyUi.channelHeaderHeight)
                , MyUi.noShrinking
                , Ui.clipWithEllipsis
                ]
                header
            , subHeader
            , content
            ]
        )


channelColumn :
    Bool
    -> Time.Posix
    -> LocalUser
    -> Call.Local
    -> Id GuildId
    -> FrontendGuild
    -> ChannelRoute
    -> Bool
    -> String
    -> Element FrontendMsg_
channelColumn isMobile time localUser calls guildId guild channelRoute canScroll2 channelSearch =
    let
        channels : SeqDict ( Id ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
        channels =
            LocalState.guildChannelMentions localUser guild.channels

        guildName : String
        guildName =
            GuildName.toString guild.name

        showSearch : Bool
        showSearch =
            SeqDict.size guild.channels > channelSearchMinChannels

        searchFilter : String
        searchFilter =
            if showSearch then
                String.trim channelSearch |> String.toLower

            else
                ""

        directMentions : Maybe (NonemptyDict ( Id ChannelId, ThreadRoute ) OneOrGreater)
        directMentions =
            SeqDict.get guildId localUser.user.directMentions

        newChannelButton : Element FrontendMsg_
        newChannelButton =
            case MembersAndOwner.isMember localUser.session.userId guild.membersAndOwner of
                IsOwner ->
                    let
                        isSelected =
                            channelRoute == NewChannelRoute
                    in
                    GuildColumn.rowLinkButton
                        (Dom.id "guild_newChannel")
                        (GuildRoute guildId NewChannelRoute ChannelsHiddenOnMobile Nothing)
                        [ Ui.paddingXY 4 8
                        , Ui.Font.color MyUi.font3
                        , Ui.attrIf isSelected (Ui.background MyUi.selectedHighlight)
                        , MyUi.hover isMobile [ Ui.Anim.fontColor MyUi.font1 ]
                        , if isSelected then
                            Ui.Font.color MyUi.font1

                          else
                            Ui.Font.color MyUi.font3
                        ]
                        [ Ui.el [ Ui.width (Ui.px 22) ] (Ui.html Icons.plusIcon)
                        , Ui.text " Add new channel"
                        ]

                _ ->
                    Ui.none
    in
    channelColumnContainer
        localUser.safeAreaInsetTop
        [ Ui.el [ MyUi.hoverText guildName ] (Ui.text guildName)
        , GuildColumn.elLinkButton
            (Dom.id "guild_inviteLinkCreatorRoute")
            (GuildRoute guildId GuildSettingsRoute ChannelsHiddenOnMobile Nothing)
            [ Ui.Font.color MyUi.font2
            , Ui.width (Ui.px 40)
            , Ui.alignRight
            , Ui.paddingXY 8 0
            , Ui.height Ui.fill
            , Ui.contentCenterY
            , MyUi.hoverText "Invite users"
            ]
            (Ui.html Icons.gear)
        ]
        (if showSearch then
            channelSearchRow isMobile channelSearch

         else
            Ui.none
        )
        (Ui.column
            [ MyUi.scrollable canScroll2
            , Ui.heightMin 0
            , Ui.paddingXY 0 8
            , Ui.attrIf isMobile (Ui.height Ui.fill)
            , MyUi.bounceScroll isMobile
            ]
            ((SeqDict.toList guild.channels
                |> List.filter (\( _, channel ) -> channelMatchesSearch searchFilter channel)
                |> List.map
                    (\( channelId, channel ) ->
                        let
                            channelMuted =
                                MuteSettings.isChannelMuted localUser.user.muteSettings guildId channelId NoThread

                            hasNotifications : ChannelNotificationType
                            hasNotifications =
                                GuildColumn.channelOrThreadHasNotifications
                                    channelMuted
                                    directMentions
                                    (SeqSet.member guildId localUser.user.notifyOnAllMessages)
                                    channelId
                                    NoThread
                                    (SeqDict.get (GuildOrDmId (GuildOrDmId_Guild { guildId = guildId, channelId = channelId })) localUser.user.lastViewedMessage)
                                    channel

                            threadNotifications : List ChannelNotificationType
                            threadNotifications =
                                SeqDict.toList channel.threads
                                    |> List.map
                                        (\( threadId, thread ) ->
                                            GuildColumn.channelOrThreadHasNotifications
                                                (MuteSettings.isChannelMuted localUser.user.muteSettings guildId channelId (ViewThread threadId))
                                                directMentions
                                                (SeqSet.member guildId localUser.user.notifyOnAllMessages)
                                                channelId
                                                (ViewThread threadId)
                                                (SeqDict.get
                                                    ( GuildOrDmId (GuildOrDmId_Guild { guildId = guildId, channelId = channelId }), threadId )
                                                    localUser.user.lastViewedThreadMessage
                                                )
                                                thread
                                        )
                        in
                        ( channelSortName (hasNotifications :: threadNotifications) channel
                        , Ui.column
                            []
                            [ channelColumnRow
                                isMobile
                                channelMuted
                                hasNotifications
                                (Call.joinedUsers
                                    localUser.session.userId
                                    (Call.GuildRoomId { guildId = guildId, channelId = channelId })
                                    calls
                                    |> SeqDict.keys
                                    |> List.filterMap (\userId -> User.getUser userId localUser)
                                )
                                channelRoute
                                guildId
                                channelId
                                channel
                            , channelColumnThreads
                                isMobile
                                time
                                channelRoute
                                directMentions
                                localUser
                                channels
                                guildId
                                channelId
                                channel
                                (case channelRoute of
                                    ChannelRoute channelIdB (ViewThreadWithFriends threadMessageIndex _ _) _ ->
                                        if channelIdB == channelId then
                                            SeqDict.insert threadMessageIndex Thread.frontendInit channel.threads

                                        else
                                            channel.threads

                                    _ ->
                                        channel.threads
                                )
                            ]
                        )
                    )
                |> List.sortBy Tuple.first
                |> List.map Tuple.second
                |> channelColumnNoResults searchFilter
             )
                ++ (if searchFilter == "" then
                        [ newChannelButton ]

                    else
                        []
                   )
            )
        )


{-| Takes the notifications of the channel and each of its threads, so that unread thread
messages also move a channel up.
-}
channelSortName : List ChannelNotificationType -> { a | name : ChannelName } -> String
channelSortName notifications channel =
    (List.map
        (\notification ->
            case notification of
                NoNotification ->
                    "c"

                NewMessage _ ->
                    "b"

                NewMessageForUser _ ->
                    "a"
        )
        notifications
        |> List.minimum
        |> Maybe.withDefault "c"
    )
        ++ ChannelName.toString channel.name


{-| The channel search input is only shown for guilds with more channels than this.
-}
channelSearchMinChannels : Int
channelSearchMinChannels =
    6


channelSearchInputId : HtmlId
channelSearchInputId =
    Dom.id "guild_channelSearchInput"


channelMatchesSearch : String -> { a | name : ChannelName } -> Bool
channelMatchesSearch searchFilter channel =
    (searchFilter == "")
        || String.contains searchFilter (String.toLower (ChannelName.toString channel.name))


channelColumnNoResults : String -> List (Element FrontendMsg_) -> List (Element FrontendMsg_)
channelColumnNoResults searchFilter channelRows =
    if (searchFilter /= "") && List.isEmpty channelRows then
        [ Ui.Prose.paragraph
            [ Ui.Font.italic
            , Ui.Font.lineHeight 1.5
            , Ui.Font.color MyUi.font2
            , Ui.paddingXY 8 8
            ]
            [ Ui.text noMatchingChannelsText ]
        ]

    else
        channelRows


{-| Sits in its own row below the channel column header's bottom border. It is part of
the fixed header area, not the scrollable channel list.
-}
channelSearchRow : Bool -> String -> Element FrontendMsg_
channelSearchRow isMobile channelSearch =
    let
        clearPaddingX =
            12
    in
    Ui.el
        [ Ui.borderWith { left = 0, right = 0, top = 0, bottom = 1 }
        , Ui.borderColor MyUi.border1
        , MyUi.noShrinking
        , Ui.spacing 4
        , if channelSearch == "" then
            Ui.el
                [ Ui.Font.color MyUi.font3
                , Ui.height Ui.fill
                , Ui.contentCenterY
                , Ui.paddingXY clearPaddingX 0
                , MyUi.noPointerEvents
                , Ui.alignRight
                ]
                (Ui.html Icons.magnifyingGlass)
                |> Ui.inFront

          else
            MyUi.elButton
                (Dom.id "guild_clearChannelSearch")
                PressedClearChannelSearch
                [ Ui.Font.color MyUi.font3
                , Ui.width Ui.shrink
                , Ui.height Ui.fill
                , Ui.contentCenterY
                , Ui.paddingXY clearPaddingX 0
                , Ui.background MyUi.inputBackground
                , Ui.alignRight
                , MyUi.hover isMobile [ Ui.Anim.fontColor MyUi.font3 ]
                , MyUi.hoverText "Clear search"
                ]
                (Ui.html Icons.x)
                |> Ui.el
                    [ -- Don't cover up the input focus outline
                      Ui.padding 2
                    , Ui.height Ui.fill
                    ]
                |> Ui.inFront
        ]
        (Ui.Input.text
            [ Ui.id (Dom.idToString channelSearchInputId)
            , Ui.background (Ui.rgba 0 0 0 0)
            , Ui.border 0
            , Ui.paddingWith { left = 8, top = 8, bottom = 8, right = clearPaddingX * 2 + 24 + 8 }
            , Ui.Font.color MyUi.font1
            ]
            { onChange = TypedChannelSearch
            , text = channelSearch
            , placeholder = Just "Search channels"
            , label = Ui.Input.labelHidden (Dom.idToString channelSearchInputId)
            }
        )


discordChannelColumn :
    Bool
    -> Time.Posix
    -> LocalUser
    -> DiscordGuildRouteData
    -> DiscordFrontendGuild
    -> Bool
    -> String
    -> Element FrontendMsg_
discordChannelColumn isMobile time localUser routeData guild canScroll2 channelSearch =
    let
        channels : SeqDict ( Discord.Id Discord.ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
        channels =
            LocalState.discordGuildChannelMentions localUser guild.channels

        guildName : String
        guildName =
            GuildName.toString guild.name

        showSearch : Bool
        showSearch =
            SeqDict.size guild.channels > channelSearchMinChannels

        searchFilter : String
        searchFilter =
            if showSearch then
                String.trim channelSearch |> String.toLower

            else
                ""

        directMentions : Maybe (NonemptyDict ( Discord.Id Discord.ChannelId, ThreadRoute ) OneOrGreater)
        directMentions =
            SeqDict.get routeData.guildId localUser.user.discordDirectMentions
    in
    channelColumnContainer
        localUser.safeAreaInsetTop
        [ Ui.row
            [ MyUi.hoverText guildName
            , Ui.spacing 4
            ]
            [ GuildIcon.discordLogo
            , Ui.text guildName
            ]
        , GuildColumn.elLinkButton
            (Dom.id "guild_inviteLinkCreatorRoute")
            (DiscordGuildRoute
                { currentDiscordUserId = routeData.currentDiscordUserId
                , guildId = routeData.guildId
                , channelRoute = DiscordChannel_GuildSettingsRoute
                , channelsVisible = ChannelsHiddenOnMobile
                , overlay = Nothing
                }
            )
            [ Ui.Font.color MyUi.font2
            , Ui.width (Ui.px 40)
            , Ui.alignRight
            , Ui.paddingXY 8 0
            , Ui.height Ui.fill
            , Ui.contentCenterY
            , MyUi.hoverText "Invite users"
            ]
            (Ui.html Icons.gear)
        ]
        (if showSearch then
            channelSearchRow isMobile channelSearch

         else
            Ui.none
        )
        (Ui.column
            [ MyUi.scrollable canScroll2
            , Ui.heightMin 0
            , Ui.paddingXY 0 8
            , Ui.attrIf isMobile (Ui.height Ui.fill)
            , MyUi.bounceScroll isMobile
            ]
            (SeqDict.toList guild.channels
                |> List.filter (\( _, channel ) -> channelMatchesSearch searchFilter channel)
                |> List.map
                    (\( channelId, channel ) ->
                        let
                            channelMuted : IsMuted
                            channelMuted =
                                MuteSettings.isDiscordChannelMuted
                                    localUser.user.muteSettings
                                    routeData.guildId
                                    channelId
                                    NoThread

                            hasNotifications : ChannelNotificationType
                            hasNotifications =
                                GuildColumn.channelOrThreadHasNotifications
                                    channelMuted
                                    directMentions
                                    (SeqSet.member routeData.guildId localUser.user.discordNotifyOnAllMessages)
                                    channelId
                                    NoThread
                                    (SeqDict.get (DiscordGuildOrDmId (DiscordGuildOrDmId_Guild { currentUserId = routeData.currentDiscordUserId, guildId = routeData.guildId, channelId = channelId })) localUser.user.lastViewedMessage)
                                    channel

                            threadNotifications : List ChannelNotificationType
                            threadNotifications =
                                SeqDict.toList channel.threads
                                    |> List.map
                                        (\( threadId, thread ) ->
                                            GuildColumn.channelOrThreadHasNotifications
                                                (MuteSettings.isDiscordChannelMuted localUser.user.muteSettings routeData.guildId channelId (ViewThread threadId))
                                                directMentions
                                                (SeqSet.member routeData.guildId localUser.user.discordNotifyOnAllMessages)
                                                channelId
                                                (ViewThread threadId)
                                                (SeqDict.get
                                                    ( DiscordGuildOrDmId (DiscordGuildOrDmId_Guild { currentUserId = routeData.currentDiscordUserId, guildId = routeData.guildId, channelId = channelId })
                                                    , threadId
                                                    )
                                                    localUser.user.lastViewedThreadMessage
                                                )
                                                thread
                                        )
                        in
                        ( channelSortName (hasNotifications :: threadNotifications) channel
                        , Ui.column
                            []
                            [ discordChannelColumnRow
                                isMobile
                                channelMuted
                                hasNotifications
                                routeData
                                channelId
                                channel
                            , discordChannelColumnThreads
                                isMobile
                                time
                                routeData
                                directMentions
                                localUser
                                channels
                                channelId
                                channel
                                (case routeData.channelRoute of
                                    DiscordChannel_ChannelRoute channelIdB (ViewThreadWithFriends threadMessageIndex _ _) _ ->
                                        if channelIdB == channelId then
                                            SeqDict.insert threadMessageIndex Thread.discordFrontendInit channel.threads

                                        else
                                            channel.threads

                                    _ ->
                                        channel.threads
                                )
                            ]
                        )
                    )
                |> List.sortBy Tuple.first
                |> List.map Tuple.second
                |> channelColumnNoResults searchFilter
            )
        )


dmColumnThreads :
    Bool
    -> Time.Posix
    -> Maybe ThreadRouteWithFriends
    -> LocalUser
    -> Id UserId
    -> { b | messages : MessageArray ChannelMessageId (Id UserId) (Id ChannelId) }
    -> SeqDict (Id ChannelMessageId) FrontendThread
    -> Element FrontendMsg_
dmColumnThreads isMobile now threadRoute localUser otherUserId channel threads =
    let
        threads2 : List ( Id ChannelMessageId, ( IsMuted, ChannelNotificationType ), Bool )
        threads2 =
            List.filterMap
                (\( threadMessageIndex, thread ) ->
                    let
                        isSelected : Bool
                        isSelected =
                            case threadRoute of
                                Just (ViewThreadWithFriends b _ _) ->
                                    b == threadMessageIndex

                                _ ->
                                    False

                        isMuted =
                            MuteSettings.isDmMuted
                                localUser.user.muteSettings
                                otherUserId
                                (ViewThread threadMessageIndex)

                        hasNotifications : ChannelNotificationType
                        hasNotifications =
                            if MuteSettings.hidesRedDot isMuted then
                                NoNotification

                            else
                                -- Every message in a DM is meant for you, so unread ones
                                -- always get the red count. Guild channels save that for
                                -- messages that mention you and show the plain one otherwise.
                                case
                                    GuildColumn.newMessageCount
                                        (SeqDict.get
                                            ( GuildOrDmId (GuildOrDmId_Dm { otherUserId = otherUserId }), threadMessageIndex )
                                            localUser.user.lastViewedThreadMessage
                                        )
                                        thread
                                        |> OneOrGreater.fromInt
                                of
                                    Just unreadCount ->
                                        NewMessageForUser unreadCount

                                    Nothing ->
                                        NoNotification
                    in
                    case ( hasNotifications, isSelected, MessageArray.last thread.messages ) of
                        ( NoNotification, False, Just message ) ->
                            if Duration.from (Message.createdAt message) now |> Quantity.lessThan Duration.week then
                                Just ( threadMessageIndex, ( isMuted, hasNotifications ), isSelected )

                            else
                                Nothing

                        _ ->
                            Just ( threadMessageIndex, ( isMuted, hasNotifications ), isSelected )
                )
                (SeqDict.toList threads)

        count =
            List.length threads2
    in
    List.indexedMap
        (\index ( threadMessageIndex, ( isMuted, hasNotifications ), isSelected ) ->
            channelColumnThreadsHelper
                isMobile
                isSelected
                isMuted
                hasNotifications
                index
                count
                (Dom.id ("guild_viewDmThread_" ++ Id.toString otherUserId ++ "_" ++ Id.toString threadMessageIndex))
                (DmRoute
                    { channelId = DmChannelId.fromUserIds otherUserId localUser.session.userId
                    , threadRoute = ViewThreadWithFriends threadMessageIndex Nothing HideChannelSettings
                    , tab = Nothing
                    , channelsVisible = ChannelsHiddenOnMobile
                    , overlay = Nothing
                    }
                )
                (threadPreviewText localUser.timezone (User.allUsers localUser) SeqDict.empty threadMessageIndex localUser.decryptedMessages channel)
        )
        threads2
        |> Ui.column []


channelColumnThreads :
    Bool
    -> Time.Posix
    -> ChannelRoute
    -> Maybe (NonemptyDict ( Id ChannelId, ThreadRoute ) OneOrGreater)
    -> LocalUser
    -> SeqDict ( Id ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> Id GuildId
    -> Id ChannelId
    -> FrontendChannel
    -> SeqDict (Id ChannelMessageId) FrontendThread
    -> Element FrontendMsg_
channelColumnThreads isMobile now channelRoute directMentions localUser channels guildId channelId channel threads =
    let
        threads2 : List ( Id ChannelMessageId, ( IsMuted, ChannelNotificationType ), Bool )
        threads2 =
            List.filterMap
                (\( threadMessageIndex, thread ) ->
                    let
                        isSelected : Bool
                        isSelected =
                            case channelRoute of
                                ChannelRoute a (ViewThreadWithFriends b _ _) _ ->
                                    a == channelId && b == threadMessageIndex

                                _ ->
                                    False

                        isMuted =
                            MuteSettings.isChannelMuted
                                localUser.user.muteSettings
                                guildId
                                channelId
                                (ViewThread threadMessageIndex)

                        hasNotifications : ChannelNotificationType
                        hasNotifications =
                            GuildColumn.channelOrThreadHasNotifications
                                isMuted
                                directMentions
                                (SeqSet.member guildId localUser.user.notifyOnAllMessages)
                                channelId
                                (ViewThread threadMessageIndex)
                                (SeqDict.get
                                    ( GuildOrDmId (GuildOrDmId_Guild { guildId = guildId, channelId = channelId }), threadMessageIndex )
                                    localUser.user.lastViewedThreadMessage
                                )
                                thread
                    in
                    case ( hasNotifications, isSelected, MessageArray.last thread.messages ) of
                        ( NoNotification, False, Just message ) ->
                            if Duration.from (Message.createdAt message) now |> Quantity.lessThan Duration.week then
                                Just ( threadMessageIndex, ( isMuted, hasNotifications ), isSelected )

                            else
                                Nothing

                        _ ->
                            Just ( threadMessageIndex, ( isMuted, hasNotifications ), isSelected )
                )
                (SeqDict.toList threads)

        count =
            List.length threads2
    in
    List.indexedMap
        (\index ( threadMessageIndex, ( isMuted, hasNotifications ), isSelected ) ->
            channelColumnThreadsHelper
                isMobile
                isSelected
                isMuted
                hasNotifications
                index
                count
                (Dom.id ("guild_viewThread_" ++ Id.toString channelId ++ "_" ++ Id.toString threadMessageIndex))
                (GuildRoute
                    guildId
                    (ChannelRoute channelId (ViewThreadWithFriends threadMessageIndex Nothing HideChannelSettings) Nothing)
                    ChannelsHiddenOnMobile
                    Nothing
                )
                (threadPreviewText localUser.timezone (User.allUsers localUser) channels threadMessageIndex localUser.decryptedMessages channel)
        )
        threads2
        |> Ui.column []


channelColumnThreadsHelper :
    Bool
    -> Bool
    -> IsMuted
    -> ChannelNotificationType
    -> Int
    -> Int
    -> HtmlId
    -> Route
    -> String
    -> Element FrontendMsg_
channelColumnThreadsHelper isMobile isSelected isMuted hasNotifications index visibleThreadCount htmlId route name =
    GuildColumn.rowLinkButton
        htmlId
        route
        [ Ui.paddingWith { left = 28, right = 8, top = 0, bottom = 0 }
        , Ui.el
            [ (if isSelected && not isMobile then
                NoNotification

               else
                hasNotifications
              )
                |> GuildIcon.notificationView 4 5 MyUi.background2
            , Ui.move { x = 0, y = 0, z = 0 }
            , Ui.Font.color MyUi.font3
            , Ui.width Ui.shrink
            ]
            (Ui.html
                (if visibleThreadCount == 1 then
                    Icons.threadSingleSegment

                 else if visibleThreadCount - 1 == index then
                    Icons.threadBottomSegment

                 else if index == 0 then
                    Icons.threadTopSegment

                 else
                    Icons.threadMiddleSegment
                )
            )
            |> Ui.inFront
        , if isSelected then
            Ui.Font.color MyUi.font1

          else
            Ui.Font.color MyUi.font3
        , MyUi.hover isMobile [ Ui.Anim.fontColor MyUi.font1 ]
        , Ui.attrIf isSelected (Ui.background MyUi.selectedHighlight)
        , Ui.clipWithEllipsis
        , Ui.height (Ui.px MyUi.channelHeaderHeight)
        , MyUi.hoverText name
        , Ui.contentCenterY
        , MyUi.noShrinking
        ]
        [ Ui.text name
        , channelIsMuted isMuted
        ]


discordChannelColumnThreads :
    Bool
    -> Time.Posix
    -> DiscordGuildRouteData
    -> Maybe (NonemptyDict ( Discord.Id Discord.ChannelId, ThreadRoute ) OneOrGreater)
    -> LocalUser
    -> SeqDict ( Discord.Id Discord.ChannelId, Maybe (Id ChannelMessageId) ) { name : String }
    -> Discord.Id Discord.ChannelId
    -> DiscordFrontendChannel
    -> SeqDict (Id ChannelMessageId) DiscordFrontendThread
    -> Element FrontendMsg_
discordChannelColumnThreads isMobile now routeData directMentions localUser channels channelId channel threads =
    let
        threads2 : List ( Id ChannelMessageId, ( IsMuted, ChannelNotificationType ), Bool )
        threads2 =
            List.filterMap
                (\( threadMessageIndex, thread ) ->
                    let
                        isSelected : Bool
                        isSelected =
                            case routeData.channelRoute of
                                DiscordChannel_ChannelRoute a (ViewThreadWithFriends b _ _) _ ->
                                    a == channelId && b == threadMessageIndex

                                _ ->
                                    False

                        isMuted =
                            MuteSettings.isDiscordChannelMuted
                                localUser.user.muteSettings
                                routeData.guildId
                                channelId
                                (ViewThread threadMessageIndex)

                        hasNotifications : ChannelNotificationType
                        hasNotifications =
                            GuildColumn.channelOrThreadHasNotifications
                                isMuted
                                directMentions
                                (SeqSet.member routeData.guildId localUser.user.discordNotifyOnAllMessages)
                                channelId
                                (ViewThread threadMessageIndex)
                                (SeqDict.get
                                    ( DiscordGuildOrDmId (DiscordGuildOrDmId_Guild { currentUserId = routeData.currentDiscordUserId, guildId = routeData.guildId, channelId = channelId })
                                    , threadMessageIndex
                                    )
                                    localUser.user.lastViewedThreadMessage
                                )
                                thread
                    in
                    case ( hasNotifications, isSelected, MessageArray.last thread.messages ) of
                        ( NoNotification, False, Just message ) ->
                            if Duration.from (Message.createdAt message) now |> Quantity.lessThan Duration.week then
                                Just ( threadMessageIndex, ( isMuted, hasNotifications ), isSelected )

                            else
                                Nothing

                        _ ->
                            Just ( threadMessageIndex, ( isMuted, hasNotifications ), isSelected )
                )
                (SeqDict.toList threads)

        count : Int
        count =
            List.length threads2
    in
    List.indexedMap
        (\index ( threadMessageIndex, ( isMuted, hasNotifications ), isSelected ) ->
            channelColumnThreadsHelper
                isMobile
                isSelected
                isMuted
                hasNotifications
                index
                count
                (Dom.id ("guild_viewThread_" ++ Discord.idToString channelId ++ "_" ++ Id.toString threadMessageIndex))
                (DiscordGuildRoute
                    { currentDiscordUserId = routeData.currentDiscordUserId
                    , guildId = routeData.guildId
                    , channelRoute =
                        DiscordChannel_ChannelRoute
                            channelId
                            (ViewThreadWithFriends threadMessageIndex Nothing HideChannelSettings)
                            Nothing
                    , channelsVisible = ChannelsHiddenOnMobile
                    , overlay = Nothing
                    }
                )
                (threadPreviewText localUser.timezone (LinkedAndOtherDiscordUsers.allDiscordUsers localUser.discordUsers) channels threadMessageIndex SeqDict.empty channel)
        )
        threads2
        |> Ui.column []


channelColumnRow :
    Bool
    -> IsMuted
    -> ChannelNotificationType
    -> List FrontendUser
    -> ChannelRoute
    -> Id GuildId
    -> Id ChannelId
    -> FrontendChannel
    -> Element FrontendMsg_
channelColumnRow isMobile isMuted hasNotification usersInCall channelRoute guildId channelId channel =
    let
        isSelected : Bool
        isSelected =
            case channelRoute of
                ChannelRoute a (NoThreadWithFriends _ _) _ ->
                    a == channelId

                _ ->
                    False
    in
    GuildColumn.rowLinkButton
        (Dom.id ("guild_openChannel_" ++ Id.toString channelId))
        (GuildRoute
            guildId
            (ChannelRoute channelId (NoThreadWithFriends Nothing HideChannelSettings) Nothing)
            ChannelsHiddenOnMobile
            Nothing
        )
        [ Ui.paddingWith { left = 26, right = 8, top = 0, bottom = 0 }
        , Ui.el
            [ (if isSelected && not isMobile then
                NoNotification

               else
                hasNotification
              )
                |> GuildIcon.notificationView 0 -3 MyUi.background2
            , Ui.width (Ui.px 20)
            , Ui.move { x = 4, y = 0, z = 0 }
            , Ui.centerY
            ]
            (Ui.html Icons.hashtag)
            |> Ui.inFront
        , if isSelected then
            Ui.Font.color MyUi.font1

          else
            Ui.Font.color MyUi.font3
        , MyUi.hover isMobile [ Ui.Anim.fontColor MyUi.font1 ]
        , Ui.attrIf isSelected (Ui.background MyUi.selectedHighlight)
        , Ui.clipWithEllipsis
        , Ui.height (Ui.px MyUi.channelHeaderHeight)
        , MyUi.hoverText (ChannelName.toString channel.name)
        , Ui.contentCenterY
        , MyUi.noShrinking
        ]
        [ Ui.text (ChannelName.toString channel.name)
        , User.multipleProfileImages usersInCall
        , channelIsMuted isMuted
        ]


channelIsMuted : IsMuted -> Element msg
channelIsMuted isMuted =
    case isMuted of
        IsPartiallyMuted ->
            Ui.el
                [ MyUi.noShrinking
                , Ui.paddingWith { left = 0, right = 0, top = 0, bottom = 0 }
                , Ui.alignRight
                ]
                (Ui.html Icons.bellSlash)

        IsFullyMuted ->
            Ui.el
                [ MyUi.noShrinking
                , Ui.paddingWith { left = 0, right = 0, top = 0, bottom = 0 }
                , Ui.alignRight
                ]
                (Ui.html Icons.bellDoubleSlash)

        IsNotMuted ->
            Ui.none


discordChannelColumnRow :
    Bool
    -> IsMuted
    -> ChannelNotificationType
    -> DiscordGuildRouteData
    -> Discord.Id Discord.ChannelId
    -> DiscordFrontendChannel
    -> Element FrontendMsg_
discordChannelColumnRow isMobile isMuted hasNotifications routeData channelId channel =
    let
        isSelected : Bool
        isSelected =
            case routeData.channelRoute of
                DiscordChannel_ChannelRoute a (NoThreadWithFriends _ _) _ ->
                    a == channelId

                _ ->
                    False
    in
    GuildColumn.rowLinkButton
        (Dom.id ("guild_openChannel_" ++ Discord.idToString channelId))
        (DiscordGuildRoute
            { currentDiscordUserId = routeData.currentDiscordUserId
            , guildId = routeData.guildId
            , channelRoute =
                DiscordChannel_ChannelRoute
                    channelId
                    (NoThreadWithFriends Nothing HideChannelSettings)
                    Nothing
            , channelsVisible = ChannelsHiddenOnMobile
            , overlay = Nothing
            }
        )
        [ Ui.paddingWith
            { left = 26
            , right = 8
            , top = 0
            , bottom = 0
            }
        , Ui.el
            [ (if isSelected && not isMobile then
                NoNotification

               else
                hasNotifications
              )
                |> GuildIcon.notificationView 0 -3 MyUi.background2
            , Ui.width (Ui.px 20)
            , Ui.move { x = 4, y = 0, z = 0 }
            , Ui.centerY
            ]
            (Ui.html Icons.hashtag)
            |> Ui.inFront
        , if isSelected then
            Ui.Font.color MyUi.font1

          else
            Ui.Font.color MyUi.font3
        , MyUi.hover isMobile [ Ui.Anim.fontColor MyUi.font1 ]
        , Ui.attrIf isSelected (Ui.background MyUi.selectedHighlight)
        , Ui.clipWithEllipsis
        , Ui.height (Ui.px MyUi.channelHeaderHeight)
        , MyUi.hoverText (ChannelName.toString channel.name)
        , Ui.contentCenterY
        , MyUi.noShrinking
        ]
        [ Ui.text (ChannelName.toString channel.name)
        , channelIsMuted isMuted
        ]


friendsColumnLazy :
    Bool
    -> Bool
    -> Time.Posix
    -> DmChannelSelection
    -> String
    -> Bool
    -> LocalState
    -> Element FrontendMsg_
friendsColumnLazy canScroll2 isMobile currentTime openedOtherUserId friendsSearch friendsSearchHasFocus local =
    let
        currentTimeRoundedToMinute : Int
        currentTimeRoundedToMinute =
            Time.posixToMillis currentTime // msInMinute |> (*) msInMinute

        packed : Int
        packed =
            encodeFriendsColumn canScroll2 currentTimeRoundedToMinute
    in
    if (friendsSearch /= "") || friendsSearchHasFocus then
        -- The search text changes too often for laziness to be worth it here
        friendsColumn
            canScroll2
            isMobile
            currentTimeRoundedToMinute
            friendsSearch
            friendsSearchHasFocus
            openedOtherUserId
            local.dmChannels
            local.discordDmChannels
            local.localUser

    else
        case openedOtherUserId of
            NoDmChannelSelected ->
                Ui.Lazy.lazy5
                    friendsColumn_NoDmChannelSelected
                    packed
                    isMobile
                    local.dmChannels
                    local.discordDmChannels
                    local.localUser

            SelectedDmChannel dmRouteData ->
                Ui.Lazy.lazy5
                    (if isMobile then
                        friendsColumn_SelectedDmChannel_Mobile

                     else
                        friendsColumn_SelectedDmChannel_NotMobile
                    )
                    packed
                    dmRouteData
                    local.dmChannels
                    local.discordDmChannels
                    local.localUser

            SelectedDiscordDmChannel discordDmRouteData ->
                Ui.Lazy.lazy5
                    (if isMobile then
                        friendsColumn_SelectedDiscordDmChannel_Mobile

                     else
                        friendsColumn_SelectedDiscordDmChannel_NotMobile
                    )
                    packed
                    discordDmRouteData
                    local.dmChannels
                    local.discordDmChannels
                    local.localUser


friendsColumn_NoDmChannelSelected : Int -> Bool -> SeqDict (Id UserId) FrontendDmChannel -> SeqDict (Discord.Id Discord.PrivateChannelId) DiscordFrontendDmChannel -> LocalUser -> Element FrontendMsg_
friendsColumn_NoDmChannelSelected packed isMobile dmChannels discordDmChannels localUser =
    let
        { canScroll, time } =
            decodeFriendsColumn packed
    in
    friendsColumn canScroll isMobile time "" False NoDmChannelSelected dmChannels discordDmChannels localUser


friendsColumn_SelectedDiscordDmChannel_Mobile : Int -> DiscordDmRouteData -> SeqDict (Id UserId) FrontendDmChannel -> SeqDict (Discord.Id Discord.PrivateChannelId) DiscordFrontendDmChannel -> LocalUser -> Element FrontendMsg_
friendsColumn_SelectedDiscordDmChannel_Mobile packed discordDmRoute dmChannels discordDmChannels localUser =
    let
        { canScroll, time } =
            decodeFriendsColumn packed
    in
    friendsColumn canScroll True time "" False (SelectedDiscordDmChannel discordDmRoute) dmChannels discordDmChannels localUser


friendsColumn_SelectedDiscordDmChannel_NotMobile : Int -> DiscordDmRouteData -> SeqDict (Id UserId) FrontendDmChannel -> SeqDict (Discord.Id Discord.PrivateChannelId) DiscordFrontendDmChannel -> LocalUser -> Element FrontendMsg_
friendsColumn_SelectedDiscordDmChannel_NotMobile packed discordDmRoute dmChannels discordDmChannels localUser =
    let
        { canScroll, time } =
            decodeFriendsColumn packed
    in
    friendsColumn canScroll False time "" False (SelectedDiscordDmChannel discordDmRoute) dmChannels discordDmChannels localUser


friendsColumn_SelectedDmChannel_Mobile : Int -> DmRouteData -> SeqDict (Id UserId) FrontendDmChannel -> SeqDict (Discord.Id Discord.PrivateChannelId) DiscordFrontendDmChannel -> LocalUser -> Element FrontendMsg_
friendsColumn_SelectedDmChannel_Mobile packed dmRoute dmChannels discordDmChannels localUser =
    let
        { canScroll, time } =
            decodeFriendsColumn packed
    in
    friendsColumn canScroll True time "" False (SelectedDmChannel dmRoute) dmChannels discordDmChannels localUser


friendsColumn_SelectedDmChannel_NotMobile : Int -> DmRouteData -> SeqDict (Id UserId) FrontendDmChannel -> SeqDict (Discord.Id Discord.PrivateChannelId) DiscordFrontendDmChannel -> LocalUser -> Element FrontendMsg_
friendsColumn_SelectedDmChannel_NotMobile packed dmRoute dmChannels discordDmChannels localUser =
    let
        { canScroll, time } =
            decodeFriendsColumn packed
    in
    friendsColumn canScroll False time "" False (SelectedDmChannel dmRoute) dmChannels discordDmChannels localUser


friendsColumn :
    Bool
    -> Bool
    -> Int
    -> String
    -> Bool
    -> DmChannelSelection
    -> SeqDict (Id UserId) FrontendDmChannel
    -> SeqDict (Discord.Id Discord.PrivateChannelId) DiscordFrontendDmChannel
    -> LocalUser
    -> Element FrontendMsg_
friendsColumn canScroll2 isMobile currentTime friendsSearch friendsSearchHasFocus dmChannelSelection dmChannels discordDmChannels localUser =
    let
        dmChannelsIncludingCurrentUser : SeqDict (Id UserId) FrontendDmChannel
        dmChannelsIncludingCurrentUser =
            SeqDict.update
                localUser.session.userId
                (\maybe -> Maybe.withDefault DmChannel.frontendInit maybe |> Just)
                dmChannels

        discordDmChannelsIncludingLinkedUsers : SeqDict (Discord.Id Discord.PrivateChannelId) DiscordFrontendDmChannel
        discordDmChannelsIncludingLinkedUsers =
            discordDmChannels

        searchIsVisible : Bool
        searchIsVisible =
            friendsSearchHasFocus || (friendsSearch /= "")

        searchFilter : String
        searchFilter =
            String.trim friendsSearch |> String.toLower

        matchesSearch : PersonName -> Bool
        matchesSearch name =
            String.contains searchFilter (String.toLower (PersonName.toString name))

        columnItems : List ( Time.Posix, Element FrontendMsg_ )
        columnItems =
            List.filterMap
                (\( otherUserId, dmChannel ) ->
                    case User.getUser otherUserId localUser of
                        Just otherUser ->
                            if matchesSearch otherUser.name then
                                let
                                    -- The route being viewed in this DM, or Nothing when the
                                    -- open DM belongs to somebody else
                                    threadRoute : Maybe ThreadRouteWithFriends
                                    threadRoute =
                                        case dmChannelSelection of
                                            SelectedDmChannel dmRoute ->
                                                if DmChannelId.otherUserId localUser.session.userId dmRoute.channelId == Just otherUserId then
                                                    Just dmRoute.threadRoute

                                                else
                                                    Nothing

                                            SelectedDiscordDmChannel _ ->
                                                Nothing

                                            NoDmChannelSelected ->
                                                Nothing
                                in
                                ( case MessageArray.last dmChannel.messages of
                                    Just message2 ->
                                        Message.createdAt message2

                                    _ ->
                                        Time.millisToPosix 0
                                , Ui.column
                                    []
                                    [ Ui.Lazy.lazy5
                                        (if isMobile then
                                            friendLabelMobile

                                         else
                                            friendLabelNotMobile
                                        )
                                        (encodeFriendLabel
                                            (case threadRoute of
                                                Just (NoThreadWithFriends _ _) ->
                                                    True

                                                _ ->
                                                    False
                                            )
                                            currentTime
                                        )
                                        localUser
                                        otherUserId
                                        otherUser
                                        dmChannel
                                    , dmColumnThreads
                                        isMobile
                                        (Time.millisToPosix currentTime)
                                        threadRoute
                                        localUser
                                        otherUserId
                                        dmChannel
                                        (case threadRoute of
                                            -- A thread that was just opened isn't in the local
                                            -- state yet but still belongs in the list
                                            Just (ViewThreadWithFriends threadMessageIndex _ _) ->
                                                SeqDict.insert threadMessageIndex Thread.frontendInit dmChannel.threads

                                            _ ->
                                                dmChannel.threads
                                        )
                                    ]
                                )
                                    |> Just

                            else
                                Nothing

                        Nothing ->
                            Nothing
                )
                (SeqDict.toList dmChannelsIncludingCurrentUser)
                ++ List.filterMap
                    (\( channelId, dmChannel ) ->
                        if
                            (searchFilter == "")
                                || List.any
                                    (\( userId, _ ) ->
                                        case User.getDiscordUser userId localUser of
                                            Just discordUser ->
                                                matchesSearch discordUser.name

                                            Nothing ->
                                                False
                                    )
                                    (NonemptyDict.toList dmChannel.members)
                        then
                            ( case MessageArray.last dmChannel.messages of
                                Just message2 ->
                                    Message.createdAt message2

                                _ ->
                                    Time.millisToPosix 0
                            , Ui.Lazy.lazy5
                                (if isMobile then
                                    discordFriendLabelMobile

                                 else
                                    discordFriendLabelNotMobile
                                )
                                currentTime
                                (case dmChannelSelection of
                                    SelectedDiscordDmChannel routeData ->
                                        routeData.channelId == channelId

                                    _ ->
                                        False
                                )
                                channelId
                                dmChannel
                                localUser
                            )
                                |> Just

                        else
                            Nothing
                    )
                    (SeqDict.toList discordDmChannelsIncludingLinkedUsers)
    in
    channelColumnContainer
        localUser.safeAreaInsetTop
        [ Ui.el
            [ Ui.height Ui.fill
            , -- The search input is always in the DOM, invisible and covering the magnifying glass
              -- icon so that clicking the icon focuses it. It has to stay in the DOM while hidden
              -- because recreating it on press would drop the browser focus that reveals it.
              Ui.inFront
                (Ui.row
                    [ Ui.height Ui.fill ]
                    [ Ui.Input.text
                        (Ui.id (Dom.idToString friendsSearchInputId)
                            :: (if searchIsVisible then
                                    [ Ui.background MyUi.background2
                                    , Ui.border 0
                                    , Ui.rounded 4
                                    , Ui.paddingXY 8 4
                                    , Ui.Font.color MyUi.font1
                                    , Ui.centerY
                                    ]

                                else
                                    [ Ui.opacity 0
                                    , Ui.border 0
                                    , Ui.width (Ui.px 40)
                                    , Ui.alignRight
                                    , Ui.height Ui.fill
                                    , Ui.pointer
                                    ]
                               )
                        )
                        { onChange = TypedFriendsSearch
                        , text = friendsSearch
                        , placeholder =
                            if searchIsVisible then
                                Just "Filter friends"

                            else
                                Nothing
                        , label = Ui.Input.labelHidden (Dom.idToString friendsSearchInputId)
                        }
                    , if searchIsVisible then
                        MyUi.elButton
                            (Dom.id "guild_clearFriendsSearch")
                            PressedClearFriendsSearch
                            [ Ui.Font.color MyUi.font2
                            , Ui.width (Ui.px 40)
                            , Ui.paddingXY 8 0
                            , Ui.height Ui.fill
                            , Ui.contentCenterY
                            , MyUi.hoverText "Clear search"
                            ]
                            (Ui.html Icons.x)

                      else
                        Ui.none
                    ]
                )
            ]
            (Ui.row
                [ Ui.height Ui.fill
                , Ui.attrIf searchIsVisible (Ui.opacity 0)
                ]
                [ Ui.el
                    [ Ui.Font.bold
                    , Ui.paddingXY 8 8
                    , Ui.Font.color MyUi.font1
                    ]
                    (Ui.text directMessagesText)
                , Ui.el
                    [ Ui.Font.color MyUi.font2
                    , Ui.width (Ui.px 40)
                    , Ui.alignRight
                    , Ui.paddingXY 8 0
                    , Ui.height Ui.fill
                    , Ui.contentCenterY
                    ]
                    (Ui.html Icons.magnifyingGlass)
                ]
            )
        ]
        Ui.none
        (case columnItems of
            [] ->
                Ui.Prose.paragraph
                    [ Ui.Font.italic
                    , Ui.Font.lineHeight 1.5
                    , Ui.paddingXY 16 20
                    ]
                    [ Ui.text "No results found for "
                    , Ui.el [ Ui.Font.bold ] (Ui.text friendsSearch)
                    ]

            [ ( _, single ) ] ->
                Ui.column
                    [ MyUi.scrollable canScroll2, Ui.heightMin 0 ]
                    [ single
                    , Ui.el
                        [ Ui.Font.size 16, Ui.padding 8, Ui.Font.color MyUi.font3 ]
                        (Ui.text "Join a guild and then click on someone's profile image to start a chat!")
                    ]

            _ ->
                List.sortBy (\( time, _ ) -> Time.posixToMillis time |> negate) columnItems
                    |> List.map Tuple.second
                    |> Ui.column [ MyUi.scrollable canScroll2, Ui.heightMin 0 ]
        )


friendsSearchInputId : HtmlId
friendsSearchInputId =
    Dom.id "guild_friendsSearchInput"


friendLabelMobile :
    Int
    -> LocalUser
    -> Id UserId
    -> FrontendUser
    -> FrontendDmChannel
    -> Element FrontendMsg_
friendLabelMobile packed localUser otherUserId otherUser channel =
    let
        { isSelected, time } =
            decodeFriendLabel packed
    in
    friendLabel True time isSelected localUser otherUserId otherUser channel


friendLabelNotMobile :
    Int
    -> LocalUser
    -> Id UserId
    -> FrontendUser
    -> FrontendDmChannel
    -> Element FrontendMsg_
friendLabelNotMobile packed localUser otherUserId otherUser channel =
    let
        { isSelected, time } =
            decodeFriendLabel packed
    in
    friendLabel False time isSelected localUser otherUserId otherUser channel


type SomeoneIsTyping
    = SomeoneIsTyping
    | SomeoneIsEditing
    | NoOneIsTyping


someoneIsTyping : Time.Posix -> SeqDict userId { a | threadRoute : ThreadRouteWithMaybeMessage, time : Time.Posix } -> SomeoneIsTyping
someoneIsTyping time lastTypedAt =
    SeqDict.foldl
        (\_ a state ->
            case state of
                SomeoneIsTyping ->
                    state

                _ ->
                    if Duration.from a.time time |> Quantity.lessThan (Quantity.plus Duration.second typingDebouncerDelay) then
                        case a.threadRoute of
                            NoThreadWithMaybeMessage (Just _) ->
                                SomeoneIsEditing

                            NoThreadWithMaybeMessage Nothing ->
                                SomeoneIsTyping

                            ViewThreadWithMaybeMessage _ _ ->
                                state

                    else
                        state
        )
        NoOneIsTyping
        lastTypedAt


friendLabel :
    Bool
    -> Time.Posix
    -> Bool
    -> LocalUser
    -> Id UserId
    -> FrontendUser
    -> FrontendDmChannel
    -> Element FrontendMsg_
friendLabel isMobile time isSelected localUser otherUserId otherUser channel =
    let
        allUsers : SeqDict (Id UserId) FrontendUser
        allUsers =
            User.allUsers localUser

        decrypted : SeqDict BytesHash (Result () (MessageContent (Id UserId) (Id ChannelId)))
        decrypted =
            localUser.decryptedMessages

        message : Maybe (Message ChannelMessageId (Id UserId) (Id ChannelId))
        message =
            MessageArray.last channel.messages

        messagePreview : String
        messagePreview =
            case someoneIsTyping time (SeqDict.remove localUser.session.userId channel.lastTypedAt) of
                SomeoneIsTyping ->
                    typingText

                SomeoneIsEditing ->
                    editingText

                NoOneIsTyping ->
                    case message of
                        Just message2 ->
                            case message2 of
                                UserTextMessage a ->
                                    (if a.createdBy == localUser.session.userId then
                                        "You: "

                                     else
                                        ""
                                    )
                                        ++ RichText.toString localUser.timezone True allUsers SeqDict.empty a.content.content

                                EncryptedUserTextMessage a ->
                                    (if a.createdBy == localUser.session.userId then
                                        "You: "

                                     else
                                        ""
                                    )
                                        ++ (case SeqDict.get (Encryption.hash a.content) decrypted of
                                                Just result ->
                                                    RichText.toString
                                                        localUser.timezone
                                                        True
                                                        allUsers
                                                        SeqDict.empty
                                                        (case result of
                                                            Ok ok ->
                                                                ok.content

                                                            Err () ->
                                                                RichText.failedToDecryptMessage
                                                        )

                                                Nothing ->
                                                    ""
                                           )

                                UserJoinedMessage _ userId _ _ ->
                                    User.toString userId allUsers
                                        ++ " joined!"

                                DeletedMessage _ ->
                                    LocalState.messageDeleted

                                CallStarted { endedAt } ->
                                    LocalState.callStartedText endedAt

                                GameStarted { gameType } ->
                                    LocalState.gameStartedText gameType

                        Nothing ->
                            ""
    in
    GuildColumn.rowLinkButton
        (Dom.id ("guild_friendLabel_" ++ Id.toString otherUserId))
        (Route.DmRoute
            { channelId = DmChannelId.fromUserIds localUser.session.userId otherUserId
            , threadRoute = NoThreadWithFriends Nothing HideChannelSettings
            , tab = Nothing
            , channelsVisible = ChannelsHiddenOnMobile
            , overlay = Nothing
            }
        )
        [ Ui.clipWithEllipsis
        , Ui.spacing 8
        , Ui.padding 4
        , MyUi.hoverText messagePreview
        , Ui.Font.color
            (if isSelected then
                MyUi.font1

             else
                MyUi.font3
            )
        , MyUi.hover isMobile [ Ui.Anim.fontColor MyUi.font1 ]
        , Ui.attrIf isSelected (Ui.background MyUi.selectedHighlight)
        ]
        [ User.profileImage (Just otherUser)
        , Ui.column
            []
            [ Ui.el [ Ui.Font.bold ] (Ui.text (PersonName.toString otherUser.name))
            , friendLabelMessagePreview time messagePreview message
            ]
        ]


friendLabelMessagePreview : Time.Posix -> String -> Maybe (Message messageId userId channelId) -> Element msg
friendLabelMessagePreview time messagePreview message =
    Ui.row
        [ Ui.Font.size 13, Ui.spacing 4 ]
        [ Ui.el [] (Ui.text messagePreview)
        , case message of
            Just message2 ->
                MyUi.timeElapsedShort time (Message.createdAt message2)
                    |> Ui.text
                    |> Ui.el [ Ui.alignRight, Ui.opacity 0.7 ]

            Nothing ->
                Ui.none
        ]


discordFriendLabelMobile :
    Int
    -> Bool
    -> Discord.Id Discord.PrivateChannelId
    -> DiscordFrontendDmChannel
    -> LocalUser
    -> Element FrontendMsg_
discordFriendLabelMobile time isSelected dmChannelId channel localUser =
    discordFriendLabel True (Time.millisToPosix time) isSelected dmChannelId channel localUser


discordFriendLabelNotMobile :
    Int
    -> Bool
    -> Discord.Id Discord.PrivateChannelId
    -> DiscordFrontendDmChannel
    -> LocalUser
    -> Element FrontendMsg_
discordFriendLabelNotMobile time isSelected dmChannelId channel localUser =
    discordFriendLabel False (Time.millisToPosix time) isSelected dmChannelId channel localUser


discordFriendLabel :
    Bool
    -> Time.Posix
    -> Bool
    -> Discord.Id Discord.PrivateChannelId
    -> DiscordFrontendDmChannel
    -> LocalUser
    -> Element FrontendMsg_
discordFriendLabel isMobile time isSelected dmChannelId channel localUser =
    let
        message : Maybe (Message ChannelMessageId (Discord.Id Discord.UserId) (Discord.Id Discord.ChannelId))
        message =
            MessageArray.last channel.messages

        messagePreview : String
        messagePreview =
            case
                someoneIsTyping
                    time
                    (SeqDict.diff channel.lastTypedAt (LinkedAndOtherDiscordUsers.linkedUsers localUser.discordUsers)
                        |> SeqDict.map (\_ a -> { threadRoute = NoThreadWithMaybeMessage a.messageIndex, time = a.time })
                    )
            of
                SomeoneIsTyping ->
                    typingText

                SomeoneIsEditing ->
                    editingText

                NoOneIsTyping ->
                    case message of
                        Just message2 ->
                            case message2 of
                                UserTextMessage a ->
                                    (if LinkedAndOtherDiscordUsers.isLinkedUser a.createdBy localUser.discordUsers then
                                        "You: "

                                     else
                                        ""
                                    )
                                        ++ RichText.toString
                                            localUser.timezone
                                            True
                                            (LinkedAndOtherDiscordUsers.allDiscordUsers localUser.discordUsers)
                                            SeqDict.empty
                                            a.content.content

                                EncryptedUserTextMessage a ->
                                    if LinkedAndOtherDiscordUsers.isLinkedUser a.createdBy localUser.discordUsers then
                                        "You: " ++ RichText.failedToDecryptMessageText

                                    else
                                        RichText.failedToDecryptMessageText

                                UserJoinedMessage _ userId _ _ ->
                                    User.toString
                                        userId
                                        (LinkedAndOtherDiscordUsers.allDiscordUsers localUser.discordUsers)
                                        ++ " joined!"

                                DeletedMessage _ ->
                                    LocalState.messageDeleted

                                CallStarted { endedAt } ->
                                    LocalState.callStartedText endedAt

                                GameStarted { gameType } ->
                                    LocalState.gameStartedText gameType

                        Nothing ->
                            ""

        maybeCurrentUserId : Maybe (Discord.Id Discord.UserId)
        maybeCurrentUserId =
            List.Extra.findMap
                (\( userId, _ ) ->
                    if NonemptyDict.member userId channel.members then
                        Just userId

                    else
                        Nothing
                )
                (SeqDict.toList (LinkedAndOtherDiscordUsers.linkedUsers localUser.discordUsers))
    in
    case maybeCurrentUserId of
        Just currentUserId ->
            let
                members2 : List (Discord.Id Discord.UserId)
                members2 =
                    NonemptyDict.remove currentUserId channel.members |> SeqDict.keys

                notification : ChannelNotificationType
                notification =
                    if isSelected then
                        NoNotification

                    else
                        case GuildColumn.discordDmHasNotifications localUser dmChannelId channel of
                            Just ( _, count ) ->
                                NewMessageForUser count

                            Nothing ->
                                NoNotification
            in
            MyUi.rowButton
                ("guild_discordFriendLabel_" ++ Discord.idToString dmChannelId |> Dom.id)
                (PressedLink
                    (DiscordDmRoute
                        { currentDiscordUserId = currentUserId
                        , channelId = dmChannelId
                        , viewingMessage = Nothing
                        , showMembersTab = HideChannelSettings
                        , tab = Nothing
                        , channelsVisible = ChannelsHiddenOnMobile
                        , overlay = Nothing
                        }
                    )
                )
                [ Ui.clipWithEllipsis
                , Ui.spacing 8
                , MyUi.hoverText messagePreview
                , Ui.padding 4
                , Ui.Font.color
                    (if isSelected then
                        MyUi.font1

                     else
                        MyUi.font3
                    )
                , MyUi.hover isMobile [ Ui.Anim.fontColor MyUi.font1 ]
                , Ui.attrIf isSelected (Ui.background MyUi.selectedHighlight)
                ]
                (case members2 of
                    [] ->
                        case User.getDiscordUser currentUserId localUser of
                            Just otherUser ->
                                [ Ui.el
                                    [ GuildIcon.discordNotificationView 4 -3 notification
                                    , Ui.width Ui.shrink
                                    ]
                                    (User.discordProfileImage currentUserId otherUser.icon)
                                , Ui.column
                                    []
                                    [ Ui.el [ Ui.Font.bold ] (Ui.text (PersonName.toString otherUser.name))
                                    , friendLabelMessagePreview time messagePreview message
                                    ]
                                ]

                            Nothing ->
                                []

                    rest ->
                        [ List.filterMap
                            (\userId ->
                                case User.getDiscordUser userId localUser of
                                    Just user ->
                                        Just ( userId, user.icon )

                                    Nothing ->
                                        Nothing
                            )
                            members2
                            |> User.multipleDiscordProfileImages
                            |> Ui.el
                                [ GuildIcon.discordNotificationView 4 -3 notification
                                , Ui.width Ui.shrink
                                ]
                        , Ui.column
                            []
                            [ List.filterMap
                                (\userId ->
                                    case User.getDiscordUser userId localUser of
                                        Just otherUser ->
                                            PersonName.toString otherUser.name |> Just

                                        Nothing ->
                                            Nothing
                                )
                                rest
                                |> String.join ", "
                                |> Ui.text
                                |> Ui.el [ Ui.Font.bold ]
                            , friendLabelMessagePreview time messagePreview message
                            ]
                        ]
                )

        Nothing ->
            Ui.text "Something went wrong"


newChannelFormInit : NewChannelForm
newChannelFormInit =
    { name = "", description = "", pressedSubmit = False }


newGuildFormInit : NewGuildForm
newGuildFormInit =
    { name = "", pressedSubmit = False }


editChannelFormInit : FrontendChannel -> EditChannelForm
editChannelFormInit channel =
    { name = ChannelName.toString channel.name
    , description = ChannelDescription.toString channel.description
    , deleteConfirmation = ""
    , showDeleteConfirmation = False
    , pressedSubmit = False
    }


deleteConfirmationInput : String -> EditChannelForm -> Element EditChannelForm
deleteConfirmationInput channelNameString form =
    let
        confirmLabel =
            Ui.Input.label
                "deleteChannelConfirmation"
                [ Ui.Font.color MyUi.font2, Ui.paddingXY 2 0 ]
                (Ui.text ("Type \"" ++ channelNameString ++ "\" to confirm deletion"))
    in
    Ui.column
        []
        [ confirmLabel.element
        , Ui.Input.text
            [ Ui.padding 6
            , Ui.background MyUi.inputBackground
            , Ui.borderColor MyUi.inputBorder
            , Ui.widthMax 500
            ]
            { onChange = \text -> { form | deleteConfirmation = text }
            , text = form.deleteConfirmation
            , placeholder = Nothing
            , label = confirmLabel.id
            }
        ]


newChannelFormView : Bool -> Id GuildId -> NewChannelForm -> Element FrontendMsg_
newChannelFormView isMobile2 guildId form =
    Ui.column
        [ Ui.Font.color MyUi.font1, Ui.alignTop ]
        [ ChannelHeader.channelHeader isMobile2 (Ui.text "Create new channel") Nothing
        , Ui.column
            [ Ui.spacing 16, Ui.padding 16 ]
            [ channelNameInput form |> Ui.map (NewChannelFormChanged guildId)
            , channelDescriptionInput form |> Ui.map (NewChannelFormChanged guildId)
            , submitButton (Dom.id "guild_createChannel") (PressedSubmitNewChannel guildId form) "Create channel"
            ]
        ]


submitButton : HtmlId -> msg -> String -> Element msg
submitButton htmlId onPress text =
    MyUi.elButton
        htmlId
        onPress
        [ Ui.paddingXY 16 4
        , Ui.background MyUi.buttonBackground
        , Ui.width Ui.shrink
        , Ui.rounded 4
        , Ui.Font.weight 500
        , Ui.borderColor MyUi.buttonBorder
        , Ui.border 1
        ]
        (Ui.text text)


submitButtonWide : HtmlId -> msg -> String -> Element msg
submitButtonWide htmlId onPress text =
    MyUi.elButton
        htmlId
        onPress
        [ Ui.paddingXY 8 4
        , Ui.background MyUi.buttonBackground
        , Ui.Font.center
        , Ui.rounded 4
        , Ui.Font.weight 500
        , Ui.borderColor MyUi.buttonBorder
        , Ui.border 1
        ]
        (Ui.text text)


channelNameInput : { a | name : String, pressedSubmit : Bool } -> Element { a | name : String, pressedSubmit : Bool }
channelNameInput form =
    let
        nameLabel =
            Ui.Input.label
                "newChannelName"
                [ Ui.Font.color MyUi.font2, Ui.paddingXY 2 0 ]
                (Ui.text "Name")
    in
    Ui.column
        []
        [ nameLabel.element
        , Ui.Input.text
            [ Ui.padding 6
            , Ui.background MyUi.inputBackground
            , Ui.borderColor MyUi.inputBorder
            , Ui.widthMax 500
            ]
            { onChange = \text -> { form | name = text }
            , text = form.name
            , placeholder = Nothing
            , label = nameLabel.id
            }
        , case ( form.pressedSubmit, ChannelName.fromString form.name ) of
            ( True, Err error ) ->
                Ui.el [ Ui.paddingXY 2 0, Ui.Font.color MyUi.errorColor ] (Ui.text error)

            _ ->
                Ui.none
        ]


channelDescriptionInput : { a | description : String, pressedSubmit : Bool } -> Element { a | description : String, pressedSubmit : Bool }
channelDescriptionInput form =
    let
        descriptionLabel =
            Ui.Input.label
                "channelDescription"
                [ Ui.Font.color MyUi.font2, Ui.paddingXY 2 0 ]
                (Ui.text "Description")
    in
    Ui.column
        []
        [ descriptionLabel.element
        , Ui.Input.multiline
            [ Ui.padding 6
            , Ui.background MyUi.inputBackground
            , Ui.borderColor MyUi.inputBorder
            , Ui.widthMax 500
            ]
            { onChange = \text -> { form | description = text }
            , text = form.description
            , placeholder = Nothing
            , label = descriptionLabel.id
            , spellcheck = True
            }
        , case ( form.pressedSubmit, ChannelDescription.fromString form.description ) of
            ( True, Err error ) ->
                Ui.el [ Ui.paddingXY 2 0, Ui.Font.color MyUi.errorColor ] (Ui.text error)

            _ ->
                Ui.none
        ]


newGuildFormView : Int -> NewGuildForm -> Element FrontendMsg_
newGuildFormView safeAreaInsetTop form =
    Ui.column
        [ Ui.Font.color MyUi.font1
        , Ui.paddingWith { left = 0, right = 0, top = safeAreaInsetTop, bottom = 16 }
        , Ui.alignTop
        , Ui.spacing 16
        , Ui.height Ui.fill
        , Ui.background MyUi.background1
        ]
        [ Ui.el [ Ui.Font.size 24, Ui.paddingXY 16 0 ] (Ui.text "Create new guild")
        , guildNameInput form |> Ui.map NewGuildFormChanged
        , Ui.row
            [ Ui.spacing 16, Ui.paddingXY 16 0 ]
            [ MyUi.secondaryButton
                (Dom.id "guild_cancelNewGuild")
                (PressedLink (HomePageRoute Nothing))
                "Cancel"
            , submitButton (Dom.id "guild_createGuildSubmit") (PressedSubmitNewGuild form) "Create guild"
            ]
        ]


guildNameInput : NewGuildForm -> Element NewGuildForm
guildNameInput form =
    let
        nameLabel =
            Ui.Input.label
                "newGuildName"
                [ Ui.Font.color MyUi.font2, Ui.paddingXY 2 0 ]
                (Ui.text "Guild name")
    in
    Ui.column
        [ Ui.paddingXY 16 0 ]
        [ nameLabel.element
        , Ui.Input.text
            [ Ui.padding 6
            , Ui.background MyUi.inputBackground
            , Ui.borderColor MyUi.inputBorder
            , Ui.widthMax 500
            ]
            { onChange = \text -> { form | name = text }
            , text = form.name
            , placeholder = Nothing
            , label = nameLabel.id
            }
        , case ( form.pressedSubmit, GuildName.fromString form.name ) of
            ( True, Err error ) ->
                Ui.el [ Ui.paddingXY 2 0, Ui.Font.color MyUi.errorColor ] (Ui.text error)

            _ ->
                Ui.none
        ]


fileUploadPreview :
    (Id FileId -> msg)
    -> (Id FileId -> msg)
    -> ({ fileId : Id FileId, removeSpoiler : Bool } -> msg)
    -> Maybe (Nonempty (RichText userId channelId))
    -> NonemptyDict (Id FileId) FileStatus
    -> Element msg
fileUploadPreview onPressDelete onPressInfo onPressSpoiler richText filesToUpload2 =
    Ui.row
        [ Ui.spacing 4
        , Ui.move { x = 0, y = -SheepGame.fileUploadPreviewSize, z = 0 }
        , Ui.width Ui.shrink
        , Ui.paddingXY 8 0
        ]
        (SheepGame.fileUploadPreview onPressDelete onPressInfo onPressSpoiler richText filesToUpload2)
